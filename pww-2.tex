\documentclass[oneside]{scrbook}
\usepackage{unicode-math}
\setmainfont{TeX Gyre Pagella}
\setmathfont{TeX Gyre Pagella Math}
\usepackage{graphicx}
\usepackage{mflogo}
\addtokomafont{section}{\clearpage}
\usepackage{luamplib}
\mplibtextextlabel{enable}
\everymplib{input colorbrewer-rgb;interim ahangle := 30;beginfig(0);
numeric tw; tw = \mpdim{\textwidth};}
\everyendmplib{numeric wd;
wd = xpart (urcorner bbox currentpicture - llcorner bbox currentpicture);
if wd > tw: currentpicture := currentpicture scaled (tw/wd); fi
endfig;}
\usepackage{etoc}
\newcommand{\minitoc}{\etocsettocstyle{\begingroup\small}{\endgroup}\localtableofcontentswithrelativedepth{+1}}
%--------------------
\title{Proofs without words II\\[6pt]{\Large More exercises in \MP}}
\date{March 2020 — January 2021\\[4in]
\centerline{\begin{mplibcode}
    path t[], h;
    numeric r; r = -30;
    t0 = (for i=0 upto 2: up scaled 21 rotated 120i -- endfor cycle) rotated r;
    h  = (for i=0 upto 5: up scaled 34 rotated 60i -- endfor cycle) rotated r;
    t1 = subpath (0, 1) of t0 -- point 1 of h -- cycle;
    t2 = subpath (1, 2) of t0 -- point 3 of h -- cycle;
    t3 = subpath (2, 3) of t0 -- point 5 of h -- cycle;
    t4 = subpath (0, 1) of h -- point 0 of t0 -- cycle;
    t5 = subpath (1, 2) of h -- point 1 of t0 -- cycle;
    t6 = subpath (2, 3) of h -- point 1 of t0 -- cycle;
    t7 = subpath (3, 4) of h -- point 2 of t0 -- cycle;
    t8 = subpath (4, 5) of h -- point 2 of t0 -- cycle;
    t9 = subpath (5, 6) of h -- point 0 of t0 -- cycle;
    fill t0 withcolor Blues 7 2;
    fill t1 withcolor Blues 7 1;
    fill t2 withcolor Blues 7 3;
    fill t3 withcolor Blues 7 3;
    fill t4 withcolor Blues 7 1;
    fill t5 withcolor Blues 7 1;
    fill t6 withcolor Blues 7 2;
    fill t7 withcolor Blues 7 5;
    fill t8 withcolor Blues 7 5;
    fill t9 withcolor Blues 7 2;
    forsuffixes @=0, 4, 5, 6, 7, 8, 9: draw t@; endfor \end{mplibcode}}}
\author{Toby Thurston}
\setcounter{secnumdepth}{-1}
\setcounter{tocdepth}{0}
% some tex defs
\def\contrib#1{\rightline{— #1}}


\begin{document}
\maketitle
\tableofcontents
\chapter{Geometry and Algebra}

\minitoc

\section{The Pythagorean theorem VII}

\vfill
$$
\begin{mplibcode}
numeric u;
u = 16;
path s[];
for i=0 upto 4:
    for j=0 upto 4:
         s[5i+j] = unitsquare shifted -(1/2,1/2)
             scaled ((4-((i+j) mod 2))*u) 
             shifted (1/2i*u*(7,-1))
             shifted (1/2j*u*(1,7));
         fill s[5i+j] withcolor if odd (i+j): Blues 8 3 else: Oranges 8 3 fi;
    endfor
endfor
fill subpath(0,7/4) of s[8] -- cycle withcolor Reds 8 4;
for i=0 upto 4:
    for j=0 upto 4:
        draw s[5i+j];
    endfor
endfor
drawoptions(withpen pencircle scaled 1);
draw point 0 of s[0] -- point 7/4 of s[24];
draw point 0 of s[2] -- point 7/4 of s[14] 
  -- point 7/4 of s[22] -- point 0 of s[10] -- cycle;
draw point 0 of s[4] -- point 7/4 of s[ 4] 
  -- point 7/4 of s[20] -- point 0 of s[20] -- cycle;
\end{mplibcode}
$$
\vfill
\contrib{Annairizi of Arabia (circa 900)}

\section{The Pythagorean theorem VIII}

\vfill
$$
\begin{mplibcode}
numeric u;
u = 12mm;
path a[], b[];
a1 = ((0,0)--(1,0)--(1,1)--cycle) scaled 3u;

b0 = unitsquare scaled u;
b1 = ((1,0)--(4,0)--(4,4)--cycle) scaled u;
b2 = ((1,0)--(4,4)--(1,1)--cycle) scaled u;

a2 = a1 reflectedabout((0,0),(1,1));
b3 = b1 reflectedabout((0,0),(1,1));
b4 = b2 reflectedabout((0,0),(1,1));

picture A[], B[];

forsuffixes $=1,2:
    A$ = image(fill a$ withcolor Blues 8 3; draw a$);
    draw A$ shifted (3u * left);
endfor
forsuffixes $=0,1,2,3,4:
    B$ = image(fill b$ withcolor Oranges 8 3; draw b$);
    draw B$ shifted (4u * down);
endfor

draw A1 shifted (0u,3u);
draw A2 shifted (4u,1u);
draw B0 shifted (3u,3u);
draw B1 rotated 90 shifted (4u,-1u);
draw B2 shifted (3u,0u);
draw B3 reflectedabout(left,right) shifted (3u,8u);
draw B4 reflectedabout(down,up) rotated 90 shifted (4u,7u);

\end{mplibcode}
$$
\vfill
\contrib{Liu Hui (3rd century \textsc{A.D.})}

\section{The Pythagorean theorem IX}

\vfill
$$
\begin{mplibcode}
    path c, t, s[];
    c = fullcircle scaled 2/5 \mpdim\textwidth;
    t = point 4 of c -- point 0 of c -- point 2.5 of c -- cycle;
    fill t withcolor Oranges 7 3;
    vardef square_on(expr a,b) = 
     unitsquare scaled abs(a-b) rotated angle (a-b) shifted b
    enddef;
    for i=1 upto 3:
    s[i] = square_on(point i-1 of t, point i of t);
    draw s[i];
    endfor
    z1 = point 2 of t rotatedabout(center s1, 180);
    draw point 2 of t  -- z1            dashed evenly;
    draw point 2 of s1 -- z1 -- point 3 of s1;
    draw point 2 of s2 -- point 3 of s3 dashed evenly;
    draw point 2 of s3 -- point 3 of s2;

\end{mplibcode}
$$
\vfill
\contrib{Leonardo da Vinci (1452–1519)}

\section{The Pythagorean theorem X}

\vfill
$$
\begin{mplibcode}
    path c, t, s[];
    c = fullcircle scaled 2/5 \mpdim\textwidth;
    t = point 4 of c -- point 0 of c -- point 2.5 of c -- cycle;
    vardef square_on(expr a,b) = 
     unitsquare scaled abs(a-b) rotated angle (a-b) shifted b
    enddef;
    for i=1 upto 3:
    s[i] = square_on(point i-1 of t, point i of t);
    endfor
    z2 = whatever [ point 3 of s3, point 2 of s2 ]
    = whatever [ point 3 of s1, point 4 of s1 ] ;
    z3 = whatever [ point 3 of s3, point 2 of s2 ]
    = whatever [ point 1 of s1, point 2 of s1 ] ;
    path p[];
    p21 = point 0 of s2 -- point 1 of s2 -- z2 -- cycle;
    p22 = point 1 of s2 -- point 2 of s2 -- z2 -- cycle;
    p23 = p21 rotatedabout(center s2, 180);
    p24 = p22 rotatedabout(center s2, 180);
    p31 = point 3 of s3 -- point 4 of s3 -- z3 -- cycle;
    p32 = point 0 of s3 -- point 1 of s3 -- z3 -- cycle;
    p33 = p31 rotatedabout(center s3, 180);
    p34 = p32 rotatedabout(center s3, 180);

    color f[];
    f21 = f23 =  Greens[7][3]; f22 = f24 =  Greens[7][2];
    f31 = f33 = Oranges[7][3]; f32 = f34 = Oranges[7][2];

    picture m[];
    forsuffixes $=21,22,23,24,31,32,33,34:
     m$ = image(fill p$ withcolor f$; draw p$);
     draw m$;
    endfor
    
    draw m21 shifted (point 3 of s1 - point 1 of s2);
    draw m23 shifted (point 1 of s1 - point 3 of s2);

    draw m32 shifted (point 2 of s1 - point 0 of s3);
    draw m34 shifted (point 0 of s1 - point 2 of s3);

    draw m22 shifted -point 1 of s2 rotated 90 shifted point 3 of s1;
    draw m24 shifted -point 3 of s2 rotated 90 shifted point 1 of s1;
    
    draw m31 shifted -point 0 of s3 rotated 90 shifted point 0 of s1;
    draw m33 shifted -point 2 of s3 rotated 90 shifted point 2 of s1;

\end{mplibcode}
$$
\vfill
\contrib{J.\@ E.\@ Böttcher}

\section{The Pythagorean theorem XI}

\vfill
$$
\begin{mplibcode}
    numeric a,b,c,u;
    a = 1.0605; b = 1.414; c = a++b; 

    u = 72;
    path t[];
    t0 = (origin -- (a,0) -- (0,b) -- cycle) scaled u;
    t1 = t0 scaled a reflectedabout(up,down) rotated angle(b,a) rotated 90;
    t2 = t0 scaled b reflectedabout(up,down) rotated angle(b,a); 
    t3 = t0 scaled c reflectedabout(up,down);
    
    t0 := t0 shifted - point 1/2 of t0;
    t1 := t1 shifted (1/2u*(a-6,-6)-point 2 of t1);
    t2 := t2 shifted (1/2u*(a-6,-6)-point 1 of t2);
    t3 := t3 shifted (point 2 of t1 reflectedabout(up,down));


    fill t0 withcolor Blues[7][1]; draw t0;
    fill t1 withcolor Blues[7][2]; draw t1;
    fill t2 withcolor Blues[7][3]; draw t2;
    fill t3 withcolor Blues[7][4]; draw t3;

    label.bot("$a$", point 1/2 of t0);
    label.lft("$b$", point 5/2 of t0);
    label.urt("$c$", point 3/2 of t0);
    
    label.bot ("$ac$",  point 1/2 of t3);
    label.rt  ("$bc$",  point 5/2 of t3);
    label.ulft("$c^2$", point 3/2 of t3);
    
    label.urt ("$a^2$", point 1/2 of t1);
    label.ulft("$ab$",  point 5/2 of t1);
    label.bot ("$ac$",  point 3/2 of t1);
    
    label.lft ("$bc$",  point 3/2 of t2);
    label.urt ("$b^2$", point 5/2 of t2);

    vardef centroid(expr t) = 2/3[point 2 of t, point 1/2 of t] enddef;
    path a[];
    for i=1 upto 3:
     a[i] = centroid(t0) -- centroid(t[i]) if i=1: shifted 20 right fi
     cutbefore fullcircle scaled 1.7u shifted centroid(t0)
     cutafter  fullcircle scaled 1.7u shifted centroid(t[i]);
     drawarrow a[i] withcolor 2/3 blue;
    endfor

    label.lrt ("${}\times a$", point 1/2 of a1);
    label.ulft("${}\times b$", point 1/2 of a2);
    label.urt ("${}\times c$", point 1/2 of a3);


\end{mplibcode}
$$
\vfill
\contrib{Frank Burk}

\section{The Pythagorean theorem XII}

\vfill
$$
\begin{mplibcode}
    numeric a,b,r,u;
    u = 1cm; r = 21;
    a = 3u-r;
    b = 3u+r;
    path t[];
    for i=0 upto 3:
     t[i] = ((-a-b,a-b) -- (-a,a-2b) -- (-a,a) -- cycle) rotated 90i;
    endfor
    fill for i=0 upto 3: point 0 of t[i] -- endfor cycle withcolor Oranges[7][3];
    fill for i=0 upto 3: point 2 of t[i] -- endfor cycle withcolor Oranges[7][1];
    for i=0 upto 3:
     fill t[i] withcolor Oranges[7][2]; draw t[i];
    endfor
    draw for i=0 upto 3: point 0 of t[i] -- endfor cycle dashed evenly;
    
    draw subpath(1,2) of t0 -- point 2 of t3 -- cycle withpen pencircle scaled 1.2;
    label.bot("$a$", 1/2[point 2 of t0, point 2 of t3]);
    label.urt("$b$", 1/2[point 2 of t0, point 1 of t0]);
    label.ulft("$c$", 1/2[point 1 of t0, point 2 of t3]);

\end{mplibcode}
$$
\bigskip
$$
a^2+b^2=c^2
$$
\vfill
\contrib{Poo-Sung Park}

\section{A generalization from Pythagoras}

The sum of the area of two squares, whose sides are the lengths of two 
diagonals of a parallelogram, is equal to the sum of the area of four
squares, whose sides are its four sides.

\bigskip

$$
\begin{mplibcode}
% DUMP squash=True
numeric a,b,r; a = 42; b = 26; r = 13; -z0 = z2 = (b,0) rotated r; -z1 = z3 = (0,a); 
z4 = z3 rotatedabout(z0,-90); z5 = z0 rotatedabout(1/2[z4,z2],180);

path p,h; p = z0--z1--z2--z3--cycle; h = z0--z2--z3--cycle;
path v,b; v = z0--z1--z3--cycle; b = z0--z4--z5--z2--cycle;

picture whole, cut_v, cut_h, fit, twos, fours;
whole = image(fill p withcolor background; draw p);
def fd(expr p, shade) = fill p withcolor shade; draw p enddef;
color s; s = Oranges 6 2; 
cut_v = image(fd(h shifted 2 up, s); fd(h rotated 180 shifted 2 down, s));
s := Greens 6 2; 
cut_h = image(fd(v shifted 2 left, s); fd(v rotated 180 shifted 2 right, s));

input mark_equal
fit = image(fd(h, Oranges[6][2]); fd(b, Blues[6][2]);
  mark_equal(z0,z2,1); mark_equal(z4,z5,1);
  mark_equal(z0,z3,2); mark_equal(z0,z4,2); mark_equal(z2,z5,2);
  draw unitsquare scaled 3 rotated angle(z4-z0) shifted z0);

path hh[], vv[];
hh1 = h rotatedabout(z0,-90); hh2 = h rotated 180 shifted 1/2[z4,z5];
vv1 = v shifted -z0 rotated -90 shifted point 2 of hh2;
vv2 = v shifted -z0 shifted point 2 of vv1;
vv3 = v shifted -z3 rotated 90 shifted point 1 of vv1;
hh3 = h shifted -z3 shifted point 0 of vv3;
fours = image(
 fd(b, Blues[6][2]);
 fd(hh1, Oranges[6][2]); fd(hh2, Oranges[6][2]); fd(hh3, Oranges[6][2]);
 fd(vv1, Greens[6][2]); fd(vv2, Greens[6][2]); fd(vv3, Greens[6][2]);
 draw point 2 of b -- point 2 of vv2;
 draw point 1 of hh3 -- point 1 of vv2;
 draw point 0 of hh3 
   -- point 2 of hh3 reflectedabout(point 0 of hh3, point 2 of vv3) 
   -- point 2 of vv3 -- point 1 of hh1);
path hh[], vv[];
hh1 = h; hh2 = h rotatedabout(point 2 of h, 180);
hh3 = h shifted point 1/2 of hh2 rotatedabout(point 0 of hh2,-90); 
vv1 = v shifted -z0 shifted point 2 of h;
vv2 = v shifted -z1 rotated -90 shifted point 2 of vv1;
vv3 = v shifted -z3 rotated 90 shifted z2;

twos = image(
 fd(b, Blues[6][2]);
 fd(hh1, Oranges[6][2]); fd(hh2, Oranges[6][2]); fd(hh3, Oranges[6][2]);
 fd(vv1, Greens[6][2]); fd(vv2, Greens[6][2]); fd(vv3, Greens[6][2]);
 draw point 1 of vv3 -- point 2 of vv2;
 draw point 1 of hh2 
   -- point 0 of hh2 reflectedabout(point 1 of hh2, point 0 of hh3)
   -- point 0 of hh3);
numeric i; i = -1;
forsuffixes $=cut_v, whole, cut_h, fit: draw $ shifted 72(incr i, 0); endfor
draw (0,-70) -- (252,-70);
numeric dy; dy = ypart (lrcorner fours - lrcorner twos);
draw fours shifted (0, -100); draw twos shifted (184,dy-100);

\end{mplibcode}
$$

\bigskip

\noindent
\textsc{Corollary}: The Pythagorean theorem (when the parallelogram is a 
rectangle).
\vfill
\contrib{David S.\@ Wise}



\section{A theorem of Hippocrates of Chios (circa 440 \textsc{BC})}

The combined area of the lunes constructed on the legs of a given right
angle triangle is equal to the area of the triangle.

\bigskip 

$$
\begin{mplibcode}
    path T; T = origin -- 90 right -- 120 up -- cycle;
    path s[];
    vardef semicircle(expr a,b) = halfcircle zscaled (a-b) shifted 1/2[a,b] enddef;
    s0 = semicircle(point 2 of T, point 1 of T);
    s1 = semicircle(point 2 of T, point 0 of T);
    s2 = semicircle(point 0 of T, point 1 of T);
    s3 = semicircle(point 1 of T, point 2 of T);
    
    path A[], S[], L[];
    for i=1 upto 3: A[i] = s[i] -- cycle; endfor
    S1 = s0 cutafter origin -- cycle;
    L1 = s0 cutafter origin .. reverse s1 .. cycle;
    S2 = s0 cutbefore origin -- cycle;
    L2 = s0 cutbefore origin .. reverse s2 .. cycle;

    picture part[];  color f; f = 7/8[blue,white];
    part1 = image(
      draw unitsquare scaled 5 withcolor 1/2 white; 
      forsuffixes $=1, 2, 3:
        fill A$ withcolor f; draw A$;
        label("$A_" & str $ & "$", 1/2[point 2 of A$, point 9/2 of A$]);
      endfor
      label("$T$", 2/3 point 3/2 of T);
    );
    part2 = image(
     fill T withcolor f; 
     draw unitsquare scaled 5 withcolor 1/2[f,black];
     draw T;
     fill L1 withcolor f; draw L1;
     fill L2 withcolor f; draw L2;
     label("$T$", 2/3 point 3/2 of T);
     label("$L_1$", point 6 of L1 shifted 18 right);
     label("$L_2$", point 5 of L2 shifted 16 up);
     label("$S_1$", point 5/2 of T shifted 12 left);
     label("$S_2$", point 1/2 of T shifted  8 down);
    );
    part3 = image(
     fill T withcolor f; 
     draw unitsquare scaled 5 withcolor 1/2[f,black];
     draw T;
     L1 := L1 shifted (-180,0); fill L1 withcolor f; draw L1;
     L2 := L2 shifted (-140,50); fill L2 withcolor f; draw L2;
     label("$T$", 2/3 point 3/2 of T);
     label("$L_1$", point 6 of L1 shifted 18 right);
     label("$L_2$", point 5 of L2 shifted 16 up);
     label("$+$", (-170,50));
     label("$=$", (-24,50));
    );

    draw part1 shifted 120 left; draw part2 shifted 120 right;
    draw part3 shifted (120,-200);
\end{mplibcode}
$$
\bigskip
$$
\vbox{\openup6pt\halign{\hfil$#$&${}=#$\hfil\cr
A_1 + A_2 & A_3 \cr
(L_1 + S_1) + (L_2 + S_2) & T + S_1 + S_2 \cr
L_1 + L_2 & T \cr
}}
$$
\vfill
\contrib{Eugene A.\@ Margerum and Michael M.\@ McDonnell}
%----------------------------------------------------
\section{The area of a right triangle with acute angle $\pi/12$}

The area of a right triangle is $\frac18(\hbox{hypotenuse})^2$ if and
only if one acute angle is $\pi/12$.

\vfill 

$$
\begin{mplibcode}
    path s;
    s = unitsquare scaled 3/4 \mpdim\textwidth;
    z1 = point 1 of s rotated 30;
    z2 = 1/2[z1,point 1 of s];
    fill origin--point 1 of s--z2--cycle withcolor 7/8[red,white];
    draw unitsquare scaled 7 rotated 195 shifted z2;
    draw s;
    draw subpath(0,1) of s shifted point -1/4 of s;
    draw subpath(0,1) of s shifted point -1/2 of s;
    draw subpath(0,1) of s shifted point -3/4 of s;

    draw origin--z2;
    for i=0 upto 3: draw z1--point i of s; endfor
    label.bot("$c$", point 1/2 of s);
    label.lft("$c$", point 7/2 of s);
    label.lrt("$c$", 3/4 [point 0 of s, z1]);
    label.urt("$c$", 3/4 [point 3 of s, z1]);
    for t=9/8 step 1/4 until 2:
    label.rt("$c/4$", point t of s);
    endfor

\end{mplibcode}
$$
\vfill
\contrib{Klara Pinter}

\section{A right angle inequality}

\vfill

Let $c$ be the hypotenuse of a right triangle whose other two sides are $a$ and $b$.
Prove that 
$$a+b\le\sqrt2c.$$  
When does equality hold?

\bigskip

$$
\begin{mplibcode}
vardef make_fig(expr wd,r) = 
    save a,b; a+b = wd; b-a=r;
    save s,t; path s,t; 
    s = unitsquare scaled (a+b);
    t = (b,0) -- (a,a+b) -- (0,a) -- cycle;
    image(
        fill t withcolor 7/8 [ blue, white ];
        draw t;
        draw s;
        label.top("$a$", (1/2 a, a+b));
        label.lft("$a$", (0, 1/2 a));
        label.bot("$b$", (1/2 b, 0));
        label.lft("$b$", (0, a + 1/2 b));
        label.lrt("$c$", point 3/2 of t);
        label.urt("$c$", point 5/2 of t);
        label.rt("$\sqrt2c$", point 1/2 of t);
    )
enddef;
numeric u; u = 2/11 \mpdim\textwidth;
picture p[];
p1 = make_fig(2u,30) shifted (-3/2u,0);
p2 = make_fig(2u, 0) shifted (+3/2u,0);
draw p1; label.bot("$a+b\le\sqrt2c$",        point 1/2 of bbox p1 shifted 8 down);
draw p2; label.bot("$a+b=\sqrt2c \iff a=b$", point 1/2 of bbox p2 shifted 8 down);

\end{mplibcode}
$$
\vfill
\contrib{Canadian Mathematical Olympiad 1969}

\section{The inradius of a right triangle}

\vfill

$$
\begin{mplibcode}
input arrow_label
numeric a, b, c, r, u; u = 32; a = 3u; b = 4u; c = a++b; 2r = a + b - c;
path s; s = fullcircle scaled (a+b-c) shifted (r, r);
path t; t = origin -- (b,0) -- (0,a) -- cycle;
picture P[];
P1 = image(
  drawarrow center s -- point 3.14 of s; label.urt("$r$", 1/2 point 3.14 of s);
  drawdot center s withpen pencircle scaled 2; draw s withcolor 2/3 red; 
  draw unitsquare scaled 4 withcolor 3/4; draw t;
  label.lft("$a$", point 5/2 of t); 
  label.bot("$b$", point 1/2 of t);
  label.urt("$c$", point 3/2 of t));
P2 = btex \vbox to 72pt{\openup8pt\halign{#\hfil\quad&$\displaystyle #$\cr
  I.&r=\frac{ab}{a+b+c}\cr   II.&r=\frac{a+b-c}{2}\cr}\vss} etex;
P3 = btex \hbox to \textwidth{\hbox to 12pt{\hss I.}\quad $ab = r(a+b+c)$\hss} etex;
P4 = image( % this one is re-used in P5 and P8
  path p[]; p0 = unitsquare scaled r; % split the triangle into 5 parts
  p1 = (0,r)--(r,r)--(0,a)--cycle; p2 = p1 reflectedabout((r,r),(0,a));
  p3 = (r,0)--(b,0)--(r,r)--cycle; p4 = p3 reflectedabout((r,r),(b,0));
  numeric i; i = -1;
  for t=1,3,3,2,2: fill p[incr i] withcolor Greens[7][t]; draw p[i]; endfor
  draw s withcolor 1/2[2/3 red,white]);
P5 = image(
  draw P4; draw P4 rotatedabout(point 3/2 of t, 180) shifted (b/a,1);    
  label.lft("$a$", point 5/2 of t); label.bot("$b$", point 1/2 of t));
P6 = image(
  fill unitsquare xscaled (a-r) yscaled r shifted (     0,0) withcolor Greens[7][3];
  fill unitsquare xscaled (r)   yscaled r shifted (   a-r,0) withcolor Greens[7][1];
  fill unitsquare xscaled (b-r) yscaled r shifted (     a,0) withcolor Greens[7][2];
  fill unitsquare xscaled (r)   yscaled r shifted ( a+b-r,0) withcolor Greens[7][1];
  fill unitsquare xscaled (a-r) yscaled r shifted (   a+b,0) withcolor Greens[7][3];
  fill unitsquare xscaled (b-r) yscaled r shifted (2a+b-r,0) withcolor Greens[7][2];

  draw (0,0)--(a+b+c,0); draw (0,r)--(a+b+c,r);
  draw   (0,0)--  (0,r)--   (a-r,0)--   (a-r,r);
  draw   (a,0)--  (a,r)-- (a+b-r,0)-- (a+b-r,r);
  draw (a+b,0)--(a+b,r)--(2a+b-r,0)--(2a+b-r,r)--(a+b+c,0)--(a+b+c,r);
   
  interim ahangle := 20;
  arrow_label((0,r),(0,0),"$r$", 10);
  arrow_label((0,0),(a-1/2,0),"$a$", 10);
  arrow_label((a+1/2,0),(a+b-1/2,0),"$b$", 10);
  arrow_label((a+b+1/2,0),(a+b+c,0),"$c$", 10));
% DUMP capture=False, replace="%... and so on to define the other pictures"
P7 = btex \hbox to \textwidth{\hbox to 12pt{\hss II.}\quad $c = a+b-2r$\hss} etex;
P8 = image(
    draw P4;
    label.lft("$r$",   1/2(0,r));
    label.bot("$r$",   1/2(r,0));
    label.lft("$a-r$", 1/2(0,a+r));
    label.urt("$a-r$", 1/2(0,a+r) reflectedabout((r,r),(0,a)));
    label.bot("$b-r$", 1/2(b+r,0));
    label.urt("$b-r$", 1/2(b+r,0) reflectedabout((r,r),(b,0)));
);
% DUMP capture=True
label.lft(P1, 24 left); label.rt(P2, 24 right);
label.bot(P3, point 1/2 of bbox currentpicture shifted 36 down);
label.bot(P5, point 1/2 of bbox currentpicture shifted  9 down);
label.bot(P6, point 1/2 of bbox currentpicture shifted  9 down);
label.bot(P7, point 1/2 of bbox currentpicture shifted 36 down);
label.bot(P8, point 1/2 of bbox currentpicture shifted  9 down);
\end{mplibcode}
$$
\vfill
\contrib{Liu Hui (3rd century \textsc{a.d.})}
%----------------------------------------------------------------------
\section{The product of the perimeter of a triangle and its inradius is 
twice the area of the triangle I}

\vfill
$$
\begin{mplibcode}
  numeric r, p[]; path c; 
  r = 89; 
  c = fullcircle scaled 2r; 
  p0 = 0.518; p1 = 6; p2 = 2.9;  % points round c
  
  pair v[], t[];  % vertexes and tangents
  for i = 0, 1, 2: j := (i+1) mod 3;
    t[i] = point p[i] of c;
    v[i] = whatever[precontrol p[i] of c, postcontrol p[i] of c]
         = whatever[precontrol p[j] of c, postcontrol p[j] of c];
  endfor
  
  for i = 0, 1, 2: j := (i+1) mod 3; 
    pair a; a = whatever[v[i], t[i]] = whatever * (v[i]-t[j]);
    pair b; b = whatever[v[i], t[j]] = whatever * (v[i]-t[i]);
    label("\small " & decimal (i+1), 1/3(a - t[j] + 2v[i])); 
    label("\small " & decimal (i+1), 1/3(b - t[i] + 2v[i])); 
    label("\small " & decimal (i+1), 1/3(a + t[i])); 
    label("\small " & decimal (i+1), 1/3(b + t[j])); 
    fill origin -- a -- v[i] -- b -- cycle withcolor Oranges 8 2;
    draw subpath (p[i] if i=0: + 8 fi, p[j]) of c withcolor Oranges 8 4;
    draw origin -- v[i]-t[i] -- v[i] -- v[i]-t[j] -- cycle withpen pencircle scaled 1/4;
    draw origin -- t[i] -- v[i] -- t[j] withpen pencircle scaled 3/4;
  endfor

  label.bot(btex \small 
    \textsc{Note}: \textit{Triangles marked with the same number are equal in area}. 
    etex, point 1/2 of bbox currentpicture shifted 32 down);
\end{mplibcode}
$$
\vfill
\contrib{Grace Lin}

%----------------------------------------------------------------------
\section{The product of the perimeter of a triangle and its inradius is 
twice the area of the triangle II}

\vfill
$$
\begin{mplibcode}
numeric r, a[]; path c; pair v[]; path t[];
r = 37; c = fullcircle scaled 2r; a0 = 0.518; a1 = 6; a2 = 2.9;
for i = 0, 1, 2: j := (i+1) mod 3;
v[i] = whatever[precontrol a[i] of c, postcontrol a[i] of c]
     = whatever[precontrol a[j] of c, postcontrol a[j] of c];
endfor
for i = 0, 1, 2: j := (i+1) mod 3;
  t[2i+1] = origin -- point a[i] of c -- v[i] -- cycle;
  t[2i+2] = origin -- point a[j] of c -- v[i] -- cycle;
endfor
picture P[];
P1 = image(
  for i=1 upto 6: fill t[i] withcolor Blues[7][ceiling (i/2)]; draw t[i]; endfor
  draw c withcolor Blues 7 7;
  label.bot("$a$", 1/2[v0, v1]); label.ulft("$b$", 1/2[v1, v2]); 
  label.urt("$c$", 1/2[v2, v0]); label.rt("$r$", 1/2 point a1 of c);
);

numeric rotb, rotc;
3 rotb = 180 - angle(v2 - v1); 3 rotc = - angle(v2 - v0);
forsuffixes $=4,5: t$ := t$ rotatedabout(v1, rotb); endfor
forsuffixes $=6,1: t$ := t$ rotatedabout(v0, rotc); endfor
P2 = image(
  for i=1 upto 6: fill t[i] withcolor Blues[7][ceiling (i/2)]; draw t[i]; endfor
  drawarrow subpath (0,1) of fullcircle scaled 2r 
      rotated (angle (point 1 of t5 - v1) + 6) shifted v1;
  drawarrow subpath (8,7) of fullcircle scaled 2r 
      rotated (angle (point 1 of t6 - v0) - 6) shifted v0;
);

forsuffixes $=4,5: t$ := t$ rotatedabout(v1, 2 rotb); endfor
forsuffixes $=6,1: t$ := t$ rotatedabout(v0, 2 rotc); endfor
P3 = image(for i=1 upto 6: fill t[i] withcolor Blues[7][ceiling (i/2)]; draw t[i]; endfor);

P4 = image(drawarrow (up--down) scaled 5/8 r; unfill fullcircle scaled 15;
   label("\large ${}\times2$", origin));

input arrow_label
forsuffixes $=1,2,3,4,5,6: t$ := t$ rotatedabout(point -1/2 of t$, 180); endfor
P5 = image(
  draw P3; for i=1 upto 6: fill t[i] withcolor Blues[7][ceiling (i/2)]; draw t[i]; endfor
  arrow_label(point 1 of t5, point 0 of t5, "$r$", 8);
  arrow_label(point 0 of t5, point 0 of t4, "$b$", 8);
  arrow_label(point 0 of t3, point 0 of t2, "$a$", 8);
  arrow_label(point 0 of t1, point 0 of t6, "$c$", 8);
);
draw P1;
draw P2 shifted 180 down;
draw P3 shifted 260 down;
draw P4 shifted 328 down;
draw P5 shifted 360 down;
\end{mplibcode}
$$
\vfill

\section{Four triangles with equal area}

\vfill
$$
\begin{mplibcode}
path t[], s[];
t0 = origin -- 72 right -- 48 right rotated 64 -- cycle;
for i=1 upto 3:
  s[i] = unitsquare zscaled (point i-1 of t0 - point i of t0) shifted point i of t0;
endfor
for i=1 upto 3:
  t[i] = point i of t0 -- point -1 of s[i] -- point 2 of s[i mod 3 + 1] -- cycle;
endfor
def fd(expr p, shade) = fill p withcolor shade; draw p; enddef;
def do_arrow(expr n) = 
  drawarrow point 2/3 of t[n] .. point (3n-2)/3 of t0 {direction (2n-1)/2 of t0 rotated 90}
    cutbefore fullcircle scaled 10 shifted point 2/3 of t[n]
    cutafter  fullcircle scaled 10 shifted point (3n-2)/3 of t0; enddef;

picture P[];
P0 = image(fd(t0, Oranges 7 4));
P1 = image(draw P0; for i=1 upto 3: draw s[i]; endfor);
P2 = image(draw P1; for i=1 upto 3: fd(t[i], Oranges[7][i]); endfor);
P3 = image(draw P0; for i=1 upto 3: fd(t[i], Oranges[7][i]); do_arrow(i); endfor);
P4 = image(
  draw P0; 
  for i=1 upto 3: 
    fd(t[i] rotatedabout(point i of t0, -90), Oranges[7][i]);
  endfor
);

draw P0; 
draw P1 shifted (140,-20);
draw P2 shifted (300,-40);

draw P3 shifted ( 60,-240);
draw P4 shifted (280,-280);
\end{mplibcode}
$$
\vfill
\contrib{Steven L.\@ Snover}

\section{The triangle of medians has 3/4 the area of the original triangle}

\vfill
$$
\begin{mplibcode}
numeric a,b;
a = 3/4 b; b = 1/3 \mpdim\textwidth;
z0 = origin; z1 = (b,0); z2 = (0,a) rotated -27;
z3 = 1/2[z1,z2];
z4 = 1/2[z2,z0];
z5 = 1/2[z0,z1];
path t[], s[], m[];
t0 = z0--z1--z2--cycle;
t1 = z0--z4--z5--cycle;
s1 = z0--z1;
s2 = z1--z2;
s3 = z0--z2;
m1 = z0--z3;
m2 = z1--z4;
m3 = z2--z5;

picture P[];
P1 = image(
    for i=1 upto 3: draw m[i] dashed evenly; endfor
    draw t0;
    label.bot("$a$", point 5/8 of t0);
    label.urt("$b$", point 13/8 of t0);
    label.ulft("$c$", point 21/8 of t0);

    label.lrt("$m_b$", point 1/3 of m1);
    label.llft("$m_c$", point 1/3 of m2);
    label.rt("$m_a$", point 1/3 of m3);
);
P2 = image(
    forsuffixes $=0,3,4,5:
        fill t1 shifted z$ withcolor 7/8[blue,background];
    endfor
    draw s1 shifted z2 dashed withdots scaled 1/2;
    draw s3 shifted z1 dashed withdots scaled 1/2;
    draw m3 & m1 shifted z5 -- cycle dashed evenly;
    draw t0;
    label.lft("$m_a$", point 2/3 of m3);
    label.lrt("$m_b$", point 2/3 of m1 shifted z5);
    label.urt("$m_c$", 2/3[point 1 of m1 shifted z5, z2]);

);

draw P1 shifted (-2/3b,0);
draw P2 shifted (2/3b,0);
\end{mplibcode}
$$
\bigskip
$$
{3\over4}
\mathop{\hbox{area}}(\triangle abc) = 
\mathop{\hbox{area}}(\triangle m_a m_b m_c)
$$
\vfill
\contrib{Norbert Hungerbühler}

\section{Heptasection of a triangle}

If the one-third points on each side of a triangle are joined to
opposite vertices, the resulting central triangle is equal in area
to one-seventh that of the initial triangle.

\bigskip
$$
\begin{mplibcode}
% DUMP squash=True
color s[];  s1 = Greens 7 3; s2 = Blues 7 3; s3 = Oranges 7 3;

z1 = origin;
z2 = 184 right rotated 4;
z3 = 116 right rotated 64;

z12 = 2/3[z1, z2];
z23 = 2/3[z2, z3];
z31 = 2/3[z3, z1];

z4 = whatever [z1, z23] = whatever [z2, z31];
z5 = whatever [z2, z31] = whatever [z3, z12];
z6 = whatever [z3, z12] = whatever [z1, z23];

z7 = whatever [z3, z1]; z7-z4 = whatever * (z3-z6);
z8 = whatever [z1, z2]; z8-z5 = whatever * (z1-z4);
z9 = whatever [z2, z3]; z9-z6 = whatever * (z2-z5);

picture P[];

P1 = image(
  fill z1--z6--z3--cycle withcolor s1;
  fill z2--z4--z1--cycle withcolor s2;
  fill z3--z5--z2--cycle withcolor s3;
  draw z1--z2--z3--cycle;
  draw z1--z23; draw z2--z31; draw z3--z12;
);
P2 = image(
  draw P1;
  draw z4--z7; draw z5--z8; draw z6--z9;
);

P31 = image(fill z1--z4--z7--cycle withcolor s1; draw z31--z4--z7--z1--z4;);
P32 = image(fill z2--z5--z8--cycle withcolor s2; draw z12--z5--z8--z2--z5;);
P33 = image(fill z3--z6--z9--cycle withcolor s3; draw z23--z6--z9--z3--z6;);
for p = (3, -87), (4, -180): numeric n, r; (n, r) = p;
  P[n] = image(
    fill z6--z3--z7--z4--cycle withcolor s1; draw z6--z3--z7--z4--cycle;
    fill z4--z1--z8--z5--cycle withcolor s2; draw z4--z1--z8--z5--cycle;
    fill z5--z2--z9--z6--cycle withcolor s3; draw z5--z2--z9--z6--cycle;
    draw P31 rotatedabout(z7, r);
    draw P32 rotatedabout(z8, r);
    draw P33 rotatedabout(z9, r);
  );
endfor
z101 = z4 rotatedabout(z7, 180);
z102 = z5 rotatedabout(z8, 180);
z103 = z6 rotatedabout(z9, 180);

P5 = image(
  fill z4--z6--z3--z101--cycle withcolor s1; draw z101--z6--z4--z101--z3--z6;
  fill z5--z4--z1--z102--cycle withcolor s2; draw z102--z4--z5--z102--z1--z4;
  fill z6--z5--z2--z103--cycle withcolor s3; draw z103--z5--z6--z103--z2--z5;
);

draw P1 shifted 120(-.9,+1.1); draw P2 shifted 120(+1,+1.1);
draw P3;
draw P4 shifted 120(-.9,-1.1); draw P5 shifted 120(+1,-1.1);

\end{mplibcode}
$$
\vfill
\contrib{William Johnston and Joe Kennedy}

\section{A Golden Section problem from the \textit{Monthly}}

(Problem E3007, \textit{American Mathematical Monthly}, 1983, p.482)

\smallskip\noindent
Let $A$ and $B$ be the midpoints of the sides $EF$ and $ED$ of an
equilateral triangle $DEF$.  Extend $AB$ to meet the circumcircle
(of $DEF$) at $C$.  Show that $B$ divides $AC$ according to the 
golden section.

\bigskip\noindent
\textsc{Solution}:
$$
\begin{mplibcode}
pair A, B, C, D, E, F, G;
E = 144 up;
F = E rotated 120;
D = F rotated 120;
A = 1/2[E, F];
B = 1/2[D, E];
path circ; circ = fullcircle scaled 2 abs E;
(whatever, t) = (A -- 2[A,B]) intersectiontimes circ;
C = point t of circ; G = point 4-t of circ;

vardef angle_mark@#(expr a, o, b) = 
  fullcircle scaled @# rotated angle (a-o) shifted o cutafter (o--b)
enddef;

draw angle_mark 28 (G, C, D) withcolor Blues 8 7;
draw angle_mark 28 (G, E, D) withcolor Blues 8 7;
draw angle_mark 28 (C, D, E) withcolor Purples 8 7;
draw angle_mark 24 (C, D, E) withcolor Purples 8 7;
draw angle_mark 28 (C, G, E) withcolor Purples 8 7;
draw angle_mark 24 (C, G, E) withcolor Purples 8 7;

draw circ withcolor Reds 8 7;
draw D--E--F--cycle;  draw E--G--C--D;

dotlabel.ulft("$A$", A);
dotlabel.urt ("$B$", B);
dotlabel.urt ("$C$", C);
dotlabel.lrt ("$D$", D);
dotlabel.top ("$E$", E);
dotlabel.llft("$F$", F);
dotlabel.lft ("$G$", G);

label.llft("$\tau$", 1/2[E,B]);
label.llft("$\tau$", 1/2[D,B]);
label.bot ("$\tau$", 1/2[A,B]);
label.bot ("$1$", 1/2[A,G]);
label.bot ("$1$", 1/2[B,C]);

\end{mplibcode}
$$
\medskip
$$\tau^2=\tau+1$$
\vfill
\contrib{Jan van de Craats}

\section{Tiling with squares and parallelograms}

If squares are constructed eternally on the sides of the parallelogram, their centres form a square.
\bigskip
$$
\begin{mplibcode}
path s[]; s0 = unitsquare shifted (-1/2, -1/2);
numeric a, n; a = 36; n=0;
for i=1 upto 4:
    for j=1 upto 4:
        s[incr n] = s0 scaled if not odd (i+j): 2a rotated -24.29519 else: a fi
            shifted (1.822875a * (i,j));   
        draw s[n];
    endfor
endfor

fill subpath (2,3) of s[6] -- subpath (0,1) of s[11] -- cycle withcolor Oranges 7 3;
draw subpath (2,3) of s[6] -- subpath (0,1) of s[11] -- cycle;

for i=0 upto 3:
    draw center s[4i+1] -- center s[4i+4] dashed evenly withcolor 2/3 blue;
    draw center s[i+1] -- center s[i+13] dashed evenly withcolor 2/3 blue;
endfor
forsuffixes @=6,7,10,11:
    fill fullcircle scaled 4 shifted center s@ withcolor Oranges 7 4;
    draw fullcircle scaled 4 shifted center s@;
endfor
currentpicture := currentpicture rotated 24.29519;
\end{mplibcode}
$$

\contrib{Alfinio Flores}

\section{The area of a quadrilateral I}

The area of a quadrilateral is less than or equal to half the
product of the lengths of its diagonals, with equality if and only 
if the diagonals are perpendicular.

\bigskip\noindent
I.\@ Convex quadrilaterals
$$
\begin{mplibcode}
% DUMP replace="%-- Convex picture ----------------------------------"
z0 = origin; z1 = (48, 60); z2 = (144, 0); z3 = (72, -64);

fill z0--z1--z2--z3--cycle withcolor Blues 6 2;
drawoptions(withpen pencircle scaled 1/4);
draw unitsquare scaled 5 rotated  90 shifted (x1,0);
draw unitsquare scaled 5 rotated -90 shifted (x3,0);
draw z0--z2;
draw (x1,0) -- z1 -- z3 -- (x3,0);
drawoptions();
draw z0--z1--z2--z3--cycle;

label.lft("$A$", z0);
label.top("$B$", z1);
label.rt ("$C$", z2);
label.bot("$D$", z3);
label.lft("$h$", (x1, 1/2 y1));
label.rt ("$k$", (x3, 1/2 y3));

label(btex \vbox{\openup3pt\halign{\hss#&${}#$\hfil\cr 
  Area&  ={1\over2}\overline{AC}\cdot(h+k)\cr
      &\le{1\over2}\overline{AC}\cdot\overline{BD}\cr
  }} etex, z2 shifted 100 right);

\end{mplibcode}
$$
\bigskip\noindent
II.\@ Concave quadrilaterals
$$
\begin{mplibcode}
% DUMP replace="%-- Concave picture ---------------------------------"
z0 = origin; z1 = (48, 120); z2 = (144, 0); z3 = (76, 42);

fill z0--z1--z2--z3--cycle withcolor Blues 6 2;
drawoptions(withpen pencircle scaled 1/4);
draw unitsquare scaled 5 shifted (x1,0);
draw unitsquare scaled 5 shifted (x3,0);
draw z0--z2;
draw (x1,0) -- z1 -- z3 -- (x3,0);
draw z3--(x1,y3) dashed withdots scaled 1/4;
drawoptions();
draw z0--z1--z2--z3--cycle;

label.lft("$A$", z0);
label.top("$B$", z1);
label.rt ("$C$", z2);
label.urt("$D$", z3);
label.lft("$h$", (x1, 1/2 y1));
label.lft("$k$", (x3, 1/2 y3));

label(btex \vbox{\openup3pt\halign{\hss#&${}#$\hfil\cr 
  Area&=  {1\over2}\overline{AC}\cdot(h-k)\cr
      &\le{1\over2}\overline{AC}\cdot\overline{BD}\cr
  }} etex, z2 shifted (100, 42));
\end{mplibcode}
$$

\vfill

\contrib{David B.\@ Sher, Ronald Skurnick, and Dean C.\@ Nataro}

\section{The area of a quadrilateral II}

The area of a quadrilateral $Q$ is equal to one-half the area of a
parallelogram $P$ whose sides are parallel to and equal in length
to the diagonals of $Q$.

\bigskip\noindent
I.\@ $Q$ convex
$$
\begin{mplibcode}
% DUMP replace="%-- Convex picture ----------------------------------"
path Q; Q = origin -- 72 dir 0 -- 62 dir 42 -- 48 dir 84 -- cycle;
pair s, t; s = point 1 of Q - point 3 of Q; t = point 2 of Q - point 0 of Q;
path box; box = origin -- s -- s+t -- t -- cycle;
picture qq, pp, grid;
qq = image(
  fill Q withcolor Oranges 7 2; draw Q;
  draw point 0 of Q -- point 2 of Q dashed evenly scaled 1/2;
  draw point 1 of Q -- point 3 of Q dashed evenly scaled 1/2;
  label.lft("$Q$:", (-5, 20));
);
pp = image(
  path A, B; A = Q; B = A shifted s;
  fill A withcolor Oranges 7 2; draw A;
  fill B withcolor Oranges 7 2; draw B;
  clip currentpicture to box; 
  draw box dashed evenly scaled 1/2;
  label.lft("$P$:", (-5, -4));
);
grid = image(
  for i = 0 upto 1:
    for j = 0 upto 1:
      pair o; o = i * (s-t) + j * (s+t); 
      path A, B; A = Q shifted o; B = A shifted s;
      fill A withcolor Oranges 7 2; draw A;
      fill B withcolor Oranges 7 2; draw B;
      draw point 0 of A -- point 1 of A shifted -t -- point 0 of B;
      draw point 2 of A -- point 1 of A shifted +t -- point 2 of B;
      draw box shifted point 0 of A dashed evenly scaled 1/2;
    endfor
  endfor
);
label.rt(qq, origin); label.rt(pp, 100 down);
label.rt(grid, point 3/2 of bbox currentpicture);
\end{mplibcode}
$$

\bigskip\noindent
II.\@ $Q$ concave
$$
\begin{mplibcode}
% DUMP capture=False, replace="% Concave picture is essentially the same, with a simpler grid"
path Q; Q = origin -- 32 dir 28 -- 81 dir -8 -- 62 dir 74 -- cycle;
pair s, t; s = point 3 of Q - point 1 of Q; t = point 2 of Q - point 0 of Q;
path box; box = origin -- s -- s+t -- t -- cycle;
picture qq, pp, grid;
qq = image(
  fill Q withcolor Greens 7 2;
  draw point 0 of Q -- point 2 of Q dashed evenly scaled 1/2;
  draw point 1 of Q -- point 3 of Q dashed evenly scaled 1/2;
  draw Q;
  label.lft("$Q$:", (-5, 20));
);
pp = image(
  path A, B; A = Q; B = A shifted -s;
  fill A withcolor Greens 7 2; draw A;
  fill B withcolor Greens 7 2; draw B;
  clip currentpicture to box; 
  draw box dashed evenly scaled 1/2;
  label.lft("$P$:", (-16, -4));
);
grid = image(
  for j=0 upto 2:
    for i=0 upto 2:
      pair o; o = i * s + j * t;
      fill Q shifted o withcolor Greens 7 2; draw Q shifted o;
      draw subpath(if j=0: 0 else: 1 fi, if i=0: 4 else: 3 fi) of box 
          shifted o dashed evenly scaled 1/2;
    endfor
  endfor);
label.rt(qq, origin); label.rt(pp, 100 down);
label.rt(grid, point 3/2 of bbox currentpicture);
\end{mplibcode}
$$
\medskip
$$\mathop{\hbox{area}}(Q)={1\over2}\mathop{\hbox{area}}(P)$$

\section{A square within a square}

If lines from the vertices of a square are drawn to the mid-points
of adjacent sides (as shown in the figure), then the area
of the smaller square so produced is one-fifth that of the given
square.

\bigskip
$$
\begin{mplibcode}
    path S, r, s, t;
    S = unitsquare scaled 144;
    s = for i=0 upto 3: 2/5[point i of S, point i+3/2 of S] -- endfor cycle;
    numeric h, w;
    h = abs(point 1 of s-point 0 of s);
    w = abs(point 2 of S-point 0 of S) +-+ h;
    r = unitsquare xscaled w yscaled h rotated angle (point 1 of s - point 0 of s);
    t = r rotatedabout(center s, 90);

    picture aa, bb;
    aa = image(
        fill s withcolor Blues 7 3;
        draw r; draw t;
        clip currentpicture to S;
        draw S;
    );
    bb = image(
        fill s withcolor Blues 7 3;
        draw r; draw t;
        for i=0 upto 3:
            draw subpath(i, i+1/2) of S dashed evenly;
            draw subpath(i+1/2, i+1) of S;
        endfor
    );
    draw aa;
    draw bb shifted 200 down;
    
\end{mplibcode}
$$

\section{Areas and perimeters of regular polygons}

The area of a regular $2n$-gon inscribed in a circle is equal to
one-half the radius of the circle times the perimeter of a 
regular $n$-gon similarly inscribed ($n\ge3$).

\bigskip
$$
\begin{mplibcode}
   numeric n; n = 5;
   path gon, gonn, circle;
   circle = for t=0 upto 2n-1: 144 right rotated (180/n*t) .. endfor cycle;
   gonn = for t=0 upto 2n-1: point t of circle -- endfor cycle; 
   gon = for t=0 upto n-1: point 1+2t of circle -- endfor cycle; 

    for i=1 upto 100:
        draw (down--200 up) rotated 42 shifted (8i,0) 
            withpen pencircle scaled 2
            withcolor Reds 8 3;
    endfor
    clip currentpicture to origin -- subpath(0,1) of gonn -- cycle;
    fill origin -- subpath(1,2n) of gonn -- cycle withcolor Reds 8 2;

    draw unitsquare scaled 5 shifted (xpart point 1 of gonn, 0);
    for i=0 upto 2n:
        draw origin -- point i of gonn;
    endfor 
    draw circle; draw gonn withcolor Reds 8 7; 

    label.lft("$r$", 1/2 point 3 of gonn);
    picture P; 
    P = thelabel.bot("$s_n$", 1/2 [point 1 of gon, point 0 of gon]);
    fill bbox P withcolor Reds 8 2; draw P;
    draw gon dashed evenly;

\end{mplibcode}
$$
\bigskip

$$
\vbox{\openup 6pt\halign{\hfil $\displaystyle #$&$\displaystyle{}=#$\hfil\cr
{1\over2n}\mathop{\hbox{area}}\left(P_{2n}\right)&{1\over2}\cdot r \cdot {1\over2}s_n\cr
\mathop{\hbox{area}}\left(P_{2n}\right)&{1\over2} r \cdot ns_n\cr
&{1\over2} r\cdot\mathop{\hbox{perimeter}}\left(P_n\right) \cr
}}$$

\bigskip\noindent
\textsc{Corollary} [Bh\=askara, \textit{Lit\=avati} (India, 12th century
\textsc{ad})]: The area of a circle is equal to one-half the product of its radius
and circumference.

\section{The area of a Putnam octagon}

(Problem B1, 39th Annual William Lowell Putnam Mathematical Competition, 1978).

\bigskip\noindent
Find the area of a convex octagon that is inscribed in a circle 
    and has four consecutive sides of length 3 units and the 
    remaining four sides of length 2 units.  Give the answer in the form
    $r+s\smash{\sqrt t}$, with $r$, $s$, and $t$ positive integers.

\bigskip\noindent
\textsc{Solution:}
$$
\begin{mplibcode}
path tt, ttt; numeric s; s = 100;
ttt = origin -- right scaled s -- right scaled s rotated 54 -- cycle;
tt = origin -- right scaled s -- right scaled s rotated 36 -- cycle;

picture TT, TTT;
TT = image(fill tt withcolor Oranges 7 3; draw tt withpen pencircle scaled 1/2);
TTT = image(fill ttt withcolor Greens 7 3; draw ttt withpen pencircle scaled 1/2);

picture Q, A;
Q = image(
    for t=-2 upto 1: 
        numeric a, b; 
        a = 54 t;
        b = 144-36t;
        draw TTT rotated a;
        draw TT  rotated b;
        if t=1:
            label("3", point 3/2 of ttt scaled 0.9 rotated a);
            label("2", point 3/2 of tt scaled 0.9 rotated b);
        fi
    endfor
    draw fullcircle scaled 2s;

);

A = image(
    for t=0 upto 3:
        draw TTT rotated (90t-27);
        draw TT  rotated (90t+27);
    endfor
    path S; S = unitsquare shifted -(1/2, 1/2) 
        scaled (2 abs (point 1 of ttt) +-+ abs(point 2 of ttt - point 1 of ttt));
    draw S;
    label.bot("$\sqrt2$", point 1/9 of S);
    label.bot("$\sqrt2$", point 8/9 of S);
    label.bot("$\phantom{\sqrt2}3$", point 1/2 of S);

    label.lft("$\sqrt2$", point 28/9 of S);
    label.lft("$\sqrt2$", point 35/9 of S);
    label.lft("$3$", point 7/2 of S);

);
draw Q;
draw A shifted (210, -180);
\end{mplibcode}
$$
\bigskip
$$A = \left(3+2\sqrt2\right)^2-4\cdot{1\over2}\left(\sqrt2\right)^2
= 9 + 6\sqrt2 + 6\sqrt2 + 8 - 4 
= 13 + 12\sqrt2$$

\section{A Putnam dodecagon}

(Problem I-1, 24th Annual William Lowell Putnam Mathematical 
Competition, 1963)

\medskip\noindent
\llap{(i)\ }Show that a regular hexagon, six squares, and six
equilateral triangles can be assembled without overlapping to form
a regular dodecagon.

\smallskip\noindent
\llap{(ii)\ }Let $P_1$, $P_2$, $\dots$, $P_{12}$ be the successive 
vertices of a regular dodecagon.  Discuss the intersection(s) of
the three diagonals $P_1P_9$, $P_2P_{11}$, and $P_4P_{12}$.

\bigskip\noindent
\textsc{Solution:}
$$
\begin{mplibcode}
path S, H, D;  numeric a; a = 48;
S = unitsquare shifted -(1/2, 1/2) scaled a 
               shifted (a/2*(sqrt(3)+1, 0)) rotated 68;
H = for t=0 upto 5: point 0 of S rotated -60t -- endfor cycle;
D = for t=0 upto 5: subpath (2,1) of S rotated -60t -- endfor cycle;

picture aa, bb;
aa = image(
    fill D withcolor Oranges 7 3; 
    fill H withcolor Blues 7 3;
    for t=0 upto 5:
        fill S rotated 60t withcolor Greens 7 3;
        draw S rotated 60t;
    endfor
    draw D;
    label("(i)", (xpart point 9 of D, ypart point 12 of D));
);
bb = image(
    fill S withcolor Greens 7 3; draw S;
    for t=1 upto 5:
        draw S rotated 60t dashed evenly;
    endfor
    draw point 1 of D -- point  9 of D withcolor 2/3 red;
    draw point 2 of D -- point 11 of D withcolor 2/3 red;
    draw point 4 of D -- point 12 of D withcolor 2/3 red;

    draw D;
    r = abs point 0 of D;
    for t=1 upto length D: 
        label("$P_{" & decimal t & "}$", point t of D scaled (1+10/r)); 
    endfor

    label("(ii)", (xpart point 9 of D, ypart point 12 of D));

);
draw aa shifted 120 left;
draw bb shifted (90, -120);
\end{mplibcode}
$$

\section{The area of a regular dodecagon}

A regular dodecagon with circumradius one has area three.

$$
\begin{mplibcode}
numeric r, s; r = 108; s = 2r * sind(15);

path D, S, T, T';
T = (r,0) -- (r,0) rotated 30 -- (s,0) rotated 15 -- cycle;
S = origin -- point 0 of T -- point 2 of T -- cycle;
D = for i=0 upto 11: point 0 of T rotated 30i -- endfor cycle;
T' = T reflectedabout(point 0 of T, point 1 of T);

picture base, first, second; 
base = image(
  drawoptions(dashed evenly scaled 1/2);
  numeric n; n = 0;
  for i=0 upto 11: 
    draw subpath (0,2) of T rotated 30i; 
    if i mod 3 = 1: 
        draw T' rotated 30i;  z[incr n] = point 2 of T' rotated 30i;
    fi
    draw S rotated 30i;
  endfor
  draw z1--z2--z3--z4--cycle;
  drawoptions();
);

first = image(
  fill origin -- subpath (0, 9) of D -- cycle withcolor Oranges 7 3;
  draw base;
  fill origin -- subpath (9, 12) of D -- cycle withcolor Greens 7 3;
  for i=9 upto 11:
    fill T rotated 30i withcolor Blues 7 3; draw T rotated 30i;
    draw S rotated 30i;
  endfor
  draw origin -- point 0 of T;
  draw D withpen pencircle scaled 1;
);
second = image(
  fill origin -- subpath (0, 9) of D -- cycle withcolor Oranges 7 3;
  draw base;
  for i=0 upto 2:
    fill T' rotated (30+90i) withcolor Blues 7 3;
    draw T' rotated (30+90i);
    fill S  rotated 90 shifted point 0 of T rotated 90i withcolor Greens 7 3;
    draw S  rotated 90 shifted point 0 of T rotated 90i;
    fill S  rotated 180 shifted z1 rotated 90i withcolor Greens 7 3;
    draw S  rotated 180 shifted z1 rotated 90i;
  endfor
  draw (x2,0) -- (x1,0) -- z1 -- z2 -- z3 -- (0, y3) -- (0,y1) withpen pencircle scaled 1;
);

draw first; draw second shifted ((2r+36) * down);
\end{mplibcode}
$$
\vfill
\contrib{J.\@ Kürshák}

\section{Fair allocation of a pizza}

\textsc{The Pizza Theorem}: If a pizza is divided into eight pieces by making 
cuts at 45° angles through an arbitrary point in the pizza, then the sums
of the areas of alternate slices are equal.

\bigskip\noindent
\textsc{Proof}:

$$
\begin{mplibcode}
path pizza; pizza = fullcircle scaled 200;
pair pp; pp = 64 right rotated 32;
numeric p[]; for i=0 upto 7:
  (p[i], whatever) = pizza intersectiontimes (origin -- 200 dir 45(i-1)) shifted pp;
endfor
path slice[]; for i=0 upto 7:
    numeric a, b; a = p[i]; b = p[(i+1) mod 8]; if b < a: b := b + 8; fi
    slice[i] = pp -- subpath (a, b) of pizza -- cycle;
    z[i] = 1/3(pp + point a of pizza + point b of pizza);
endfor

picture aa, bb;
aa = image(for i=0 upto 7:
  fill slice[i] withcolor YlOrRd 7 if odd i: 2 else: 4 fi; draw slice[i];
endfor);
bb = image(draw aa;
    label("$A$", z0); label("$a$", z0 reflectedabout(up, down));
    label("$b$", z1); label("$B$", z1 reflectedabout(up, down));
    label("$C$", z2); label("$c$", z2 reflectedabout(left, right));
    label("$d$", z3); label("$D$", z3 reflectedabout(left, right));
    draw slice[0] reflectedabout(up, down);
    draw slice[1] reflectedabout(up, down);
    draw slice[2] reflectedabout(left, right);
    draw slice[3] reflectedabout(left, right);

    for i=0 upto 3:
      z[10+i] = pp reflectedabout(dir 45i, -dir 45i);
    endfor
    
    path E, F, G;
    E = subpath(p[4], 4-p[2]) of pizza -- z12 -- (x11, y12) -- z11 -- cycle;
    draw subpath (-2, -1) of E; draw subpath (-3, -2) of E rotated 90;
    label("$E$", center E); label("$e$", center E rotated 90);
    
    F = pp -- pp reflectedabout(left, right) -- subpath(8-p[2], p[0]) of pizza -- cycle;
    draw F rotated -90;
    label("$f$", center F); label("$F$", center F rotated -90);

    G = pp -- z13 -- pp rotated -90 -- z10 -- cycle;
    draw subpath (2,3) of G reflectedabout(pp, z13);
    label("$G$", 2/3[z13, z10] shifted 15 up);
    label("$g$", 2/3[z13, z10] shifted 15 up reflectedabout(pp, z13));
    label("$\scriptstyle H$", 2/3[pp, point -3/2 of E] - (2,1) );
    label("$\scriptstyle h$", 2/3[pp, point -3/2 of E] rotated 90 + (12,-12));
    
);
label.lft(aa, 10 left); label.rt(bb, 10 right);

\end{mplibcode}
$$

\section{A three-circle theorem}

Given three non-intersecting, mutually external circles, connect the 
intersection of the internal common tangents of each pair of circles
with the centre of the other circle.  Then the resulting three line 
segments are concurrent.

\vfill
$$
\begin{mplibcode}
pair A,B,C,D,E,F,P;
numeric r[];
A = origin;    r1 = 60 + 5 normaldeviate;
B = (80,-240); r2 = 80 + 5 normaldeviate;
C = (240,-10); r3 = 40 + 5 normaldeviate;

path c[];
c1 = fullcircle scaled 2r1 shifted A;
c2 = fullcircle scaled 2r2 shifted B;
c3 = fullcircle scaled 2r3 shifted C;

D = whatever[point 4 of c1, point 0 of c2] = whatever[point 0 of c1, point 4 of c2];
E = whatever[point 4 of c2, point 0 of c3] = whatever[point 0 of c2, point 4 of c3];
F = whatever[point 2 of c1, point 6 of c3] = whatever[point 6 of c1, point 2 of c3];
P = whatever [C,D] = whatever [A,E];

vardef tangent_point(expr c, p) = 
  c intersectionpoint fullcircle scaled abs(p-center c) shifted 1/2[p,center c]
enddef;

pair t[];
t111 = tangent_point(c1,D); t113 = tangent_point(c1,F);
t121 = tangent_point(c2,D); t122 = tangent_point(c2,E);   
t132 = tangent_point(c3,E); t133 = tangent_point(c3,F);
t211 = tangent_point(reverse c1,D); t213 = tangent_point(reverse c1,F);
t221 = tangent_point(reverse c2,D); t222 = tangent_point(reverse c2,E);
t232 = tangent_point(reverse c3,E); t233 = tangent_point(reverse c3,F);

drawoptions(withcolor 3/4 white);
draw t111 -- t121; draw t211 -- t221; 
draw t113 -- t233; draw t213 -- t133;
draw t122 -- t232; draw t222 -- t132;
drawoptions(dashed withdots scaled 1/2);
draw A -- t113; draw t233 -- C; label.lft ("$r_1$", 1/2[A,t113]);
draw A -- t211; draw t221 -- B; label.llft("$r_2$", 1/2[B,t221]);
draw B -- t222; draw t132 -- C; label.urt ("$r_3$", 1/2[C,t132]);
drawoptions(withcolor 2/3 red); draw c1; draw c2; draw c3;
drawoptions(withcolor 2/3 blue); draw A--E; draw B--F; draw C--D;  
drawoptions(); draw A--B--C--cycle;

forsuffixes s = A, B, C, D, E, F:
  label("$" & str s & "$", (1+9/abs(s-P))[P, s]);
endfor
label("$P$", P + 12 dir 110);

label.urt(btex \vbox{\openup3pt\halign{\hfill$\displaystyle #$&$\displaystyle{}=#$\hfil\cr
\frac{AD}{DB}&\frac{r_1}{r_2}\cr
\frac{BE}{EC}&\frac{r_2}{r_3}\cr
\frac{CF}{FA}&\frac{r_3}{r_1}\cr
\frac{AD}{DB}\cdot \frac{BE}{EC}\cdot \frac{CF}{FA}&1 \Rightarrow AE \cap BF \cap CD = P\cr
&\omit\qquad\textit{via Ceva's theorem}\hfil\cr
}} etex, (xpart point 0 of c2, ypart point 6 of c2));
\end{mplibcode}
$$
\vfill
\contrib{R.\@ S.\@ Hu}

\section{A constant chord}

Suppose two circles $Q$ and $R$ intersect in $A$ and $B$. A point $P$ on 
the arc of $Q$ which lies outside $R$ is projected through $A$ and $B$
to determine chord $CD$ of $R$. Prove that no matter where $P$ is
chosen on its arc, the length of chord $CD$ is always the same.

$$
\begin{mplibcode}
path Q, R; pair A, B, C, D, P;
Q = fullcircle scaled 120 rotated 180;
R = fullcircle scaled 144 shifted 106 right rotated -6;
P = point 1/4 of Q;
A = Q intersectionpoint R;
B = reverse Q intersectionpoint R;
C = reverse R intersectionpoint (P -- 4[P, A]);
D = R intersectionpoint (P -- 4[P, B]);

picture upper;
upper = image(
    draw P--C--D--cycle withcolor 2/3 blue;
    draw Q; draw R;

    interim labeloffset := 8;
    dotlabel.bot("$A\thinspace$", A);
    dotlabel.top("$\thinspace B$", B);
    dotlabel.bot("$C$", C);
    dotlabel.top("$D$", D);
    dotlabel.lft("$P$", P);

    interim labeloffset := 4;
    label.ulft("$\scriptstyle Q$", point 6.5 of Q);
    label.urt("$\scriptstyle R$", point 0.5 of R);
);

draw upper shifted 200 up;
pair P', C', D';
P' = point 7.4 of Q;
C' = reverse R intersectionpoint (P' -- 4[P', A]);
D' = R intersectionpoint (P' -- 4[P', B]);

path ark, bark;
ark = quartercircle rotated angle (C'-A) scaled 42 shifted A cutafter (A--C);
draw ark withcolor red;
draw ark rotatedabout(A, 180) withcolor red;
bark = quartercircle rotated angle (D'-B) scaled 42 shifted B cutafter (B--D);
draw bark withcolor red;
draw bark rotatedabout(B, 180) withcolor red;

draw P'--C'--D'--cycle dashed evenly scaled 1/2 withcolor 2/3 blue;
draw upper;
interim labeloffset := 8;
dotlabel.bot("$C'$", C');
dotlabel.urt("$D'$", D');
dotlabel.lft("$P'$", P');
\end{mplibcode}
$$
\def\overarc#1{\hbox to 0pt{\kern #1 pt\raise8pt\hbox{$\frown$}\hss}}
$$\vbox{\openup6pt\halign{\hss # \hss\cr
$\angle C'AC = \angle P'AP = \angle P'BP = \angle D'BD$ \cr
$\overarc4{C'C} = \overarc5{D'D}$, \quad $\overarc5{C'D'} = \overarc3{CD}$ \cr
$C'D' = CD$\cr}}$$

\section{A Putnam area problem}

Let $s$ be any arc of the unit circle lying entirely in the first quadrant.
Let $A$ be the area of the region lying below $s$ and above the $x$-axis, and 
let $B$ be the area of the region lying to the right of the $y$-axis and to the left
of $s$.  Prove that $A+B$ depends only on the arc length, and not on the position, of 
$s$.

\bigskip\noindent
\textsc{Solution}:

\bigskip
$$
\begin{mplibcode}
numeric r; r = 81;
path Q, s, A, B; 
Q = (quartercircle -- origin -- cycle) scaled 2r;
s = subpath (0.81, 1.44) of Q;
z1 = point 0 of s; z2 = point infinity of s; z3 = (x2, y1);
A = s -- (x2, 0) -- (x1, 0) -- cycle;
B = s -- (0, y2) -- (0, y1) -- cycle;

color aa, bb, ff; aa = Reds 8 6; bb = Blues 8 6; ff = Blues 8 2;
picture P[];
input thatch
P1 = image(
  path a, b; a = unitsquare scaled 13 shifted (r + 42, 42);
  b = a shifted 18 down;
  thatch_angle := -45; rule A withcolor aa; rule a withcolor aa; draw a;
  thatch_angle := +45; rule B withcolor bb; rule b withcolor bb; draw b;
  draw A; draw B; draw Q;
  interim ahangle := 180; interim ahlength := 2; drawdblarrow s withpen pencircle scaled 3/4;
  label.urt("$s$", point 1.125 of s);
  label.lft("$A$:\ ", point 3.5 of a);
  label.lft("$B$:\ ", point 3.5 of b);
);
P2 = image(
  draw A dashed evenly withpen pencircle scaled 1/8;
  draw B dashed evenly withpen pencircle scaled 1/8;
  draw Q;
);
P3 = image(path p; p = origin -- z2 -- z3 -- cycle; fill p withcolor ff; draw P2; draw p);
P4 = image(path p; p = origin -- z1 -- z3 -- cycle; fill p withcolor ff; draw P2; draw p);
P5 = image(path p; p = s -- z3 -- cycle;            fill p withcolor ff; draw P2; draw p);
P6 = image( 
  draw P3; draw P4 shifted 100 right; draw P5 shifted 200 right;
  numeric c; c = 6;
  path brk; brk = (0, -c) -- (-c, -c) -- (-c, r+c) -- (0, r+c);
  draw brk; draw brk reflectedabout(up, down) shifted (200 + r, 0);
  label.lft("$=2\times{}$", point 1.5 of brk);
  label("$+$", point 1.5 of brk shifted 92 right);
  label("$+$", point 1.5 of brk shifted 192 right);

);
P7 = image(
  fill s -- origin -- cycle withcolor ff; draw P2;
  path a; a = quartercircle scaled 42 rotated angle z1 cutafter (origin -- z2);
  draw a; label.urt("$\theta$", point 1/2 of a);
  draw s -- origin -- cycle;
  label.lft("$=2\times{}$", point 1.5 of brk);
);
draw P1;
draw P6 shifted 120 down;
draw P7 shifted 240 down;
label("$\displaystyle A + B = 2 \times {\theta \over 2} = \theta = \ell(s)$", (200, -200));
\end{mplibcode}
$$
\section{The area under a polygonal arch}

The area under a polygonal arch generated by one vertex of a regular $n$-gon rolling
along a straight line is three times the area of the polygon.

\bigskip
$$
\begin{mplibcode}
    numeric r, s, u; r = 42; s = r * cosd 18; u = 2r * sind 18; 
    picture P[];
    path D, side, base, arch;
    D = (for t=0 upto 9: (r, 0) rotated -72 rotated 36t -- endfor cycle) shifted (1/2 u, s); 
    side = subpath (9, 10) of D;
    base = (origin -- (12u, 0)) shifted -(u, 0);
    P1 = image(
        arch = 
        for i=0 upto 9:
            point -1 of D --
            hide(draw D withpen pencircle scaled 1/4; D := D rotatedabout(point i of D, -36);)
        endfor point -1 of D;
        draw arch withcolor 2/3 blue;
        draw base;
    );
    P2 = image(
        for i=1 upto 8:
            path part, part', part''; 
            part = side shifted (i*u, 0) -- point i of arch -- cycle;
            part' = part rotatedabout(point if i > 4: 5/2 else: 3/2 fi of part, 180);
            part'' = part rotatedabout(point if i > 4: 3/2 else: 5/2 fi of part, 180);
            fill part withcolor Blues 7 3; 
            fill part' withcolor Greens 7 2; draw part';
            fill part'' withcolor Oranges 7 2; draw part'';
            path m; 
            m = point i if i > 4: +1 else: -1 fi of arch 
             -- point   if i > 4: 0  else:  1 fi of part'';
            draw m; draw m shifted (u * if i > 4: left else: right fi);
        endfor
        draw arch -- cycle;
    );
    P3 = image(
        D := D shifted - center D rotated -18 shifted (s, r);
        fill D withcolor Greens 7 2;
        draw D; for i=1 upto 7: draw point 0 of D -- point 1+i of D; endfor
        D := D rotatedabout(center D, 180) shifted (5u-s, 0);
        fill D withcolor Blues 7 3;
        draw D; for i=1 upto 7: draw point 0 of D -- point 1+i of D; endfor
        D := D rotatedabout(center D, 180) shifted (5u-s, 0);
        fill D withcolor Oranges 7 2;
        draw D; for i=1 upto 7: draw point 0 of D -- point 1+i of D; endfor
    );

    draw P1; 
    draw P2 shifted (3r * down);
    draw P3 shifted (6r * down);
\end{mplibcode}
$$

\bigskip\noindent
\textsc{Corollary}: The area under one arch of a cycloid is three times the area of
the generating circle.

\bigskip
\contrib{Philip R.\@ Mallinson}

\section{The length of a polygonal arch}

The length of the polygonal arch generated by one vertex of a regular $n$-gon
rolling along a straight line is four times the length of the in-radius plus four
times the length of the circum-radius of the $n$-gon.

\bigskip
$$
\begin{mplibcode}
vardef decorate(expr rhombus, number, s) = 
  fill rhombus withcolor Blues[7][s]; 
  draw rhombus withcolor Blues[7][5];
  path A; A = point 0 of rhombus -- point 2 of rhombus; 
  cutdraw A withpen pencircle scaled 1;
  label(
    "$" & if odd number: "2" & fi char (101-s) & "$",
    point 1/2 of A + 6 unitvector(direction 1/2 of A rotated 90)
  );
enddef;
path gon[]; picture R[], P[]; numeric r; r = 50; pair side[];
for n=9, 10:
  gon[n] = for t=-1/2 upto n-1: down scaled r rotated -(360/n*t) -- endfor cycle;
  side[n] = point 0 of gon[n] - point 1 of gon[n];
  P[n] = image(
    for i=1 upto n-1:
      path R; R = subpath (i,i+1) of gon[n] shifted ((i-1)*side[n]) 
               -- subpath (i+1,i) of gon[n] shifted (i*side[n]) -- cycle;
      decorate(R, n, round(abs(n/2-i)));
    endfor
    for i=0 upto n-1:
        draw gon[n] shifted (i*side[n]) withpen pencircle scaled 1/4;
    endfor
  );
endfor
P90 = image(
  % DUMP capture=False, replace="  % See source for the internals of the nonagon."
  label.lft("Odd $n$\dots", point 2 of gon9 shifted 20 left);
  clearxy; gon9 := reverse gon9 rotated 160;
  z1 = point 0 of gon9 reflectedabout(point 1 of gon9, point -1 of gon9);
  z2 = point 1 of gon9 reflectedabout(point 2 of gon9, point -2 of gon9);
  z3 = point 5 of gon9 reflectedabout(point 3 of gon9, point -3 of gon9);
  z4 = point 6 of gon9 reflectedabout(point 2 of gon9, point -2 of gon9);

  z5 = point 2 of gon9 reflectedabout(point 1 of gon9, z2);
  z6 = point 6 of gon9 reflectedabout(point 5 of gon9, z3);
  z7 = point 7 of gon9 reflectedabout(point 6 of gon9, z4);

  path r[];
  r4 = subpath(0, 1) of gon9 -- z1 -- point -1 of gon9 -- cycle;
  r2 = subpath(1, 2) of gon9 -- z2 -- z5 -- cycle;
  r3 = subpath(5, 6) of gon9 -- z3 -- z6 -- cycle;
  r1 = subpath(6, 7) of gon9 -- z4 -- z7 -- cycle;

  pair m[]; for i=1 upto 4: 
    m[i] = whatever[point 0 of r[i], point 2 of r[i]]
         = whatever[point 1 of r[i], point 3 of r[i]];
  endfor

  for i=1 upto 4:
    fill r[i] withcolor Blues[7][i];
    draw r[i] withcolor Blues[7][5];
    cutdraw point 0 of r[i] -- m[i] withpen pencircle scaled 1;
    label.lft("$" & char (101-i) & "$", 1/2[m[i], point 0 of r[i]]);
  endfor
  drawoptions(dashed evenly);
    draw m1 -- m2; draw m3 -- point 0 of r1; draw m4 -- point 0 of r2;
  drawoptions();
  draw gon9;
  % DUMP capture=True
);
P100 = image(
  z1 = point 5 of gon10 reflectedabout(point 4 of gon10, point 6 of gon10);
  z2 = point 6 of gon10 reflectedabout(point 4 of gon10, point 7 of gon10);
  z3 = z2 reflectedabout(point 0 of gon10, point 5 of gon10);
  z4 = z1 reflectedabout(z2, z3);
  z5 = z4 reflectedabout(point 0 of gon10, point 6 of gon10);
  decorate(z1 -- subpath (4,6) of gon10       -- cycle, 10, 4);
  decorate(subpath(6, 7) of gon10 -- z2 -- z1 -- cycle, 10, 3);
  decorate(z4 -- z2 -- z1 -- z3               -- cycle, 10, 2);
  decorate(z2 -- z4 -- point 0 of gon10 -- z5 -- cycle, 10, 1);
  cutdraw point 0 of gon10 -- z4 withpen pencircle scaled 1;
  label.lft("$\frac12e$", 1/2[point 0 of gon10, z4]);
  label.rt("Even $n$\dots", point 7 of gon10 shifted 20 right);
  draw gon10;
);
draw P9; draw P90 shifted (tw-2.4r , 2.4r);
draw P10 shifted (0, 6r); draw P100 shifted (0, 3.6r);
draw ulcorner P9 shifted (0, 20) -- lrcorner P10 shifted (0, 6r-36);
\end{mplibcode}
$$

\bigskip\noindent
\textsc{Corollary}: The arc length of one arch of a cycloid is eight times the
radius of the generating circle.

\bigskip
\contrib{Philip R.\@ Mallinson}
%------------
\section{The volume of a frustrum of a square pyramid} 

\vfill
$$
\begin{mplibcode}
numeric a, b, c, h; 2c = b - a; a = 55; b = 89; h = 34;
% quick and dirty, fixed isometric projection
vardef p(expr x, y, z) = 
    0.40824829 * (1.73205x + 1.73205z, -x + 2y + z)
enddef;
picture P[];
P1 = image(
    path f, t, s;
    f = origin -- p(a, 0, 0) -- p(a, h, 0) -- p(0, h, 0) -- cycle;
    t = p(0, h, 0) -- p(a, h, 0) -- p(a, h, a) -- p(0, h, a) -- cycle;
    s = p(a, 0, 0) -- p(a, 0, a) -- p(a, h, a) -- p(a, h, 0) -- cycle;
    fill t withcolor Blues 8 2;
    fill s withcolor Blues 8 4;
    draw f; draw s; draw t;
    label.ulft("$a$", point 7/2 of t);
    label.urt("$a$", point 5/2 of t);
    label.rt("$h$", point 3/2 of s);
    label.lft("$P_1$", point 0 of t);
);
P2 = image(
    path f, t, s;
    f = origin -- p(b, 0, 0) -- p(b, h, 0) -- p(0, h, 0) -- cycle;
    t = p(0, h, 0) -- p(b, h, 0) -- p(b, h, a) -- p(0, h, a) -- cycle;
    s = p(b, 0, 0) -- p(b, 0, a) -- p(b, h, a) -- p(b, h, 0) -- cycle;
    fill t withcolor Blues 8 2;
    fill s withcolor Blues 8 4;
    draw f; draw s; draw t;
    draw point 0 of f -- p(c, h, 0) -- p(c, h, a); draw p(c, h, 0) -- p(c, 0, 0);
    draw point 1 of f -- p(b-c, h, 0) -- p(b-c, h, a); draw p(b-c, h, 0) -- p(b-c, 0, 0);
    label.ulft("$a$", point 7/2 of t);
    label.urt("$a$", point 5/2 of t);
    label.urt("$c$", p(c/2, h, a));
    label.urt("$c$", p(b-c/2, h, a));
    label.rt("$h$", point 3/2 of s);
    label.llft("$b$", point 1/2 of f);
    label.lft("$P_2$", point 0 of t);
);
% DUMP capture=False, replace="%... and so on to define four more pictures"
P3 = image(
    path f, t, s;
    f = origin -- p(b, 0, 0) -- p(b, h, 0) -- p(0, h, 0) -- cycle;
    t = p(0, h, 0) -- p(b, h, 0) -- p(b, h, b) -- p(0, h, b) -- cycle;
    s = p(b, 0, 0) -- p(b, 0, b) -- p(b, h, b) -- p(b, h, 0) -- cycle;
    fill t withcolor Blues 8 2;
    fill s withcolor Blues 8 4;
    draw f; draw s; draw t;
    draw p(c, 0, 0) -- p(c, h, 0) -- p(c, h, b);
    draw p(b-c, 0, 0) -- p(b-c, h, 0) -- p(b-c, h, b);
    draw p(0, h, c) -- p(b, h, c) -- p(b, 0, c);
    draw p(0, h, b-c) -- p(b, h, b-c) -- p(b, 0, b-c);
    label.ulft("$a$", point 5/2 of s shifted p(-c, 0, 0));
    label.urt ("$a$", point 5/2 of f shifted p(0, c, 0));
    label.llft("$b$", point 1/2 of f);
    label.lrt("$b$", point 1/2 of s);
    label.ulft("$c$", p(0, h, b-c/2));
    label.urt("$c$", p(c/2, h, b));
    label.urt("$c$", p(b-c/2, h, b));
    label.rt("$h$", point 3/2 of s);
    label.lft("$P_3$", point 0 of t);
);
P4 = image(path f, t, s; 
    f = p(0, 0, 0) -- p(c, 0, 0) -- p(c, h, 0) -- p(0, h, 0) -- cycle;
    t = p(0, h, 0) -- p(c, h, 0) -- p(c, h, c) -- p(0, h, c) -- cycle;
    s = p(c, 0, 0) -- p(c, 0, c) -- p(c, h, c) -- p(c, h, 0) -- cycle;
    fill t withcolor Blues 8 2;
    fill s withcolor Blues 8 4;
    draw f; draw s; draw t;
    label.llft("$c$", point 1/2 of f);
    label.lrt("$c$", point 1/2 of s);
    label.rt("$h$", point 3/2 of s);
    label.lft("$P_4$", point 0 of t);
);
P5 = image(path f, s; 
    f = p(0, 0, 0) -- p(c, 0, 0) -- p(c, h, c) -- cycle;
    s = p(c, 0, 0) -- p(c, 0, c) -- p(c, h, c) -- cycle;
    fill s withcolor Blues 8 4;
    draw point 0 of f -- p(0,0,c) -- point 2 of f dashed withdots scaled 1/2;
    draw point 1 of s -- p(0,0,c) dashed withdots scaled 1/2;
    draw f; draw s; 
    label.llft("$c$", point 1/2 of f);
    label.lrt("$c$", point 1/2 of s);
    label.rt("$h$", point 3/2 of s);
    label("$P_5$", p(0,h,c));
);
P6 = image(path f, s, t;
    f = p(0,0,0) -- p(b,0,0) -- p(b-c,h,c) -- p(c,h,c) -- cycle;
    t = p(c,h,c) -- p(b-c,h,c) -- p(b-c,h,b-c) -- p(c,h,b-c) -- cycle;
    s = p(b,0,0) -- p(b,0,b) -- p(b-c,h,b-c) -- p(b-c,h,c) -- cycle;
    fill t withcolor Blues 8 2;
    fill s withcolor Blues 8 4;
    drawoptions(dashed withdots scaled 1/2);
    draw p(0,0,0) -- p(0, 0, b) -- p(b,0,b);
    draw p(0,0,b) -- point 3 of t;
    draw p(c,h,c) -- p(0,0,c) -- p(b,0,c);
    draw p(c,h,c+a) -- p(0,0,c+a) -- p(b,0,c+a);
    draw p(c,h,c+a) -- p(c,0,b) -- p(c,0,0);
    draw p(c+a,h,c+a) -- p(c+a,0,b) -- p(c+a,0,0);
    for i=0 upto 3: draw point i of t -- point i of t shifted p(0,-h,0); endfor
    drawoptions();
    draw p(c,0,0) -- p(c,h,c);
    draw p(c+a,0,0) -- p(c+a,h,c) -- p(b,0,c);
    draw point 2 of t -- p(b,0,c+a);
    draw f; draw s; draw t;
    label.ulft("$a$", point 7/2 of t);
    label.urt("$a$", point 5/2 of t);
    label.llft("$b$", point 1/2 of f);
    label.lrt("$b$", point 1/2 of s);
    label.lft("$h$", p(a+c, 1/2 h, a+c)) withcolor 1/2 Blues 8 4;
    label.rt("$P$", point 1 of s);
);
% DUMP capture=True
draw P1;
draw P2 shifted p(3/2a - 2c, 0, 2a);
draw P3 shifted p(3/2a - 2c, 0, -3.5a);
draw P4 shifted p(3/2a + b - 3c, 0, -3/4a);
draw P5 shifted p(3/2a + b - 3c, 0, +1/2a);
draw P6 shifted p(4a, 0, -a);

label.bot(btex \vbox{\openup=6pt\halign{\hss # \hss\cr
$P_4 = 3P_5$\cr
$P_1+P_3 = 2P_2 + 4P_4 \quad\Rightarrow\quad 
        P_1 + P_2 + P_3 = 3P_2 + 12P_5 = 3(P_2 + 4P_5) = 3P$\cr
$\therefore\quad V = \frac{h}{3}\left(a^2 + ab + b^2\right)$\cr
}} etex, point 1/2 of bbox currentpicture shifted 21 down);
\end{mplibcode}
$$

\vfill

\contrib{Sidney J.\@ Kung}

\section{The product of four (positive) numbers in arithmetic progression is always the difference
    of two squares}


\vfill
$$
\begin{mplibcode}
numeric a, d; d = 6.07; a = 4.7;

path A, B, C;
A = unitsquare xscaled ((a+d)*(a+2d)) yscaled (a*(a+3d));
B = unitsquare scaled (a**2 +3a*d+d**2) shifted point 1 of A shifted 32 right;
C = unitsquare scaled (d**2) rotated 180 shifted point 2 of B;

fill A withcolor OrRd 8 2; 
draw subpath(1,2) of A shifted -(d**2, 0) dashed evenly;
draw subpath(1,2) of A shifted -2(d**2, 0) dashed evenly;
draw A;

fill B withcolor OrRd 8 2; 
draw B;

filldraw C withpen pencircle scaled 1 withcolor background;
draw subpath (-1, 1) of C dashed evenly;
draw subpath (1, 3) of C;

draw (xpart point 0 of B, ypart point 2 of C) 
     -- point 2 of C 
     -- (xpart point 2 of C, ypart point 0 of B) dashed evenly;

label.lft(textext("$a(a+3d)$") rotated 90, point -1/2 of A);
label.bot("$(a+d)(a+2d) = a(a+3d) + 2d^2$", point 1/2 of A);
label.lft(textext("$a^2+3ad+d^2$") rotated 90, point -1/2 of B);
label.bot("$a^2+3ad+d^2$", point 1/2 of B);

label.top("$d^2$", point 2 of A shifted (-1/2d*d, 0));
label.top("$d^2$", point 2 of A shifted (-3/2d*d, 0));

\end{mplibcode}
$$
\bigskip
$$a(a+d)(a+2d)(a+3d)=\left(a^2+3ad+d^2\right)^2-\left(d^2\right)^2$$
\vfill
\contrib{RBN}
\section{Algebraic areas III: Factoring the sum of two squares}

\vfill
$$
\begin{mplibcode}
numeric x, y; x = 90; y = 28;
path a; a = unitsquare scaled x;
path b; b = unitsquare scaled y shifted point 1 of a;
path c; c = unitsquare xscaled x yscaled y shifted point 3 of a;
path d; d = unitsquare xscaled y yscaled x shifted point 3 of b;

vardef double_arrow_label(expr t, a, b) =
  save p; picture p; p = thelabel(t, origin) rotated angle (b-a);
  save o, wd; numeric o, wd; o = labeloffset; wd = ypart (urcorner p - llcorner p);
  if wd > 2o: o := o + 1/2 wd; fi
  save arc; path arc; arc = (a--b) shifted (unitvector(a-b) rotated 90 scaled o);
  drawdblarrow arc; 
  p := p rotated - angle (b-a) shifted point 1/2 of arc;
  unfill bbox p; draw p;
enddef;

picture P[];
P1 = image(draw a; draw b; label("$x^2$", center a); label("$y^2$", center b));
P2 = image(
  forsuffixes @=c, d:
    fill @ withcolor YlGn 7 2; draw @ dashed evenly scaled 1/2;
    label("$xy$", center @); endfor
  draw a; draw b; 
  undraw subpath(3,4) of b withpen pencircle scaled 1;
  draw subpath(3,4) of b dashed evenly scaled 1/2;
  double_arrow_label("\strut $x+y$", point 0 of a, point 1 of b);
  double_arrow_label("\strut $x+y$", point 1 of b, point 2 of d);
);

path A; A = unitsquare xscaled (x+y-sqrt(2x*y)) yscaled (x+y);
path B; B = unitsquare yscaled (x+y-sqrt(2x*y)) xscaled sqrt(2x*y) shifted point 1 of A;
path C; C = unitsquare scaled sqrt(2x*y) shifted point 3 of B;
path D; D = A rotated 90 shifted point 0 of B;
P3 = image(
  fill C withcolor YlGn 7 2; draw C dashed evenly scaled 1/2;
  label("$2xy$", center C); draw A; draw B; 
  undraw subpath(3,4) of B withpen pencircle scaled 1;
  draw subpath(3,4) of B dashed evenly scaled 1/2;
  double_arrow_label("\strut $\sqrt{2xy}$", point 1 of C, point 2 of C);
  double_arrow_label("\strut $\sqrt{2xy}$", point 2 of C, point 3 of C);
);
P4 = image(
  draw D; draw B; undraw subpath(3,4) of B withpen pencircle scaled 1;
  draw subpath(3,4) of B dashed evenly scaled 1/2;
  double_arrow_label("\strut $x+y+\sqrt{2xy}$", point 3 of D, point 1 of B);
  double_arrow_label("\strut $x+y-\sqrt{2xy}$", point 1 of B, point 2 of B);
);
draw P2 shifted 120 left; draw P3 shifted 120 right;
label.top(P1, point 5/2 of bbox currentpicture shifted 30 up);
label.bot(P4, point 1/2 of bbox currentpicture shifted 30 down);
label.top(btex $\displaystyle
x^2 + y^2 = \left(x + \sqrt {2xy} + y\right)\left(x - \sqrt {2xy} + y\right)
$ etex, point 5/2 of bbox currentpicture shifted 21 up);
\end{mplibcode}$$
\vfill
\chapter{Triognometry, Calculus, \& Analytic Geometry}

\minitoc

\section{Sine of the sum - II}
\vfill
$$
\begin{mplibcode}
numeric alpha, beta; alpha = 42; beta = 55;
numeric h; h = 89; z0 = h * up; y1 = y2 = 0;
z0 - z1 = whatever * dir (90 - alpha);
z0 - z2 = whatever * dir (90 + beta);
path t; t = z0--z1--z2--cycle;
picture P[];
P1 = image(
  draw subpath(1,3) of unitsquare scaled 5 withcolor 1/4 white;
  draw origin -- up * h dashed evenly scaled 1/2;
  draw t withcolor 2/3 blue;
  label("$\alpha$", 15 down rotated -1/2 alpha shifted point 0 of t);
  label("$\beta$", 15 down rotated 1/2 beta shifted point 0 of t);
  label.lft("$h$", (0, 1/2 h));
  label.ulft("$a$", point 1/2 of t);
  label.urt("$b$", point -1/2 of t);
  label(btex $\displaystyle \alpha, \beta \in (0, \pi/2)\quad\Longrightarrow
     \quad h=a \cos \alpha = b\cos\beta$ etex, 36 down);
);
P2 = image(
  path t', t'';
  t'  = (subpath (0, 1) of t -- (xpart point 0 of t, ypart point 1 of t) -- cycle);
  t'' = (subpath (2, 3) of t -- (xpart point 0 of t, ypart point 1 of t) -- cycle); 
  t' := t' shifted 42 left;
  t'' := t'' shifted 42 right;
  draw t' withcolor 2/3 blue;
  draw t'' withcolor 2/3 blue;
  label("$\alpha$", 15 down rotated -1/2 alpha shifted point 0 of t');
  label("$\beta$", 15 down rotated 1/2 beta shifted point 1 of t'');
  label.rt("$h$", point -1/2 of t');
  label.lft("$h$", point 3/2 of t'');
  label.ulft("$a$", point 1/2 of t');
  label.urt("$b$", point 1/2 of t'');
  label("${}+{}$", 1/2[point -1/2 of t', point 3/2 of t'']);
  label("${}={}$", (xpart point 1 of t', ypart point -1/2 of t'));
);
draw P1;
draw P2 shifted 180 down;
label.bot(btex \vbox{\openup6pt\halign{\hfil $\displaystyle #$&$\displaystyle {}=#$\hfil\cr
 \frac12 ab\sin(\alpha+\beta)&\frac12 ah\sin\alpha + \frac12 bh\sin\beta\cr
&\frac12 ab\cos\beta\sin\alpha + \frac12 ba\cos\alpha\sin\beta\cr
\therefore\quad\sin(\alpha+\beta)&\sin\alpha\cos\beta + \cos\alpha\sin\beta\cr
}} etex, point 1/2 of bbox currentpicture shifted 34 down);
\end{mplibcode}
$$
\vfill
\contrib{Christopher Brüningsen}
\section{Sine of the sum – III}

\vfill

$$
\begin{mplibcode}
numeric a, b, u; a = 38; b = 62; u = 144;
numeric ca, cb, sa, sb; ca = cosd(a);  sa = sind(a); cb = cosd(b);  sb = sind(b);
path s, t[]; 
s = unitsquare shifted -(1/2, 1/2) xscaled (ca+cb) yscaled (sa+sb) scaled u;
t0 = (origin -- ca * right -- sa * up -- cycle) scaled u;
t1 = (origin -- cb * left  -- sb * up -- cycle) scaled u; 
beginfig(1);
picture P[];
P1 = image(
  fill s withcolor Oranges 7 2;
  for i=0, 1:
    for j=0, 180:
      path p; p = t[i] shifted point i of s rotated j;
      fill p withcolor if odd i: Blues else: Greens fi 7 1; draw p;
      if i+j = 0:
        label.urt("$1$", point 3/2 of p);
        label("$\alpha+\beta$", point 2 of p + 18 dir 1/2(b-a-12));
      elseif i+j = 181:
        label.lrt("$1$", point 3/2 of p);
        label("$\alpha$", point 1 of p + 21 dir (- 1/2 a));
        label("$\beta$", point 1 of p + 12 dir (180 + 1/2 b));
      fi
    endfor
  endfor
);
P2 = image(
  fill s withcolor Oranges 7 2; draw s;
  for i = 0, 1: 
    path p; p = t[i] shifted (point 0 of s - point i of t[i]) rotated 180(1-i);
    for j=0, 180:
      p := p rotatedabout(point 3/2 of p, j);
      fill p withcolor if odd i: Blues else: Greens fi 7 1; draw p;
      if i+j=180:
        label.bot("$\cos\alpha$", point 1/2 of p);
        label.lft(TEX("$\sin\alpha$") rotated 90, point -1/2 of p);
      elseif i+j=1:
        label.rt(TEX("$\sin\beta$") rotated 90, point -1/2 of p);
      elseif i+j=181:
        label.top("$\cos\beta$", point 1/2 of p);
      fi
    endfor
  endfor
);
% DUMP capture=False, replace="% part II is much the same, but with smaller s"
s := unitsquare shifted -(1/2, 1/2) xscaled (ca-cb) yscaled (sa-sb) scaled u;
P3 = image(
  fill s withcolor Oranges 7 2;
  for i=0, 1: 
      path p; p = t[i] shifted point 3i of s;
    for j=0, 180:
      p := p rotated j;
      fill p withcolor if odd i: Blues else: Greens fi 7 2; draw p;
      if i+j = 1:
        label.ulft("$1$", point 3/2 of p);
        label("$\beta$", point 1 of p - 12 dir (180 + 1/2 b));
      elseif i+j = 180:
        label.llft("$1$", point 3/2 of p);
        label("$\alpha$", point 1 of p + 21 dir (- 1/2 a));
      fi
    endfor
  endfor
);
P4 = image(
  fill s withcolor Oranges 7 2; draw s;
  for i=0, 1:
      path p; p = t[i] shifted point 3i of s rotated 180;
    for j=0, 180:
      p := p rotatedabout(point 3/2 of p, j);
      fill p withcolor if odd i: Blues else: Greens fi 7 2; draw p;
      if i+j = 0:
        label.top("$\cos\beta$", 1/2[point 1 of p, point 3 of s]);
      elseif i+j=1:
        label.top("$\cos\alpha$", 1/2[point 1 of p, point 0 of s]);
      elseif i+j = 180:
        label.lft(TEX("$\sin\alpha$") rotated 90, point -1/2 of p);
      elseif i+j=181:
        label.rt(TEX("$\sin\beta$") rotated 90, point 5/2 of p);
      fi
    endfor
  endfor
  draw point 3 of s -- (xpart point 3 of s, ypart point 0 of p) dashed evenly; 
);
% DUMP capture=True
labeloffset := 20;
P5 = image(draw P1; draw P2 shifted (3/2u, 0); label.ulft("I.", ulcorner currentpicture));
P6 = image(draw P3; draw P4 shifted (3/2u, 0); label.ulft("II.", ulcorner currentpicture));
draw P5; draw P6 shifted (0, -7/4u);
label.top("$\sin(\alpha + \beta) = \sin\alpha\cos\beta + \sin\beta\cos\alpha$",  
  point 5/2 of bbox currentpicture shifted 13 up);
\end{mplibcode}$$
\vfill
\contrib{Volker Priebe and Edgar A.\@ Ramos}
\section{Cosine of the sum}
\vfill
$$
\begin{mplibcode}
numeric alpha, beta, a, b; 
alpha = 32;
beta = 42;
a = 280;
b * cosd(alpha) = a * sind(beta);
z0 = right scaled a rotated (90 - beta);
z1 = right scaled b rotated alpha;
path t;
t = origin -- (x0, 0) -- z0 -- cycle;

draw unitsquare scaled 5 rotated 90 shifted point 1 of t withpen pencircle scaled 1/4;
draw t;
draw origin -- z1;

label.bot("$b\cos\alpha = a\sin\beta$", point 1/2 of t);
label.rt(TEX("$a\cos\beta$") rotated 90, point 1.44 of t);
label.lft(TEX("$b\sin\alpha$") rotated 90, (x0, 1/2y1));

label.ulft("$a$", 1/2 z0);
label.ulft("$b$", 1/2 z1);

label("$\alpha$", 24 dir 1/2 alpha);
label("$\beta$", 20 dir (270 - 1/2 beta) shifted point 2 of t);
z2 = 42 up;
drawarrow z2 {dir -72} .. 32 dir 1/2(90 + alpha - beta);
label.top("$\pi/2-(\alpha+\beta)$", z2);

\end{mplibcode}
$$
\bigskip
$$\vbox{\openup6pt\halign{\hfil$\displaystyle #$&$\displaystyle{}=#$\hfil\cr
\frac12 ab\sin\left(\frac{\pi}{2}-(\alpha+\beta)\right)& 
\frac12 b\cos\alpha \cdot a\cos\beta-\frac12 b\sin\alpha \cdot a\sin\beta\cr
\therefore \quad \cos(\alpha+\beta)&
\cos\alpha\cos\beta - \sin\alpha\sin\beta\cr
}}$$
\vfill
\contrib{Sidney H.\@ Kung}
\section{Geometry of addition formulas}
\vfill
$$
\begin{mplibcode}
vardef angle_mark(expr a, o, b, r, t, shade) = 
  draw fullcircle scaled 2r rotated angle (a-o) shifted o cutafter (o--b)
       withpen pencircle scaled 1/4;
  save p; picture p;
  p = thelabel(t, origin);
  save offset, alpha; numeric offset, alpha;
  alpha = 1/2 (angle (a-o) + angle (b-o));
  offset = r + arclength ((origin -- 100 dir alpha
  shifted center bbox p) cutafter bbox p);
  draw p shifted o shifted (offset * dir alpha) withcolor shade; 
enddef;
numeric alpha, beta; alpha = 28; beta = 45 - 1/2 alpha;
z4 = 144 up;
z1 = whatever * right; z4-z1 = whatever * dir (180-alpha-beta);
z2 = whatever * right; z4-z2 = whatever * dir (180-alpha);
z3 = whatever[z2, z4]; z1-z3 = whatever * dir (90-alpha);
path t; t = origin -- z1 -- z2 -- z3 -- z4 -- cycle;
picture P[];
P1 = image(
  angle_mark(z3, z2, z1, 24, "$\alpha$", Blues 6 5);
  angle_mark(z1, z4, z3, 32, "$\beta$", Blues 6 5);
  angle_mark(z4, z1, origin, 24, "$\alpha+\beta$", Blues 6 5);
  draw unitsquare scaled 6 withpen pencircle scaled 1/4;
  draw unitsquare scaled 6 rotated angle (z1-z3) shifted z3 withpen pencircle scaled 1/4;
  draw z3--z1--z4; draw t;
  drawoptions(withcolor Reds 6 5);
  label.urt("$1$", 1/2[z1, z4]);
  label.lft(TEX("$\sin(\alpha+\beta)$") rotated 90, 1/2z4);
  draw thelabel.top(TEX("$\sin\beta$"), origin) rotated angle (z3-z1) shifted 1/2[z3, z1];
  draw thelabel.top(TEX("$\cos\beta$"), origin) rotated angle (z3-z4) shifted 1/2[z3, z4];
  draw thelabel.top(TEX("$\sin\beta\cos\alpha/\sin\alpha$"), origin) 
  rotated angle (z2-z3) shifted 1/2[z2, z3];
  drawoptions();
  label(btex \vbox{\openup6pt\halign{\hfil$\displaystyle #$&$\displaystyle{}=#$\hfil\cr
  \sin\alpha & {\sin(\alpha+\beta) \over \cos\beta + \sin\beta\cos\alpha/\sin\alpha}\cr
  \sin(\alpha+\beta) & \sin\alpha\cos\beta + \cos\alpha\sin\beta\cr}} etex, (x2, y4-12));            
);
% DUMP capture=False, replace="% similarly for P2"
P2 = image(
    angle_mark(z3, z2, z1, 24, "$\alpha$", Blues 6 5);
    angle_mark(origin, z4, z1, 32, "$\beta$", Blues 6 5);
    angle_mark(z3, z1, z4, 24, "$\alpha+\beta$", Blues 6 5);
    draw unitsquare scaled 6 withpen pencircle scaled 1/4;
    draw unitsquare scaled 6 rotated angle (z1-z3) shifted z3 withpen pencircle scaled 1/4;
    draw z3--z1--z4; draw t;
    drawoptions(withcolor Reds 6 5);
    label.urt("$1$", 1/2[z1, z4]);
    label.lft(TEX("$\cos\beta$") rotated 90, 1/2z4);
    label.bot(TEX("\strut$\sin\beta$") rotated 0, 1/2z1);
    label.bot(TEX("\strut$\cos(\alpha+\beta)/\sin\alpha$") rotated 0, 1/2[z1, z2]);
    draw thelabel.bot(TEX("\strut$\cos(\alpha+\beta)$"), origin) rotated angle (z3-z1) shifted 1/2[z1, z3];
    drawoptions();
    label(btex \vbox{\openup6pt\halign{\hfil$\displaystyle #$&$\displaystyle{}=#$\hfil\cr
    {\sin\alpha\over\cos\alpha} & {\cos\beta \over \sin\beta + \cos(\alpha+\beta)/\sin\alpha}\cr
    \cos(\alpha+\beta) & \cos\alpha\cos\beta - \sin\alpha\sin\beta\cr}} etex, (x2, y4-12));            
);
% DUMP capture=True
draw P1;
draw P2 shifted 240 down;
\end{mplibcode}
$$
\vfill
\contrib{Leonard M.\@ Smiley}
\section{Geometry of subtraction formulas}
\vfill
$$
\begin{mplibcode}
% DUMP capture=False, replace="% Using the same angle_mark macro defined above..."
input markle    
% DUMP capture=True
numeric alpha, beta; alpha = 62; beta = 1/2 alpha;

z4 = 144 up;
z1 = whatever * right; z4-z1 = whatever * dir (90 + beta);
z2 = whatever * right; z4-z2 = whatever * dir (90 + alpha);
z3 = whatever[z2, z4]; z1-z3 = whatever * dir alpha;

picture P[]; 
P0 = image(   % -------------- the common parts
  % mark the right angles
  draw unitsquare scaled 6 withpen pencircle scaled 1/4;
  draw unitsquare scaled 6 rotated angle (z1-z3) shifted z3 withpen pencircle scaled 1/4;
  % mark the other angles
  angle_mark(origin, z4, z1, 48, "$\beta$", Blues 6 5);
  angle_mark(z1, z4, z2, 42, "$\alpha-\beta$", Blues 6 5);
  % do the complicated angle mark by hand...
  draw subpath (6, 6+alpha/45) of fullcircle scaled 72 shifted z4 withpen pencircle scaled 1/4;
  picture A; A = thelabel("$\alpha$", 28 down rotated 1/2 alpha shifted z4); 
  % draw the triangle and the internal lines
  draw z4 -- origin -- z2 -- z4 -- z1 -- z3;
  % and now erase the background and add the alpha label
  unfill bbox A; draw A withcolor Blues 6 5;
);
P1 = image(
  draw P0;
  % labels...
  drawoptions(withcolor Reds 6 5);
    label.urt("$h$", 1/2[z1,z4]); 
    label.urt("$1$", 1/2[z2,z4]); 
    label.lrt("$x$", 1/2[z1,z3]);
    label.bot("$h\sin\beta$", 1/2z1);
    label.bot("$\sin\alpha-h\sin\beta$", 1/2[z1,z2]);
  drawoptions();
  label("\vbox{\openup6pt\halign{\hfil$\displaystyle #$&$\displaystyle{}=#$\hfil\cr" &
    "                 h & \cos\alpha/\cos\beta \cr" &
    "                 x & h\sin(\alpha-\beta) \cr" &
    "                 x & (\sin\alpha-h\sin\beta)\cos\alpha \cr" &
    "\sin(\alpha-\beta) & \sin\alpha\cos\beta - \cos\alpha\sin\beta \cr }}", (x2, y4-12));            
);
% DUMP capture=False, replace="% P2 is the same except for the labels"
P2 = image(
  draw P0;
  drawoptions(withcolor Reds 6 5);
    label.urt("$h$", 1/2[z1, z4]);
    label.lft("$x$", 1/2z4);
    label.bot("$1$", 1/2[z1, z2]);
    draw thelabel.top(TEX("$\cos\alpha$"), origin) rotated angle (z3-z1) shifted 1/2[z1, z3];
    draw thelabel.top(TEX("$\sin\alpha$"), origin) rotated angle (z2-z3) shifted 1/2[z2, z3];
    draw thelabel.top(TEX("$h\cos\beta$"), origin) rotated angle (z3-z4) shifted 1/2[z4, z3];
  drawoptions();
  label("\vbox{\openup6pt\halign{\hfil$\displaystyle #$&$\displaystyle{}=#$\hfil\cr" &
  "h&\cos\alpha/\sin\beta\cr" &
  "x&h\cos(\alpha-\beta)\cr" &
  "x&(\sin\alpha+h\cos\beta)\cos\alpha\cr" &
  "\cos(\alpha-\beta) & \cos\alpha\cos\beta + \sin\alpha\sin\beta\cr}}", (x2, y4-12));            
);
% DUMP capture=True
draw P1;
draw P2 shifted 240 down;
\end{mplibcode}
$$
\vfill
\contrib{Leonard M.\@ Smiley}
\section{The difference identity for tangents I}
\vfill
$$
\begin{mplibcode}
numeric alpha, beta;
alpha = beta + 16; beta = 52;
z.C = origin;
z.B = 89 left;
x.A = x.D = x.C = y.F;
z.A - z.B = whatever * dir alpha;
z.D - z.B = whatever * dir beta;
z.E = whatever[z.A, z.B]; z.E - z.D = whatever * (z.A - z.B) rotated 90;
z.F = whatever[z.D, z.E];

% DUMP capture=False, replace="% Using the same angle_mark macro defined above..."
input markle    
% DUMP capture=True, replace=" "
angle_mark(z.A, z.D, z.E, 16, "$\alpha$", Blues 6 5);
angle_mark(z.C, z.D, z.F, 16, "$\alpha$", Blues 6 5);
angle_mark(z.C, z.B, z.D, 32, "$\beta$", Blues 6 5);
angle_mark(z.C, z.B, z.A, 16, "$\alpha$", Blues 6 5);
angle_mark(z.D, z.B, z.A, 52, "$\scriptstyle\alpha-\beta$", Blues 6 5);

draw unitsquare scaled 6 rotated angle (z.B-z.E) shifted z.E withpen pencircle scaled 1/4;
draw unitsquare scaled 6 rotated angle (z.A-z.C) shifted z.C withpen pencircle scaled 1/4;

drawoptions(withcolor Reds 6 5);
label.rt("$\tan\beta$", 1/2[z.C, z.D]);
label.rt("$\tan\alpha-\tan\beta$", 1/2[z.A, z.D]);
label.bot("\strut$1$", 1/2[z.B, z.C]);
label.bot("\strut$\tan\alpha\tan\beta$", 1/2[z.C, z.F]);
drawoptions();

draw z.C--z.A--z.B--z.F--z.E; draw z.B--z.D;
dotlabel.top ("$A$", z.A);
dotlabel.llft("$B$", z.B);
dotlabel.bot ("$C$", z.C);
dotlabel.urt ("$D$", z.D);
dotlabel.ulft("$E$", z.E);
dotlabel.lrt ("$F$", z.F);

label.bot(btex \vbox{\openup8pt\halign{\hfil$\displaystyle#$&$\displaystyle{}=#$\hfil\cr
{BF\over BE}&{AD\over DE}\cr
\therefore\quad\tan(\alpha-\beta)={DE\over BE}&
{AD\over BF} ={\tan\alpha-\tan\beta\over1+\tan\alpha\tan\beta}\cr
}} etex, point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Guanshen Ren}
\section{The difference identity for tangents II}
\vfill
$$
\begin{mplibcode}
numeric alpha, beta; alpha = beta + 18; beta = 42;
z.A = 216 right;
x.A = x.B = x.C = x.D;
z.B = whatever * dir beta;
z.C = whatever * dir alpha;
z.E = whatever [origin, z.C];
z.E - z.B = whatever * z.B rotated 90;
y.E = y.D;

% DUMP capture=False, replace="% Using the same angle_mark macro defined above..."
input markle    
% DUMP capture=True, replace=" "
angle_mark(z.A, origin, z.C, 28, "$\alpha$", Blues 6 5);
angle_mark(z.A, origin, z.B, 42, "$\beta$", Blues 6 5);
angle_mark(z.B, origin, z.C, 52, "$\scriptstyle\alpha-\beta$", Blues 6 5);
angle_mark(z.D, z.B, z.E, 16, "$\beta$", Blues 6 5);
angle_mark(z.D, z.E, z.C, 16, "$\alpha$", Blues 6 5);

draw unitsquare scaled 6 rotated 90        shifted z.A withpen pencircle scaled 1/4;
draw unitsquare scaled 6 rotated (90+beta) shifted z.B withpen pencircle scaled 1/4;
draw unitsquare scaled 6 rotated 90        shifted z.D withpen pencircle scaled 1/4;

drawoptions(withcolor Reds 6 5);
label.bot("$1$", 1/2 z.A);
label.rt("$\tan\beta$", 1/2[z.A, z.B]);
label.rt("$\tan(\alpha-\beta)$", 1/2[z.B, z.D]);
label.rt("$\tan(\alpha-\beta)\tan\beta\tan\alpha$", 1/2[z.C, z.D]);
draw thelabel.top("$\sec\beta$", origin) 
    rotated beta shifted 1/2 z.B;
draw thelabel.bot("$\sec\beta\tan(\alpha-\beta)$", origin) 
    rotated (beta-90) shifted 1/2[z.B, z.E];
drawoptions();

draw origin -- z.A -- z.C -- cycle; 
draw origin -- z.B -- z.E -- z.D;
dotlabel.bot ("$A$", z.A);
dotlabel.rt  ("$B$", z.B);
dotlabel.rt  ("$C$", z.C);
dotlabel.rt  ("$D$", z.D);
dotlabel.ulft("$E$", z.E);
dotlabel.llft("$O$", origin);

label.bot(btex \vbox{\openup8pt\halign{\hfil$\displaystyle#$&$\displaystyle{}=#$\hfil\cr
AC-AB&BD+DC\cr
\therefore\tan\alpha-\tan\beta&\tan(\alpha-\beta)+\tan\alpha\tan\beta\tan(\alpha-\beta)\cr
\tan(\alpha-\beta)&{\tan\alpha-\tan\beta\over1+\tan\alpha\tan\beta}\cr
}} etex, point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Fukuzo Suzuki}
%--------------------------------------
\section{One figure, six identities}

\vbox to 0pt{\noindent\begin{mplibcode}
picture F; path t, s;
t = (origin -- (200, 0) -- (200, 100) -- cycle) rotated (angle (1,2) - angle (2,1)) ;
interim bboxmargin := 0; s = bbox t;
F = image(draw point 2 of t -- (xpart point 2 of t, 0) dashed evenly; 
         draw unitsquare scaled 6 rotated (90  + angle point 1 of t) shifted point 1 of t;
         draw t; draw s);
draw F scaled 1/2;
label.bot("The figure", point 1/4 of bbox F);
\end{mplibcode}\vss}

$$
\begin{mplibcode}
picture F[]; path t, t', t'', s; numeric alpha; alpha = angle(1, 2) - angle (2, 1);
t = (origin -- (180, 0) -- (180, 90) -- cycle) rotated alpha;
t' = t reflectedabout(point 0 of t, point 2 of t);
t'' = t' rotatedabout(point 5/2 of t, 180);
interim bboxmargin := 0; s = bbox t;
vardef angled_label@#(expr t, p, r) = draw thelabel@#(t, origin) rotated r shifted p; enddef;
F0 = image(draw point 2 of t -- (xpart point 2 of t, 0) dashed evenly; 
         draw unitsquare scaled 6 rotated (90  + angle point 1 of t) shifted point 1 of t;
         draw t; draw s);
F1 = image(
  fill t'' withcolor Blues 8 3;
  path a[];
  a1 = quartercircle scaled 32 cutafter subpath (0,1) of t;
  a2 = quartercircle scaled 42 cutbefore subpath (0,1) of t cutafter subpath (2,3) of t;
  a3 = quartercircle scaled 32 rotated 90 shifted point 1 of t cutafter subpath (1,2) of t;
  forsuffixes $=1,2,3: draw a$ withpen pencircle scaled 1/4; endfor
  drawoptions(withcolor 2/3 blue);
    label.rt("$\alpha$", point arctime 1/2 arclength a1 of a1 of a1 shifted 2 up);
    label.urt("$\beta$", point arctime 1/2 arclength a2 of a2 of a2);
    label.top("$\alpha$", point arctime 1/2 arclength a3 of a3 of a3);
  drawoptions();
  draw F0;
  drawoptions(withcolor Reds 6 5);
    label.ulft("$1$", point 5/2 of t);
    label.bot("$\cos\alpha\cos\beta$", point 1/2 of s);
    label.top("$\sin\alpha\sin\beta$", 1/2[point 2 of t, point 2 of s]);
    angled_label.top("$\cos\beta$", point 1/2 of t, alpha);
    angled_label.bot("$\sin\beta$", point 3/2 of t, alpha-90);
    angled_label.top("$\cos\alpha\sin\beta$", 1/2[point 1 of t, point 2 of s], -90);
    angled_label.top("$\sin\alpha\cos\beta$", 1/2[point 1 of t, point 1 of s], -90);
  drawoptions();
  label.lft("$\sin(\alpha+\beta) = \sin\alpha\cos\beta + \cos\alpha\sin\beta$", (-10, 32));
  label.lft("$\cos(\alpha+\beta) = \cos\alpha\cos\beta - \sin\alpha\sin\beta$", (-10, 16));
);
% DUMP capture=False, replace="% more of the same for F2 ..."
F2 = image(
   fill t' withcolor Blues 8 3;
   path a[];
   a1 = quartercircle scaled 58 cutbefore subpath (0,1) of t;
   a2 = quartercircle scaled 68 cutbefore subpath (0,1) of t cutafter subpath (2,3) of t;
   a3 = quartercircle scaled 32 rotated 180 shifted point 1 of t cutbefore subpath (0, 1) of t;
   a4 = quartercircle scaled 32 rotated 270 shifted point 2 of t cutbefore subpath (1, 2) of t;
   forsuffixes $=1,2,3, 4: draw a$ withpen pencircle scaled 1/4; endfor
   draw F0; 
   undraw subpath (-35/256, -11/128) of t;
   drawoptions(withcolor 2/3 blue);
   label.bot("$\alpha$", point arctime 1/2 arclength a1 of a1 of a1 shifted 2.4 left);
   label.urt("$\beta$", point arctime 1/2 arclength a2 of a2 of a2) ;
   label.llft("$\alpha$", point arctime 5/8 arclength a3 of a3 of a3);
   label.lrt("$\alpha$", point arctime 5/8 arclength a4 of a4 of a4);
   drawoptions(withcolor Reds 6 5);
   label.ulft("$1$", point 5/2 of t);
   label.bot("$\sin\alpha\cos\beta$", point 1/2 of s);
   label.top("$\cos\alpha\sin\beta$", 1/2[point 2 of t, point 2 of s]);
   draw thelabel.top("$\cos\beta$", origin) rotated alpha shifted point 1/2 of t;
   draw thelabel.bot("$\sin\beta$", origin) rotated (alpha-90) shifted point 3/2 of t;
   draw thelabel.top("$\sin\alpha\sin\beta$", origin) rotated -90 shifted 1/2[point 1 of t, point 2 of s];
   draw thelabel.top("$\cos\alpha\cos\beta$", origin) rotated -90 shifted 1/2[point 1 of t, point 1 of s];
   drawoptions();
   label.lft("$\sin(\alpha-\beta) = \sin\alpha\cos\beta - \cos\alpha\sin\beta$", (-10, 32));
   label.lft("$\cos(\alpha-\beta) = \cos\alpha\cos\beta + \sin\alpha\sin\beta$", (-10, 16));
);
% DUMP capture=True
draw F1;
draw F2 shifted 280 down;

\end{mplibcode}
$$
\vfill
\section{One figure, six identities}
\vfill
$$
\begin{mplibcode}
% DUMP capture=False, replace="% very similar to previous page ..."
vardef angled_label@#(expr t, p, r) = draw thelabel@#(t, origin) rotated r shifted p; enddef;
picture F[]; path t, t', t'', s; numeric alpha; alpha = angle(1, 2) - angle (2, 1);
t = (origin -- (180, 0) -- (180, 90) -- cycle) rotated alpha;
t' = t reflectedabout(point 0 of t, point 2 of t);
t'' = t' rotatedabout(point 5/2 of t, 180);
interim bboxmargin := 0; s = bbox t;
F0 = image(draw point 2 of t -- (xpart point 2 of t, 0) dashed evenly; 
         draw unitsquare scaled 6 rotated (90  + angle point 1 of t) shifted point 1 of t;
         draw t; draw s);
F1 = image(
   fill t'' withcolor Oranges 8 3;
   path a[];
   a1 = quartercircle scaled 32 cutafter subpath (0,1) of t;
   a2 = quartercircle scaled 42 cutbefore subpath (0,1) of t cutafter subpath (2,3) of t;
   a3 = quartercircle scaled 32 rotated 90 shifted point 1 of t cutafter subpath (1,2) of t;
   forsuffixes $=1,2,3: draw a$ withpen pencircle scaled 1/4; endfor
   drawoptions(withcolor 2/3 blue);
   label.rt("$\alpha$", point arctime 1/2 arclength a1 of a1 of a1 shifted 2 up);
   label.urt("$\beta$", point arctime 1/2 arclength a2 of a2 of a2);
   label.top("$\alpha$", point arctime 1/2 arclength a3 of a3 of a3);
   drawoptions();
   draw F0;
   drawoptions(withcolor Reds 6 5);
   label.bot("$1$", point 1/2 of s);
   label.top("$\tan\alpha\tan\beta$", 1/2[point 2 of t, point 2 of s]);
   angled_label.top("$\sec\alpha$", point 1/2 of t, alpha);
   angled_label.bot("$\sec\alpha\tan\beta$", point 3/2 of t, alpha-90);
   angled_label.top("$\tan\beta$", 1/2[point 1 of t, point 2 of s], -90);
   angled_label.top("$\tan\alpha$", 1/2[point 1 of t, point 1 of s], -90);
   drawoptions();
   label.lft("$\displaystyle\tan(\alpha+\beta) = {\tan\alpha+\tan\beta\over1 + \tan\alpha\tan\beta}$", (-10, 24));
);
% DUMP capture=True
F2 = image(
  fill t' withcolor Oranges 8 3;
  path a[];
  a1 = quartercircle scaled 58 cutbefore subpath (0,1) of t;
  a2 = quartercircle scaled 68 cutbefore subpath (0,1) of t cutafter subpath (2,3) of t;
  a3 = quartercircle scaled 32 rotated 180 shifted point 1 of t cutbefore subpath (0, 1) of t;
  a4 = quartercircle scaled 32 rotated 270 shifted point 2 of t cutbefore subpath (1, 2) of t;
  forsuffixes $=1,2,3, 4: draw a$ withpen pencircle scaled 1/4; endfor
  draw F0; 
  undraw subpath (-35/256, -11/128) of t;
  drawoptions(withcolor 2/3 blue);
  label.bot("$\alpha$", point arctime 1/2 arclength a1 of a1 of a1 shifted 2.4 left);
  label.urt("$\beta$", point arctime 1/2 arclength a2 of a2 of a2) ;
  label.llft("$\alpha$", point arctime 5/8 arclength a3 of a3 of a3);
  label.lrt("$\alpha$", point arctime 5/8 arclength a4 of a4 of a4);
  drawoptions(withcolor Reds 6 5);
  label.bot("$\tan\alpha$", point 1/2 of s);
  label.top("$\tan\beta$", 1/2[point 2 of t, point 2 of s]);
  angled_label.top("$\sec\alpha$", point 1/2 of t, alpha);
  angled_label.bot("$\sec\alpha\tan\beta$", point 3/2 of t, (alpha-90));
  angled_label.top("$\tan\alpha\tan\beta$", 1/2[point 1 of t, point 2 of s], -90);
  label.rt("$1$", 1/2[point 1 of t, point 1 of s]);
  drawoptions();
  label.lft(btex $\displaystyle\tan(\alpha-\beta) = 
     {\tan\alpha-\tan\beta\over1 + \tan\alpha\tan\beta}$ etex, (-10, 24));
);

draw F1;
draw F2 shifted 280 down;

\end{mplibcode}
$$
\vfill
\contrib{RBN}
%==========================
\section{The double-angle formulas II}
\vfill
$$
\begin{mplibcode}
vardef mark_angle(expr a, o, b, r, rr, t) =
  draw fullcircle scaled 2r rotated angle (a-o) shifted o cutafter (o--b)
      withpen pencircle scaled 1/4;
  label(t, o shifted (rr * unitvector(unitvector(a-o)+unitvector(b-o))));
enddef;
vardef rotated_label@#(expr t, p, r) =
  draw thelabel@#(t, origin) rotated r shifted p 
enddef;

path R; R = unitsquare xscaled 144 yscaled 89;  
z0 = center R;
picture P[]; 
P1 = image(
  fill subpath (-1, 1) of R -- cycle withcolor Blues 8 3;
  mark_angle(point 3 of R, point 1 of R, point 0 of R, 18, 24, "$\theta$");
  draw subpath (-1, 3) of R -- point 1 of R;
  label.urt("$2$", z0);
  rotated_label.top("$2\sin\theta$", point -1/2 of R, 90);
  label.bot("$2\cos\theta$", point  1/2 of R);
);
P2 = image(
  pair p; p = whatever[point 1 of R, point 3 of R]; 
  p - point 2 of R = whatever * (point 1 of R - point 3 of R) rotated 90;
  fill subpath (3, 1) of R -- cycle withcolor Blues 8 3;
  mark_angle(point 3 of R, point 1 of R, point 0 of R, 18, 24, "$\theta$");
  mark_angle(point 1 of R, point 0 of R, point 2 of R, 18, 24, "$\theta$");
  mark_angle(point 1 of R, z0, point 2 of R, 16, 24, "$2\theta$");
  draw subpath (-1, 3) of R -- point 1 of R;
  draw p -- point 2 of R -- point 0 of R;
  label.ulft("$1$", 1/4[point 0 of R, point 2 of R]);
  label.ulft("$1$", 3/4[point 0 of R, point 2 of R]);
  rotated_label.bot("$\sin2\theta$", 1/2[p, point 2 of R], angle (point 2 of R - p));
);
P3 = image(draw P1 shifted 89 left; draw P2 shifted 89 right;
  label.bot("$2\sin\theta\cos\theta = \sin 2\theta$", 
  point 1/2 of bbox currentpicture shifted 21 down)
);
% DUMP capture=False, replace="% P4, P5, and P6 are similar to P1, P2, and P3"
path S; S = unitsquare yscaled -1/2 scaled abs(point 1 of R - point 0 of R);
P4 = image(
  mark_angle(point 1 of S, point 0 of S, z0, 18, 24, "$\theta$");
  mark_angle(z0, point 1 of S, point 0 of S, 18, 24, "$\theta$");
  fill S withcolor Greens 8 3;
  draw point 1 of S -- z0 -- S;
  draw z0 -- (x0, 0) dashed evenly scaled 1/2;
  label.urt("$1$", 1/2[z0, point 1 of S]);
  label.ulft("$1$", 1/2[z0, point 0 of S]);
  label.bot("$\cos\theta$", point 1/4 of S);
  label.bot("$\cos\theta$", point 3/4 of S);
  label.bot("$2\cos\theta$", point 5/2 of S);
  rotated_label.top("$\cos\theta$", point -1/2 of S, 90);
);
path T; T = subpath (0, 1) of S -- subpath(2, 3) of S shifted (y0, 0) -- cycle;
z1 = whatever[point 0 of T, z0] = whatever[point 1 of T, point 2 of T];
z2 = z1 rotatedaround(1/2[point 0 of T, point 2 of T], 180);
P5 = image(
  fill T withcolor Greens 8 3;
  mark_angle(point 1 of S, point 0 of S, z0, 18, 24, "$\theta$");
  mark_angle(z0, point 1 of S, point 0 of S, 18, 24, "$\theta$");
  mark_angle(point 3 of S, point 0 of T, point 3 of T, 18, 24, "$\theta$");
  mark_angle(point 1 of S, z0, z1, 14, 21, "$2\theta$");
  draw point 0 of S -- z1 -- point 1 of S -- z0;
  draw z0 -- (x0, 0) dashed evenly scaled 1/2;
  draw subpath (4, 3) of S -- point 3 of T -- z2 dashed evenly scaled 1/2;
  draw point 2 of T -- z2; draw T;
  label.urt("$1$", 1/2[z0, point 1 of S]);
  label.ulft("$1$", 1/2[z0, point 0 of S]);
  label.llft("$1$", point -1/2 of T);
  label.urt("$1$", point 3/2 of T);
  rotated_label.top("$\cos2\theta$", 1/2[z0, z1], angle z0);
  rotated_label.top("$1+\cos2\theta$", 1/2[z2, point 2 of T], angle z0);
% DUMP capture=True
);
P6 = image(draw P4 shifted 89 left; draw P5 shifted 89 right;
          label("$2\cos^2\theta = 1 + \cos 2\theta$", 
          point 1/2 of bbox currentpicture shifted 21 down));
label.top(P3, 10 up); label.bot(P6, 10 down);
\end{mplibcode}
$$

\vfill
\contrib{Yihnan David Gau}
%-------------------------------
\section{The double-angle formulas III (via the laws of sines and cosines)}
\vfill
$$
\begin{mplibcode}
path t; t = 216 left -- 84 down -- 84 up -- cycle;
fill subpath (3/2, 3) of t -- cycle withcolor Oranges 8 3;

draw unitsquare scaled 5 rotated 90 shifted point 3/2 of t
                withpen pencircle scaled 1/4;

draw fullcircle scaled 24 rotated 90 shifted point 1 of t 
                cutafter subpath(0,1) of t 
                withpen pencircle scaled 1/4;

draw point 0 of t -- point 3/2 of t dashed evenly scaled 1/2;
draw t; 

label.ulft("$1$", point -1/2 of t);
label.llft("$1$", point 1/2 of t);
label.rt("$\sin\theta$", point 5/4 of t);
label.rt("$\sin\theta$", point 7/4 of t);

numeric theta; theta = angle (point 2 of t - point 0 of t);
label("$\theta$", point 0 of t shifted 36 dir 1/2 theta);
label("$\theta$", point 0 of t shifted 36 dir -1/2 theta);
label(TEX("$\displaystyle {\pi\over2}-\theta$") scaled 3/4, 
    point 1 of t shifted 24 dir (90 + 1/2 (90 - theta)));

label.bot(btex \vbox{\openup6pt\halign{\hfil$\displaystyle # $\hfil\cr
{\sin2\theta\over2\sin\theta} = {\sin(\pi/2 - \theta) \over 1} = \cos\theta\cr
\sin2\theta = 2\sin\theta\cos\theta\cr
\noalign{\vskip36pt}
\left(2\sin\theta\right)^2 = 1^2 + 1^2 - 2 \cdot 1 \cdot 1 \cdot \cos2\theta\cr
\cos2\theta = 1 - 2\sin^2\theta \cr
}} etex, point 1/2 of bbox currentpicture shifted 21 down);
\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}
%--------------
\section{The sum-to-product identities I}
\vfill
$$
\begin{mplibcode}
numeric alpha, beta, gamma, theta, u;
theta = 1/2 (alpha - beta); gamma = 1/2 (alpha + beta); alpha = 117; beta = 42;
u = \mpdim{\hsize} / 2.236;

path xx, yy, hh;
xx = (1.1 left -- 1.1 right) scaled u;
yy = (0.1 down -- 1.1 up) scaled u;
hh = halfcircle scaled 2u;

z0 = point 0 of hh rotated alpha;
z1 = point 0 of hh rotated beta;
z2 = whatever[z0, z1] = whatever * point 0 of hh rotated gamma;

drawoptions(withpen pencircle scaled 1/4);
  draw fullcircle scaled 72 cutafter (origin--z0); 
  draw fullcircle scaled 84 cutafter (origin--z1); 
  draw fullcircle scaled 24 cutafter (origin--z2); 
  draw fullcircle scaled 36 cutafter (origin--z0) cutbefore (origin--z2); 
  draw unitsquare scaled 5 rotated 90 shifted (x0, 0);
  draw unitsquare scaled 5 rotated 90 shifted (x1, 0);
  draw unitsquare scaled 5 rotated 90 shifted (x2, 0);
  draw unitsquare scaled 5 rotated angle (z0-z1) shifted z2;
drawoptions();

draw (x0,0) -- z0 -- z1 -- (x1,0);
draw origin -- z0;
draw origin -- z1;

draw hh withcolor 2/3 blue;
drawarrow xx; drawarrow yy;
draw origin -- (x2, 0) -- z2 -- cycle withcolor 2/3 red;
dotlabel.urt("$(t, s)$", z2) withcolor 1/3 red;
dotlabel.urt("$(\cos\beta, \sin\beta)$", z1);
dotlabel.ulft("$(\cos\alpha, \sin\alpha)$", z0);
dotlabel.llft("$0$", origin);
dotlabel.bot("$1$", point 0 of hh);
dotlabel.bot("$-1$", point 4 of hh);
label("$\alpha$", 42 dir 1/2 alpha);
label("$\beta$", 48 dir 1/2 beta);
label("$\gamma$", 18 dir (beta+1/2(gamma-beta))); 
label("$\theta$", 24 dir (gamma + 4 + 1/2 (alpha-gamma))); 
label.bot(btex \vbox{\openup 12pt\halign{\hfil #\hfil\cr
    $\displaystyle \theta = {\alpha-\beta\over2}$, \quad 
    $\displaystyle \gamma = {\alpha + \beta \over 2}$\cr
    $\displaystyle {\sin\alpha+\sin\beta\over2}=s
      =\cos{\alpha-\beta\over2}\sin{\alpha+\beta\over2}$\cr
    $\displaystyle {\cos\alpha+\cos\beta\over2}=t
      =\cos{\alpha-\beta\over2}\cos{\alpha+\beta\over2}$\cr}} etex,
    point 1/2 of bbox currentpicture shifted 34 down);
\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}
\section{The difference-to-product identities I}
\vfill
$$
\begin{mplibcode}
numeric alpha, beta, gamma, theta, u;
theta = 1/2 (alpha - beta); gamma = 1/2 (alpha + beta); alpha = 117; beta = 42;
u = \mpdim{\hsize} / 2.236;
path xx; xx = (1.1 left -- 1.1 right) scaled u;
path yy; yy = (0.1 down -- 1.1 up) scaled u;
path hh; hh = halfcircle scaled 2u;
z0 = point 0 of hh rotated alpha;
z1 = point 0 of hh rotated beta;
z2 = whatever[z0, z1] = whatever * point 0 of hh rotated gamma;
drawoptions(withpen pencircle scaled 1/4);
  draw fullcircle scaled 72 cutafter (origin--z0); 
  draw fullcircle scaled 84 cutafter (origin--z1); 
  draw fullcircle scaled 24 cutafter (origin--z2); 
  draw fullcircle scaled 36 cutafter (origin--z0) cutbefore (origin -- z2);
  draw quartercircle scaled 24 rotated -90 shifted z0 cutafter (z0--z1); 
  draw unitsquare scaled 5 shifted (x0, y1);
  draw unitsquare scaled 5 rotated 90 shifted (x0, 0);
  draw unitsquare scaled 5 rotated 90 shifted (x1, 0);
  draw unitsquare scaled 5 rotated angle (z0-z1) shifted z2;
drawoptions();
draw (x0,0) -- z0 -- z1 -- (x1,0);
draw origin -- z0; draw origin -- z1; draw origin -- z2;
draw hh withcolor 2/3 blue;
drawarrow xx; drawarrow yy;
draw (x0, y1) -- z1 -- z0 -- cycle withcolor 2/3 red;
dotlabel.urt("$(\cos\beta, \sin\beta)$", z1);
dotlabel.ulft("$(\cos\alpha, \sin\alpha)$", z0);
label.lft("$v$", (x0, 1/2 y0 + 1/2 y1)) withcolor 1/3 red;
label.bot("$u$", (1/2 x0 + 1/2 x1 - 10 , y1)) withcolor 1/3 red;
label.urt("$2\sin{\alpha-\beta\over2}$", 1/2[z0, z1]) withcolor 1/3 red;
dotlabel.llft("$0$", origin);
dotlabel.bot("$1$", point 0 of hh);
dotlabel.bot("$-1$", point 4 of hh);
label("$\alpha$", 42 dir 1/2 alpha);
label("$\beta$", 48 dir 1/2 beta);
label("$\gamma$", 18 dir (beta+1/2(gamma-beta))); 
label("$\theta$", 24 dir (gamma + 4 + 1/2 (alpha-gamma))); 
label("$\gamma$", z0 + 18 dir (1/2 gamma-86)); 
\end{mplibcode}
$$
\bigskip
$$\vbox{\openup 8pt\halign{\hfil #\hfil\cr
$\displaystyle \theta = {\alpha-\beta\over2}$, \quad $\displaystyle \gamma = {\alpha + \beta \over 2}$\cr
$\displaystyle \sin\alpha-\sin\beta = v = 2\sin{\alpha-\beta\over2}\cos{\alpha+\beta\over2}$\cr
$\displaystyle \cos\beta-\cos\alpha = u = 2\sin{\alpha-\beta\over2}\sin{\alpha+\beta\over2}$\cr
}}$$
\vfill
\contrib{Sidney H.\@ Kung}
%--------------------
\section{The sum-to-product identities II}
\vfill
$$
\begin{mplibcode}
numeric u, alpha, beta; u = 220; alpha = 68; beta = 42;
z1 = right rotated alpha scaled u;
z2 = right rotated beta scaled -u;
z3 = whatever * z1; y3 = y2;
z4 = whatever[z1, z2]; z4 rotated 90 = whatever * (z1-z2);

fill z1 -- z2 -- (x1, y2) -- cycle withcolor Greens 9 1;
fill origin -- z1 -- (x1, 0) -- cycle withcolor 3/4[Greens 9 1, Blues 9 2];
fill origin -- z2 -- (0, y2) -- cycle withcolor 3/4[Greens 9 1, Oranges 9 2];

drawoptions(withpen pencircle scaled 1/4);
draw subpath (1,3) of unitsquare scaled 5 rotated (180 + 1/2 alpha + 1/2 beta) shifted z4;
draw quartercircle scaled 52 shifted z2 cutafter (origin -- z2);
draw quartercircle scaled 76 shifted z2 cutafter (z1 -- z2);

drawoptions(dashed evenly scaled 1/2 withcolor 1/2 white);
draw z1--z2;
draw (0,y2) -- (x1,y2) -- z1;
draw origin -- z3;
draw origin -- z4;
draw origin -- (0, y2);
draw origin -- (x1, 0);
drawoptions();
draw (0,y2) -- z2 -- origin -- z1;

label("$\alpha$", 10 dir 1/2 alpha); 
label("$\alpha$", 10 dir 1/2 alpha shifted z3); 
label("$\beta$", 18 dir 1/2 beta shifted z2); 
label("$\alpha+\beta\over2$", 48 dir 1/4 (alpha + beta) shifted z2); 
label.ulft("$2\cos{\alpha-\beta\over2}$", z4);
label.bot("\strut$\cos\alpha$", (1/2 x1, y2));
label.bot("\strut$\cos\beta$", (1/2 x2, y2));
label.rt("$\sin\alpha$", (x1, 1/2 y1));
label.rt("$\sin\beta$", (x1, 1/2 y2));
label.ulft("$1$", 1/2 z1);
label.ulft("$1$", 1/2 z2);

draw quartercircle scaled 64 rotated (180 + 1/2 alpha + 1/2 beta) shifted z1
     cutafter (origin -- z1) withpen pencircle scaled 1/4;
draw quartercircle scaled 64 rotated beta shifted z2 
     cutafter (z1 -- z2) withpen pencircle scaled 1/4;

z8 = z1 shifted 36 left shifted 12 down;
z9 = z2 shifted 36 up;

label.top("$\alpha-\beta\over2$", z8);
label.top("$\alpha-\beta\over2$", z9);

drawarrow z8 {dir -42} .. 28 dir (180 + 1/4 beta + 3/4 alpha) shifted z1; 
drawarrow z9 {dir -60} .. 28 dir (3/4 beta + 1/4 alpha) shifted z2; 

\end{mplibcode}
$$
\vfill
\centerline{$\cos\alpha + \cos\beta = 2 \cos {\alpha - \beta \over 2} \cos {\alpha + \beta \over 2}$}
\bigskip
\centerline{$\sin\alpha + \sin\beta = 2 \cos {\alpha - \beta \over 2} \sin {\alpha + \beta \over 2}$}
\vfill
\contrib{Yukio Kobayashi}
%--------------------
\section{The difference-to-product identities II}
\vfill
$$
\begin{mplibcode}
numeric u, alpha, beta; u = 377; alpha = 56; beta = 22;
z1 = right rotated alpha scaled u;
z2 = right rotated beta scaled u;
z3 = (x1, y2);
z4 = (x1, 0);
z5 = (x2, 0);
z6 = 1/2[z1, z2];
z7 = whatever[origin, z2] = whatever[z1, z4];

fill origin -- z1 -- z4 -- cycle withcolor Blues 9 2;
fill origin -- z2 -- z5 -- cycle withcolor Oranges 9 2;
fill origin -- z7 -- z4 -- cycle withcolor 1/2[Blues 9 2, Oranges 9 2];
fill z1 -- z2 -- z3 -- cycle withcolor Greens 9 1;

drawoptions(withpen pencircle scaled 1/4);
draw subpath (1,3) of unitsquare scaled 5 shifted z3;
draw subpath (1,3) of unitsquare scaled 5 rotated angle (z1-z2) shifted z6;
path a[];
a1 = quartercircle scaled 72 cutafter (origin -- z1); draw a1;
a2 = quartercircle scaled 90 cutafter (origin -- z2); draw a2;
a3 = quartercircle scaled 108 rotated angle z6 cutafter (origin -- z1); draw a3;
a4 = quartercircle scaled 64 rotated -90 shifted z1 cutafter (z1 -- z2); draw a4;
label.rt("$\alpha$", point arctime 5/8 arclength a1 of a1 of a1);
label.rt("$\beta$", point arctime 1/2 arclength a2 of a2 of a2);

z8 = point infinity of a3 shifted (-8, 8);  label.top("$\alpha-\beta\over2$", z8);
z9 = z1                   shifted (20, -8); label.top("$\alpha+\beta\over2$", z9);
drawarrow z8 {down} .. 49 dir (alpha - 3/8 beta);
drawarrow z9 {down} .. 28 dir (1/4 alpha + 1/4 beta - 90) shifted z1;

drawoptions(dashed evenly scaled 1/2 withcolor 1/2 white);
draw z3 -- z4; draw origin -- z6;
drawoptions(dashed evenly scaled 1/2);
draw z1 -- z3 -- z2;
drawoptions();
draw origin -- z1 -- z2 -- z5 -- origin -- z2;

label.urt("$2\sin{\alpha-\beta\over2}$", z6);
label.top("\strut$\cos\alpha$", 5/8 z4);
label.bot("\strut$\cos\beta$", 1/2 z5);
draw thelabel.top("$\sin\alpha$", origin) rotated 90 shifted 3/8[z4, z1];
draw thelabel.bot("$\sin\beta$", origin) rotated 90 shifted 1/2[z5, z2];
label.ulft("$1$", 1/2 z1);
label.ulft("$1$", 1/2 z2);
\end{mplibcode}
$$
\vfill
\centerline{$\cos\beta - \cos\alpha = 2 \sin {\alpha - \beta \over 2} \sin {\alpha + \beta \over 2}$}
\bigskip
\centerline{$\sin\alpha - \sin\beta = 2 \sin {\alpha - \beta \over 2} \cos {\alpha + \beta \over 2}$}
\vfill
\contrib{Yukio Kobayashi}


\section{Adding like sines}

\vfill
$$
\begin{mplibcode}
numeric x, theta, phi, s;
x = 36; theta = 24; phi = 49;
s = 4 in;

path box; box = unitsquare scaled s;
z0 = point 0 of box;
z1 - z0 = whatever * dir (x+theta); y1 = ypart point 2 of box;
z2 - z0 = whatever * dir x; 
z1 - z2 = whatever * dir (x + phi);
z3 = whatever [z0, z2]; z1-z3 = whatever * (z2-z0) rotated 90;
x4 = xpart point 1 of box; y4 = y2;

path ra_mark; 
ra_mark = subpath (1, 3) of unitsquare scaled 5;

drawoptions(withpen pencircle scaled 1/4 withcolor 1/2 white); 
draw ra_mark rotated 90 shifted point 1 of box;
draw ra_mark rotated 90 shifted z4;
draw ra_mark rotated 180 shifted point 2 of box;
draw ra_mark rotated 270 shifted point 3 of box;
draw box dashed evenly scaled 1/2;
draw z2--z4 dashed evenly scaled 1/2;

drawoptions(withpen pencircle scaled 1/4);
draw ra_mark rotated (90+x) shifted z3;
draw z1 -- z3 -- z2;

drawoptions(withcolor 2/3 blue);
draw z0 -- z1 -- z2 -- cycle;

drawoptions();

label("$x$", 24 dir 1/2 x shifted z0);
label("$\theta$", 32 dir (x + 1/2 theta) shifted z0);
label("$\phi$", 24 dir (x + 1/2 phi) shifted z2);

label.ulft("$R_\phi$", 1/2[z0, z1]);
label.lrt("$A$", 1/2[z0, z2]);
label.lrt("$B$", 1/2[z1, z2]);

draw thelabel.top("$B\sin\phi$", origin) rotated angle (z3 - z1) shifted 1/2[z1, z3];
draw thelabel.bot("$B\cos\phi$", origin) rotated angle (z3 - z2) shifted 1/2[z2, z3];
draw thelabel.top("$A\sin x$", origin) rotated 90 shifted 1/2[point 1 of box, z4];
draw thelabel.top("$B\sin (x+\phi)$", origin) rotated 90 shifted 1/2[point 2 of box, z4];

\end{mplibcode}$$

\bigskip
\let\implies\Rightarrow
$$\vbox{\openup8pt \halign{\hfil # \hfil\cr
$R_\phi = \sqrt{A^2 + B^2 + 2 AB \cos\phi}$, \quad $\tan\theta = {B\sin\phi\over
A+B\cos\phi}$\cr
$A\sin x + B \sin(x+\phi) = R_\phi \sin(x + \theta)$\cr
$\phi = \pi/2 \implies \tan\theta = B/A$\cr
$\therefore\quad A \sin x + B \cos x = \sqrt{A^2 + B^2} \sin(x + \theta)$\cr
}}$$
\vfill
\contrib{Rick Mabry and Paul Deiermann}

\section{A complex approach to the laws of sines and cosines}

\vfill
$$
\begin{mplibcode}
numeric a, b, c, alpha, beta, gamma;
alpha = 28;
gamma = 36;
beta = 180 - alpha - gamma;
z0 = origin;
z1 = 216 right;
z2 = whatever * dir alpha = whatever * dir (alpha + gamma) shifted z1;

drawoptions(dashed evenly scaled 1/2 withpen pencircle scaled 1/4 withcolor 1/2 white);
draw (right -- 36 right) shifted z1;
draw (left -- right) scaled 36 shifted z2;

drawoptions(withpen pencircle scaled 1/4);
draw quartercircle scaled 24 shifted z0 cutafter (z0--z2);
draw quartercircle scaled 24 rotated (180+alpha) shifted z2 cutafter (z1--z2);
interim ahangle := 25;
drawarrow subpath (8, 8 - beta/45 + 1/16) of fullcircle scaled 20 shifted z2;
drawarrow subpath (4 - beta/45, 4 - 1/16) of fullcircle scaled 20 shifted z1;

drawoptions(withcolor 2/3 blue);
drawarrow z0 -- z1 cutafter fullcircle scaled 2 shifted z1;
drawarrow z0 -- z2 cutafter fullcircle scaled 2 shifted z2;
drawarrow z2 -- z1 cutafter fullcircle scaled 2 shifted z1;
drawoptions();
drawdot z1;

label("$\alpha$", 24 dir 1/2 alpha shifted z0);
label("$-\beta$", 16 dir -1/2 beta shifted z2);
label("$\beta$",  16 dir (alpha + gamma + 1/2 beta) shifted z1);
label("$\gamma$", 24 dir (180 + alpha + 1/2 gamma) shifted z2);

label.bot("$c$", 1/2[z0, z1]);
label.ulft("$be^{i\alpha}$", 1/2[z0, z2]);
label.lrt("$ae^{-i\beta}$", 1/2[z1, z2]);

label(btex $c=be^{i\alpha} + ae^{-i\beta}
             =(b\cos\alpha + a\cos\beta) + i(b\sin\alpha - a\sin\beta)$ etex, 
    point 1/2 of bbox currentpicture shifted 36 down);

label(btex if $c$ is real, then $b\sin\alpha-a\sin\beta=0$, 
      hence $\displaystyle {a\over\sin\alpha}={b\over\sin\beta}$ etex,
    point 1/2 of bbox currentpicture shifted 36 down);

label(btex \vbox{\openup 6pt\halign{\hfil $#$&${}=#$\hfil\cr
c^2 & \left|c^2\right| = \left(b\cos\alpha + a\cos\beta\right)^2 + 
                         \left(b\sin\alpha - a\sin\beta\right)^2\cr
    & a^2 + b^2 + 2ab\cos(\alpha + \beta)\cr
    & a^2 + b^2 - 2ab\cos\gamma\cr}} etex, 
    point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{William V.\@ Grounds}

\section{Eisenstein's duplication forumula}

\vfill
$$
\begin{mplibcode}
numeric theta;
theta = 25;
z0 = origin;
z1 = 200 right;
z2 = z1 rotated 2 theta;
z3 - z2 = whatever * (z2-z0) rotated 90; x3 = x1;
z4 = whatever[z2, z3]; 
z5 = whatever[z1, z2]; 
x5 = x4 = x0;

draw quartercircle scaled 2 abs(z1) shifted z0 withcolor 2/3 red;
draw z1 -- z5;
draw z1 -- z3 -- z4;
draw z0 -- z2;
draw z0 -- z3;
draw z1 shifted 10 right -- z0 -- z5 shifted 10 up;

label.bot("$1$", z1) withcolor 2/3 red;

label("$\theta$", 32 dir 1/2 theta);
label("$\theta$", 32 dir 3/2 theta);
label("$\theta$", 32 dir (1/2 theta - 90) shifted z5);
label("$\theta$", 32 dir (3/2 theta + 90) shifted z2);
label("$2\theta$", 24 dir (theta - 90) shifted z4);

label.urt("$\tan\theta$", 1/2[z2, z3]);
picture mark; mark = image(for i=-1,1: draw (up--down) scaled 3 rotated 10 shifted (i,0) 
    withpen pencircle scaled 1/4; endfor);
draw mark rotated angle (z2-z4) shifted 1/2[z2, z4];
draw mark rotated angle (z4-z5) shifted 1/2[z4, z5];

path a[];
a1 = (z0 -- z4) shifted 16 left;
a2 = (z4 -- z5) shifted 16 left;

picture t[];
t1 = thelabel(TEX("$\csc 2\theta$") rotated 90, point 1/2 of a1); 
t2 = thelabel(TEX("$\cot\theta - \csc 2\theta$") rotated 90, point 1/2 of a2); 

forsuffixes $=1,2:
    drawdblarrow a$; unfill bbox t$; draw t$;
endfor

label.top("$2\csc 2\theta = \tan\theta + \cot\theta$", point 5/2 of bbox currentpicture);

\end{mplibcode}
$$
\vfill
\rightline{G.\@ Eisenstein, \textsl{Mathematische Werke}, Chelsea, NY. 1975, p.411}

\section{A familiar limit for $e$}

\vfill
$$
\begin{mplibcode}
numeric u; u = 90;

path xx, yy, ff;
xx = 5 left -- 2.2 u * right;
yy = xx rotated 90;

% f(x) = 1/x, f' = -1/x^2
ff = ((1/2, 2){1, -4} .. (1, 1){1, -1} .. (3/2, 2/3){1, -4/9} .. (2, 1/2){1, -1/4}) scaled u;

numeric n; n = 3/2;
z1 = (1, n/(n+1)) scaled u;
z2 = ((n+1)/n, 1) scaled u;

fill z1 -- (x2, y1) -- z2 -- (x1, y2) -- cycle withcolor Blues 7 3;
fill z1 -- (x2, y1) -- (x2, 0) -- (x1, 0) -- cycle withcolor Blues 7 4;
input thatch
thatch_space := 2;
rule buildcycle(xx, yy shifted (x1, 0), ff, yy shifted (x2, 0)) withcolor Blues 7 1;

draw (x1, 0) -- (x1, y2) -- z2 -- (x2, 0);
draw z1 -- (x2, y1);
draw z1 -- (0, y1) dashed evenly scaled 1/2 withcolor 1/2 white;
draw (x1,y2) -- (0, y2) dashed evenly scaled 1/2 withcolor 1/2 white;

draw ff withcolor 2/3 blue; label.urt("$xy=1$", point 1/8 of ff) withcolor 2/3 blue;

drawarrow xx; label.rt("$x$", point 1 of xx);
drawarrow yy; label.top("$y$", point 1 of yy);


label.bot("$\strut 1$", (x1, 0));
label.bot("$\strut 1 + {1\over n}$", (x2, 0));

label.lft("$1$", (0, y2));
label.lft("$n\over n+1$", (0, y1));

label.top("$\displaystyle \lim_{n\to\infty} \left(1+{1\over n}\right)^n = e$", 
    point 5/2 of bbox currentpicture shifted 12 up);

label.bot(btex \vbox{\openup 12pt\halign{\hfil $\displaystyle #$ \hfil\cr
    {1\over n}\cdot{n\over n+1} \le \ln\left(1+{1\over n}\right) \le {1\over n}\cdot 1\cr
    {n\over n+1} \le n \cdot \ln\left(1+{1\over n}\right) \le 1\cr
    \therefore \quad \lim_{n\to\infty} \ln\left(\left(1+{1\over n}\right)^n\right) = 1\cr
    }} etex, point 1/2 of bbox currentpicture shifted 36 down);

\end{mplibcode}
$$
\vfill
\section{A common limit}

\vfill
$$
\begin{mplibcode}
path xx, yy, ff;
xx = 5 left -- 320 right;
yy = 5 down -- 160 up;

% f=1/x f'= -1/x^2
numeric u; u = 88;
ff = ((5/8, 8/5){25, -64} .. (1, 1){1, -1} 
   .. (9/4, 4/9){81, -16} .. (13/4, 4/13){169, -16}) scaled u;

z1 = point 1 of ff;
z2 = point 2 of ff;
z3 = whatever [z1, z2]; y3 = 0;

path ln, trig;
ln = (x1, 0) -- (x2, 0) -- subpath(2, 1) of ff -- cycle;
trig = (x1, 0) -- z3 -- z1 -- cycle;

fill trig withcolor Oranges 7 2;
fill ln withcolor Oranges 7 3;


drawoptions(withpen pencircle scaled 1/4);
draw (x1, 0) -- z1 -- z3;
draw (x2, 0) -- z2;
draw (left--right) scaled 2 shifted (0, u); label.lft("$1$", (-2, u));
draw (2 down -- origin) shifted (x1, 0); label.bot("\strut$1$", (x1, 0));
draw (2 down -- origin) shifted (x2, 0); label.bot("\strut$x$", (x2, 0));
draw (2 down -- origin) shifted (x3, 0); label.bot("\strut$x+1$", (x3, 0));

drawoptions(withpen pencircle scaled 3/4 withcolor 2/3 blue);
draw ff; label.urt("$y=1/x$", point 0 of ff);

drawoptions();
drawarrow xx; label.rt("$x$", point 1 of xx);
drawarrow yy; label.top("$y$", point 1 of yy);

label("$\ln x$", 1/2[(x1,0), z2]);
label("$\displaystyle \lim_{x\to\infty} {x\over e^x} = 0$", 
    point 5/2 of bbox currentpicture shifted 72 up);
label(btex \vbox{\openup 8pt\halign{\hfil $\displaystyle # $ \hfil\cr
    \ln x < {1\over2}x \cr
    \therefore\: \lim_{x\to\infty} {x \over e^x} = 
                 \lim_{x\to\infty} {1 \over e^{x-\ln x}} = 0 \cr
    }} etex, point 1/2 of bbox currentpicture shifted 36 down);
\end{mplibcode}
$$
\vfill
\contrib{Alan H.\@ Stein and Dennis McGavran}
\section{Geometric evaluation of a limit}

\vfill
$$
\begin{mplibcode}
numeric u, v; u = 72; v = 3/2 u;
path xx; xx = 5 left -- 300 right;
path yy; yy = 5 down -- 240 up;
path ff; ff = (origin {up} for x=1 upto 5: .. (x,sqrt(x)){1, 1/(2sqrt(x))} endfor)
         xscaled u yscaled v;
path ll; ll = ((2,0) -- (4.25,2.25)) xscaled u yscaled v;

interim ahangle := 20;

numeric x, y; x = 2; y = 0;
string xlabel, ylabel; xlabel = "2"; ylabel = "\sqrt{2}";
draw ((x, y) 
    for i=1 upto 10:
      hide(if odd i: 
        if i < 6:
          drawarrow (x*u, 7-8i) -- (x*u, -1) withpen pencircle scaled 1/2 withcolor 1/2;
          label.bot("$" & xlabel & "$", (x*u, 7-8i));
          xlabel := "2 + \sqrt{" & xlabel & "}";
        fi y := sqrt(x); 
      else:
        if i < 6:
          label.lft("$" & ylabel & "$", (0, y*v));
          ylabel := "\sqrt{2+" & ylabel & "}";
        fi x := 2 + y; 
      fi) -- (x,y)
      hide(if i < 6:
        draw ((x,y) -- if odd i: (0,y) else: (x,0) fi) 
        xscaled u yscaled v
        dashed evenly scaled 1/2
        withpen pencircle scaled 1/4
        withcolor 1/2 white;
      fi)
    endfor) xscaled u yscaled v withpen pencircle scaled 1/4;
draw((0,2v)--(4u,2v)--(4u,0)) withpen pencircle scaled 1/4;
label.lft("$2$", (0, 2v));
label.bot("$4$", (4u, 0));

drawarrow xx; label.rt("$x$", point 1 of xx);
drawarrow yy; label.top("$y$", point 1 of yy);
drawoptions(withcolor 2/3 blue);
draw ff; picture t; t = thelabel.top("$y=\sqrt x$", origin); 
    draw t rotated angle direction 1 of ff shifted point 1 of ff;
draw ll; picture t; t = thelabel.top("$y=x-2$", origin); 
    draw t rotated angle direction 1/4 of ll shifted point 1/4 of ll;
drawoptions();
fill fullcircle scaled dotlabeldiam shifted (4u, 2v);

label("$\displaystyle \sqrt{2+\sqrt{2 + \sqrt{2 + \sqrt{\cdots}}}} = 2$", 
    point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Guanshen Ren}

\section{The derivative of the inverse sine}

\vfill
$$
\begin{mplibcode}
interim ahangle := 20;
path xx, yy, hh, arc; 
hh = halfcircle scaled 288; 
xx = (left--right) scaled 160;
yy = 5 down -- 160 up;
numeric t; t = 0.94;
arc = quartercircle scaled 60 cutbefore (origin -- point t of hh);

drawarrow arc;
draw origin -- point t of hh -- (xpart point t of hh, 0) dashed evenly scaled 1/2;
draw hh;  draw subpath (t, 2) of hh withpen pencircle scaled 1 withcolor 1/2 red;
drawarrow xx; 
drawarrow yy; 


label.bot("\strut $1$", point 0 of hh);
label.bot("\strut $x$", (xpart point t of hh, 0));
label.bot("\strut $-1$", point 4 of hh);
label.llft("\strut $0$", origin);

label.urt("$L$", point 1+1/2t of hh);
label.ulft("$y=\sqrt{1-x^2}$", point 2.818 of hh);

label.urt("$\scriptstyle \sin^{-1}x$", point 1.8 of arc);


label.bot(btex \vbox{\openup12pt\halign{\hfil $\displaystyle #$\hfil\cr
L = \sin^{-1} x = \int_0^x {1\over \sqrt{1-t^2}}\,dt\cr
\therefore\quad {d\over dx}\sin^{-1}x = {1\over\sqrt{1-x^2}}\cr}} etex, 
point 1/2 of bbox currentpicture shifted 36 down);

\end{mplibcode}
$$
\vfill
\contrib{Craig Johnson}
%-----------------------------
\section{The logarithm of a product}
\vfill
$$
\hbox to \textwidth{\hss
\begin{mplibcode}
interim ahangle := 25;
path xx, yy; xx = 8 left -- 200 right; yy = 8 down -- 125 up; 
% f=1/x f'=-1/x^2
path ff; ff = ((1/4, 4){1, -16} .. (1/2, 2){1, -4} .. (1, 1){1, -1} .. (3/2, 2/3){9, -4} 
         .. (2, 1/2){4, -1} .. (5/2, 2/5){25, -4} .. (3, 1/3){9, -1}) xscaled 64 yscaled 30;  
path ll; ll = origin--(xpart point infinity of ff, ypart point 0 of ff); z1=point 7/8 of ll;
picture p[]; p0 = image(
    draw (x1,0) -- z1 -- (0, y1) withpen pencircle scaled 1/4;
    draw ff withpen pencircle scaled 5/8 withcolor 2/3 blue;
    draw ll withpen pencircle scaled 5/8 withcolor 2/3 red;
    drawarrow xx; drawarrow yy;
    draw origin withpen pencircle scaled 1/2 dotlabeldiam;
);
input thatch
p1 = image(
    z11 = point 9/16 of ll;
    z12 = ff intersectionpoint (xx shifted (0, y11));
    z13 = ff intersectionpoint (yy shifted (x11, 0));
    path a; a = (0, y12) -- z12 -- (x12, y1) -- (0, y1) -- cycle;
    path b; b = (x13, 0) -- (x1, 0) -- (x1, y13) -- z13 -- cycle;
    fill a withcolor Blues 8 3; thatch_angle :=  45; rule a withcolor white;
    fill b withcolor Blues 8 3; thatch_angle := -45; rule b withcolor white;
    draw (x11, 0) -- z11 -- (0, y11) withpen pencircle scaled 1/4;
    draw (x12, 0) -- (x12, y1) withpen pencircle scaled 1/4;
    draw (0, y13) -- (x1, y13) withpen pencircle scaled 1/4;
    draw p0;
    label.ulft("$x=by$", point infinity of ll) withcolor 2/3 red;
    label.urt("$xy=1$", point 0 of ff) withcolor 2/3 blue;
    label.lft("$p$", (0, y1)); label.bot("$bp$", (x1, 0));
    label.lft("$q$", (0, y11)); label.bot("$bq$", (x11, 0)); 
    label.lft("$1/bq$", (0, y13)); label.bot("$1/q$", (x12, 0));
);
% DUMP capture=False, replace = "% ... similar for p2, p3, p4"
p2 = image(
    for i=0 upto 4:
        z[20 + i] = (1/2 + i/8)  * z1;
        z[200 + i] = ff intersectionpoint (xx shifted (0, y[20 + i]));
        z[210 + i] = ff intersectionpoint (yy shifted (x[20 + i], 0));
    endfor
    path a[], b[];
    for i=0 upto 3:
        a[i] = (0, y[20 + i]) -- z[200 + i] -- (x[200 + i], y[21 + i]) -- (0, y[21 + i]) -- cycle;
        thatch_angle := 45;
        fill a[i] withcolor Blues 8 3; rule a[i] withcolor white; 
        draw subpath (1,2) of a[i] withpen pencircle scaled 1/4;
        
        b[i] = (x[20 + i], 0) -- (x[21 + i], 0) -- (x[21 + i], y[210 + i]) -- z[210 + i] -- cycle;
        thatch_angle := -45;
        fill b[i] withcolor Blues 8 3; rule b[i] withcolor white; 
        draw subpath (2,3) of b[i] withpen pencircle scaled 1/4;

        draw (0, y[20 + i]) -- z[20 + i] -- (x[20 + i], 0) withpen pencircle scaled 1/4;
    endfor
    draw p0;
    label.ulft("$x=by$", point infinity of ll) withcolor 2/3 red;
    label.urt("$xy=1$", point 0 of ff) withcolor 2/3 blue;
    label.lft("$a$", (0, y1));
    label.lft("$1$", (0, y20));
    label.bot("$b$", (x20, 0));
    label.bot("$ab$", (x1, 0));
);
p3 = image(
    path a, b;
    a = buildcycle(yy, (0,y1) -- z1, ff, z20 -- (0, y20));
    thatch_angle := 45;
    fill a withcolor Blues 8 3; rule a withcolor white; 

    b = buildcycle(xx, (x1, 0) -- z1, ff, z20 -- (x20, 0));
    thatch_angle := -45;
    fill b withcolor Blues 8 3; rule b withcolor white; 

    draw (0, y20) -- z20 -- (x20, 0) withpen pencircle scaled 1/4;
    draw p0;
    label.lft("$a$", (0, y1));
    label.lft("$1$", (0, y20));
    label.bot("$b$", (x20, 0));
    label.bot("$ab$", (x1, 0));
);
p4 = image(
    path c;
    c = buildcycle(xx, (x200, 0) -- z200, ff, z20 -- (x20, 0));
    draw (x200, 0) -- z200 withpen pencircle scaled 1/4;
    fill c withcolor Reds 7 2;
    draw p3;
    label.bot("$1$", (x200,0));
    label("$\ln a$", center a shifted (-4, -12));
    label("$\ln a$", center b shifted (-12, -4));
    label("$\ln b$", center b shifted (-66, -4));
);
% DUMP capture=True
draw p1 shifted (-125, +84) shifted - center bbox p0;
draw p2 shifted (+125, +84) shifted - center bbox p0; 
draw p3 shifted (-125, -84) shifted - center bbox p0; 
draw p4 shifted (+125, -84) shifted - center bbox p0; 

drawoptions(withpen pencircle scaled 2 withcolor 3/4 white);
interim ahangle := 42;
interim linejoin := mitered;
drawarrow (left--right) scaled 12 shifted 84 up;
drawarrow (left--right) scaled 12 rotated 225;
drawarrow (left--right) scaled 12 shifted 84 down;
drawoptions();

label("$\ln ab = \ln a + \ln b$", point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}\hss}
$$

\vskip 36pt
% This has to be done separately from the main figure, because
% you can't include an mplibcode environment in a label within 
% another mplibcode environment.
$$
\mathord{\hbox{Area}}\left(\vcenter{%
\begin{mplibcode}
% DUMP capture=False, replace="% See source for how the legend is done"
input thatch; path s; s = unitsquare scaled 18; 
fill s withcolor Blues 8 3; rule s withcolor white; draw s;
\end{mplibcode}}\right)=
\mathord{\hbox{Area}}\left(\vcenter{%
\begin{mplibcode}
% DUMP capture=False
input thatch; path s; s = unitsquare scaled 18; interim thatch_angle := -45;
fill s withcolor Blues 8 3; rule s withcolor white; draw s;
\end{mplibcode}
}\right)$$


\vfill
\contrib{Jeffery Ely}

\section{An integral of a sum of reciprocal powers}

\vfill
$$
\begin{mplibcode}
numeric u; u = 260; 

vardef f(expr x) = x**(7/3) enddef;

path xx, yy, ff;
xx = 12 left -- (u + 32) * right;
yy = xx rotated 90;
ff = (origin{right} for x=1/8 step 1/8 until 9/8:
    .. (x, f(x)) endfor) scaled u
    cutafter (xx shifted point 1 of yy);

fill subpath(0, 8) of ff -- (0, u) -- cycle withcolor Oranges 8 2;
fill subpath(0, 8) of ff -- (u, 0) -- cycle withcolor Reds 8 2;
draw ff withcolor 2/3 red withpen pencircle scaled 3/4;
draw (0, u) -- (u, u) -- (u, 0);
drawarrow xx; label.rt ("$x$", point 1 of xx); label.bot("$1$", (u,0));
drawarrow yy; label.top("$y$", point 1 of yy); label.lft("$1$", (0,u));

label.urt("$x = y^{p/q}$", point 8 of ff shifted (6, 2));
label.lft("$y = x^{q/p}$", point 9 of ff);

label.top("$\displaystyle \int_0^1 \left(t^{p/q} + t^{q/p} \right)\, dt = 1$", 
    point 5/2 of bbox currentpicture);
\end{mplibcode}
$$
\vfill
\contrib{Peter R.\@ Newbury}
\section{The arctangent integral}
\vfill
$$
\begin{mplibcode}
numeric u; u = 3in; z.O = u * down;
path tt; tt = 12 left -- (3/2u + 32) * right;
path yy; yy = (u + 12) * down -- (1/2u + 32) * up;
path ff; ff = ((0, 1/2){1,0} for t=1/4 step 1/4 until 3/2:
    .. (t, 1/(2 * (1+t**2))){1, -t/(t**2 +1)/(t**2 +1)} endfor) scaled u;
path gg; gg = origin for i=1 upto 6: 
    .. u * dir angle (xpart point i of ff, u) shifted z.O endfor;
path aa; aa = quartercircle scaled 100 shifted z.O cutbefore (z.O--point 5 of gg);

fill origin -- (xpart point 5 of ff, 0) -- subpath (5, 0) of ff -- cycle withcolor Blues 8 2;
fill z.O -- subpath (5, 0) of gg -- cycle withcolor Blues 8 2;
z.P = whatever[z.O, point 3 of gg]; y.P = ypart point 4 of gg;
z.S = whatever[z.O, point 4 of gg]; y.S = ypart point 3 of gg;
fill z.O -- point 4 of gg -- z.P -- cycle withcolor 3/4[Blues 8 2, Oranges 7 2];
fill subpath (3, 4) of gg -- z.P -- cycle withcolor 1/2[Blues 8 2, Greens 7 2];
fill subpath (3, 4) of gg -- z.S -- cycle withcolor Greens 7 2;
z.C = (xpart point 3 of ff, ypart point 4 of ff);
z.F = (xpart point 4 of ff, ypart point 3 of ff);
fill (xpart point 3 of ff, 0) -- (xpart point 4 of ff, 0) -- 
    point 4 of ff -- z.C -- cycle withcolor 3/4[Blues 8 2, Oranges 7 2];
fill subpath (3, 4) of ff -- z.C -- cycle withcolor 1/2[Blues 8 2, Greens 7 2];
fill subpath (3, 4) of ff -- z.F -- cycle withcolor Greens 7 2;

drawoptions(withpen pencircle scaled 1/4);
  for i=1 upto 5:
    draw subpath (-2, 1) of unitsquare 
        xscaled (xpart point i-1 of ff - xpart point i of ff)
        yscaled (ypart point i-1 of ff - ypart point i of ff)
        shifted point i of ff if i <> 4: dashed evenly scaled 1/2 fi;
    draw z.O -- (xpart point i of ff, 0) -- point i of ff 
        if i < 3: dashed evenly scaled 1/2 fi;
    draw (origin -- u * left) shifted point i of gg
        cutafter (z.O -- point i-1 of gg)
        if i <> 4: dashed evenly scaled 1/2 fi;
    draw (origin -- u * right) shifted point i-1 of gg
        cutafter (z.O -- (xpart point i of ff, 0))
        if i <> 4: dashed evenly scaled 1/2 fi;
  endfor
drawoptions(withpen pencircle scaled 3/4 withcolor 2/3 blue);
  draw ff; draw gg; draw aa;
  label.rt("$y={1\over 2\left(1+t^2\right)}$", point 6 of ff);
  label.rt("$t^2 + \left(y+1\right)^2=1$", point 6 of gg);
  label.lrt("$\arctan x$", point 0 of aa);
drawoptions();

drawarrow tt; label.rt("$t$", point 1 of tt);
drawarrow yy; label.top("$y$", point 1 of yy);
% DUMP capture=False, replace="% ... the rest is labels ... "
label.lft("$1/2$", (0, 1/2u));
label.llft("$0$", origin);
label.lft("$-1$", (0, -u));

interim dotlabeldiam := 3/4 dotlabeldiam;

label.lrt("$t_{i-1}$", (xpart point 3 of ff, 0));
label.lrt("$t_{i}$", (xpart point 4 of ff, 0));
dotlabel.lrt("$x$", (xpart point 5 of ff, 0));

dotlabel.lrt ("$\scriptstyle O$", z.O);
dotlabel.ulft("$\scriptstyle P$", z.P);
dotlabel.top ("$\scriptstyle R$\strut", point 3 of gg);
dotlabel.lrt ("$\scriptstyle S$", z.S);
dotlabel.rt  ("$\scriptstyle Q$", point 4 of gg);
dotlabel.ulft("$\scriptstyle A$", (xpart point 3 of ff, 0));
dotlabel.urt ("$\scriptstyle B$", (xpart point 4 of ff, 0));
dotlabel.lft ("$\scriptstyle C$", z.C);
dotlabel.urt ("$\scriptstyle D$", point 4 of ff);
dotlabel.urt ("$\scriptstyle E$", point 3 of ff);
dotlabel.urt ("$\scriptstyle F$", z.F);

path mark; mark = (xpart point 3 of ff + 1, 1/2 ypart point 4 of ff)
               -- (xpart point 4 of ff - 1, 1/2 ypart point 4 of ff);
drawdblarrow mark withpen pencircle scaled 1/4;
picture m; m = thelabel("$\Delta t$", point 1/2 of mark);
fill bbox m withcolor 3/4[Blues 8 2, Oranges 7 2]; draw m;

label.top("$\displaystyle \arctan x = \int_0^x{1\over 1+t^2}\,dt$", point 5/2 of bbox currentpicture);
label(btex 
\def\Area(#1){\mathord{\hbox{Area}}\left(#1\right)}
\def\rect#1#2{\mathord{\vcenter{\vbox{\hrule height.#2pt
    \hbox{\vrule width.#2pt height#1pt \kern#1pt \vrule width.#2pt}\hrule height.#2pt}}}\,}
\vbox{\openup 8pt\halign{\hfil $\displaystyle #$\hfil\cr
\Area(\rect74 ABCD) = \Area(\triangle OPQ) = {1\over2}\cdot{1\over1+t_{i-1}^2}\cdot\Delta t\cr
\Area(\rect74 ABEF) = \Area(\triangle ORS) = {1\over2}\cdot{1\over1+t_{i}^2}\cdot\Delta t\cr
\therefore\quad{1\over2}\arctan x = \int_0^x{1\over2\left(1+t^2\right)}\,dt\cr
}} etex, point 5/8 of bbox currentpicture);
% DUMP capture=True
\end{mplibcode}
$$
\vfill
\contrib{Aage Bondesen}
%----------------------------------------------------
\section{The method of last resort — Weierstrass substitution}

\vfill
$$
\begin{mplibcode}
path hh;
hh = halfcircle scaled 5in;
z0 = origin;
z1 = point 4 of hh;
z4 = point 0 of hh;
z5 = point 1.273 of hh;
z3 = (x5, 0);
z2 = whatever * up = whatever [z1, z5];

draw unitsquare scaled 8 rotated 90 shifted z3 withcolor 1/2 white;
draw unitsquare scaled 8 rotated angle (z1-z5) shifted z5 withcolor 1/2 white;
forsuffixes $=0, 1, 3, 4: draw z5 -- z$; endfor
draw hh -- cycle;
draw z0 -- point 2 of hh;

dotlabel.bot("$O$", z0);
dotlabel.bot("$A$", z1);
dotlabel.bot("$C$", z3);
dotlabel.bot("$D$", z4);

dotlabel.ulft("$B$", z2);
dotlabel.urt("$E$", z5);

drawoptions(withcolor 2/3 red);
label("$\theta$", 16 dir 1/2 angle z5);
label("$\scriptstyle\theta/2$", 24 dir 1/4 angle z5 shifted z1);
label("$\scriptstyle\theta/2$", 32 dir (270 + 1/4 angle z5) shifted z5);

drawoptions(withcolor 2/3 blue);
label.lft("$u$", 1/2[z0, z2]);
label.ulft("$1$", 1/2[z0, z5]);
label.top("$\cos\theta$", 1/2[z0, z3]);
label.lft("$\sin\theta$", 5/8[z5, z3]);
label.top("$1-\cos\theta$", 1/2[z3, z4]);
label.ulft("$\sqrt{1+u^2}$", 1/2[z1, z2]);

drawoptions();

label.bot(btex \vbox{\openup 12pt\halign{\hfil $\displaystyle #$ \hfil\cr
  u=\tan\frac{\theta}{2}, \quad DE=2\sin\frac{\theta}{2}=\frac{2u}{\sqrt{1+u^2}}\cr
  \frac{CE}{DE}=\frac{OA}{BA} \quad\Longrightarrow\quad \sin\theta=\frac{2u}{1+u^2}\cr
  \frac{CD}{DE}=\frac{OB}{BA} \quad\Longrightarrow\quad \cos\theta=\frac{1-u^2}{1+u^2}\cr
}} etex, point 1/2 of bbox currentpicture shifted 42 down);


\end{mplibcode}
$$
\vfill
\contrib{Paul Deiermann}

\section{The trapezoidal rule — for increasing functions}

\vfill
$$
\begin{mplibcode}
path xx, yy; xx = 10 left -- 300 right; yy = 10 down -- 240 up;
path ff; ff = subpath (3.7, 2.15) of fullcircle xscaled 560 yscaled 410 shifted (300, -20);

numeric t[];
for i=0 upto 6:
  (t[i], whatever) = ff intersectiontimes
          yy shifted (i/7)[(xpart point 0 of ff, 0), (xpart point infinity of ff, 0)];
  z[10 + i] = point t[i] of ff;
endfor
for i=0 upto 5:
    z[i] = (xpart point t[i+1] of ff, ypart point t[i] of ff);
endfor
for i=1 upto 5:
    fill subpath (t[i], t[i+1]) of ff -- z[i] -- cycle withcolor Blues 8 4;
    fill z[i] -- (x[i-1], y[i]) -- (x[i-1], 0) -- (x[i], 0) -- cycle withcolor Blues 8 3;
    draw z[i] -- (x[i-1], y[i]) -- (x[i-1], 0) -- (x[i], 0) -- cycle;
endfor
for i=2 upto 5:
    path trig; 
    trig = (z[10+i] -- z[11+i] -- z[i] -- cycle) shifted (x1-x[i], 0);
    fill trig withcolor Blues 8 4;
    draw trig;
    draw point 1 of trig -- z[11+i] dashed evenly;
endfor
draw z5 -- z16; draw (x1, y16) -- (x0, y16) -- z11;
draw z11 -- (0, y11) dashed evenly;
draw (x0, y16) -- (0, y16) dashed evenly;
draw ff withpen pencircle scaled 3/4 withcolor Blues 8 8;
drawarrow xx; label.rt("$x$", point 1 of xx); 
drawarrow yy; label.top("$y$", point 1 of yy);

label.lft("$f\left(x_0\right)$", (0, y11));
label.lft("$f\left(x_n\right)$", (0, y16));
label.urt("$y=f\left(x\right)$", point infinity of ff) withcolor Blues 8 8;

label.bot("\strut\llap{$a={}$}$x_0$", (x0, 0));
label.bot("\strut$x_1$", (x1, 0));
label.bot("\strut$\cdots$", (1/2(x2+x3), 0));
label.bot("\strut$x_{n-1}$", (x4, 0));
label.bot("\strut$x_n$\rlap{${}=b$}", (x5, 0));

label.bot(btex $\displaystyle
\int_a^b f\left(x\right)dx = \sum_{i=0}^{n-1} f\left(x_i\right) {b-a\over n}
+ {1\over2} \biggl(\,f\left(x_n\right) - f\left(x_0\right) \biggr)   {b-a\over n}
$ etex, point 1/2 of bbox currentpicture shifted 42 down);



\end{mplibcode}
$$
\vfill
\contrib{Jesús Urías}

\section{Construction of a hyperbola}

\vfill
\noindent
\begin{mplibcode}
numeric r; r = 150;   
path xx; xx = 12 left -- 3/2 r * right;
path yy; yy = xx rotated 90;
path base; base = quartercircle scaled 2r;
path hh; hh = point 0 of base for t=4 step 4 until 48:
    hide(numeric a; a = ypart dir t / xpart dir t;)
    .. (a ++ 1, a) scaled r
endfor;
z1 = point 10 of hh; z2 = (r, y1);

picture P[];
P0 = image(
    draw base withcolor 2/3 blue;
    draw hh withcolor 2/3 red;
    draw xx; draw yy;
);
P1 = image(
    draw origin -- z2 -- z1 withcolor 1/2 white;
    draw fullcircle scaled 2 abs(z1-z2) shifted z2 withcolor 1/2 white;
    draw yy shifted point 0 of base withcolor 1/2 white;
    draw P0;
    label.lft("I.", point 3 of bbox P0 shifted 30 left);
);
P2 = image(
    draw origin -- z2 -- z1 -- cycle withcolor 1/2 white;
    z3 = whatever * z1;
    z4 = whatever * z2;
    x3 = r; y3 = y4;
    draw z3 -- z4 withcolor 1/2 white;
    draw yy shifted point 0 of base withcolor 1/2 white;
    draw P0;
    label.lft("II.", point 3 of bbox P0 shifted 30 left);
);

draw P1;
draw P2 shifted (1.8r * down);
\end{mplibcode}

\vfill
\contrib{Ernest J.\@ Eckert}
\section{The focus and directrix of an ellipse}
\vfill
$$
\begin{mplibcode}
numeric a, b, c, d;
a = 233; b = 144; c = a +-+ b; d/a = a/c;

path xx, yy, ee, directrix, cf, cd;
xx = 13 left -- (d+34) * right;
yy = 13 down -- (b+21) * up;
ee = fullcircle xscaled 2a yscaled 2b;
directrix = (36 down -- (b+6) * up) shifted (d, 0);
cf = fullcircle scaled 2a shifted (0, b);
cd = fullcircle scaled 2d shifted (0, b);

z1 = whatever[(0,b), (c,0)]; x1 = a + 21; x2 = a; y2 = y1;
drawoptions(withcolor 1/2 white);
    draw z1 -- (0, b) -- point 0 of cd dashed evenly;
    draw z2 --           point 0 of cf dashed evenly;
    draw subpath (8, 6) of cf cutafter ((0,b) -- z1);
    draw subpath (8, 6) of cd cutafter ((0,b) -- z1);
drawoptions();

draw directrix withcolor 1/2 blue;
draw subpath (-1/2, 2) of ee withcolor 2/3 red;
drawarrow xx;
drawarrow yy;

label.urt("$a$", 1/2[(0,b), (c,0)]);
label.lft("$b$", 1/2(0,b));

dotlabel.llft("$0$", (0,0));
dotlabel.llft("$F:(c,0)$", (c,0));
dotlabel.lft("$(0, b)$", (0, b));
dotlabel.urt("$(a, 0)$", (a, 0));
dotlabel.urt("$(d, 0)$", (d, 0));

label.rt("$x$", point 1 of xx);
label.top("$y$", point 1 of yy);

label.urt("$\displaystyle {x^2\over a^2} + {y^2\over b^2} = 1$", point 1.1 of ee) 
    withcolor 1/2 red;
label.bot("$\displaystyle D : x = {a^2\over c}$", point 0 of directrix) 
    withcolor 1/3 blue;
label.bot(btex $\displaystyle c=\sqrt{a^2-b^2}$, \quad 
               $\displaystyle{d\over a}={a\over c}$, \quad 
               $\displaystyle d={a^2\over c}$ etex, 
    (1/2c, ypart point 0 of directrix));


\end{mplibcode}
$$
\vfill
\contrib{Michel Bataille}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\chapter{Inequalities}
\minitoc
\section{The arithmetic mean – geometric mean inequality IV}
\vfill
$$
\begin{mplibcode}
numeric a, b; a = 233; b = 144;
path S[]; 
S0 = unitsquare scaled 288;
S1 = for i=0 upto 3: 
    point i + a/(a+b) of S0 -- 
  endfor cycle;
S2 = for i=0 upto 3: 
    point i of S0 reflectedabout(point i-1 of S1, point i of S1) -- 
  endfor cycle;

fill S0 withcolor Greens 7 2;
fill S1 withcolor Oranges 7 2;
fill S2 withcolor Reds 7 1;

for i=0 upto 3: 
  draw point i of S1 -- point i of S2 dashed evenly; 
endfor
draw S1; draw S0; 

label.lft("$a$", 1/2[point 3 of S1, point 3 of S0]);
label.lft("$b$", 1/2[point 3 of S1, point 0 of S0]);
label.bot("$a$", 1/2[point 0 of S1, point 0 of S0]);
label.bot("$b$", 1/2[point 0 of S1, point 1 of S0]);
label.urt("$b$", 1/2[point 0 of S1, point 1 of S2]);

draw thelabel.top(TEX("$b$"), origin) 
    rotated (90 - 2 angle (a,b))
    shifted 1/2[point 1 of S1, point 2 of S2];

draw thelabel.top(TEX("$a-b$"), origin) 
    rotated (90 - 2 angle (a,b))
    shifted point 3/2 of S2;

label.bot(btex $\displaystyle \left(a+b\right)^2 \ge 4ab$ 
               \quad\Longrightarrow\quad 
               $\displaystyle {a+b\over2} \ge \sqrt{ab}$
          etex, point 1/2 of bbox currentpicture shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{Ayoub B.\@ Ayoub}
\section{The arithmetic mean – geometric mean inequality V}
\vfill
$$
\begin{mplibcode}
path base; base = fullcircle scaled 160;
path zz; zz = (point 0 of base -- point 4 of base) 
    scaled 4 shifted 42 up rotated -42
    cutbefore subpath (4, 8) of base
    cutafter subpath (0, 4) of base;
numeric s; s = 11/16; 
numeric t; (t, whatever) = base intersectiontimes 
    (point 1/2 of zz -- 16[point s of base, point 1/2 of zz]);
path xy; xy = point s of base -- point t of base;
% z0 = intersection of xy and zz, z1 = closest point on xy to center, z2 = for label
z0 = whatever[point s of base, point t of base] = whatever[point 0 of zz, point 1 of zz];
z1 = whatever[point s of base, point t of base];
z1 - center base = whatever * (point s of base - point t of base) rotated 90;
z2 = point 6 of base shifted 21 down;
picture P[];
P1 = image(
  draw xy withcolor 1/2 red; draw zz withcolor 1/2 red;
  draw base; drawdot center base withpen pencircle scaled dotlabeldiam;
  label.ulft("$x$", 3/8[point t of base, z0]);
  label.ulft("$y$", 1/2[point s of base, z0]);
  label.llft("$z$", point 1/4 of zz);
  label.llft("$z$", point 3/4 of zz);
  label.bot("$z^2 = xy$", z2);
);
P2 = image(
  draw unitsquare scaled 5 rotated angle (center base - z1) shifted z1 withcolor 3/4 white;
  draw xy withcolor 1/2 red; draw zz withcolor 1/2 red;
  draw z0 -- center base -- z1 withcolor 2/3 blue; draw base;
  draw center base withpen pencircle scaled dotlabeldiam;
  label.ulft("$x$", 3/8[point t of base, z0]);
  label.ulft("$y$", 1/2[point s of base, z0]);
  label.llft("$\sqrt{xy}$", point 1/4 of zz);
  label.llft("$\sqrt{xy}$", point 3/4 of zz);
  label.lft("$d$", 1/2[z1, center base]);
  label.lrt("$c$", 1/2[z0, center base]);
  label.bot("$d<c$\quad\Longrightarrow\quad$x+y>2\sqrt{xy}$", z2);
);
P3 = image(
  for p = xy, zz:
    draw (point 0 of base -- point 4 of base) 
      rotated angle (point 1 of p - point 0 of p) withcolor 1/2 red;
  endfor
  draw base; draw center base withpen pencircle scaled dotlabeldiam;
  label.bot("$d=c=0$\quad\Longrightarrow\quad$x+y=2\sqrt{xy}$", z2);
);
draw P1 shifted 240 up; draw P2 shifted 120 left; draw P3 shifted 120 right;
\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}
\section{The arithmetic mean – geometric mean inequality VI}
\vfill
$$
\begin{mplibcode}
path xx, yy, ee, ll;
xx = 12 left -- 300 right;
yy = 12 down -- 240 up;

ee = ((0, 1) for x=1 upto 36: .. (8x, mexp(14x)) endfor) yscaled 36;
z1 = point 6 of ee;
z2 = point 30 of ee;
z0 = whatever[z1, z2]; x0 = 0;
z3 = whatever[z1, z2]; x3 = xpart point 1 of xx - 12;
z4 = point 21 of ee;
z5 = whatever[z1, z2]; x5 = x4;

ll = z0 -- z3;

draw (0, y1) -- z1 -- (x1, 0) dashed evenly;
draw (0, y2) -- z2 -- (x2, 0) dashed evenly;
draw (0, y4) -- z4 -- (x4, 0) dashed evenly;
draw (0, y5) -- z5 -- z4      dashed evenly;


draw ee withcolor 2/3 red;
draw ll withcolor 1/2 blue;

forsuffixes $=1, 2, 4, 5: drawdot z$ withpen pencircle scaled dotlabeldiam; endfor

drawarrow xx;
drawarrow yy;

label.top("$y=e^x$", point infinity of ee) withcolor 1/2 red;
label.lft("$a$", (0, y1)); label.bot("$\ln a$", (x1, 0));
label.lft("$b$", (0, y2)); label.bot("$\ln b$", (x2, 0));
label.lft("$a^{1-t} b^t$", (0, y4)); 
label.lft("$(1-t)a + tb$", (0, y5)); 
label.bot("$(1-t)\ln a + t\ln b$", (x4, 0));

label.bot(btex 
  $0 < a < b$,\quad $0 < t < 1$ \quad\Longrightarrow\quad $(1-t)a + tb > a^{1-t}b^t$
  etex, point 1/2 of bbox currentpicture shifted 36 down);
label.bot(btex
  $t=\frac12$ \quad\Longrightarrow\quad $\displaystyle \frac{a+b}{2} > \sqrt{ab}$
  etex, point 1/2 of bbox currentpicture shifted 36 down);
\end{mplibcode}
$$
\vfill
\contrib{Michael K.\@ Brozinsky}
\section{The arithmetic mean – geometric mean inequality for three positive numbers}
\vfill
$$
\begin{mplibcode}
numeric a, b, c, k;
k = 48; k * a = 72; b = 3/4 a; c = 3/8 a;

path ab, bc, ac, asq, bsq, csq;
ab = unitsquare xscaled b yscaled a scaled k;
bc = unitsquare xscaled c yscaled b scaled k shifted point 1 of ab;
ac = unitsquare xscaled a yscaled c scaled k shifted point 1 of bc;

asq = unitsquare scaled a scaled k;
bsq = unitsquare scaled b scaled k shifted point 1 of asq;
csq = unitsquare scaled c scaled k shifted point 1 of bsq;

picture P[];
P1 = image(
    fill ab withcolor Spectral 9 5;
    fill bc withcolor Spectral 9 4;
    fill ac withcolor Spectral 9 3;
    forsuffixes $=asq, bsq:
        draw subpath (1, 3) of $ dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
    endfor
    forsuffixes $=ab, bc, ac:
        draw $;
        label("$" & str $ & "$", center $);
    endfor
);
P2 = image(
    forsuffixes $=asq, bsq, csq:
        draw $;
        label("$" & substring (0,1) of str $ & "^2$", center $);
    endfor
);

draw P1 shifted (100 left  - center bbox P1);
draw P2 shifted (100 right - center bbox P2);
label("${}\le{}$", origin);
label("\textsc{Lemma}: $ab + bc + ac \le a^2 + b^2 + c^2$", 64 up);

% DUMP capture=False, replace="% and much the same for the Theorem pictures"
path bca, bcb, bcc, aca, acb, acc, aba, abb, abc;

bca = unitsquare yscaled (-b*c) xscaled a scaled k; 
bcb = unitsquare yscaled (-b*c) xscaled b scaled k shifted point 1 of bca;
bcc = unitsquare yscaled (-b*c) xscaled c scaled k shifted point 1 of bcb;

aca = unitsquare yscaled (-a*c) xscaled a scaled k shifted point 3 of bca;
acb = unitsquare yscaled (-a*c) xscaled b scaled k shifted point 1 of aca;
acc = unitsquare yscaled (-a*c) xscaled c scaled k shifted point 1 of acb;

aba = unitsquare yscaled (-a*b) xscaled a scaled k shifted point 3 of aca;
abb = unitsquare yscaled (-a*b) xscaled b scaled k shifted point 1 of aba;
abc = unitsquare yscaled (-a*b) xscaled c scaled k shifted point 1 of abb;

P3 = image(
    numeric i; i = 2;
    forsuffixes @=bcc, bcb, acc, aca, abb, aba:
        fill @ withcolor Spectral[9][incr i];
    endfor
    forsuffixes @=bca, bcb, bcc, aca, acb, acc, aba, abb, abc:
        draw @;
    endfor
    forsuffixes @=bca, acb, abc:
        label("$abc$", center @);
    endfor
    label.top("$a$", point 1/2 of bca);
    label.top("$b$", point 1/2 of bcb);
    label.top("$c$", point 1/2 of bcc);

    label("\llap{$bc$\enspace}", point -1/2 of bca);
    label("\llap{$ac$\enspace}", point -1/2 of aca);
    label("\llap{$ab$\enspace}", point -1/2 of aba);
);

path aaa, aab, aac, bba, bbb, bbc, cca, ccb, ccc;
aaa = unitsquare yscaled (-a*a) xscaled a scaled k; 
aab = unitsquare yscaled (-a*a) xscaled b scaled k shifted point 1 of aaa; 
aac = unitsquare yscaled (-a*a) xscaled c scaled k shifted point 1 of aab;

bba = unitsquare yscaled (-b*b) xscaled a scaled k shifted point 3 of aaa;
bbb = unitsquare yscaled (-b*b) xscaled b scaled k shifted point 1 of bba;
bbc = unitsquare yscaled (-b*b) xscaled c scaled k shifted point 1 of bbb;

cca = unitsquare yscaled (-c*c) xscaled a scaled k shifted point 3 of bba;
ccb = unitsquare yscaled (-c*c) xscaled b scaled k shifted point 1 of cca;
ccc = unitsquare yscaled (-c*c) xscaled c scaled k shifted point 1 of ccb;

P4 = image(
    numeric i; i = 2;
    forsuffixes @=ccb, bbc, cca, aac, bba, aab:
        fill @ withcolor Spectral[9][incr i];
    endfor
    forsuffixes @=aaa, aab, aac, bba, bbb, bbc, cca, ccb, ccc:
        draw @;
    endfor
    forsuffixes @=aaa, bbb, ccc:
        label("$" & substring (1,2) of str @ & "^3$", center @);
    endfor
    label.top("$a$", point 1/2 of aaa);
    label.top("$b$", point 1/2 of aab);
    label.top("$c$", point 1/2 of aac);

    label("\rlap{\enspace$a^2$}", point 3/2 of aac);
    label("\rlap{\enspace$b^2$}", point 3/2 of bbc);
    label("\rlap{\enspace$c^2$}", point 3/2 of ccc);
);

draw P3 shifted ((-100, -100) - point 5/2 of bbox P3);
draw P4 shifted ((+100, -100) - point 5/2 of bbox P4);
label("${}\le{}$", 200 down);
label.bot("\textsc{Theorem}: $3abc \le a^3 + b^3 + c^3$", 72 down);
\end{mplibcode}
$$
\vfill
\contrib{Claudi Alsina}
\section{The arithmetic-geometric-harmonic mean inequality}
\vfill
$$
\begin{mplibcode}
path sc; sc = (halfcircle -- cycle) scaled 300;
z.A = origin;
z.M = point 1.2345 of sc;
x.G = x.M; y.G = y.A; 
z.H = whatever [z.A, z.M];
z.H - z.G = whatever * (z.M - z.A) rotated 90;

draw unitsquare scaled 6 shifted z.G withcolor 1/2 white;
draw unitsquare scaled 6 rotated angle (z.A - z.H) shifted z.H withcolor 1/2 white;
draw z.A -- z.M -- z.G -- z.H withcolor 3/4 blue;
draw sc; 

path a, b;
a = (point 4 of sc -- z.G) shifted 21 down;
b = (z.G -- point 0 of sc) shifted 21 down;
forsuffixes $=a, b:
drawdblarrow $;
unfill fullcircle scaled 24 shifted point 1/2 of $;
label("$" & str $ & "$", point 1/2 of $);
endfor
dotlabel.bot ("$A$", z.A);
dotlabel.bot ("$G$", z.G);
dotlabel.ulft("$H$", z.H);
dotlabel.urt ("$M$", z.M);

interim bboxmargin := 32;
label.top(btex 
  $a,b > 0$ \quad\Longrightarrow\quad 
  $\displaystyle {a+b\over2}\ge\sqrt{ab}\ge{2ab\over a+b}$
  etex, point 5/2 of bbox currentpicture);
label.bot(btex \vbox{\openup 12pt\halign{\hfil #\hfil\cr
  $\displaystyle \overline{AM} = {a+b\over2}$, \quad
  $\displaystyle \overline{GM} = \sqrt{ab}$, \quad
  $\displaystyle \overline{HM} = {2ab\over a+b}$,\cr
  $\overline{AM} \ge \overline{GM} \ge \overline{HM}$.\cr
  }} etex, point 1/2 of bbox currentpicture);
\end{mplibcode}
$$
\vfill
\contrib{Pappus of Alexandria (circa \textsc{a.d.} 320)}
\section{The arithmetic-logarithmic-geometric mean inequality}
\vfill
$$
\begin{mplibcode}
path xx, yy, ff; numeric t; t=2;
xx = 12 left -- (18,0) -- (18+t, 4t) -- (18+3t, -4t) -- (18+4t, 0) -- (240, 0);
yy = 12 down -- 160 up;

ff = ((1,1){1, -1} .. (2, 1/2){4, -1} .. (3, 1/3){9, -1} .. (4, 1/4){16, -1}) 
xscaled 64 yscaled 108 shifted (-36, 12);

numeric a, b, m, s, t, u, v, w;
a = 42;
b = 200;
m = 1/2a + 1/2b;
s = sqrt(a*b);

z1 = (a, 0);
z2 = (b, 0);
z3 = (m, 0);
z4 = (s, 0);

(t, t') = ff intersectiontimes (yy shifted z1);
(u, u') = ff intersectiontimes (yy shifted z2);
(v, v') = ff intersectiontimes (yy shifted z3);
(w, w') = ff intersectiontimes (yy shifted z4);

picture P[];

P0 = image(
draw ff withcolor 2/3 red withpen pencircle scaled 3/4;
label.top("$\displaystyle y={1\over x}$", point 0 of ff) withcolor 1/2 red;
drawarrow xx; label.rt("$x$", point infinity of xx);
drawarrow yy; label.top("$y$", point infinity of yy);
label.bot("\strut $a$", z1);
label.bot("\strut $b$", z2);
);

P1 = image(
x5 = x1; z5 = whatever * direction v of ff shifted point v of ff;
x6 = x2; z6 = whatever * direction v of ff shifted point v of ff;
fill z2 -- z1 -- subpath (t, u) of ff -- cycle withcolor Oranges 7 1;
fill z2 -- z1 -- z5 -- z6 -- cycle withcolor Oranges 7 3;
draw z1 -- point t of ff withpen pencircle scaled 1/4;
draw z2 -- point u of ff withpen pencircle scaled 1/4;
draw z3 -- point v of ff withpen pencircle scaled 1/4;
draw z5 -- z6 withpen pencircle scaled 1/4;
draw point u of ff -- (xpart point u of ff, ypart point v of ff) -- (xpart point t of ff, ypart point v of ff)
    dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
draw P0;
label.bot("\strut ${a+b\over2}$", z3);
label.bot(btex \vbox{\openup 12pt\halign{\hfil$\displaystyle # $\hfil\cr
\ln b - \ln a > {2\over a+b} (b - a)\cr
{a+b\over2} > {b-a\over \ln b - \ln a}\cr}} etex, point 2 of bbox P0);

);
P2 = image(
fill z2 -- z1 -- subpath (t, u) of ff -- cycle withcolor Oranges 7 2;
draw z1 -- point t of ff -- point w of ff -- point u of ff -- z2 withpen pencircle scaled 1/4;
draw z4 --                  point w of ff                        withpen pencircle scaled 1/4; 
draw P0;
label.bot("\strut $\scriptstyle\sqrt{ab}$", z4);
label.bot(btex \vbox{\openup 12pt\halign{\hfil$\displaystyle # $\hfil\cr
\ln b - \ln a < {ab-a\over 2a\sqrt{ab}} + {a-ab\over2b\sqrt{ab}} = {b - a\over\sqrt{ab}}\cr
\sqrt{ab} < {b-a\over \ln b - \ln a}\cr}} etex, point 2 of bbox P0);
);

draw P1;
draw P2 shifted 240 down;
label.top("$\displaystyle b > a > 0 \;\Longrightarrow\; {a+b\over2} > {b-a\over \ln b - \ln a} > \sqrt{ab}$", point 5/2 of bbox currentpicture shifted 24 up);

\end{mplibcode}
$$
\vfill
\contrib{RBN}
\section{The mean of the squares exceeds the square of the mean}
\vfill
$$
\begin{mplibcode}
path a[], b[], c[], d[];
numeric u; u = 5mm;
a0 = unitsquare scaled 3u;
b0 = unitsquare scaled 4u;
c0 = unitsquare scaled 6u;
d0 = unitsquare scaled 7u;

a1 = a0 rotated -90;
b1 = b0 rotated -90 shifted point 1 of a1; 
b2 = b0 rotated -90 shifted point 3 of a1; 
c1 = c0 rotated -90 shifted point 1 of b1; 
c2 = c0 rotated -90 shifted point 3 of b2; 
d1 = d0 rotated -90 shifted point 1 of c1; 
d2 = d0 rotated -90 shifted point 3 of c2; 
a2 = a0 shifted point 2 of d1;
b3 = b0 shifted point 1 of a2;
c3 = c0 shifted point 1 of b3;
a3 = a0 rotated 180 shifted point 2 of d2;
b4 = b0 rotated 180 shifted point 3 of a3;
c4 = c0 rotated -90 shifted (subpath (2,3) of b1 intersectionpoint subpath (1,2) of b2);
d3 = d0 rotated -90 shifted (subpath (2,3) of c1 intersectionpoint subpath (1,2) of c4);
d4 = d0 rotated -90 shifted (subpath (2,3) of c4 intersectionpoint subpath (1,2) of c2);
a4 = a0 rotated -90 shifted (subpath (2,3) of d3 intersectionpoint subpath (1,2) of d4);

color sh[];
sh1 = Greens 7 3;
sh2 = Oranges 7 2;
sh3 = Blues 7 2;
sh4 = Reds 7 2;
numeric i; i = 0;
forsuffixes $=a,b,c,d:
    i := i + 1;
    forsuffixes @=1,2,3,4:
        if known $.@:
            fill $.@ withcolor sh[i];
            draw $.@;
            % label(str $ & str @, center $.@);
        fi
    endfor
endfor

path o[]; 
o1 = buildcycle(b1, b2); fill o1 withcolor 15/16 sh2;       
o2 = buildcycle(c3, a4); fill o2 withcolor 1/2[sh3, sh1]; 
o3 = buildcycle(c1, c4); fill o3 withcolor 15/16 sh3;      
o4 = buildcycle(c2, c4); fill o4 withcolor 15/16 sh3;      
o5 = buildcycle(d1, d3); fill o5 withcolor 15/16 sh4;      
o6 = buildcycle(d4, d3); fill o6 withcolor 15/16 sh4;      
o7 = buildcycle(d2, d4); fill o7 withcolor 15/16 sh4;      
o8 = buildcycle(b3, d3); fill o8 withcolor 3/4[sh2, sh4];      
o9 = buildcycle(b4, d4); fill o9 withcolor 3/4[sh2, sh4];      

for i=1 upto 9: draw o[i]; endfor

label.top("$|x_1|$", point -1/2 of a1);
label.lft("$|x_1|$", point +1/2 of a1);
label.top("$|x_2|$", point -1/2 of b2);
label.lft("$|x_2|$", point +1/2 of b1);
label.top("\strut$\cdots$", point -1/2 of c2);
label.lft("\hbox to 16pt{\hss$\vdots$\hss}", point +1/2 of c1);
label.top("$|x_n|$", point -1/2 of d2);
label.lft("$|x_n|$", point +1/2 of d1);

label.top("$\displaystyle {1\over n}\sum_{i=1}^n x_i^2 \ge \left({1\over n} \sum_{i=1}^n x_i \right)^2$",
point 5/2 of bbox currentpicture shifted 21 up);

label.bot(btex \vbox{\openup 8pt \halign{\hfil$\displaystyle #$\hfil\cr
n\left(x_1^2 + x_2^2 + \cdots + x_n^2\right) \ge
 \left(|x_1| + |x_2| + \cdots + |x_n|\right)^2 \ge
 \left( x_1  +  x_2  + \cdots +  x_n \right)^2\cr
 \therefore\quad{x_1^2 + x_2^2 + \cdots + x_n^2\over n} \ge 
          \left({x_1   + x_2   + \cdots + x_n  \over n}\right)^2\cr}} etex, 
          point 1/2 of bbox currentpicture shifted 21 down);



\end{mplibcode}
$$
\vfill
\contrib{RBN}
\section{The Chebyshev inequality for positive monotone sequences}
\vfill
$$
\begin{mplibcode}
numeric wd, ht; wd = 400; ht = 300;
path box; box = unitsquare xscaled wd yscaled ht;
for i=6,15,34,59,90:
z[i] = (i/120)*(wd, ht);
draw subpath (0,1) of box shifted (0, y[i]) dashed evenly withpen pencircle scaled 1/4;
draw subpath (3,4) of box shifted (x[i], 0) dashed evenly withpen pencircle scaled 1/4;
endfor
path s[];
s1 = unitsquare xscaled (x34-x15) yscaled (y34-y15);
s2 = unitsquare xscaled (x90-x59) yscaled (y34-y15);
s3 = unitsquare xscaled (x34-x15) yscaled (y90-y59-y34+y15);
s11 = s1 shifted z15;
s21 = s2 shifted z59;
s31 = s3 shifted (x59, y59+y34-y15);
s12 = s1 shifted (x15,y59);
s22 = s2 shifted (x59,y15);
s32 = s3 shifted (x15,y59+y34-y15);
fill s11 withcolor Blues 6 2; draw s11;
fill s21 withcolor Blues 6 2; draw s21;
fill s31 withcolor Blues 6 2; draw s31;
fill s12 withcolor Blues 6 3; draw subpath (-1,2) of s12;
fill s22 withcolor Blues 6 3; draw s22;
fill s32 withcolor Blues 6 3; draw subpath (1,4) of s32; draw subpath (0,1) of s32 dashed evenly; 

drawarrow center s12 -- point 5/2 of s11 shifted 10 up;
drawarrow center s22 -- point 1/2 of s21 shifted 10 down;
drawarrow center s32 -- point 7/2 of s31 shifted 10 left;

draw box withpen pencircle scaled 3/4;

label.bot("$x_1$", 1/2(0+x6, 0));
label.bot("$x_i$", 1/2(x15+x34, 0));
label.bot("$x_j$", 1/2(x59+x90, 0));
label.bot("$x_n$", 1/2(x90+wd, 0));
label.lft("$y_1$", 1/2(0,   0+ y6));
label.lft("$y_i$", 1/2(0, y15+y34));
label.lft("$y_j$", 1/2(0, y59+y90));
label.lft("$y_n$", 1/2(0, y90+ ht));

label.top("$\displaystyle \sum_{i=1}^n x_i \sum_{i=1}^n y_i \le \sum_{i=1}^n x_i y_i$", point 5/2 of bbox currentpicture shifted 24 up);

label.bot(btex \vbox{\openup12pt\halign{\hfil$#$\hfil\cr
x_i < x_j \;\&\; y_i < y_j \quad\Rightarrow\quad x_i y_j + x_j y_i \le x_i y_i + x_j y_j \cr 
\therefore\quad \left(x_1 + x_2 + \cdots + x_n\right)\left(y_1 + y_2 + \cdots + y_n\right)
\le n\left(x_1y_1 + x_2y_2 + \cdots + x_ny_n\right) \cr}} etex, point 1/2 of bbox currentpicture shifted 24 down);
\end{mplibcode}
$$
\vfill
\contrib{RBN}
\section{Jordan's inequality}
\vfill
$$
\begin{mplibcode}
path C, C'; 
C = fullcircle scaled 320;
pair A, B, M, O, P, Q;
O = origin;
A = point 0 of C;
t = 1.2;
P = point t of C;
Q = point 8-t of C;
ypart M = ypart O; xpart M = xpart P;
C' = fullcircle scaled abs (P-Q) shifted M;
B = point 0 of C';

draw fullcircle scaled 32 shifted O 
    cutafter (O--P) withpen pencircle scaled 1/4;
draw unitsquare scaled 6 shifted M withpen pencircle scaled 1/4;

draw 12 left shifted point 4 of C -- 12 right shifted B;
draw O -- P -- Q;
draw C withcolor 2/3 blue;
draw C' withcolor 2/3 red;

forsuffixes $=P: dotlabel.urt("$" & str $ & "$", $); endfor
forsuffixes $=O, Q: dotlabel.bot("$" & str $ & "$", $); endfor
forsuffixes $=A,B,M: dotlabel.lrt("$" & str $ & "$", $); endfor

label.ulft("$1$", 1/2[O,P]);
label("$x$", 22 dir 1/2 angle (P-O));
draw thelabel.top("$\sin x$", origin) rotated 90 shifted 1/2[M,P];

label.top("$\displaystyle 0 \le x \le {\pi\over2}\quad\Rightarrow\quad{2x\over\pi}\le\sin x \le x$",
   point 5/2 of bbox currentpicture shifted 21 up);

label.bot(btex \vbox{\openup 12pt\halign{\hfil$#$&$\displaystyle\enspace\Rightarrow\enspace #$\hfil\cr
OB=OM+MP \ge OA & \overarc5 PBQ \ge \overarc{6.5} PAQ \ge \overline{PQ}\cr
&\pi \sin x \ge 2x \ge 2 \sin x\cr
& {2x\over\pi} \le \sin x \le x\cr}} etex, point 1/2 of bbox currentpicture shifted 24 down);

\end{mplibcode}
$$
\vfill
\contrib{Feng Yuefeng}
\section{Young's inequality}

W.\@ H.\@ Young, “On classes of summable functions and their Fourier series”,
\textsl{Proc.\@ Royal Soc.} (A), 87 (1912) 225–229.

\bigskip
\noindent
\textsc{Theorem}: Let $\phi$ and $\psi$ be two functions, continuous,
vanishing at the origin, strictly increasing, and inverse to each others.  Then for
$a, b \ge 0$ we have
$$
ab \le \int_0^a\phi(x)dx + \int_0^b\psi(y)dy
$$
with equality if and only if $b=\phi(a)$.

\bigskip
\noindent
\textsc{Proof}:
$$
\begin{mplibcode}
path xx, yy, ff;
picture P[];

z0 = (160, 133);
xx = 10 left -- (x0, 0);
yy = 10 down -- (0, y0);

ff = origin .. 1/2 z0 {dir 1/2 angle z0} .. z0;

numeric a, b, c;
a = arctime  3/4 arclength ff of ff;
b = arctime 11/12 arclength ff of ff;
c = arctime  1/2 arclength ff of ff;
z1 = point a of ff; 
z2 = point b of ff; 
z3 = point c of ff; 

P0 = image(
    draw z1 -- (x1, 0) withpen pencircle scaled 1/4; label.bot("$a$", (x1, 0));
    draw ff withcolor Reds 8 8;
    drawarrow xx; 
    drawarrow yy;
    label.rt("$x$", point 1 of xx);
    label.top("$y$", point 1 of yy);
    label.top(btex\vbox{\openup2pt\halign{\hfil$#$\hfil\cr y=\phi(x)\cr x=\psi(y)\cr}} etex, point infinity of ff);
);

P1 = image(
    fill subpath(0, b) of ff -- (0, y2) -- cycle withcolor Blues 7 2;
    fill subpath(0, a) of ff -- (x1, 0) -- cycle withcolor Reds 7 2;
    draw z2 -- (0, y2) withpen pencircle scaled 1/4; label.lft("$b$", (0, y2));
    draw z1 -- (x1,y2) withpen pencircle scaled 1/4;
    draw P0;
    label.bot("$b > \phi(a)$", point 1/2 of bbox P0 shifted 8 down);
);
P2 = image(
    fill subpath(0, c) of ff -- (0, y3) -- cycle withcolor Blues 7 2;
    fill subpath(0, a) of ff -- (x1, 0) -- cycle withcolor Reds 7 2;
    draw (x1, y3) -- (0, y3) withpen pencircle scaled 1/4; label.lft("$b$", (0, y3));
    draw P0;
    label.bot("$b < \phi(a)$", point 1/2 of bbox P0 shifted 8 down);
);

draw P1 shifted (109 left - center P0);
draw P2 shifted (109 right - center P0);


\end{mplibcode}
$$

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\chapter{Integer sums}
\minitoc
\section{Sums of integers III}
\vfill
$$
\begin{mplibcode}
input paintball

for j=1 upto 6:
for i=0 upto 6:
    draw if j > i: bball shifted (20i - 140, -20j) else: rball shifted (20 + 20i, -20j) fi;
endfor
endfor
for j=0 upto 5:
for i=0 upto 5:
    draw if j > i: bball else: rball fi shifted (20i - 140, -20j - 200);
endfor
draw bball shifted(40 + 20j, -20j - 200);
endfor
label("${}+{}$", (0, -70));
label("${}+{}$", (0, -250));
drawarrow 140 down -- 180 down withpen pencircle scaled 1;

label.bot("$\displaystyle 1+2+\cdots+n={1\over2}\left(n^2 + n\right)$", point 1/2 of bbox currentpicture shifted 36 down);
\end{mplibcode}
$$
\vfill
\contrib{S.\@ J.\@ Barlow}
%-----------------
\section{Sums of consecutive positive integers}

Every integer $N > 1$, not a power of two, can be expressed as
the sum of two or more positive integers.

\vfill
$$
\begin{mplibcode}
input paintball

numeric u; u = 36;
for i=-4 upto 4:
for j=0 upto 3:
    draw if i+j > 1: rball else: bball fi shifted ((i, j) scaled u);
endfor
endfor

draw (-3/2u, 3u) -- (3/2u, 0);
vardef mark_dimen(expr S, a, b) = 
save t; t = 1 + 4/abs(a-b);
drawdblarrow t[b,a] -- t[a,b]; 
save P; picture P; P = thelabel(S, origin);
unfill bbox P shifted 1/2[a,b];
draw P shifted 1/2[a,b];
enddef;

interim bboxmargin := 4;
mark_dimen("$M$", (-4u, -3/4u), (4u, -3/4u));
mark_dimen("$m$", (-4.75u, 0), (-4.75u, 3u));
mark_dimen("${M-m+1\over2}$", (-4u, 3.75u), (-2u, 3.75u));
mark_dimen("${M+m-1\over2}$", (-1u, 3.75u), (+4u, 3.75u));

label.top(btex 
\vbox{\openup8pt\halign{\hfil $#$&${} = #$\hfil\cr
N & 2^n(2k+1)\quad \hbox{($n\ge0$, $k\ge1$)}\cr
m & \min\left\{ 2^{n+1}, 2k+1\right\}\cr
M & \max\left\{ 2^{n+1}, 2k+1\right\}\cr
2N & mM\cr}} etex, point 5/2 of bbox currentpicture shifted 24 up);

label.bot(btex $\displaystyle
N = \left({M-m+1\over2}\right) + \left({M-m+1\over2}+1\right) + \cdots +
\left({M+m-1\over2}\right)$ etex, point 1/2 of bbox currentpicture shifted 24 down);
\end{mplibcode}
$$
\vfill
\contrib{C.\@ L.\@ Frenzen}
\section{Consecutive sums of consecutive integers II}
\vfill
$$
\begin{mplibcode}
vardef f(expr n, c) = 
save s, p; picture p; p = image(
for y = 0 upto n-1:
    for x = 0 upto y:
        path s; s = unitsquare shifted (x, y) scaled 8;
        fill s withcolor c;
        draw s;
    endfor
endfor); p
enddef;
numeric x; x = 0;
numeric dy; dy = 0;
for i=1 upto 4:
picture F, J, K; 
F = f(i, Oranges 7 3);
J = f(i, Blues 7 3); 
J := J rotatedabout(center J, 180) shifted 8 right;

x := x + 20i + 24;
dy := - 8(i**2+2i);
for y=0 upto i-1:
    draw F shifted (x, dy + y*i*8);
endfor
for y=0 upto i:
    draw J shifted (x, dy + y*i*8);
endfor

for y=0 upto i-1:
    draw F shifted (x + (i+2)*8, dy + y*i*8 + (y+1)*8);
endfor
for y=0 upto i:
    draw J shifted (x + (i+1)*8, dy + y*i*8 + (y)*8);
endfor

string s, t; 
s = decimal (i**2) for k = i**2 + 1 upto (i+1)**2 -1: & if k=(i+1)**2-i: "&" else: "+" fi & decimal k endfor & "\cr";
t = "&" & decimal (2i+1) & "T_{" & decimal i & "}\hidewidth\cr";
label.llft("$\vbox{\halign{\hfil$#$&${}=#$\hfil\cr" & s & t & "}}$", (x + (2i+2.4)*8, dy - 4));
endfor
label.urt("$T_k = 1+ 2 + \cdots + k$ \quad \Longrightarrow", point -1 of bbox currentpicture shifted 32 up);
label.bot(btex \vbox{\openup 8pt\halign{\hfil$#$&${}=#$\hfil\cr
n^2 + (n^2+1) + \cdots + (n^2 + n)&(n^2+n+1) + \cdots + (n^2+2n)\cr&(2n+1)T_n\cr}} etex, point 1/2 of bbox currentpicture shifted 32 down);

\end{mplibcode}
$$
\vfill
\section{Sums of squares VI}
\vfill
$$
\begin{mplibcode}
vardef p(expr x, y, z) = 
0.50824829 * (1.73205x + 1.6z, -x + 1.8y + z)
enddef;
picture P[]; 
    numeric q, s, h; s = 100; h = 3/4 s; q = 1/4 s;
    path base; pair apex;
    base = p(0,0,0) -- p(s, 0, 0) -- p(s, 0, s) -- p(0, 0, s) -- cycle;
    apex = p(1/2s, h, 1/2s);
P1 = image(
    draw subpath(2, 4) of base   withcolor 7/8 white;
    draw point 3 of base -- apex withcolor 7/8 white;
    draw subpath(0, 2) of base; 
    for i=0 upto 2: 
        draw point i of base -- apex; 
    endfor
);
picture cube, wedge, corner;
cube = image(
    path ft; ft = p(0,0,0) -- 
    p(q,0,0) -- 
    p(q, 1/4h, 0) -- 
    p(q, 1/4h, q) -- 
    p(0, 1/4h, q) -- 
    p(0, 1/4h, 0) -- cycle; 
    fill ft withcolor white; draw ft; draw point 2 of ft -- point -1 of ft;
);
wedge = image(
    path vv; vv = p(0,0,0) -- p(0, 0, q) -- p(0, 1/4h, q) -- p(-1/2q, 1/4h, q) -- p(-1/2q, 1/4h, 0) -- cycle;
    fill vv withcolor Oranges 8 3; 
    draw point 1 of vv -- point 3 of vv withcolor Oranges 8 4;
    draw vv withcolor Oranges 8 8; 
    forsuffixes $=0, 2, 4: draw p(0, 1/4h, 0) -- point $ of vv withcolor Oranges 8 8;
    endfor
);
corner = image(
    path ww; ww = p(0,0,0) -- p(0, 1/4h, 1/2q) -- p(-1/2q, 1/4h, 1/2q) -- p(-1/2q, 1/4h, 0) -- cycle;
    fill ww withcolor Blues 8 3;
    draw ww withcolor Blues 8 8;
    forsuffixes $=0, 1, 3: draw p(0, 1/4h, 0) -- point $ of ww withcolor Blues 8 8;
    endfor
);
P2 = image(
    for k=0 upto 3:
        for i=0 upto 3-k:
            for j=3-k downto 0:
                draw cube shifted p(i*q + 1/2q*k, k/4*h, j*q + 1/2q*k);
            endfor
        endfor
    endfor
    picture nn; nn = thelabel("$n$", origin);
    numeric o; o = 12;
    path a[]; 
    a1 = p(s+o, 0, 0) -- p(s+o, 0, s);
    a2 = p(s+o, h, s) -- p(s+o, 0, s);

    forsuffixes $=1,2:
        drawdblarrow a$; 
        unfill bbox nn shifted point 1/2 of a$; 
        draw nn shifted point 1/2 of a$;
    endfor

);
P3 = image(
    for k=0 upto 3:
        for j=3-k downto 0:
            draw wedge shifted p(s - 1/2 q*k, k/4 * h, j*q + 1/2q * k);
        endfor
    endfor
);
P4 = image(
    for k=0 upto 3:
        draw corner shifted p(s - 1/2 q * k, k/4 * h, 1/2q * k);
    endfor
);
draw P1 shifted (-6/4s, 0); 
draw P3 shifted (0, 0);
draw P4 shifted (6/4s, 0);

draw P2 shifted (0, 7/4s);
draw P3 shifted (0, 7/4s);
draw P4 shifted (0, 7/4s);

label("$=$", (2s+20, 2s-20));
label("${}+\quad4\times{}$", (52, 20));
label("${}-\quad4\times{}$", (202,20));

label.bot(btex \vbox{\openup 16pt\halign{\hfil$#={}$&$\displaystyle #$\hfil\cr
1^2 + 2^2 + \cdots + n^2 & {1\over3}n^2\times n \quad{}+{}\quad 4 \times {n(n+1)\over2} \times {1\over4}
\quad - \quad 4 \times n \times {1\over12}\cr & {1\over6} n(n+1)(2n+1)\cr}} etex, 
point 1/2 of bbox currentpicture shifted 42 down);


\end{mplibcode}
$$
\vfill
\contrib{I.\@ A.\@ Sakmar}
\section{Sums of squares VII}
\vfill
$$
\begin{mplibcode}
vardef p(expr x, y, z) = 
0.50824829 * (1.73205x + 1.4z, -x + 2y + 1.5z)
enddef;
picture cube[];
path pcube;
numeric u;
u = 10;
pcube = origin -- p(u,0,0) -- p(u,0,u) -- p(u,u,u) -- p(0,u,u) -- p(0,u,0) -- cycle;
cube0 = image(
fill pcube withcolor Greens 8 2;
draw pcube withcolor 1/2 white; 
for t=1,3,5: draw point t of pcube -- p(u,u,0); endfor
);
cube1 = image(
fill pcube withcolor Oranges 8 2;
draw pcube withcolor 1/2 white; 
for t=1,3,5: draw point t of pcube -- p(u,u,0); endfor
);
cube2 = image(
fill pcube withcolor Blues 8 2;
draw pcube withcolor 1/2 white; 
for t=1,3,5: draw point t of pcube -- p(u,u,0); endfor
);

picture slab[], tee[];
for k=1 upto 4:
slab[k] = image(
    for j=1 upto k:
        for i=1 upto k:
            draw cube[k mod 2] shifted p(i*u, 4u, -j*u);
        endfor
    endfor
);
tee[k] = image(
    for j=1 upto k:
        for i=1 upto 2j-1:
            draw cube[k mod 2] shifted p((i-j+1)*u, (j-k+2)*u, (-k-3)*u);
        endfor
    endfor
);
pair t; t = (1 + 3/4(k**2), 5) scaled 2u;
draw slab[k] shifted t;
draw tee[k] shifted t;
label("$" & decimal k & "^2$", p(k*u+u, 4u, -k*u-u)) shifted t;
endfor

for k=4 downto 1:
draw slab[k] shifted p(-2u,     -k*u, 0);
draw tee[k]  shifted p( 6u,  (k-4)*u, (2k-1)*u);
draw slab[k] shifted p(16u-k*u, -k*u, 0);
endfor

picture kk; kk = thelabel("$\sum k^2$", origin);
for x=-5.1, 3.5, 8.6: draw kk shifted p(x*u, -4u, -u); endfor

picture bank, block;
bank = image(
for k=4 downto 1:
draw slab[k] shifted p(0, -k*u, 0);
for j=k+1 upto 9-k: 
    draw cube[(5-k) mod 2] shifted p(j*u, (4-k)*u, -k*u);
endfor
draw slab[k] shifted p(9u-k*u, -k*u, 0);
endfor
);
block = image(
draw bank;
for k=0 upto 3:
    for j=k-5 downto -5:
        for i=1 upto 9:
            draw cube2 shifted p(i*u, k*u, j*u);
        endfor
    endfor
endfor
);

draw bank shifted (-50, -150);  label.bot("$3\sum k^2$", point 1/2 of bbox bank  shifted (-50, -150));
draw block shifted (200, -150); label.bot("$6\sum k^2=n(n+1)(2n+1)$", point 1/2 of bbox block shifted (200, -150));
label.top("$\displaystyle \sum_{k=1}^n k^2 = {n(n+1)(2n+1)\over 6}$", point 5/2 of bbox currentpicture shifted 32 up);
\end{mplibcode}
$$
\vfill
\contrib{Nanny Wermuth and Hans-Jürgen Schuh}
\section{Sums of squares VIII}
\vfill
$$
\begin{mplibcode}
path t[]; 
t0 = for i=0 upto 2: 72 dir (90+120i) -- endfor cycle;
numeric s; s = 30 + arclength subpath (0, 1) of t0;

for i=1 upto 3:
t[i] = t0 rotated 120(2-i) shifted ((i-2)*s, s);
endfor
for i=0 upto 3:
filldraw t[i] withpen pencircle scaled 3 withcolor Blues 8 2;
endfor

picture elips;
elips = thelabel("$\cdots$", origin);
for i=1, 3, 5:
numeric n; n = 5 - 1/2(i-1);
for k=1 upto 3:
    for j=0 upto n:
        label(if j=2: elips rotated 60(1+k) else: "$" & decimal i & "$" fi, 
        ((i-1)/10)[(j/n)[point 2 of t[k], point 1 of t[k]], point 0 of t[k]]);
    endfor
    label("$\scriptstyle 2n-1$", point 0 of t[k]);
    label("$\scriptstyle 2n-3$", 4/5[point 1 of t[k], point 0 of t[k]]);
    label("$\scriptstyle 2n-3$", 4/5[point 2 of t[k], point 0 of t[k]]);
    label(elips rotated (60k-60), 3/5[point 1 of t[k], point 0 of t[k]]);
    label(elips rotated (60k), 3/5[point 2 of t[k], point 0 of t[k]]);
endfor
endfor
label("$\scriptstyle 2n+1$", point 0 of t0);
label(elips rotated 60, 3/5[point 0 of t0, point 1 of t0]);
label(elips rotated -60, 3/5[point 0 of t0, point 2 of t0]);
for i=1, 2, 4, 5:
for j=0 upto i:
    label(if j=3: elips else: "$\scriptstyle 2n+1$" fi, 
    (i/5)[point 0 of t0, (j/i)[point 1 of t0, point 2 of t0]]);
endfor
endfor
z1 = 1/2[center t1, center t2];
z2 = 1/2[center t2, center t3];
z3 = center t0;
label("${}+{}$", z1);
label("${}+{}$", z2);
label("${}={}$", (x1, y3));



label.top("$\displaystyle k^2 = 1+3+\cdots+(2k-1) \quad\implies\quad \sum_{k=1}^n k^2 = {n(n+1)(2n+1)\over 6}$",
point 5/2 of bbox currentpicture shifted 42 up);

label.bot(btex \vbox{\openup 12pt\halign{\hss $\displaystyle #$&$\displaystyle {}=#$\hss\cr
3\left(1^2 + 2^2 + \cdots + n^2 \right) & (2n+1)(1+2+\cdots+n)\cr
\therefore\quad 1^2 + 2^2 + \cdots + n^2& {2n+1\over3}\cdot{n(n+1)\over2}\cr
}} etex, point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill

\section{Sums of squares IX (via centroids)}

\vfill
$$
\begin{mplibcode}
numeric u, n;
n = 6; u = 42;
pair uu, vv;

uu = right scaled u;
vv = uu rotated 120;

path xx, yy, ss; 
xx = (left -- right) scaled 4u shifted (3/4 u * down);
yy = point 1/2 of xx -- point 1/2 of xx shifted (7 vv + 3.5 uu);
ss = yy shifted (5u * left);

x0 = 0; y0 = y1 = 2/3(n-1) * ypart vv;
x1 = xpart point 0 of ss;

drawoptions(dashed evenly scaled 1/2 withpen pencircle scaled 1/4); 
for t=1,2: draw yy rotatedabout(z0, 120t); endfor
draw z0 -- z1;
drawoptions();
draw xx; drawarrow yy; draw ss;

drawdot z0 withpen pencircle scaled 4;

input paintball

for i=0 upto n-1:
for j=0 upto i:
    draw bball shifted (i * vv + j * uu);
endfor 
endfor

label.top("$y$", point 1 of yy);
y = -1;
for i=1 upto n:
draw (left--right) scaled 3 shifted (x1, incr y * ypart vv);
if i <= 2:
    label.lft("$" & decimal i & "$", (x1 -3, y * ypart vv));
elseif i = n-1:
    label.lft("$n-1$", (x1 -3, y * ypart vv));
elseif i = n:
    label.lft("$n$", (x1 -3, y * ypart vv));
fi
endfor
label.lft("$\bar{y}$", (x1 - 3, y1));

picture b; b = bball shifted (-1/4u, -3/2u);

label.lft("$\Bigl($", point -1/2 of bbox b shifted 4 right);
draw b; 
label.rt("${}=1\Bigr)$", point 3/2 of bbox b shifted 4 left);

label.bot(btex \vbox{\openup 12pt\halign{\hfil$\displaystyle # $\hfil\cr
\bar{y} = 1 + {2\over3}(n-1) = {1\cdot1+2\cdot2+\cdots+n\cdot n\over1+2+\cdots+n}\cr
\therefore\quad 1^2 + 2^2 + \cdots + n^2 = {n(n+1)\over2}\cdot{1\over3}(2n+1) = {1\over6} n(n+1)(2n+1) \cr}} etex,
point 1/2 of bbox currentpicture shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}
\section{Sums of odd squares}
\vfill
$$
\begin{mplibcode}
input isometric_projection
set_projection(24, -32); ipscale := 16;
path upper, front, side;  
upper = p(0,1,0) -- p(1,1,0) -- p(1,1,1) -- p(0,1,1) -- cycle;
front = p(0,0,0) -- p(1,0,0) -- p(1,1,0) -- p(0,1,0) -- cycle;
side  = p(1,0,0) -- p(1,0,1) -- p(1,1,1) -- p(1,1,0) -- cycle;
picture Cube[];
Cube1 = image(
fill upper withcolor Oranges 8 3;
fill front withcolor background;
fill side withcolor Oranges 8 1;
draw upper; draw front; draw side;
);
Cube2 = image(
fill upper withcolor Blues 8 3;
fill front withcolor background;
fill side withcolor Blues 8 1;
draw upper; draw front; draw side;
);
Cube3 = image(
fill upper withcolor Greens 8 3;
fill front withcolor background;
fill side withcolor Greens 8 1;
draw upper; draw front; draw side;
);


picture ix, xxv;

ix = image(
for i=0 downto -2:
    for j=0 upto 2:
        draw Cube1 shifted p(j,0,i);
    endfor
endfor
draw p(3, 0, -1) -- p(3, 1, -1) -- p(2,1,-1) -- p(2,1,0) -- p(1,1,0) -- p(1,1,1) withpen pencircle scaled 1;
);
xxv = image(
for i=0 downto -4:
    for j=0 upto 4:
        draw Cube1 shifted p(j,0,i);
    endfor
endfor
draw p(5, 0, -3) for t=4 downto 1: -- p(t+1, 1, 1-t) -- p(t,1,1-t) -- p(t,1,2-t) endfor withpen pencircle scaled 1;
);

picture P[];
P0 = image(
draw Cube1;
draw ix shifted p(0,-2,0);
draw xxv shifted p(0, -5, 0);
label.top("$1^2$",      (-72, ypart p(0,  0,  0)));
label.top("$3^2$",      (-72, ypart p(0, -2, -2)));
label.top("$\vdots$",   (-72, ypart p(0, -3.5, -3)));
label.top("$(2n-1)^2$", (-72, ypart p(0, -5, -4)));
);
P1 = image(
for j=-4 upto 0:
    for k=0 downto j:
        for i=0 upto -k:
            draw Cube1 shifted p(i, 5/4 j, k);
            draw Cube1 shifted p(i, j, k) shifted 108 right;
        endfor
    endfor
endfor
);
P2 = image(
for j=-4 upto 0:
    for k=0 downto j:
        for i=0 upto -k:
            draw Cube1 shifted p(i, j, k);
        endfor
    endfor
endfor
for i=0 upto 4:
    for j=0 upto 4-i:
        for k=0 downto -i:
            draw Cube2 shifted p(3 + i, j - 4, k);
        endfor
    endfor
endfor
for j=-4 upto 0:
    for k=0 downto j:
        for i=-j upto 4:
            draw Cube3 shifted p(8+i, j, k);
        endfor
    endfor
endfor
);
P3 = image(
path a[]; picture d[];
a1 = p(0, -4, -5) -- p(7, -4, -5);
a2 = p(8, -4, -4) -- p(8, -4, 1);
a3 = p(8, -4, 1) -- p(8, 1, 1);
d1 = thelabel("$\scriptstyle 2n+1$", origin) slanted -1/4 rotated angle direction 1/2 of a1 shifted point 1/2 of a1;
d2 = thelabel("$\scriptstyle 2n-1$", origin) slanted  .68 rotated angle direction 1/2 of a2 shifted point 1/2 of a2;
d3 = thelabel("$\scriptstyle 2n-1$", origin) slanted  .34 rotated angle direction 1/2 of a3 shifted point 1/2 of a3;
forsuffixes $=1,2,3:
    drawdblarrow a$; unfill bbox d$; draw d$;
endfor
for j=-4 upto 0:
    for k=0 downto j:
        for i=0 upto 0-j:
            draw Cube1 shifted p(i, j, k);
        endfor
        for i=1-j upto 1-j:
            draw Cube2 shifted p(i, j, k);
        endfor
        for i=2-j upto 6:
            draw Cube3 shifted p(i, j, k);
        endfor
    endfor
endfor
for i=2,3:
    draw p(i-1, 1, 1) 
        for j=1 downto -2:
            -- p(i-j, j, j-1) -- p(i-j, j-1, j-1) -- p(i-j+1, j-1, j-1)
        endfor
            -- p(i+3, -3, -4) -- p(i+3, -4, -4)
    withpen pencircle scaled 1;
endfor


);

draw P0;
draw P1 shifted 160 right;
draw P2 shifted 180 down shifted 40 left;
draw P3 shifted 180 down shifted 200 right;

label.top("$\displaystyle 1^2 + 2^2 + \cdots + \left(2n-1\right)^2 = {n(2n-1)(2n+1)\over3}$", 
      point 5/2 of bbox currentpicture shifted 42 up);

label.bot(btex \vbox{\openup 12pt\halign{\hfil$\displaystyle #$&$\displaystyle{}=#$\hfil\cr
3 \times \left(1^2 + 3^2 + \cdots + \left(2n-1\right)^2\right)
&\left(1+2+\cdots+(2n-1)\right) \times (2n+1)\cr
&{(2n-1)(2n)(2n+1)\over2} = n(2n-1)(2n+1)\cr}} etex,
point 1/2 of bbox currentpicture shifted 24 down);
\end{mplibcode}
$$
\vfill
\contrib{RBN}
\section{Sums of sums of squares}
\vfill
$$
\begin{mplibcode}
numeric u; u = 13;
picture P[];
P0 = image(for i=1 upto 4:
for j=i downto 1:
    path f; f = unitsquare scaled (-j*u) shifted (0, -u * i * (i+1)/2); 
    fill f withcolor RdBu[8][2+j]; 
    for k=1 upto j-1:
        draw subpath(1,2) of f shifted (k*u, 0) dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
        draw subpath(2,3) of f shifted (0, k*u) dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
    endfor
   draw f;
endfor
endfor);

draw P0 shifted 80 left;
for n=1 upto 4:
P[n] = image(
pair start; start = origin;
for i=1 upto n:
    numeric k; k = n + 1 - i;
    path s, t; 
    s = unitsquare scaled u xscaled k yscaled (2i-1) shifted start;
    t = subpath (0,1) of s shifted (0, -k*u) --
        subpath (2,3) of s shifted (0, +k*u) -- cycle;
    fill s withcolor RdBu[8][2+i]; 
    for j=1 upto k-1:
        draw subpath (0, -1) of t shifted (j*u, 0) dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
        draw subpath (0, -1) of s shifted (j*u, 0);
    endfor
    for j=1 upto 2n:
        draw subpath (0, 1) of t shifted (0, j*u) dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
    endfor
    draw s;
    draw t;
    start := point 1 of s shifted (0, -u);
endfor) if odd n: rotated -90 fi;
endfor
P2 := P2 shifted (-u, -4u);
P3 := P3 shifted (5u, 0);
P4 := P4 shifted (-u, -11u);
interim bboxmargin := 0;
draw P1; draw bbox P1 withpen pencircle scaled 3/4;
draw P2; draw subpath (-1, 2) of bbox P2 withpen pencircle scaled 3/4;
draw P3; draw subpath (0, 3)  of bbox P3 withpen pencircle scaled 3/4;
draw P4; draw subpath (-1, 2) of bbox P4 withpen pencircle scaled 3/4;
label.top("$1+2+\cdots+n$", point 19/7 of bbox P3);    
draw thelabel.top("$1+2+\cdots+(n+1)$", origin) rotated 90 shifted point -5/6 of bbox P4;


label.top(btex $\displaystyle \sum_{k=1}^n \sum_{i=1}^k i^2
={1\over3}{n+1\choose 2}{n+2 \choose 2}$ etex, 
point 5/2 of bbox currentpicture shifted 42 up);
label.bot(btex $\displaystyle
3\left(1^2\right) + 
3\left(1^2 + 2^2\right) + 
3\left(1^2 + 2^2 + 3^2\right) + 
\cdots +
3\left(1^2 + 2^2 + \cdots + n^2\right) =
{n+1\choose2}{n+2\choose2}$ etex,
point 1/2 of bbox currentpicture shifted 24 down);
\end{mplibcode}
$$
\vfill
\contrib{C.\@ G.\@ Wastun}
\section{Pythagorean runs}
\vfill
$$
\begin{mplibcode}
numeric u; u = 3; 
path base; 
picture piece[];
base = unitsquare shifted 1/2 down scaled 24u;
forsuffixes @=1,2,3:
piece@ = image(
    path p; p = subpath (0, @/24) of base --
                subpath (3-@/24, 3) of base -- cycle;
    fill p withcolor Blues 8 2;
    for i=1 upto @-1:
        draw subpath (0, -1) of p shifted (i*u,0) withpen pencircle scaled 1/4;
    endfor
    draw p;
);
endfor

vardef dimity(expr p, t) = 
save pp, tt; 
path pp; pp = p shifted (unitvector(direction 1/2 of p) rotated 90 scaled 3 labeloffset); 
picture tt; tt = thelabel(t, point 1/2 of pp);
drawdblarrow pp; unfill bbox tt; draw tt;
enddef;

picture P[];
label.top("$4T_3 = 4(1+2+3)$", point 5/2 of base);
dimity(subpath(0, -1) of base, "$24$");
fill base withcolor Blues 8 3;
draw base;  
for i=0 upto 3:
draw piece1 shifted (36u +  i*u, 0);
draw piece2 shifted (45u + 2i*u, 0);
draw piece3 shifted (58u + 3i*u, 0);
endfor
label("$=$", (30u, 0));
label("$+$", (42.5u, 0));
label("$+$", (55.5u, 0));
for i=1 upto 3:
P[i] = image(
    path s, t; 
    s = unitsquare shifted 1/2 down scaled (24u - i*u);
    t = s shifted (58u, 0);
    draw s;
    dimity(subpath (0, -1) of s, decimal (24-i));
    dimity(point 2 of t + (i,i)*u -- point 1 of t + (i,-i)*u, decimal (24+i));
    for j=0 upto 3:
        draw piece[i] shifted (36u + (2j-3)*(1/2i*u+u), 0);
        draw piece[i] shifted (0, 1/2i*u) rotated 90j shifted point j+3/2 of t;
    endfor
    label("$=$", (51u, 0));
    label("$+$", (27.5u - i*u, 0));
);
draw P[i] shifted (0, -32u*i);
endfor

label.ulft("e.g., $n=3$:", point -1 of bbox currentpicture shifted 6 up);
label.top(btex \vbox{\openup 8pt\halign{\hfil$#$&\hss${}#{}$\hss&$#$\hfil\cr
3^2 + 4^2&=&5^2\cr
10^2 + 11^2 + 12^2&=&13^2 + 14^2\cr
21^2 + 22^2 + 23^2 + 24^2&=&25^2 + 26^2 + 27^2\cr
&\vdots\cr
T_n = 1 + 2 + \cdots + n \quad\implies\quad
\left(4T_n-n\right)^2 + \cdots + \left(4T_n\right)^2&=&
\left(4T_n+1\right)^2 + \cdots + \left(4T_n+n\right)^2\cr
}} etex, point 5/2 of bbox currentpicture shifted 24 up);
\end{mplibcode}
$$
\vfill
\contrib{Michael Boardman}
\section{Sums of cubes VII}
\vfill
$$
\begin{mplibcode}
numeric c; u = 18;
numeric x, y; x = y = 0;
for i=1 upto 4:
path s; s = unitsquare shifted down scaled (i*u);
if i<4:
    label.top("$" & decimal (i*(i-1)+1) for j=i*(i-1)+3 step 2 until i*(i-1)+2i-1: 
        & "+" & decimal j 
    endfor & "$", (x + i*i/2 * u, 0));
    label.bot("$" & decimal i & "^3$", (x + i*i/2 * u, y - i * u));
else:
    label.top("$\bigl(n(n-1)+1\bigr) + \bigl(n(n-1)+3\bigr) + \cdots + \bigl(n(n-1)+2n-1\bigr)$", (x + i*i/2 * u, -5u));
    label.bot("$n^3$", (x + i*i/2 * u, y - i * u));
fi
for j=1 upto i:
    fill s shifted (x, y) withcolor RdBu[8][1+i];
    for k=1 upto i-1:
        draw 
        if j < i:
            subpath (0,1) of s shifted (x, y+k*u) 
        else:
            subpath (0, 1-k/i) of s shifted (x, y+k*u) -- point 2+k/i of s shifted (x, y)
        fi
        withpen pencircle scaled 1/4;
    endfor
    draw s shifted (x, y);
    x := x + (i*u);
endfor
x := x + u;
if i=3:
    x := 0; y := -5u;
fi
endfor
def trrt(expr x) = ceiling 1/2(sqrt(8x + 1) - 1) enddef;
for i=10 downto 1:
path s; s = unitsquare shifted down scaled (i*u) shifted (3u, -12u);
fill s withcolor RdBu[8][1 + trrt(i)];
draw subpath (0, 2) of s withpen pencircle scaled 1/4;
endfor
path s; path s; s = unitsquare shifted down scaled (10u) shifted (3u, -12u);
draw s; 
label.top("$1+2+3+\cdots+n$", point 5/2 of s);
label.lft("$\displaystyle n(n+1)\over 2$", point 7/2 of s);
label.bot("$\displaystyle 1^3 + 2^3 + \cdots + n^3=1+3+5+\cdots+2{n(n-1)\over2}-1 = \left(n(n-1)\over2\right)^2$", point 1/2 of bbox currentpicture shifted 16 down);
\end{mplibcode}
$$
\vfill
\contrib{Alfinio Flores}
\section{Sums of integers as sums of cubes}
\vfill
$$
\begin{mplibcode}
input isometric_projection
set_projection(21, -33); ipscale := 14;
picture Cube, sideways_cube; 
path lid, face, side;
face = p(0,0,0) -- p(1, 0, 0) -- p(1,1,0) -- p(0, 1, 0) -- cycle;
side = p(1,0,0) -- p(1, 0, 1) -- p(1,1,1) -- p(1, 1, 0) -- cycle;
lid  = p(0,1,0) -- p(1, 1, 0) -- p(1,1,1) -- p(0, 1, 1) -- cycle;
Cube = image(
fill face withcolor white;
fill side withcolor Oranges 8 2;
fill lid withcolor Oranges 8 4;
drawoptions(withpen pencircle scaled 1/4);
draw face; draw subpath (0, 3) of side; draw subpath (2, 4) of lid;
drawoptions();
);
sideways_cube = image(
fill face withcolor Oranges 8 4;
fill side withcolor white;
fill lid withcolor Oranges 8 2;
drawoptions(withpen pencircle scaled 1/4);
draw face; draw subpath (0, 3) of side; draw subpath (2, 4) of lid;
drawoptions();
);
% spaced out...
for z=9 downto 5:
for y=0 upto 4:
    for x=0 upto 4:
        if 5y + x < 16 + z: 
            draw Cube shifted p(x, y, 2z);
        fi
    endfor
endfor
endfor
for z=4 downto 1:
for x=0 upto 4:
    for y=0 upto if x=4: z-1 else: 3 fi:
        draw Cube shifted p(x, y, 2z);
    endfor
endfor
endfor
for z=1,2:
draw p(5.5, 0, 2z + 1/2) -- p(6.5, 0, 2z + 1/2);
label.rt("$\scriptstyle n^2 +" & decimal z & "$", p(6.5, 0, 2z + 1/2));
endfor
draw p(5.5, 0, 16.25) -- p(6.5, 0, 16.25);
draw p(5.5, 0, 18.75) -- p(6.5, 0, 18.75);
label.rt("$\scriptstyle \left(n+1\right)^2-1$", p(6.5, 0, 16.25));
label.rt("$\scriptstyle \left(n+1\right)^2$", p(6.5, 0, 18.75));
draw thelabel("$\cdots$", origin) rotated 33 shifted p(7, 0, 10);
% now together...
numeric dx, dz; dx = 18; dz = -25;
for z=9 downto 5:
for y=0 upto 4:
    for x=0 upto 4:
        if 5y + x < 16 + z: 
            draw Cube shifted p(dx+x, y, z);
        fi
    endfor
endfor
endfor
for z=4 downto 1:
for x=0 upto 4:
    for y=0 upto if x=4: z-1 else: 3 fi:
        draw Cube shifted p(dx+x, y, z);
    endfor
endfor
endfor
draw p(dx+4,0,1) -- p(dx+5, 0,1) -- p(dx+5, 0, 5) -- p(dx+5, 4, 5) 
-- p(dx+4, 4, 5) -- p(dx+4, 4, 4) 
-- p(dx+4, 3, 4) -- p(dx+4, 3, 3)
-- p(dx+4, 2, 3) -- p(dx+4, 2, 2)
-- p(dx+4, 1, 2) -- p(dx+4, 1, 1)
-- cycle
withpen pencircle scaled 1;
%
% assembled as cubes...
for z=9 downto 5:
for y=0 upto 4:
    for x=0 upto 4:
        if 5y + x < 16 + z: 
            draw Cube shifted p(dx+x, y, dz+z);
        else:
            draw sideways_cube shifted p(dx+x, y, dz+z);
        fi
    endfor
endfor
endfor
for z=4 downto 1:
for x=0 upto 3:
    for y=0 upto 3:
        draw Cube shifted p(dx+x, y, dz+z);
    endfor
endfor
endfor
draw p(dx+1,4,dz+5) -- p(dx+5, 4, dz+5)
 -- p(dx+5, 4, dz+9) -- p(dx+5, 5, dz+9) 
 -- p(dx+4, 5, dz+9) -- p(dx+4, 5, dz+8) 
 -- p(dx+3, 5, dz+8) -- p(dx+3, 5, dz+7) 
 -- p(dx+2, 5, dz+7) -- p(dx+2, 5, dz+6) 
 -- p(dx+1, 5, dz+6) -- p(dx+1, 5, dz+5) 
-- cycle
withpen pencircle scaled 1;

dx := dx + 16; dz := dz + 3;
for z=9 downto 5:
for y=0 upto 4:
    for x=0 upto 4:
        draw Cube shifted p(dx+x, y, dz+z);
    endfor
endfor
endfor
dz := dz - 3;
for z=4 downto 1:
for x=0 upto 3:
    for y=0 upto 3:
        draw Cube shifted p(dx+x, y, dz+z);
    endfor
endfor
endfor

label.top(btex \vbox{\openup 8pt\halign{\hfil$#$&\hss${}#{}$\hss&$#$\hfil\cr
2 + 3 + 4&=&1 + 8\cr
5 + 6 + 7 + 8 + 9&=&8 + 27\cr
10 + 11 + 12 + 13 + 14 + 15 + 16 &=&27 + 64\cr
&\vdots\cr
\left(n^2+1\right) +
\left(n^2+2\right) + 
\cdots + 
\left(n+1\right)^2&=&
n^3 + \left(n+1\right)^3\cr
}} etex, point 5/2 of bbox currentpicture shifted 24 up);
\end{mplibcode}
$$
\vfill
\contrib{RBN}

\section{The square of any odd number is the difference between two triangular
numbers}
\vfill
$$
\begin{mplibcode}
input paintball

numeric n, s; n = 4; s = 21;
picture P[], txt[], arr[];
txt0 = thelabel("$1$", origin);
txt1 = thelabel("$n$", origin);
txt2 = thelabel("$2n+1$", origin);
txt3 = thelabel("$3n+1$", origin);
arr1 = image(drawdblarrow (left--right) scaled (3/8 n * s + 1/8 s); unfill bbox txt1; draw txt1);
arr2 = image(drawdblarrow (left--right) scaled (    n * s + 1/8 s); unfill bbox txt3; draw txt2);
arr3 = image(drawdblarrow (left--right) scaled (3/2 n * s + 1/8 s); unfill bbox txt3; draw txt3);

P1 = image(
for i=-n upto n:
    for j = -n upto n:
        draw if j < i + 5: bball else: rball fi shifted ((i, j) scaled s);
    endfor
endfor
draw txt0 shifted (       (0, n+1) scaled s);
draw arr1 shifted ((+(n+1)/2, n+1) scaled s);
draw arr1 shifted ((-(n+1)/2, n+1) scaled s);
draw arr2 shifted (      (0, -n-1) scaled s);
draw arr2 rotated 90 shifted ((-n-1, 0) scaled s);
);
P2 = image(
for j=-2n upto n:
    for i=j upto n:
        draw if i < -n: gball elseif j > 0: rball else: bball fi shifted ((i, j) scaled s);
    endfor
endfor
draw arr1 shifted ((-13/8n, -2n-1) scaled s);
draw arr3 rotated -90 shifted ((n+1, -1/2n) scaled s);
);

draw P1;
draw P2 shifted (2n*s, -n*s);

label.top("$1+2+\cdots+n=T_n\quad\Rightarrow\quad\left(2n+1\right)^2 = T_{3n+1} - T_n$", 
point 5/2 of bbox currentpicture shifted 16 up);
\end{mplibcode}
$$
\vfill
\contrib{RBN}
\section{Triangular numbers mod 3}
\vfill
$$
\begin{mplibcode}
numeric u, v; u = 18; v = ypart ((u,0) rotated 60);
picture b[], P[]; path o; o = fullcircle scaled 1/2 u; 
forsuffixes $=1,2,3: b$ = image(fill o withcolor RdYlGn 3 $; draw o withpen pencircle scaled 1/4); endfor
P1 = image(
for i=1,2,3:
    for j=6,5,4:
        for k=1 upto j:
            draw b[i] shifted (k*u - 1/2j*u - 2u, (10/3-j)*v) rotated 120i;
        endfor
    endfor
endfor
label.bot("$T_{3k} = 3\left(T_{2k}-T_k\right)$", (0, -10/3v));
);
P2 = image(
for i=1,2,3:
    for j=7,6,5:
        for k=1 upto j:
            draw b[i] shifted (k*u - 1/2j*u - 2u, (12/3-j)*v) rotated 120i;
        endfor
    endfor
endfor
draw o withpen pencircle scaled 1/4;
for t=-45, 45: draw (left -- right) scaled 1/2 u rotated t withcolor 2/3 red; endfor
label.bot("$T_{3k+1} = 1 + 3\left(T_{2k+1}-T_{k+1}\right)$", (0, -11/3v));
);
P3 = image(
for i=1,2,3:
    for j=7,6,5,4:
        for k=1 upto j:
            draw b[i] shifted (k*u - 1/2j*u - 5/2u, (11/3-j)*v) rotated 120i;
        endfor
    endfor
endfor
label.bot("$T_{3k+2} = 3\left(T_{2k+1}-T_k\right)$", (0, -4v));
);
draw P1;
draw P2 shifted (6u, 11v);
draw P3 shifted (12u, -3v);
label.top(btex $1+2+\cdots+n=T_n \enspace\Rightarrow\enspace
\left\{\vcenter{\vbox{\openup8pt\halign{#\hfil\cr
$T_n \equiv 1 \bmod 3$, \quad $n\equiv 1 \bmod 3$\cr
$T_n \equiv 0 \bmod 3$, \quad $n\not\equiv 1 \bmod 3$\cr
}}}\right.$ etex, point 5/2 of bbox currentpicture shifted 21 up);
\end{mplibcode}
$$
\vfill
\section{Counting triangular numbers IV: Counting cannonballs}
\vfill
$$
\begin{mplibcode}
input paintball
input isometric_projection
set_projection(-16, 0);
ipscale := 16;

picture P[];
P1 = image(
for k=4 downto 0:
    for j=k downto 0:
        for i=0 upto j:
            draw cball shifted p(i - 0.5 j, -0.866 k, -0.866 j + 0.5k);
        endfor
    endfor
endfor
path a; picture n; a = p(3,0,-1) -- p(3, -4*.866, -1);
n = thelabel("$n$", point 1/2 of a);
drawdblarrow a; unfill bbox n; draw n;
);
P2 = image(
for k=4 downto 0:
    for j=k downto 0:
        for i=0 upto j:
            draw cball shifted p(i - 0.5 j, -2 k, -0.866 j + 0.5k);
        endfor
    endfor
    if k < 4:
        label("$" & if k<3: decimal ((k+1)*(k+2)/2) else: "\vdots" fi & "$", p(5, -2k, 0));
        label("$+$",                                                         p(5, -2k-1, 0));
    else:
        label("$n(n+1)/2$", p(5, -2k, 0));
    fi
endfor
);
P3 = image(
set_projection(-16, 32);
for i=4 downto 0:
    for j=i downto 0:
        for k=0 upto 4-i:
            draw cball shifted p(i-1/2k, -0.866 k, -0.866j + 0.5k) shifted (42i, -16i);
        endfor
    endfor
endfor
label.bot(btex $
1(n)   \quad + \quad 
2(n-1) \quad + \quad 
3(n-2) \quad + \quad 
\cdots \quad + \quad n(1)$ etex, (92, -88));
);

draw P1;
draw P2 shifted (180, 40);
draw P3 shifted (0, -144);

label.top(btex $\displaystyle
1+2+\cdots+k=T_k \enspace\Rightarrow\enspace
\sum_{k=1}^n T_k = \sum_{k=1}^n k(n-k+1)
$ etex, point 5/2 of bbox currentpicture shifted 21 up);
\end{mplibcode}
$$
\vfill
\contrib{Deanna B.\@ Haunsperger and Stephen F.\@ Kennedy}

\section{Alternating sums of triangular numbers}
\vfill
$$
\begin{mplibcode}
input paintball
picture P[];
P1 = image(
z1 = 9 up;
for k=1 upto 5:
    for i=1 upto k:
        for j=1 upto i: 
            draw if odd i: bball else: rball fi shifted (18 * (i-j, j)) shifted z1;
        endfor
    endfor
    x1 := x1 + 18k + 24;
    y1 := y1 - 9;
    if k < 5:
        label.lft("$" & if odd k: "-" else: "+" fi & "$", (-14,  9k+18) + z1);
    fi
endfor
);
P2 = image(
for i=1 step 2 until 5:
    for j=1 upto i: 
        draw bball shifted (18 * (i-j, j));
    endfor
endfor
draw origin -- (56, 56) dashed evenly withpen pencircle scaled 1/4;
);
P3 = image(
for i=-1 upto 1:
    for j=-1 upto 1:  
        draw bball shifted (36/sqrt(2) * (i,j));
    endfor
endfor
draw (2 left--right) scaled 30 rotated 45 shifted 13 up 
    dashed evenly withpen pencircle scaled 1/4;
);

label(P1, origin);
label("$=$",       (-120, -120));
label(P2,           (-60, -124));
label("$=$",          (0, -120));
label(P3 rotated 45, (80, -122));


label.top(btex $\displaystyle
1+2+\cdots+k=T_k \enspace\Rightarrow\enspace
\sum_{k=1}^{2n-1} \left(-1\right)^{k+1} T_k = n^2
$ etex, point 5/2 of bbox currentpicture shifted 21 up);
\end{mplibcode}
$$
\vfill
\contrib{RBN}

\section{The sum of the squares of consecutive triangular numbers is triangular}
\vfill
$$
\begin{mplibcode}
picture P[];
vardef T(expr n) = 
numeric u, m; u = 8; m = abs(n);
numeric r; r = n/m; % don't use n=0
picture s; s = image(
    fill unitsquare scaled u withcolor if r > 0: Oranges 8 1 else: Greens 8 2 fi;
    draw unitsquare scaled u withpen pencircle scaled 1/4;
);
picture trig; 
trig = image(
    for i=0 upto m - 1:
        for j=0 upto m - 1 - i:
            draw s shifted (u*(i, j));
        endfor
    endfor
    draw (origin -- for i=0 upto m-1: (m-i, i) -- (m-i, i+1) -- endfor (0, m) -- cycle) scaled u;
) rotated 90r;
trig
enddef;
for n=1 upto 4:
P[n] = image(
    for i=0 upto n:
        for j=0 upto n-i:
            if (i < n) and (j  < n - i):
                draw T(-n) shifted (u*(n+1)*(-i-1,j+1));
            fi
            draw T(n+1) shifted (u*(n+1)*(-i,j));
        endfor
    endfor
    label.bot("$T_" & decimal n & "^2 + T_" & decimal (n+1) & "^2 = T_{" & decimal (n*n+2n+1) & "}$", 
    (-u/2 * (n+1) * (n+1), 0));
);
draw P[n] shifted (5/2u * n * n * (1,-1));
endfor
P5 = image(draw T(4); draw T(-3) shifted 4(-u,u))
shifted lrcorner bbox currentpicture shifted 80 down;

draw P5;
label.lft("\textsc{Note}: This is a companion result to the more familiar $T_{n-1} + T_n = n^2 \,\rightarrow$", point -1/2 of bbox P5);

label.top(btex $\displaystyle
1+2+\cdots+n=T_n \enspace\Rightarrow\enspace
T_{n-1}^2 + T_n^2 = T_{n^2}
$ etex, point 5/2 of bbox currentpicture shifted 21 up);
\end{mplibcode}
$$
\vfill
\contrib{RBN}

\section{Recursion for triangular numbers}
\vfill
$$
\begin{mplibcode}
input isometric_projection
set_projection(20, -32); ipscale := 16;
path upper, front, side;  
upper = p(0,1,0) -- p(1,1,0) -- p(1,1,1) -- p(0,1,1) -- cycle;
front = p(0,0,0) -- p(1,0,0) -- p(1,1,0) -- p(0,1,0) -- cycle;
side  = p(1,0,0) -- p(1,0,1) -- p(1,1,1) -- p(1,1,0) -- cycle;
picture Cube[];
Cube1 = image(
fill upper withcolor Oranges 8 3;
fill front withcolor background;
fill side withcolor Oranges 8 1;
draw upper; draw front; draw side;
);
Cube2 = image(
fill upper withcolor Blues 8 3;
fill front withcolor background;
fill side withcolor Blues 8 1;
draw upper; draw front; draw side;
);
Cube3 = image(
fill upper withcolor Greens 8 3;
fill front withcolor background;
fill side withcolor Greens 8 1;
draw upper; draw front; draw side;
);
vardef dimity(expr p, t) = 
save pp, tt; 
path pp; pp = p shifted (unitvector(direction 1/2 of p) rotated 90 scaled 4 labeloffset); 
picture tt; tt = thelabel(t, point 1/2 of pp);
drawdblarrow pp; unfill bbox tt; draw tt;
enddef;

picture P[];
P1 = image(
for x=0 upto 4:
    for y=0 upto 4-x:
        draw Cube1 shifted p(x,y,0);
    endfor
endfor
dimity(p(0,0,0) -- p(0,5,0), "$\scriptstyle n$");
dimity(p(5,0,0) -- p(0,0,0), "$\scriptstyle n$");
label("$T_n$", p(2,-3,0));
);
P2 = image(
for x=0 upto 5:
    for y=0 upto 5-x:
        for z=4 downto 0:
            draw if x=0: Cube2 else: Cube1 fi shifted p(x,y,z);
        endfor
    endfor
endfor
dimity(p(0,0,0) -- p(0,6,0), "$\scriptstyle n+1$");
dimity(p(6,0,0) -- p(0,0,0), "$\scriptstyle n+1$");
dimity(p(6,0,6) -- p(6,0,1), "$\scriptstyle n$");
label("$n\cdot T_{n+1}$", p(3,-3,0));
);
P3 = image(
for x=0 upto 5:
    for y=0 upto 5-x:
        for z=4 downto 0:
            draw if x=0: Cube2 shifted p(-3,y,z-1) else: Cube1 shifted p(x,y,z) fi;
        endfor
    endfor
endfor
draw p(-3,5,-1) -- p(-2,5,-1) -- p(-2,5,0) -- p(-2,4,0) 
  -- p(-2,4,1) -- p(-2,3,1) -- p(-2,3,2) -- p(-2,2,2) 
  -- p(-2,2,3) -- p(-2,1,3) -- p(-2,1,4) withpen pencircle scaled 1.21;
label("$2\cdot T_n + n\cdot T_n$", p(1, -3, 0));
);
P4 = image(
for x=0 upto 4:
    for y=0 upto 4-x:
        for z=6 downto 0:
            draw
            if z mod 6 = 0:
                Cube2 shifted p(x,y,z if z>0: + else: - fi 1/8)
            else:
                Cube1 shifted p(x,y,z)
            fi;
        endfor
    endfor
endfor
dimity(p(0,0,0) -- p(0,5,0), "$\scriptstyle n$");
dimity(p(5,0,0) -- p(0,0,0), "$\scriptstyle n$");
dimity(p(5,0,8) -- p(5,0,1), "$\scriptstyle n+2$");
label("$(n+2)\cdot T_n$", p(5,-2,0));
);

draw P1 shifted 60 left;
draw P2 shifted 60 right;
draw P3 shifted (-72, -180);
draw P4 shifted (+96, -180);

label.top(btex $\displaystyle
1+2+\cdots+n=T_n \enspace\Rightarrow\enspace T_{n+1} = {n+2\over n}T_n
$ etex, point 5/2 of bbox currentpicture shifted 21 up);
label.bot(btex $\displaystyle
n\cdot T_{n+1} = (n+2) \cdot T_n \enspace\Rightarrow\enspace T_{n+1} = {n+2\over n}T_n
$ etex, point 1/2 of bbox currentpicture shifted 21 down);
\end{mplibcode}
$$
\vfill

\section{Indenties for triangular numbers}
\vfill
$$
\begin{mplibcode}
numeric u; u = 8;
vardef T(expr n) = 
numeric m; m = abs(n);
numeric r; r = n/m; % don't use n=0
picture s; s = image(
    fill unitsquare scaled u withcolor if r < 0: Oranges 8 1 else: Greens 8 2 fi;
    draw unitsquare scaled u withpen pencircle scaled 1/4;
);
picture trig; 
trig = image(
    for i=0 upto m - 1:
        for j=0 upto m - 1 - i:
            draw s shifted (u*(i, j));
        endfor
    endfor
    draw (origin -- for i=0 upto m-1: (m-i, i) -- (m-i, i+1) -- endfor (0, m) -- cycle) scaled u;
) rotated (90+90r);
trig
enddef;
picture P[];
picture t[]; t5 = T(-5); t6 = T(6);

P1 = image(
for x=0 upto 3:
    for y=0 upto 3-x:
        draw t5 shifted (6u*(x,y));
        if x+y < 3:
            draw t6 shifted (6u*(x+1, y+1));
        fi
    endfor
endfor
label.urt("$T_{n-1}T_k + T_n T_{k-1} = T_{nk-1}$", (20u, 4u));
);
P2 = image(
for x=0 upto 3:
    for y=0 upto 3-x:
        draw t6 shifted (-6u*(x,y));
        if x+y < 3:
            draw t5 shifted (-6u*(x+1, y+1));
        fi
    endfor
endfor
label.llft("$T_{n}T_k + T_{n-1} T_{k-1} = T_{nk}$", -(20u, 4u));
);
draw P1;
draw P2 shifted 36(u, u);
label.top("$T_n=1+2+\cdots+n\enspace\Rightarrow$", point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{RBN}

\section{More identities for triangular numbers}
\vfill
$$
\begin{mplibcode}
numeric u; u = 8;
vardef T(expr n) = 
numeric m; m = abs(n);
numeric r; r = n/m; % don't use n=0
picture s; s = image(
    fill unitsquare scaled u withcolor if r < 0: Oranges 8 1 else: Greens 8 2 fi;
    draw unitsquare scaled u withpen pencircle scaled 1/4;
);
picture trig; 
trig = image(
    for i=0 upto m - 1:
        for j=0 upto m - 1 - i:
            draw s shifted (u*(i, j));
        endfor
    endfor
    draw (origin -- for i=0 upto m-1: (m-i, i) -- (m-i, i+1) -- endfor (0, m) -- cycle) scaled u;
) rotated (90+90r);
trig
enddef;
picture P[];
picture t[]; t6 = T(6); t5 = T(5) rotated 180;
picture sq; sq = image(
fill unitsquare scaled 6u withcolor Oranges 8 1;
for i=1 upto 5:
    draw ((i, 0) -- (i, 6)) scaled u dashed withdots scaled 1/4 withpen pencircle scaled 1/4;
    draw ((0, i) -- (6, i)) scaled u dashed withdots scaled 1/4 withpen pencircle scaled 1/4;
endfor
draw unitsquare scaled 6u;
);
P1 = image(
for x=0 upto 3:
    for y=0 upto 3-x:
        draw if x+y < 3: sq else: t5 fi shifted (6u*(x,y));
    endfor
endfor
label.urt("$n^2T_{k-1} + kT_{n-1} = T_{nk-1}$", (20u, 4u));
);
P2 = image(
for x=0 upto 3:
    for y=0 upto 3-x:
        draw if x+y < 3: sq rotated 180 else: t6 fi shifted (-6u*(x,y));
    endfor
endfor
label.llft("$n^2T_{k-1} + kT_{n} = T_{nk}$", -(20u, 4u));
);
draw P1;
draw P2 shifted 36(u, u);
label.top("$T_n=1+2+\cdots+n\enspace\Rightarrow$", point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{James O.\@ Chilaka}

\section{Identities for pentagonal numbers}
\vfill
$$
\begin{mplibcode}
numeric s, r, d; s = 18; 1.8d = s; r = s / 2 / cosd 54;
path p[];
picture P[], xo, o[];
color cb[]; cb0 = Blues 7 6; cb1 = Oranges 7 3;  cb2 = Greens 7 1;
xo = image(draw fullcircle scaled .9d; 
draw (left--right) scaled 0.6 d rotated 45;
draw (left--right) scaled 0.6 d rotated -45;
);
for i=0 upto 2:
   o[i] = image(
       fill fullcircle scaled d withcolor cb[i];
       draw fullcircle scaled d;
    );
endfor

p1 = (for t=0 upto 4: up scaled r rotated  72t -- endfor cycle) shifted (0, -r);
p2 = origin -- for t=0 upto 3: left scaled s rotated 60t -- endfor cycle;
pair u, v; 
u = point 2 of p2; v = point 3 of p2;

for k=1 upto 2:
   P[k] = image(
       for i = 1 upto 3:
           draw p[k] scaled i;
       endfor
       for i = 1 upto 3:
           for t = i upto i * length(p[k]) - i: 
               draw o0 shifted (point t/i of p[k] scaled i);
           endfor
       endfor
       draw o0;
       if k=2:
           draw xo shifted 3u shifted v;
           draw xo shifted 3u shifted 2v;
           draw xo shifted 3u shifted 3v;
           draw xo shifted 2u shifted 2v;
           draw xo shifted 2u shifted 3v;
           draw xo shifted  u shifted 3v;
       fi
   );
endfor

drawarrow (left--right) scaled 20;

label.lft(P1, 42 left);
label.rt(P2, 42 right);
label.bot("$P_n = T_{2n-1} - T_{n-1}$", 64 down);

P3 = image(
    for i=0 upto 2:
        for j=1 upto 3:
            for k=1 upto j * length p2:
                draw o[i] shifted (point k/j of p2 scaled j) 
                    shifted -11/3 v shifted 1/3 u 
                    rotated 120i;
            endfor
        endfor
    endfor
    label.bot("$3P_n = T_{3n-1}$", 4(u+v));
);
draw P3 shifted 180 down;

label.top(btex $\left.
\vcenter{\openup 4pt\halign{$#$\hfill\cr
P_n = 1 + 4 + 7 + \cdots + (3n-2)\cr
T_n = 1 + 2 + 3 + \cdots + n\cr}} \right\} \Rightarrow {}$ etex,
point 5/2 of bbox currentpicture shifted 42 up);


\end{mplibcode}
$$
\vfill

\section{Sums of octagonal numbers}

\vfill
$$
\begin{mplibcode}
numeric s, r, d; s = 18; 2d = s; 2r * cosd 67.5 = s;
picture P[], o[];
color cb[], cf[]; 
cf0 = Blues 7 1; cf1 = Oranges 7 1;  cf2 = Greens 7 1;
cb0 = Blues 7 3; cb1 = Oranges 7 3;  cb2 = Greens 7 3;
for i=0 upto 2:
   o[i] = image(
       fill fullcircle scaled d withcolor cb[i];
       draw fullcircle scaled d;
   );
endfor

P1 = image(
   path octagon, O[];
   octagon = (for t=0 upto 7: down scaled r rotated (45t-45/2) -- endfor cycle) shifted (0, -r);
   O1 = octagon shifted - point 0 of octagon;
   O2 = O1 scaled 2; 
   fill point 0 of O2 -- subpath (3,5) of O2 -- cycle withcolor cf0;
   for p = -1, 1:
       fill subpath (1/2p, 3/2p) of O2 -- cycle withcolor cf1;
       fill subpath (2p, 5/2p) of O2 -- point 2p of O1 -- cycle withcolor cf2;
   endfor
   draw O1; draw O2;
   for t = 10, 13, 14, 15, 23, 23.5, 24, 24.5, 25:
       draw o0 shifted point t mod 10 of O[floor (t/10)];
   endfor
   for t = 20.5, 21, 21.5, 26.5, 27, 27.5:
       draw o1 shifted point t mod 10 of O[floor (t/10)];
   endfor
   for t = 12, 22, 22.5, 25.5, 26, 16:
       draw o2 shifted point t mod 10 of O[floor (t/10)];
   endfor
);
P2 = image(
   picture t[], q;
   for i=1,2:
       t[i] = image(
           fill for j=0 upto 2: (0, d) rotated 120j -- endfor cycle withcolor cf[i];
           for j=0 upto 2: draw o[i] shifted (0,d) rotated 120j; endfor);
   endfor
   q = image(
       fill unitsquare shifted -(1/2, 1/2) scaled 2s withcolor cf0;
       for j=-1 upto 1: for k=-1 upto 1: draw o0 shifted (j*s, k*s); endfor endfor);
   draw t1 shifted (-s, 0); draw t1 shifted (s, 0); 
   draw t2 shifted (-s, 3/2s); draw t2 shifted (s, 3/2s);
   draw q shifted (0, 4s);
);
vardef octaframe(expr n, tag) = 
   save t, m; numeric t, m;
   m + 1 = n; 2t = m * m + m;
   save A; picture A; 
   save a, b, c; path a, b, c;
   a = unitsquare xscaled t yscaled 2 scaled s;
   b = unitsquare xscaled n yscaled m scaled s shifted point 1 of a;
   c = unitsquare scaled n scaled s shifted point 3 of b; 
   A = image(
       if n > 1:
           fill a withcolor cf1; draw a; draw point -1/2 of a -- point 3/2 of a;
           fill b withcolor cf2; draw b; 
           draw (left--right) scaled 400 rotated -45 shifted center b 
               cutbefore subpath (3,2) of b cutafter subpath (1, 0) of b;
       fi
       fill c withcolor cf0; draw c;
       for j=1 upto 2:
           for i=1 upto t:
               draw o1 shifted (i*s - 1/2 s, j*s - 1/2 s);
           endfor
       endfor
       for i=1 upto n:
           for j=1 upto m:
               draw o2 shifted (i*s - 1/2 s, j*s - 1/2 s) shifted point 0 of b;
           endfor
       endfor
       for i=1 upto n:
           for j=1 upto n:
               draw o0 shifted (i*s - 1/2 s, j*s - 1/2 s) shifted point 0 of c;
           endfor
       endfor
       label.top(tag, point 5/2 of c);
       draw point 0 of a -- point 1 of b -- point 2 of c withpen pencircle scaled 1;
   ); A enddef;
P3 = image(
   draw octaframe(3, nullpicture);
);

draw P1 shifted 144 left; draw P2; draw P3 shifted 89 right;

label.bot("$T_k = 1 + 2 + \cdots + k \Rightarrow O_k = k^2 + 4T_{k-1}$", 21 down);

P4 = image(
   draw octaframe(5, "$n$"); 
   draw octaframe(4, "$\cdots$") shifted (0, 2s);
   draw octaframe(3, "$3$")      shifted (0, 4s);
   draw octaframe(2, "$2$")      shifted (0, 6s);
   draw octaframe(1, "$1$")      shifted (0, 8s);
   label.lft("$1$", (0, 17/2s));
   label.rt("$n$", (15s, 13/2s));
   label.rt("$n-1$", (15s, 2s));
   picture a, b; path p, q;
   p = (0, -s) -- (15s, -s);
   q = (17s, 0) -- (17s, 9s);
   a = thelabel("$\displaystyle {n(n+1)\over2}$", origin) shifted point 1/2 of p;
   b = thelabel("$\displaystyle 2n - 1$", origin) shifted point 1/2 of q;
   drawdblarrow p; unfill bbox a; draw a;
   drawdblarrow q; unfill bbox b; draw b;
);

draw P4 shifted (-15/2s, -15s);


label.bot(btex $\displaystyle
\sum_{k=1}^n O_k = 1 + 8 + 21 + 40 + \cdots + \left(n^2 + 4T_{n-1}\right) = {n(n+1)(2n-1)\over 2}
$ etex, point 1/2 of bbox currentpicture shifted 24 down);
\end{mplibcode}
$$
\vfill
\contrib{James O.\@ Chilaka}

\section{Sums of products of consecutive integers I}
\vfill
$$
\begin{mplibcode}
numeric s; s = 23;
vardef box(expr n) = 
    path a, b;
    a = unitsquare xscaled n yscaled (n + 1) scaled s;
    b = unitsquare xscaled 1 yscaled (n*(n+1)/2) scaled s rotated 90 shifted point 2 of a;
    picture p; p = image(
        fill a withcolor Blues 7 2; 
        fill b withcolor Oranges 7 2; 
        label(if n > 4: "$n(n+1)$" elseif n = 4: "$\cdots$" else: "$" & decimal n & "\cdot" & decimal (n+1) & "$" fi, 
        (xpart center a, ypart center b - s));
        label(if n > 4: "$1+2+\cdots+n$" elseif n = 4: "$\cdots$" else: "$1" for i=2 upto n: & "+" & decimal i endfor & "$" fi, center b);
        path t; t = origin -- right scaled s;
        draw t shifted (0,s) for i=2 upto n: -- t shifted (s*(i-1,i)) endfor dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
        draw a;
        draw b;
    ); p
enddef;

for n=1 upto 5:
    draw box(n) shifted (s*n*(n-1)/2, 0);
endfor

path xx, yy; picture lx, ly; 
xx = (origin -- 15 right * s) shifted 22 down;
yy = (origin --  7 up * s) shifted 24 left;
drawdblarrow xx; drawdblarrow yy; interim bboxmargin := 8;
lx = thelabel("$\displaystyle {n(n+1)\over2}$", point 1/2 of xx); unfill bbox lx; draw lx;
ly = thelabel("$n+2$", point 1/2 of yy); unfill bbox ly; draw ly;

label.top("$\displaystyle \sum_{k=1}^n k (k+1) = {n(n+1)(n+2)\over3}$", point
5/2 of bbox currentpicture shifted 42 up);
label.bot(btex
\vbox{\openup 16pt\halign{#\hfill\cr
$T_k = 1 + 2 + \cdots + k \enspace\Rightarrow$\cr
\hfill $\displaystyle 1\cdot2 + 2\cdot3 + \cdots + n(n+1) + \left(T_1+T_2+\cdots+T_n\right)={n(n+1)(n+2)\over2}$,\cr
\hfill $\displaystyle \left(T_1+T_2+\cdots+T_n\right) = {1\over2}\left( 1\cdot2 + 2\cdot3 + \cdots + n(n+1)\strut\right)$,\cr
\hfill \therefore\quad$\displaystyle {3\over2}\left( 1\cdot2 + 2\cdot3 + \cdots + n(n+1)\strut\right) = {n(n+1)(n+2)\over2}$.\cr
}} etex, point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{James O.\@ Chilaka}

\section{Sums of products of consecutive integers II}
\vfill
$$
\begin{mplibcode}
numeric u, k, n; u = 12; k = 4; n = 5;
vardef t(expr n) = n*(n+1)/2 enddef;
path b[]; for i=1 upto n: b[i] = unitsquare xscaled t(i + 2) yscaled -t(i) scaled u; endfor
picture P[]; P1 = image(
fill b4 withcolor Oranges 8 4;
fill b3 withcolor background;
z0 = (xpart point 2 of b3, ypart point 2 of b4);
fill subpath (2,3) of b3 -- point 3 of b4 -- z0 -- cycle
    withcolor Oranges 8 3;
draw z0 -- point 2 of b3 dashed evenly withpen pencircle scaled 1/4;
draw b3; draw b4;
for i=0 upto k+2: draw (up--down) scaled 2 shifted (t(i)*u, 0); endfor
for i=0 upto k: draw (left--right) scaled 2 shifted (0, -t(i)*u); endfor
for i=1,2:
    label.top("$" & decimal i & "$", 1/2u*(t(i-1)+t(i), 0));
    label.lft("$" & decimal i & "$", -1/2u*(0, t(i-1)+t(i)));
endfor
label.lft("$\vdots$", -1/2u*(0, t(2)+t(3)));
label.lft("$k$",      -1/2u*(0, t(3)+t(4)));
label.top("$\cdots$", 1/2u*(t(2)+t(3), 0));
label.top("$k+1$", 1/2u*(t(4)+t(5), 0));
label.top("$k+2$", 1/2u*(t(5)+t(6), 0));
label.top("$1+2+\cdots+(k+1)$", point 5/2 of b3);
label("$\displaystyle k{(k+1)(k+2)\over2}$", 1/2[point 2 of b3, point 3 of b4]);
label("$\displaystyle (k+2){k(k+1)\over2}$", 1/2[point 1 of b4, z0]);
label.rt(TEX("$1+2+\cdots+k$") rotated 90, point 3/2 of b4);
);
P2 = image(
for i=n downto 1:
    filldraw b[i] withcolor if i=4: background else: Blues[9][i] fi;
    draw if i=4: subpath (1,3) of fi b[i];
    label("$" & if i=n: "n(n+1)(n+2)" elseif i=n-1: "\ddots" else:
        decimal i for j=1,2: & "\cdot" & decimal (i+j) endfor fi & "$",
        point 5/2 of b[i] shifted (0, 1/2u*(t(i)-t(i-1))));
endfor

% ticks
for i=0 upto n+2: draw (up--down) scaled 2 shifted (t(i)*u, 0); endfor
for i=0 upto n: draw (left--right) scaled 2 shifted (0, -t(i)*u); endfor

for i=1 upto n:
    label.top("$" & decimal i & "$", 1/2u*(t(i-1)+t(i), 0));
endfor
label.top("$\cdots$", 1/2u*(t(5)+t(6), 0));
label.top("$n+2$", 1/2u*(t(6)+t(7), 0));

for i=1 upto 3:
    label.lft("$" & decimal i & "$", -1/2u*(0, t(i-1)+t(i)));
endfor
label.lft("$\vdots$", -1/2u*(0, t(3)+t(4)));
label.lft("$n$",      -1/2u*(0, t(4)+t(5)));

path xx, yy; picture lx, ly; 
xx = subpath (2, 3) of b5 shifted 18 down;
yy = subpath (3, 4) of b5 shifted 24 left;
drawdblarrow xx; drawdblarrow yy; interim bboxmargin := 8;
lx = thelabel("${(n+2)(n+3)\over2}$", point 1/2 of xx); unfill bbox lx; draw lx;
ly = thelabel("${n(n+1)\over2}$", point 1/2 of yy); unfill bbox ly; draw ly;
label.lft("$\vdots$", -1/2u*(0, t(3)+t(4)));
);
label.top(P1, 2u * up);
label.bot(P2, 3u * down + u * left);
label(btex $\displaystyle 
k{(k+1)(k+2)\over2}+(k+2){k(k+1)\over2}=k(k+1)(k+2)
$ etex, origin);
label.top("$\displaystyle \sum_{k=1}^n k(k+1)(k+2) = {n(n+1)(n+2)(n+3)\over4}$",
point 5/2 of bbox currentpicture shifted 21 up);
label.bot(btex \vbox{%
\leftline{$1\cdot 2 \cdot 3 + 2 \cdot 3 \cdot 4 + \cdots + n(n+1)(n+2)$}
\smallskip
\rightline{$\displaystyle {}= {n(n+1)\over2}\times{(n+2)(n+3)\over2}={n(n+1)(n+2)(n+3)\over4}$}
} etex, 
point 1/2 of bbox currentpicture);
\end{mplibcode}
$$
\vfill
\contrib{James O.\@ Chilaka}

\section{Fibonacci identities}

\vfill
\leftline{$F_1 = F_2 = 1$, \enspace $F_n = F_{n-1} + F_{n-2}$ \quad\Rightarrow}
\bigskip
$$
\begin{mplibcode}
path p, q, r, s, t, u; 
p = unitsquare shifted -(1/2, 1/2) scaled 34;
q = unitsquare shifted -(1/2, 1/2) scaled 55;
r = unitsquare shifted -(1/2, 1/2) scaled 89;
s = unitsquare shifted -(1/2, 1/2) scaled 144;
t = unitsquare shifted -(1/2, 1/2) xscaled 34 yscaled 55;
u = unitsquare shifted -(1/2, 1/2) xscaled 55 yscaled 89;
picture P[]; 
P0 = image(
label.top("$F_{n-1}$", point 5/2 of u shifted (point 1 of p - point 0 of u));
path a; a = subpath(0, 1) of s shifted 10 down;
picture t; t = thelabel("$F_{n+1}$", point 1/2 of a);
drawdblarrow a; unfill bbox t; draw t;
);
P1 = image(
fill s withcolor Oranges 8 3;
fill p withcolor Oranges 8 2;
for i=0 upto 4:
    draw subpath (1,4) of u shifted (point 1 of p - point 0 of u) rotated 90i;
endfor

label.top("$F_{n-2}$", point 5/2 of p);
label.top("$F_{n}$",   point 3/2 of u shifted (point 1 of p - point 0 of u) rotated 90);
draw P0;
label.bot("$F_{n+1}^2 = 4 F_n F_{n-1} + F_{n-2}^2$", point 1/2 of bbox currentpicture shifted 12 down);
);
P2 = image(
path a, b;
a = r shifted (point 1 of p - point 1 of r);
b = r shifted (point 3 of p - point 3 of r);
fill s withcolor Oranges 8 3;
fill a withcolor Oranges 8 4;
fill b withcolor Oranges 8 4;
fill p withcolor Oranges 8 5;
draw subpath (0, 2) of a; draw subpath (2, 4) of b;
draw s;

label.top("$F_{n-2}$", point 5/2 of p);
label.top("$F_{n}$",   point 3/2 of u shifted (point 1 of p - point 0 of u) rotated 90);
draw P0;

label.bot("$F_{n+1}^2 = 2 F_n^2 + 2 F_{n-1}^2 - F_{n-2}^2$", point 1/2 of bbox currentpicture shifted 12 down);
);
P3 = image(
fill s withcolor Oranges 8 3;
fill p withcolor Oranges 8 2;
for i=0 upto 4:
    path aa; aa = q shifted (point 2 of p - point 0 of q) rotated 90i;
    fill aa withcolor Oranges 8 4; draw subpath (-1, 1) of aa;
endfor
draw p; draw s;
draw P0;
label.top("$F_{n-1}$", point 5/2 of q shifted (point 3 of p - point 1 of q));
label.top("$F_{n-2}$", point 5/2 of s);

label.bot("$F_{n+1}^2 = 4 F_{n-1}^2 + 4F_{n-1}F_{n-2} + F_{n-2}^2$", 
point 1/2 of bbox currentpicture shifted 12 down);

path aa; aa = subpath (5/2 - 1/2 sqrt(5), 2) of s shifted 17 right;
picture tt; tt = thelabel("$F_n$", point 1/2 of aa);
drawdblarrow aa; unfill bbox tt; draw tt;
);
P4 = image(
fill s withcolor Oranges 8 5;
fill p withcolor Oranges 8 7;
for i=0 upto 4:
    path aa; aa = q shifted (point 2 of p - point 0 of q) rotated 90i;
    fill aa withcolor Oranges 8 4; draw subpath (-1, 1) of aa;
endfor
draw p; draw s;
draw P0;
label.top("$F_{n-1}$", point 5/2 of q shifted (point 3 of p - point 1 of q));
label.top("$F_{n-2}$", point 5/2 of s);

label.bot("$F_{n+1}^2 = 4 F_n^2 - 4F_{n-1}F_{n-2} - 3F_{n-2}^2$", 
point 1/2 of bbox currentpicture shifted 12 down);
);
draw P1 shifted (-89, +144); draw P2 shifted (+89, +144);
draw P3 shifted (-89, -89); draw P4 shifted (+89, -89);
\end{mplibcode}
$$
\vfill

\contrib{Alfred Brousseau}

\section{Sums of powers of three}

\vfill
$$
\begin{mplibcode}
picture t[];
numeric s; s = 6;
t0 = image(fill fullcircle scaled s withcolor Blues 8 3; 
           draw fullcircle scaled s);
for i=1 upto 5:
    numeric delta; delta = 2**(i-1);
    t[i] = image(
        draw t[i-1];
        draw t[i-1] shifted ((3/2s * delta, 0) rotated -60);
        draw t[i-1] shifted ((-3/2s * delta, 0) rotated 60);
    );
endfor
numeric x; x = 0;
for i=0 upto 4:
    draw t[i] shifted (x, 0); label.bot("$3^" & decimal i & "$", point 1/2 of bbox t[i] + (x, 0));
    x := x + 4.5s * (i+1);
endfor
z0 = (22, -20) * s;
t5 := t5 shifted z0;
draw t5;
draw point 1/2 of bbox t5 -- point 5/2 of bbox t5;
drawdot z0 withpen pencircle scaled 0.9s withcolor Reds 8 3;
label.top("$\displaystyle \sum_{k=0}^{n-1} 3^k = {3^n-1 \over 2} $", point 5/2 of bbox currentpicture shifted 12 up);
label("$\displaystyle 3^n-1 = 2\sum_{k=0}^{n-1} 3^k$", point 1.5 of bbox t5);
\end{mplibcode}
$$
\vfill
\contrib{David B.\@ Sher}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\chapter{Infinite series, linear algebra, \& other topics}
\minitoc
\section{A geometric series}
\vfill
$$
\begin{mplibcode}
path t; 
t = for i=0 upto 2: up scaled 233 rotated 120i -- endfor cycle;

fill t withcolor Greens 8 1;
for i=1 upto 8:
path tt, ttt; 
numeric s; s = 2**(i-1);
tt = t shifted - point 1 of t scaled (1/2 ** i) shifted point 1/s of t;
ttt = tt reflectedabout(up, down);
fill tt withcolor Blues 7 2; draw tt withpen pencircle scaled 1/4;
fill ttt withcolor Oranges 7 2; draw ttt withpen pencircle scaled 1/4;
endfor
draw t;

label.top("$\displaystyle {1\over4} + \left(1\over4\right)^2 + \left(1\over4\right)^3 + \cdots = {1\over 3}$",
point 5/2 of bbox currentpicture shifted 21 up);
\end{mplibcode}
$$
\vfill
\contrib{Rick Mabry}

\section{An alternating series}

\vfill
$$
\begin{mplibcode}
numeric u; u = 88;
path a, b; numeric x, y; x = y = 1;
a = (origin for i=1 upto 8: 
hide(y := y * 1/2;) -- (1-x, 1-y) 
hide(x := x * 1/2;) -- (1-x, 1-y) 
endfor) scaled u;
b = a reflectedabout(left, right);
picture P[];
P0 = image(
draw unitsquare shifted (-1/2, -1/2) scaled 2u;
draw u * left -- origin;
draw a;
draw b;
);
P1 = image(
draw P0;
for i=1 upto 3: 
    label("$A_" & decimal i & "$", 5/8u * dir 120i); 
endfor
);
P2 = image(
draw P0;
drawoptions(dashed evenly scaled 1/2 withpen pencircle scaled 1/4);
draw origin -- u * right;
for t=1 upto length(a):
    if t < 7:
        picture ff; 
        ff = thelabel("${1\over" & decimal (2**t) & "}-{1\over" & decimal (2**(t+1)) & "}$", origin) scaled (7/8 ** t);
    fi
    forsuffixes $=a, b:
        numeric signum; signum = ypart point t of $ / abs(ypart point t of $);
        draw point t of $ -- if odd t: (xpart point t of $, u * signum) else: (u, ypart point t of $) fi;
        if t < 7:
            draw ff shifted if odd t:
                (xpart point t of $ - (1/2 ** ((t+1)/2)) * u, ypart point t of $)
            else: 
                (xpart point t of $, 1/2(ypart point t of $ + ypart point t-2 of $))
            fi;
        fi
    endfor
endfor
drawoptions();
label.lft("$1\over2$", (-u, 1/2u));
label.lft("$1\over2$", (-u, -1/2u));
label.bot("$1\over2$", (-1/2u, -u));
label.bot("$1\over4$", (+1/4u, -u));
label.bot("$1\over8$", (+5/8u, -u));
);
draw P1 shifted (-5/4u, 0);
draw P2 shifted (+5/4u, 0);

label.top("$\displaystyle \frac12 - \frac14 + \frac18 - \frac1{16} + \frac1{32} - \frac1{64} + \cdots = \frac13$",
point 5/2 of bbox currentpicture shifted 34 up);

label.bot(btex \vbox{\openup 16pt\halign{\hss # \hss\cr
$\displaystyle A_1 = \frac12 - \frac14 + \frac18 - \frac1{16} + \frac1{32} - \frac1{64} + \cdots$,\cr
$A_1 = A_2 = A_3$, \quad $A_1 + A_2 + A_3 = 1$,\cr
\therefore \quad $\displaystyle A_1 = \frac13$.\cr}} etex, point 1/2 of bbox currentpicture shifted 34 down);

\end{mplibcode}
$$
\vfill
\contrib{James O.\@ Chilaka}

\section{A generalized geometric series}

Let $\left\{k_1, k_2, k_3 \right\}$ be a sequence of integers, each of which is at least 2.  Then
$$
\begin{mplibcode}
vardef brace_label@#(expr port, a, b) = 
pair m, u, v; u = unitvector(b-a); v = u rotated -90;
m = 1/2[a, b] shifted 8v;
draw a shifted (1/2u+v) {v rotated 10}.. {u} a shifted (4u + 4v) 
    -- 
    m shifted (-4u - 4v) {u} .. {v} m {-v} .. {u} m shifted (4u - 4v) 
    -- 
    b shifted (4v-4u) {u} .. {-v rotated -10} b shifted (v-1/2u);
label@#(port, m);
enddef;
numeric u; u = 144;
picture P[]; path s; s = unitsquare shifted -(1/2, 1/2) scaled u;
P1 = image(
for i=0 upto 3:
    path t; t = unitsquare xscaled 1/5u yscaled u 
                shifted point 1/5i of s; 
    fill t withcolor Blues[6][5-i]; 
    draw subpath (1,2) of t withpen pencircle scaled 1/4;
endfor
);
P2 = image(
draw P1; 
for i=0 upto 2:
    path t; t = unitsquare xscaled -1/5u yscaled -1/4u shifted point 2-1/4i of s; 
    fill t withcolor Oranges[6][5-i]; 
    draw subpath (2,3) of t withpen pencircle scaled 1/4;
endfor
);
P3 = image(
draw P2;
for i=0 upto 1:
    path t; t = unitsquare xscaled (1/3 * 1/5u) yscaled 1/4u shifted point 4/5 + 1/15i of s;
    fill t withcolor Greens[4][3-i];
    draw subpath (1,2) of t withpen pencircle scaled 1/4;
endfor
);
P4 = image(
draw P3; 
for i=0 upto 2:
    path t; t = unitsquare xscaled -1/15u yscaled -1/16u shifted point 5/4-1/16i of s; 
    fill t withcolor Reds[6][4-i]; 
    draw subpath (2,3) of t withpen pencircle scaled 1/4;
endfor
);
P11 = image(
draw P1; draw s;
brace_label.bot("$k_1-1\over k_1$", point 0 of s, point 4/5 of s);
brace_label.bot("$1\over k_1$", point 4/5 of s, point 1 of s);
);
P12 = image(
draw P2; draw s;
brace_label.rt("${k_2-1\over k_2}\cdot{1\over k_1}$", point 5/4 of s, point 2 of s);
brace_label.rt("${1\over k_2 k_1}$", point 1 of s, point 5/4 of s);
);
P13 = image(
draw P3; draw s;
brace_label.bot("${k_3-1\over k_3}\cdot{1\over k_2 k_1}$", point 4/5 of s, point 14/15 of s);
);
P14 = image(
draw P4; draw s; label.bot("$\cdots$", point 1 of s shifted 5 down);
);

draw P11 shifted 100 left shifted 100 up;
draw P12 shifted 100 right shifted 100 up;
draw P13 shifted 100 left shifted 100 down;
draw P14 shifted 100 right shifted 100 down;

drawarrow (left--right) scaled 20 shifted 100 up;
drawarrow (left--right) scaled 20 rotated 225;
drawarrow (left--right) scaled 20 shifted 100 down;

label.top(btex $\displaystyle
{k_1-1\over k_1} +
{k_2-1\over k_2 k_1} +
{k_3-1\over k_3 k_2 k_1} + \cdots = 1$ etex, point 5/2 of bbox currentpicture shifted 21 up);

\end{mplibcode}
$$
\vfill
\contrib{John Mason}

\section{Divergence of a series}

\vfill
$$
\begin{mplibcode}
numeric u;
u = 144;
z0 = left * u;
z1 = right * u;
z2 = up * u shifted z1;
z3 = whatever[z0, z2]; z3-z1 = whatever * (z2-z0) rotated 90;
z4 = z2 shifted 13 right shifted 21 down;
picture P[];
P0 = image(
draw unitsquare scaled 5 shifted z1 withcolor 1/2 white;
draw unitsquare scaled 5 rotated angle (z0-z3) shifted z3 withcolor 1/2 white;
draw z1 shifted 12 right -- z0 -- z2 -- z1 -- z3;
label.ulft("$\sqrt{k}$", 5/8[z0, z2]);
label.bot ("$\sqrt{k-1}$", 9/16[z0, z1]);
label.rt  ("$1$", 1/2[z1, z2]);
);
P1 = image(
path a; a = quartercircle scaled 60 shifted z0 cutafter (z0--z2);
draw a withcolor Reds 7 6;
draw a shifted -z0 rotated 90 shifted z1 withcolor Reds 7 6;
fill z2 -- z3 -- ((z3--z2) shifted 3/2 down cutbefore (z3--z1)) -- cycle
    withcolor Reds 7 5;
draw P0;
label.rt("\rlap{$\displaystyle 1\over\sqrt{k}$}", z4);
drawarrow z4 {left} .. 1/2[z2, z3] cutafter ((z3--z2) shifted 4 down) withcolor Reds 7 6;
);
P2 = image(
numeric theta; theta = angle (z2-z0);
path a, b; 
a = subpath(0, 5/4 theta / 45) of fullcircle scaled 4u rotated -1/8 theta shifted z0;
b = buildcycle(z2 -- z3, a, (z3--z2) shifted 3/2 down, z1--z2);
fill b withcolor Reds 7 5;
draw a withcolor 1/2;
draw P0;
fill fullcircle scaled 2 shifted z0 withcolor 1/2;
fill fullcircle scaled 1 shifted z0 withcolor 1;
label.rt("\rlap{$\sqrt{k} - \sqrt{k-1}$}", z4);
drawarrow z4 {left} .. center b cutafter ((z3--z2) shifted 4 down) withcolor Reds 7 6;
);
label.top(P1, 7 up);
label.bot(P2, 7 down);
label.lrt("$\hbox{$k > 1$} \Rightarrow {}$", ulcorner bbox currentpicture);
label.top("$\displaystyle n > 1 \enspace\Rightarrow\enspace \sum_{k=1}^n {1\over\sqrt{k}} > \sqrt{n}$", 
      point 5/2 of bbox currentpicture shifted 13 up);
label.bot(btex \vbox{\openup 8pt\def\osr#1{{1\over\sqrt{#1}}}\halign{\hss $\displaystyle #$\hss\cr
\osr{k} > \sqrt{k} - \sqrt{k-1}\cr
\osr2 + \osr3 + \cdots + \osr{n} > \left(\sqrt2-1\right) + \left(\sqrt3-\sqrt2\right) + \cdots + \left(\sqrt{n} - \sqrt{n-1}\right)\cr
\therefore\quad 1 + \osr2 + \osr3 + \cdots + \osr{n} > \sqrt{n}\cr
}} etex, point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}

\section{Galileo's ratios}

\vfill
$$
\begin{mplibcode}
numeric u, n; u = 21; n = 5;
picture a, b, c;
path gg;
gg = (origin for i=1 upto n: -- (2i-1, 1-i) -- (2i-1, -i) endfor -- (0, -n) -- cycle) scaled u;
vardef go(suffix shade) = 
save p; picture p; p = image(
    for i=1 upto n:
        fill unitsquare xscaled ((1-2i)*u) yscaled -u shifted point 2i-1 of gg withcolor shade[9][7-i];
    endfor
    draw gg;
); p enddef;
a = go(Oranges);
b = go(Greens) reflectedabout(up, down);
c = go(Blues) reflectedabout(left, right) shifted (-2n*u, -n*u);
draw a; draw b; draw c;
for i=1 upto n:
label("$" & if i=n: "2n-1" elseif i=n-1: "\cdots" else: decimal (2i-1) fi & "$", u * (i-1/2, 1/2-i));
endfor
path nn; nn = (origin -- 2n * u * left) shifted (1/2u * up); 
picture pp; pp = thelabel("$2n$", point 1/2 of nn);
drawdblarrow nn; unfill bbox pp; draw pp;

label.bot(btex $\displaystyle 
{1\over3} = {1+3\over5+7} = {1+3+5\over7+9+11} = \cdots = 
{1+3+5+\cdots+(2n-1)\over (2n+1)+(2n+3)+\cdots+(2n+2n-1)}$ etex, point 1/2 of bbox currentpicture shifted 42 down);



\end{mplibcode}
$$
\vfill
\contrib{Antonio Flores}

\section{Sums of harmonic numbers}

\vfill
$$
\begin{mplibcode}
numeric u, n, r;
u = 108; r = 1/5; 
n = 7;

path s[];
s0 = origin;  
for i=1 upto n:
s[i] = unitsquare xscaled (1/i) yscaled r scaled u;
endfor

picture P[];
P1 = image(
for i=1 upto n-1:
    numeric x, y;
    x = 0; y = -r * i * 1.05 u;
    for j=1 upto i:
        fill s[j] shifted (x, y) withcolor 1/2[Oranges[9][i], white];
        draw s[j] shifted (x, y);
        if (3 < i) and (i < n-1):
            if j=1: label("$\vdots$", center s1 shifted (x, y)); fi
        else:
            label("$" & if j=1: "1" elseif j=n-1: "{1\over n-1}" 
            elseif j > 3: "\cdots" else: "{1\over " & decimal j & "}" fi & "$",
            center s[j] shifted (x, y));
        fi
        x := x + u/j;
    endfor
endfor
);
P2 = image(
for i=1 upto n:
    numeric x, y;
    x = 0; y = -r * i * 1.05 u;
    for j=1 upto i-1:
        x := x + u/j;
    endfor
    for j=i upto n:
        fill s[j] shifted (x, y) withcolor 1/2[Blues[9][j], white];
        draw s[j] shifted (x, y);
        if (3 < i) and (i < n-1):
            if j=1: label("$\vdots$", center s1 shifted (x, y)); fi
        else:
            label("$" & if j=1: "1" elseif j=n-1: "{1\over n-1}" elseif j=n: "{1\over n}"
            elseif j > 3: "\cdots" else: "{1\over " & decimal j & "}" fi & "$",
            center s[j] shifted (x, y));
        fi
        x := x + u/j;
    endfor
endfor
);
P3 = image(
draw P1;
draw P2 shifted (0, r * 1.05u);
label.lft("${}={}$", point -1/2 of bbox currentpicture shifted 13 left);
begingroup; interim bboxmargin := 0;
path a, b; 
a = subpath (2, 3) of bbox P2 shifted (0, 8 + 1.05 * r * u);
b = subpath (1, 2) of bbox P2 shifted (8, 0 + 1.05 * r * u);
drawdblarrow a;
drawdblarrow b;
endgroup;
picture hh, nn; 
hh = thelabel("$H_n$", point 1/2 of a); 
nn = thelabel("$n$", point 1/2 of b); 
unfill bbox hh; draw hh;
unfill bbox nn; draw nn;
);

draw P1 shifted - center bbox P1 shifted (-3/4u, -2r * u);
draw P2 shifted - center bbox P2 shifted ( 1/8u,  1/2r * u);
label("${}+{}$", origin);

draw P3 shifted - center bbox P3 shifted (-1/8u, -9/4u);
label.bot("$\displaystyle \sum_{k=1}^{n-1} H_k + n = nH_n$", point 1/2 of bbox currentpicture shifted 13 down);
label.top("$\displaystyle H_k = 1 + {1\over2} + {1\over3} + \cdots + {1\over k} \enspace\Rightarrow\enspace \sum_{k=1}^{n-1} H_k = nH_n - n$",
point 5/2 of bbox currentpicture shifted 21 up);
\end{mplibcode}
$$
\vfill

\section{$\left(\mathbf{A}\mathbf{B}\right)^T = \mathbf{B}^T\mathbf{A}^T$, where
$\mathbf{A}$ and $\mathbf{B}$ are matrices}
\vfill
$$
\begin{mplibcode}
path A, B, AB, A', B', AB';
numeric ax, ay, bx, by;
ax = 89; ay = 55; bx = 61; by = 98.7; 

A = unitsquare xscaled ax yscaled ay;
B = unitsquare xscaled bx yscaled by shifted point 2 of A;
AB = unitsquare xscaled bx yscaled ay shifted point 1 of A;
A' = A reflectedabout((-1,1), (1,-1));
AB' = AB reflectedabout((-1,1), (1,-1));
B' = B reflectedabout((-1,1), (1,-1));

forsuffixes $=A,B,AB:
draw $; label("$\mathbf{" & str $ & "}$", center $);
draw $'; label("$\mathbf{" & str $ & "}^T$", center $');
endfor
draw (left--right) scaled 144 rotated -45 dashed evenly withcolor Reds 7 7;

\end{mplibcode}
$$
\vfill
\contrib{James G.\@ Simmonds}

\section{The distributive property of the triple scalar product}

\vfill
\def\vA{\vec{A}}
\def\vB{\vec{B}}
\def\vC{\vec{C}}
\def\vD{\vec{D}}
$$
\begin{mplibcode}
input isometric_projection
set_projection(10, -21); ipscale := 16;
pair A, B, C, D;
A = p(-1, 3, 0);
B = p(2, 4, 0);
C = p(5, 0, 0);
D = p(0, 0, 5);

picture P[];

P1 = image(
drawoptions(dashed evenly withpen pencircle scaled 1/4 withcolor 1/2);
draw origin -- D -- D+C;  draw D -- D+A;
drawoptions(withpen pencircle scaled 1/4);
draw A -- A+D -- A+D+C -- A+C -- cycle;
draw A+C -- C;
draw A+C+D -- C+D;
drawoptions();
drawarrow origin -- A; label.lft("$\vA$", 1/2A);
drawarrow origin -- C; label.bot("$\vC$", 1/2C);
drawarrow C -- C + D; label.lrt("$\vD$", 1/2[C, C+D]);
);
P2 = image(
drawoptions(dashed evenly withpen pencircle scaled 1/4 withcolor 1/2);
draw origin -- D -- D+C;  draw D -- D+B;
drawoptions(withpen pencircle scaled 1/4);
draw B -- B+D -- B+D+C -- B+C -- cycle;
draw B+C -- C;
draw B+C+D -- C+D;
drawoptions();
drawarrow origin -- B; label.lft("$\vB$", 1/2B);
drawarrow origin -- C; label.bot("$\vC$", 1/2C);
drawarrow C -- C + D; label.lrt("$\vD$", 1/2[C, C+D]);
);
P3 = image(
drawoptions(dashed evenly withpen pencircle scaled 1/4 withcolor 1/2);
draw origin -- D -- D+C;  
draw D -- A+D -- A+B+D;
draw A -- A+D -- A+C+D;
drawoptions(withpen pencircle scaled 1/4);
draw A -- A+C -- A+C+D;
draw C -- A+C -- A+B+C -- A+B;
draw C+D -- A+C+D -- A+B+C+D -- A+B+D;
draw A+B -- A+B+D;
draw A+B+C -- A+B+C+D;
drawoptions();
drawarrow origin -- A; label.lft("$\vA$", 1/2A);
drawarrow A -- A+B; label.lft("$\vB$", 1/2[A, A+B]);
drawarrow origin -- C; label.bot("$\vC$", 1/2C);
drawarrow C -- C + D; label.lrt("$\vD$", 1/2[C, C+D]);
);
P4 = image(
pair dx; dx = -1/2C;
drawoptions(dashed evenly withpen pencircle scaled 1/4 withcolor 1/2);
draw origin -- D -- D+C;  
draw D -- A+B+D;
draw A + dx -- A+D + dx;
draw D + dx -- A+D + dx -- A+B+D + dx;
drawoptions(withpen pencircle scaled 1/4);
draw C -- A+C -- A+B+C -- A+B;
draw C+D -- A+C+D -- A+B+C+D -- A+B+D;
draw A+B -- A+B+D;
draw A+C -- A+C+D;
draw A+B+C -- A+B+C+D;
draw origin + dx -- D+ dx -- A+B+D+ dx -- A+B+ dx;
drawoptions();
drawarrow origin -- A+B;
drawarrow origin -- C;
drawarrow C -- C + D; 
drawarrow origin + dx -- A + dx; 
drawarrow origin + dx -- A+B + dx; 
drawarrow A + dx -- A+B + dx;
label("$\vA+\vB$", 1/2(A+B+ dx));
label.bot("$\vC$", 1/2C);
label.lft("$\vA$", 1/2A+ dx);
label.lft("$\vB$", 1/2[A+ dx, A+B+ dx]);
label.lrt("$\vD$", 1/2[C, C+D]);
);
P5 = image(
pair dx; dx = 3/2C;
drawoptions(dashed evenly withpen pencircle scaled 1/4 withcolor 1/2);
draw origin -- D -- D+C;  
draw D -- A+B+D;
draw A + dx -- A+D + dx;
draw D + dx -- A+D + dx -- A+B+D + dx;
drawoptions(withpen pencircle scaled 1/4);
draw C -- A+C -- A+B+C -- A+B;
draw C -- C+D -- A+C+D -- A+B+C+D -- A+B+D;
draw A+B -- A+B+D;
draw A+C -- A+C+D;
draw A+B+C -- A+B+C+D;
draw D + dx -- A+B+D+ dx -- A+B+ dx;
drawoptions();
drawarrow origin + dx -- D + dx;
drawarrow origin -- A+B;
drawarrow origin -- C;
drawarrow + dx -- A+ dx; 
drawarrow + dx -- A+B+ dx; 
drawarrow A+ dx -- A+B+ dx;
label.lft("$\vA+\vB$", 1/2(A+B));
label.bot("$\vC$", 1/2C);
label.lrt("$\vD$", 1/2[dx, dx+D]);
);
P6 = image(
drawoptions(dashed evenly withpen pencircle scaled 1/4 withcolor 1/2);
draw origin -- D -- D+C;  draw D -- A+B+D;
drawoptions(withpen pencircle scaled 1/4);
draw C -- A+B+C -- A+B;
draw C+D -- A+B+C+D -- A+B+D;
draw A+B -- A+B+D;
draw A+B+C -- A+B+C+D;
drawoptions();
drawarrow origin -- A+B;
drawarrow origin -- C;
drawarrow C -- C + D; 
label.lft("$\vA+\vB$", 1/2(A+B));
label.bot("$\vC$", 1/2C);
label.lrt("$\vD$", 1/2[C, C+D]);
);
draw P1; label("$+$", 1/4(A+B+6C+2D));
draw P2 shifted 2C;     
draw P3 shifted -3/2(A+B);
draw P4 shifted (3C-3/2(A+B));
draw P5 shifted -3(A+B);
draw P6 shifted (3C-3(A+B));

label("$=$", 1/4(A+B+6C+2D)+2C);
label("$=$", 1/2(A+B+3C+D)-3/2(A+B));
label("$=$", 1/2(A+B+3C+D)-3/2(A+B)+3C);
label("$=$", 1/2(A+B+3C+D)-3(A+B)+1/2C);

label.top("$\vA\cdot\left(\vC \times \vD\right) + \vB\cdot\left(\vC \times \vD\right) =\left(\vA+\vB\right)\cdot\left(\vC \times \vD\right)$",
point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Constance C.\@ Edwards and Prashant S.\@ Sansgiry}

\section{Cramer's rule}

\vfill
$$
\begin{mplibcode}
input isometric_projection
set_projection(13, -36); ipscale := 23;

numeric x, y, z; 
x = 7/2;
y = 9/8;
z = 9/8;

pair a, b, c, d, o; 
o = origin;
a = p(3/4, 2, 0);
b = p(4, 0, 0);
c = p(0, 0, 5);
d = x*a + y*b + z*c;

path base[]; 
base0 = o -- b -- b + c -- c -- cycle;
base1 = base0 shifted a;
base2 = base0 shifted (x*a);
base3 = base0 shifted d;

draw point 3 of base2 -- point 3 of base3 - y*b -- point 3 of base3 dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
draw point 1 of base2 -- point 1 of base3 - z*c -- point 1 of base3 dashed evenly scaled 1/2 withpen pencircle scaled 1/4;

fill base0 withcolor Blues 8 3;
draw base0 withpen pencircle scaled 1/4;
forsuffixes $=2, 3:
draw point 1 of base 0 -- point 1 of base$ withpen pencircle scaled 1/4 withcolor 0;
draw point 2 of base 0 -- point 2 of base$ withpen pencircle scaled 1/4 withcolor 1/4;
draw point 3 of base 0 -- point 3 of base$ dashed withdots scaled 1/4 withcolor 3/4;
endfor
draw base1 withpen pencircle scaled 1/4;
forsuffixes $=2, 3:
fill base$ withcolor Blues[8][2];
draw base$ withpen pencircle scaled 1/4;
endfor
forsuffixes $=a,b,c,d:
drawarrow origin -- $ withpen pencircle scaled 1;
endfor
drawarrow a -- x*a withpen pencircle scaled 1;

drawoptions(withpen pencircle scaled 1/4 withcolor Blues 8 8);
drawarrow p(0, 0,  -4) -- o -- p(0, 9, 0);
draw      p(0, 2, -3) -- p(0, 2, 0) -- a;
draw      p(0, 2x, -4) -- p(0, 2x, 0) -- x*a;
numeric s; s = 1/4;
draw p(0,2,-s)--p(0,2-s,-s)--p(0,2-s,0)--p(s,2-s,0)--p(s,2,0);
draw p(0,2x,-s)--p(0,2x-s,-s)--p(0,2x-s,0)--p(s,2x-s,0)--p(s,2x,0);
path hx, h; picture lhx, lh;
h = p(0, 0, -5/2)--p(0, 2, -5/2);
hx = p(0, 0, -7/2)--p(0, 2x, -7/2);
interim ahangle := 20; ahlength := 2;
drawdblarrow h; lh = thelabel("$h$", point 1/2 of h); unfill bbox lh; draw lh;
drawdblarrow hx; lhx = thelabel("$xh$", point 3/4 of hx); unfill bbox lhx; draw lhx;
drawoptions();

label.rt("$\textbf{a}$", 3/4a);
label.rt("$x\textbf{a}$", 3/4a*x);
label.bot("$\textbf{b}$", 3/4b);
label.ulft("$\textbf{c}$", 3/4c);
label.ulft("$\textbf{d}$", 5/8d);

label.bot(btex \vbox{\openup 12pt \def\va{\textbf{a}}\def\vb{\textbf{b}}\def\vc{\textbf{c}}\def\vd{\textbf{d}}
\halign{\hss $\displaystyle # $\hss\cr
x\va+ y\vb+ z\vc = \vd \enspace\Rightarrow\enspace \det(\vd,\vb,\vc) = \det(x\va,\vb,\vc) = x\det(\va,\vb,\vc)\cr
\therefore\quad x={\det(\vd,\vb,\vc)\over\det(\va,\vb,\vc)}\cr}} etex, point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{\textit{The Mathematics Initiative}, Education Development Center}

\section{Parametric representation of primitive Pythagorean triples}

\vfill
$$
\begin{mplibcode}
path t; t = (origin -- 144 up -- 233 right -- cycle) scaled 1.414;
draw unitsquare scaled 8 withcolor 1/2; draw t withcolor Blues 8 8;
label.lft("$a$", point 1/2 of t);
label.bot("$b$", point -1/2 of t);
label.urt("$c$", point 3/2 of t);

label.top("${a\over2}, b, c \in \mathbb{Z}^+$,\quad $(a,b)=1$", point 5/2 of bbox currentpicture shifted 21 up);
label.bot(btex \vbox{\openup 12 pt\halign{#\hfil&$\displaystyle {}\Rightarrow #$\hfil\cr
$\displaystyle {c+b\over a}={n\over m}$, \enspace $(n, m) = 1$ 
& {c-b\over a} = {m\over n},\cr
& {c \over a} = {n^2 + m^2 \over 2mn},\enspace
  {b \over a} = {n^2 - m^2 \over 2mn},\cr
& n \not\equiv m \pmod2.\cr
\therefore\enspace $(a,b,c) = \bigl(2mn, n^2-m^2, n^2+m^2\bigr)$.\hidewidth&\omit\cr
}} etex, point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Raymond A.\@ Beauregard and E.\@ R.\@ Suryanarayan}

\section{On perfect numbers}
\vfill
$$
\begin{mplibcode}
numeric u, n; u = 13; n = 4;
for i=0 upto n:
path s; s = unitsquare xscaled -1 yscaled -(2**i) scaled u shifted ((0, 2**i-1)*-u);
fill s withcolor Oranges[7][i+1]; draw s;
label.lft("$" & if i=n: "2^n" elseif i=n-1: "\vdots" else: decimal (2**i) fi & "$", point 3/2 of s);
if i=0: label.top("$1$", point 1/2 of s); fi
endfor
for i=0 upto n-1:
path s; s = unitsquare xscaled (2**i) yscaled -(2**(n+1)-1) scaled u shifted ((2**i-1, 0)*u);
fill s withcolor Blues [8][i+1];
draw s;
label.top("$" & if i=n-1: "2^{n-1}" elseif i=n-2: "\cdots" else: decimal (2**i) fi & "$", point 1/2 of s);
label.top("$" & if i=n-1: "2^{n-1}p" elseif i=n-2: "\cdots" elseif i=0: "p" else: decimal (2**i) & "p" fi & "$", center s);
endfor
path ax, ay; picture lax, lay;
ax = ((1, 2**(n+1)-1) -- (-(2**n-1), 2**(n+1)-1)) scaled -u shifted 8 down;
ay = ((2**n, 0) -- (2**n, -(2**(n+1)-1))) scaled u shifted 21 right;
lax = thelabel("$2^n$", point 1/2 of ax);
lay = thelabel("$p=2^{n+1}-1$", point 1/2 of ay);
drawdblarrow ax; unfill bbox lax; draw lax;
drawdblarrow ay; unfill bbox lay; draw lay;

label.top("$p=2^{n+1}-1$ prime ${}\Rightarrow{}$ $N=2^np$ perfect", 
point 5/2 of bbox currentpicture shifted 13 up);
label.bot("$1+2+\cdots+2^n+p+2p+\cdots+2^{n-1}p = 2^np = N$", 
point 1/2 of bbox currentpicture shifted 13 down);
\end{mplibcode}
$$
\vfill
\contrib{Don Goldberg}

\section{Self-complementary graphs}

A graph is \textit{simple} if it contains no loops or multiple edges.  
A simple graph $G=(V,E)$ is \textit{self-complementary} of $G$ is isomorphic
to its \textit{complement} $\bar{G}=(V,\bar{E})$, where 
$$\bar{E} = \left\{ \hbox{$\{v, w\}: v, w \in V$, $v\ne w$, and $\{v, w\} \not\in E$} \right\}.$$
It is a standard exercise to show that if $G$ is a self-complementary simple graph with $n$ vertices, 
then $n \equiv 0 \pmod 4$ or $n\equiv 1 \pmod 4$.  A converse also holds, as we now show.

\bigskip\noindent
\textsc{Theorem}:  \textit{If $n$ is a positive integer and either $n\equiv 0 \pmod 4$ or
$n\equiv1\pmod4$, then there exists a self-complementary simple graph $G_n$ with $n$ vertices}.

\bigskip\noindent
\textsc{Proof}:

$$
\begin{mplibcode}
numeric u, v; u = 13; v = -24;
picture G[], dot, rdot, bdot; 
dot = image(fill fullcircle scaled 4 withcolor 3/4; draw fullcircle scaled 4);
rdot = image(fill fullcircle scaled 4 withcolor Reds 8 4; draw fullcircle scaled 4);
bdot = image(fill fullcircle scaled 4 withcolor Blues 8 4; draw fullcircle scaled 4);
G1  = image(draw bdot; label.bot("$p$", point 1/2 of bbox currentpicture); label.lft("$G_1 : {}$", point -1/2 of bbox currentpicture));
G11 = image(draw bdot; label.bot("$p$", point 1/2 of bbox currentpicture); label.lft("$\overline{G_1} : {}$", point -1/2 of bbox currentpicture));

G4 = image(
draw origin -- right scaled 3u; 
numeric x; x = -1;
forsuffixes s=a,b,c,d:
    draw if odd ASCII str s: rdot else: bdot fi shifted ((incr x) * u, 0); 
    label.top("$" & str s & "$", (x*u, -16)); 
endfor
label.lft("$G_4 : {}$", point -1/2 of bbox currentpicture)
);
G41 = image(
draw origin -- right scaled 3u; 
numeric x; x = -1;
forsuffixes s=b,d,a,c:
    draw if odd ASCII str s: rdot else: bdot fi shifted ((incr x) * u, 0); 
    label.top("$" & str s & "$", (x*u, -16)); 
endfor
label.lft("$\overline{G_4} : {}$", point -1/2 of bbox currentpicture)
);
G5 = image(
path pp; pp = for i=0 upto 3: (i*u, 0) -- endfor (3/2u, v) -- cycle; draw pp;
numeric t; t = -1;
forsuffixes s=x, y, z, w, p: 
    draw if odd ASCII str s: rdot else: bdot fi shifted point incr t of pp; 
    label("$\strut " & str s & "$", point t of pp shifted if t>3: 8 down else: 10 up fi);
endfor
label.lft("$G_5 : {}$", point -1/2 of bbox currentpicture)
);
G51 = image(
path pp; pp = for i=0 upto 3: (i*u, 0) -- endfor (3/2u, v) -- cycle; draw pp;
numeric t; t = -1;
forsuffixes s=y, w, x, z, p: 
    draw if odd ASCII str s: rdot else: bdot fi shifted point incr t of pp; 
    label("$\strut " & str s & "$", point t of pp shifted if t>3: 8 down else: 10 up fi);
endfor
label.lft("$\overline{G_5} : {}$", point -1/2 of bbox currentpicture)
);
G8 = image(
path pp; pp = for i=0 upto 3: (i*u, 0) -- endfor for i=3 downto 0: (i*u, v) -- endfor cycle; draw pp;
for t = 4 upto 7: draw point 0 of pp -- point t of pp -- point 3 of pp; endfor  
numeric t; t = -1;
forsuffixes s=x, y, z, w, d, c, b, a: 
    draw if odd ASCII str s: rdot else: bdot fi shifted point incr t of pp; 
    label("$\strut " & str s & "$", point t of pp shifted if t>3: 10 down else: 10 up fi);
endfor
label.lft("$G_8 : {}$", point -1/2 of bbox currentpicture);
);
G81 = image(
path pp; pp = for i=0 upto 3: (i*u, 0) -- endfor for i=3 downto 0: (i*u, v) -- endfor cycle; draw pp;
for t = 4 upto 7: draw point 0 of pp -- point t of pp -- point 3 of pp; endfor  
numeric t; t = -1;
forsuffixes s = y, w, x, z, c, a, d, b: 
    draw if odd ASCII str s: rdot else: bdot fi shifted point incr t of pp; 
    label("$\strut " & str s & "$", point t of pp shifted if t>3: 10 down else: 10 up fi);
endfor
label.lft("$\overline{G_8} : {}$", point -1/2 of bbox currentpicture);
);
G0 = image(
path pp, ee; 
ee = fullcircle xscaled 6u yscaled 2u;
draw ee;
pp = (origin for i=1 upto 3: -- (i*u, 0) endfor) shifted (-3/2u, -2v); draw pp;
for i=1 upto 5: 
    z[i] = (i/6)[point 4 of ee, point 0 of ee]; 
endfor
for i=1, 2, 5:
    draw point 0 of pp -- z[i] -- point 3 of pp;
    draw dot shifted z[i]; 
endfor
for i=3, 4:
    draw point 0 of pp -- z[i] cutafter ee scaled 2;
    draw point 3 of pp -- z[i] cutafter ee scaled 2;
endfor
numeric t; t = -1;
forsuffixes s=x, y, z, w:
    draw if odd ASCII str s: rdot else: bdot fi shifted point incr t of pp; 
    label("$\strut " & str s & "$", point t of pp shifted 10 up);
endfor
label("$G_k$", 7/12[point 4 of ee, point 0 of ee]);
label("$G_{k+4} : {}$", point -1/2 of bbox currentpicture);
);
G10 = image(
path pp, ee; 
ee = fullcircle xscaled 6u yscaled 2u;
draw ee;
pp = (origin for i=1 upto 3: -- (i*u, 0) endfor) shifted (-3/2u, -2v); draw pp;
for i=1 upto 5: 
    z[i] = (i/6)[point 4 of ee, point 0 of ee]; 
endfor
for i=1, 2, 5:
    draw point 0 of pp -- z[i] -- point 3 of pp;
    draw dot shifted z[i]; 
endfor
for i=3, 4:
    draw point 0 of pp -- z[i] cutafter ee scaled 2;
    draw point 3 of pp -- z[i] cutafter ee scaled 2;
endfor
numeric t; t = -1;
forsuffixes s=y, w, x ,z:
    draw if odd ASCII str s: rdot else: bdot fi shifted point incr t of pp; 
    label("$\strut " & str s & "$", point t of pp shifted 10 up);
endfor
label("$\overline{G_k}$", 7/12[point 4 of ee, point 0 of ee]);
label("$\overline{G_{k+4}} : {}$", point -1/2 of bbox currentpicture);
);


numeric dx, dy; dx = 108; dy = -3/4 dx;;
label(G1, (0dx, 0dy)); label(G11, (1dx, 0dy)); label(G4, (2dx, 0dy)); label(G41, (3dx, 0));
label(G5, (0dx, 1dy)); label(G51, (1dx, 1dy)); label(G8, (2dx, 1dy)); label(G81, (3dx, 1dy));
label(G0, (3/4dx, 5/2dy));
label(G10, (9/4dx, 5/2dy));
\end{mplibcode}$$
\vfill
\contrib{Stephan C.\@ Carlson}

\section{Tiling with trominoes}

A \textit{tronimo} is a plane figure composed of three squares: 
\begin{mplibcode}path t; t = origin -- subpath (-1/2, 5/2) of unitsquare shifted -(1/2, 1/2) scaled 20 -- cycle;
draw point 5/2 of t -- origin -- point 7/2 of t withpen pencircle scaled 1/4 withcolor 1/2; 
draw t withpen pencircle scaled 1; \end{mplibcode}

\bigskip\noindent
\textsc{Theorem}: \textit{If $n$ is a power of two, then an $n\times n$ chess board with any one square removed
can be tiled with trominoes}.

\bigskip\noindent
\textsc{Proof} (by induction):

\bigskip\noindent
I. $$
\begin{mplibcode}path t; t = origin -- subpath (-1/2, 5/2) of unitsquare shifted -(1/2, 1/2) scaled 20 -- cycle;
fill t withcolor Blues 8 6; fill origin -- subpath (5/2, 7/2) of t -- cycle withcolor Blues 8 2;
draw t withpen pencircle scaled 1; \end{mplibcode}
$$

\bigskip\noindent
II.
$$
\begin{mplibcode}
numeric n, u; n = 16; u = 10;
picture P[];
P0 = image(
for i=0 upto n-1: for j = 0 upto n-1:
    fill unitsquare scaled u shifted (u*(i, j)) withcolor Blues[8][if odd (i+j): 2 else: 6 fi];
endfor endfor
unfill unitsquare scaled u shifted (2u,11u); 
draw   unitsquare scaled u shifted (2u,11u) withpen pencircle scaled 1;
draw unitsquare scaled (u*n) withpen pencircle scaled 1;
);
P2 = image(draw P0; 
begingroup; interim bboxmargin := 0;
for t=-1/2, 1/2:
    draw point t of bbox P0 -- point 2+t of bbox P0 withpen pencircle scaled 1;
endfor
path hole; hole = (origin -- (0, u) -- (u, u) -- (u, -u) -- (-u, -u) -- (-u, 0) -- cycle) shifted center P0;
unfill hole; draw hole withpen pencircle scaled 1;
path a, b; picture p, q; 
a = subpath (0, 1/2) of bbox P0 shifted 12 down; 
b = subpath (1/2, 1) of bbox P0 shifted 12 down; 
endgroup; 
drawdblarrow a;
drawdblarrow b;
p = thelabel("$n$", point 1/2 of a); unfill bbox p; draw p;
q = thelabel("$n$", point 1/2 of b); unfill bbox q; draw q;
);
P1 = image(draw P0; path a; picture p; 
begingroup; interim bboxmargin := 0;
a = subpath (0, 1) of bbox P0 shifted 12 down; 
endgroup; drawdblarrow a;
p = thelabel("$2n$", point 1/2 of a); unfill bbox p; draw p;
);

label(P1, 112 left);
label(P2, 112 right);
label("$=$", origin);
\end{mplibcode}
$$

\vfill
\contrib{Solomon W.\@ Golomb}

\bigskip\noindent
\textsc{Note}: Except when $n=5$, an $n\times n$ chessboard with any one square removed can be tiled with
tronimoes if and only if $n\not\equiv 0 \pmod 3$. See I-Ping Chu and Richard Johnsonbaugh, “Tiling deficient boards with
tronimoes”, \textit{Mathematics Magazine}, 59 (1986) 34–40.

\end{document}
