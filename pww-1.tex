\documentclass[oneside]{scrbook}
\usepackage{unicode-math}
\setmainfont{TeX Gyre Pagella}
\setmathfont{TeX Gyre Pagella Math}
\usepackage{graphicx}
\usepackage{mflogo}
\addtokomafont{section}{\clearpage}
\usepackage{luamplib}
\mplibtextextlabel{enable}
\everymplib{input colorbrewer-rgb;interim ahangle := 30;beginfig(0);
numeric tw; tw = \mpdim{\textwidth};}
\everyendmplib{numeric wd;
wd = xpart (urcorner bbox currentpicture - llcorner bbox currentpicture);
if wd > tw: currentpicture := currentpicture scaled (tw/wd); fi
endfig;}
\usepackage{minitoc}
\mtcsetrules{*}{off}
% some tex defs
\def\slope{\mathop{\mathrm m}\nolimits}
%--------------------
\title{Proofs without words I\\[6pt]{\Large Exercises in \MP}}
\date{March 2021 —\\[4in]
\centerline{\begin{mplibcode}
    path t[], h;
    numeric r; r = -30;
    t0 = (for i=0 upto 2: up scaled 21 rotated 120i -- endfor cycle) rotated r;
    h  = (for i=0 upto 5: up scaled 34 rotated 60i -- endfor cycle) rotated r;
    t1 = subpath (0, 1) of t0 -- point 1 of h -- cycle;
    t2 = subpath (1, 2) of t0 -- point 3 of h -- cycle;
    t3 = subpath (2, 3) of t0 -- point 5 of h -- cycle;
    t4 = subpath (0, 1) of h -- point 0 of t0 -- cycle;
    t5 = subpath (1, 2) of h -- point 1 of t0 -- cycle;
    t6 = subpath (2, 3) of h -- point 1 of t0 -- cycle;
    t7 = subpath (3, 4) of h -- point 2 of t0 -- cycle;
    t8 = subpath (4, 5) of h -- point 2 of t0 -- cycle;
    t9 = subpath (5, 6) of h -- point 0 of t0 -- cycle;
    fill t0 withcolor Blues 7 2;
    fill t1 withcolor Blues 7 1;
    fill t2 withcolor Blues 7 3;
    fill t3 withcolor Blues 7 3;
    fill t4 withcolor Blues 7 1;
    fill t5 withcolor Blues 7 1;
    fill t6 withcolor Blues 7 2;
    fill t7 withcolor Blues 7 5;
    fill t8 withcolor Blues 7 5;
    fill t9 withcolor Blues 7 2;
    forsuffixes @=0, 4, 5, 6, 7, 8, 9: draw t@; endfor
\end{mplibcode}}}
\author{Toby Thurston}
\setcounter{secnumdepth}{-1}
\setcounter{tocdepth}{0}
\def\contrib#1{\rightline{— #1}}
\def\implies{\ensuremath\enspace\Longrightarrow\enspace}
\def\sint{\sin\theta}
\def\cost{\cos\theta}

\begin{document}
\dominitoc[n]
\maketitle
\tableofcontents

\chapter{Geometry and Algebra}

\minitoc

\section{The Pythagorean theorem I}

\vfill
$$
\begin{mplibcode}
path s, t;
s = unitsquare shifted -(1/2, 1/2) scaled 144;
t = point 0 of s -- point 2/3 of s -- point -1/3 of s -- cycle;
picture P[];
P2 = image(
    for i=0 upto 3:
        fill t rotated 90i withcolor if odd i: Blues 7 2 else: Oranges 7 2 fi;
        draw t rotated 90i;
    endfor
    draw s;
);
P1 = image(
    fill t withcolor Oranges 7 2; draw t;
    t := t rotatedabout(point 3/2 of t, 180);
    fill t withcolor Oranges 7 2; draw t;
    t := t shifted (point 0 of t - point 2 of t);
    t := t rotatedabout(point 2 of t, -90);
    fill t withcolor Blues 7 2; draw t;
    t := t rotatedabout(point 3/2 of t, 180);
    fill t withcolor Blues 7 2; draw t;
    draw s;
);
draw P1;
draw P2 shifted 200 right;
\end{mplibcode}
$$
\vfill
\contrib{adapted from the \textit{Chou pei san ching}}

\section{The Pythagorean theorem II}

\vfill
$$
\begin{mplibcode}
path s, t;
s = fullcircle scaled 144;
t = (point 4 of s -- point 0 of s -- point sqrt(2) of s -- cycle) shifted point 6 of s;
picture P[];
P1 = image(
    for i=0 upto 3:
        fill t rotated 90i withcolor if odd i: Oranges 7 2 else: Blues 7 2 fi;
        draw t rotated 90i;
    endfor
);
P2 = image(
    t := t rotatedabout(point 0 of t, 180 - angle (point 2 of t - point 0 of t)) shifted (point 1 of t - point 0 of t);
    fill t withcolor Blues 7 2; draw t;
    t := t rotatedabout(point 1/2 of t, 180);
    fill t withcolor Blues 7 2; draw t;
    t := t rotatedabout(point 1 of t, -90);
    fill t withcolor Oranges 7 2; draw t;
    t := t rotatedabout(point 1/2 of t, 180);
    fill t withcolor Oranges 7 2; draw t;
    draw subpath (0, 2) of unitsquare scaled (abs(point 2 of t - point 1 of t) - abs(point 2 of t - point 0 of t)) shifted point 2 of t;
);

draw P1;
draw P2 shifted 180 right;
label.bot("\textit{Behold!}", point 1/2 of bbox currentpicture shifted 36 down);
\end{mplibcode}
$$

\vfill
\contrib{Bh\=askara (12th century)}

\section{The Pythagorean theorem III}

\vfill
$$
\begin{mplibcode}
path s, t, a, b, c;
s = fullcircle scaled 72;
t = (point 4 of s -- point 0 of s -- point sqrt(6) of s -- cycle) shifted point 6 of s;
a = unitsquare scaled abs(point 2 of t - point 0 of t) rotated angle (point 2 of t - point 0 of t) shifted point 0 of t;
b = unitsquare scaled abs(point 1 of t - point 2 of t) rotated angle (point 1 of t - point 2 of t) shifted point 2 of t;
c = unitsquare scaled abs(point 0 of t - point 1 of t) rotated angle (point 0 of t - point 1 of t) shifted point 1 of t;
color v, w; v = Oranges 7 1; w = Greens 7 1;
picture P[];
P0 = image(
    draw a;
    draw b;
    draw c;
);
P1 = image(
    fill a withcolor v;
    fill b withcolor v;
    draw P0
);
z0 = whatever[point 2 of a, point 3 of a] = whatever[point 2 of b, point 3 of b];
z1 = whatever[z0, point 3 of a]; x1 = xpart point 0 of a;
z2 = whatever[z0, point 2 of b]; x2 = xpart point 1 of b;
path wedge; wedge = subpath (0,1) of a -- subpath (0, 1) of b -- z2 -- z0 -- z1 -- cycle;

P2 = image(
    draw point 2 of a -- z0 -- point 3 of b dashed evenly scaled 1/2;
    path a', b'; numeric t, u;
    t = angle (point 1 of a - point 0 of a);
    u = angle (point 1 of b - point 0 of b);
    a' = a shifted - point 0 of a rotated -t slanted 1/2 rotated t shifted point 0 of a;
    b' = b shifted - point 0 of b rotated -u slanted -1/3 rotated u shifted point 0 of b;
    fill a' withcolor 1/4[v,w]; draw a';
    fill b' withcolor 1/4[v,w]; draw b';
    draw P0
);
P3 = image(
    draw point 2 of a -- z0 -- point 3 of b dashed evenly scaled 1/2;
    fill wedge withcolor 1/2[v,w]; draw wedge; draw point 1 of a -- z0;
    draw P0
);
P4 = image(
    fill wedge shifted (point 0 of a - z1) withcolor 3/4[v,w];
    draw wedge shifted (point 0 of a - z1);
    draw P0
);
P5 = image(
    fill c withcolor w;
    draw P0
);

draw P1;
draw P2 shifted (144,0);
draw P3 shifted (288,0);
draw P4 shifted (72, -200);
draw P5 shifted (216, -200);
\end{mplibcode}
$$
\vfill
\contrib{based on Euclid's proof}

\section{The Pythagorean theorem IV}

\vfill
$$
\begin{mplibcode}
path c, a, a', bq, bq'; numeric r; r = 59;
c = unitsquare shifted -(1/2, 1/2) scaled 160;
a = c scaled cosd(r) rotated r;
pair p, q;
p = whatever[point 0 of a, point 1 of a] = whatever[point 0 of c, point 1 of c];
q = whatever[point 0 of a, point 3 of a] = whatever[point 0 of c, point 3 of c];
bq = point 0 of c -- p -- point 0 of a -- q -- cycle;

fill a withcolor Blues 7 2;
for i=0 upto 3:
    fill bq rotated 90i withcolor if odd i: Oranges 7 2 else: Purples 7 2 fi;
    draw bq rotated 90i;
endfor

a' = a shifted (point 3 of c - point 0 of a);
fill a' withcolor Blues 7 2;
draw a';

bq' = bq rotated 180 shifted (point 1 of a' - point 2 of (bq rotated 180));
pair o; o = point 0 of bq';
for i=0 upto 3:
    fill bq' rotatedabout(o, 90i) withcolor if odd i: Oranges 7 2 else: Purples 7 2 fi;
    draw bq' rotatedabout(o, 90i);
endfor
\end{mplibcode}
$$
\vfill
\contrib{H.\@ E.\@ Dudeney (1917)}

\section{The Pythagorean theorem V}

\vfill
$$
\begin{mplibcode}
path t, t';
t = (origin -- 377 right -- 144 up -- cycle) scaled 3/4;
t' = t rotated -90 shifted (point 2 of t + point 1 of t rotated 90);

draw unitsquare scaled 8 withcolor 1/2;
draw unitsquare scaled 8 rotated -90 shifted point 0 of t' withcolor 1/2;
draw unitsquare scaled 8 rotated angle (point 1 of t - point 2 of t)
      shifted point 2 of t withcolor 1/2;

draw t;
draw t';
draw point 1 of t -- point 2 of t';

label.lft("$a$", point -1/2 of t);
label.bot("$b$", point 1/2 of t);
label.urt("$c$", point 3/2 of t);
label.top("$a$", point -1/2 of t');
label.lft("$b$", point 1/2 of t');
label.lrt("$c$", point 3/2 of t');

label.bot(btex \vbox{\openup 24pt\halign{\hfil $#$ \hfil\cr
A = 2 \cdot \frac12 ab + \frac12 c^2 = \frac12\left(a+b\right)^2\cr
c^2 = a^2 + b^2\cr}} etex, (xpart point 1 of t, ypart point 2 of t' - 12));
\end{mplibcode}
$$
\vfill
\contrib{James A.\@ Garfield (1876)}

\section{The Pythagorean theorem VI}

\vfill
$$
\begin{mplibcode}
numeric r;
r = 144;  z1 = r * dir 66;

draw unitsquare scaled 8 rotated 90 shifted (x1, 0) withcolor 1/2;
draw (left--right) scaled r withcolor Blues 7 7;
draw origin -- z1 -- (x1, 0) withcolor Blues 7 7;
draw fullcircle scaled 2r withcolor Reds 7 7;

label.top("$a$", (1/2 x1, 0));
label.rt("$b$", (x1, 1/2 y1));
label.ulft("$c$", 1/2 z1);
label.top("$c$", (-1/2 r, 0));
label.top("$c-a$", (1/2(r+x1), 0));

label.lft(btex \vbox{\openup 24pt\halign{\hfil $\displaystyle#$ \hfil\cr
{c+a\over b} = {b \over c-a} \cr
a^2 + b^2 = c^2\cr}} etex, point -1/2 of bbox currentpicture + 16 left);
\end{mplibcode}
$$
\vfill
\contrib{Michael Hardy}

\section{A Pythagorean theorem: $aa' = bb' + cc'$}

\vfill
$$
\begin{mplibcode}
path c; c = fullcircle scaled 377;
z0 = point 4 of c; z1 = point 0 of c; z2 = point 2.828 of c;
z3 = 5/16[z0, z1];
z4 = whatever [z1, z2];
z4 - z3 = whatever * (z2 - z0);
picture P;
P = image(
    draw unitsquare scaled 6 rotated angle (z0 - z2) shifted z2 withcolor 1/2;
    draw unitsquare scaled 6 rotated angle (z3 - z4) shifted z4 withcolor 1/2;
    draw z3 -- z1 -- z2 -- z0 -- z3 -- z4;

    label.bot ("$a$", 1/2[z0, z1] shifted 10 down); label.top("$a'$", 7/16[z3, z1]);
    label.ulft("$b$", 1/2[z0, z2]); label.ulft("$b'$", 1/2[z3, z4]);
    label.urt ("$c$", 1/2[z1, z2]); label.llft("$c'$", 9/16[z1, z4]);

);

draw P shifted 200 up;
x5 = x4; y5 = 0;
draw unitsquare scaled 6 shifted z5 withcolor Blues 7 4;
draw z4--z5 withcolor Blues 7 4;
draw P;
label.bot("$\scriptstyle x$", 1/2[z3, z5]) withcolor Blues 7 6;
label.bot("$\scriptstyle y$", 1/2[z1, z5]) withcolor Blues 7 6;

label.bot(btex \vbox{\openup 8pt\halign{\hfil $\displaystyle # $\hfil\cr
{x\over b'} = {b\over a} \implies {x\over b} = {b'\over a} \implies ax = bb';\cr
{y\over c'} = {c\over a} \implies {y\over c} = {c'\over a} \implies ay = cc';\cr
\therefore\quad aa' = a\left(x+y\right) = bb' + cc'.\cr
}} etex, point 1/2 of bbox currentpicture shifted 24 down);
\end{mplibcode}
$$
\vfill
\contrib{Enzo R.\@ Gentile}

\section{The rolling circle squares itself}

\vfill
$$
\begin{mplibcode}
numeric r; r = 64;
numeric pi; pi = 3.141592653589793;
path base, h, c, c', s;

base = (left--right) scaled 7/2r;
h = halfcircle rotated 180 scaled (pi * r + r);
c = fullcircle scaled 2r rotated 90 shifted point 0 of h shifted (0, r);
c' = fullcircle scaled 2r rotated 270 shifted point 4 of h shifted (-r, r);
s = unitsquare scaled (sqrt(pi) * r) rotated -90 shifted point 0 of c';

fill c withcolor Blues 7 1;
fill s withcolor Blues 7 1;

draw base withcolor 1/2;
draw subpath (0, 4 + 1/45 angle point 1 of s) of h withcolor 1/2;
draw subpath (4 + 1/45 angle point 1 of s, 4) of h withcolor Blues 7 3;
draw s;
draw point infinity of h -- point 2 of c' dashed evenly;

forsuffixes $=c, c':
    draw point 0 of $ -- center $ -- point 2 of $ dashed evenly;
    draw $; drawdot point 0 of $ withpen pencircle scaled dotlabeldiam;
endfor

drawarrow subpath (5/4, 1/4) of fullcircle scaled (2r + 16)
    shifted center c withcolor Reds 7 7;
\end{mplibcode}
$$
\vfill
\contrib{Thomas Elsner}

\section{On trisecting an angle}
\vfill
$$
\begin{mplibcode}
  picture link, pointer, pointer_groove;
  color metal, light_metal;
  metal = 1/256 (181, 166, 66);
  light_metal = 3/4[metal, white];

  link = image(
      path a, b, a', b', c;
      a = fullcircle scaled 3; a' = a shifted (98,0);
      b = fullcircle scaled 5; b' = b shifted center a';
      c = subpath(2,6) of b -- subpath(-2,2) of b' -- cycle;
      fill c withcolor light_metal; draw c;
      fill a withcolor metal; draw a;
      fill a' withcolor metal; draw a';
  );
  pointer = image(
      path a, b, c; numeric r;
      a = fullcircle scaled 18;
      b = fullcircle scaled 24;
      r = 1/3;
      c = subpath(r,8-r) of b --
          point 8-r of b shifted (10cm,0) --
          point 0   of b shifted (116mm,0) --
          point 8+r of b shifted (10cm,0) -- cycle;
      fill c withcolor light_metal;
      fill subpath (9,11) of c -- cycle withcolor 7/8 white;
      draw point 9 of c -- point 11 of c;
      draw c;
      fill a withcolor white; draw a;
  );
  pointer_groove = image(
      draw pointer;
      path g;
      g = (halfcircle scaled 4 rotated 90 --
           halfcircle scaled 4 rotated 270 shifted (4cm,0) -- 
           cycle) shifted (5cm,0);
      fill g withcolor 7/8[metal,white]; draw g;
  );
  draw pointer        rotated 42;
  draw pointer_groove rotated 28;
  draw pointer_groove rotated 14;
  draw pointer        rotated 0;
  z0 = 210 right rotated 14;
  z1 = 120 right;
  numeric t; t = angle (z0-z1);
  draw link rotated t shifted z1 rotatedabout(z0,-34.5);
  draw link rotated t shifted z1 rotatedabout(z0,-34.5) rotated 14;
  draw link rotated t shifted z1;
  draw link rotated t shifted z1 rotated 14;
\end{mplibcode}
$$
\vfill
\contrib{Rufus Isaacs}

\section{Trisection in an infinite number of steps}
\vfill
$$
\begin{mplibcode}
numeric alpha, beta;
alpha = 144;
beta = 0;
for i=1 upto 9:
    beta := beta if odd i: + else: - fi alpha * (2 ** -i);
    path ray;
    ray = origin -- (130 + 10i) * dir beta;
    draw ray withcolor 3/4;
    if i < 7:
        picture t;
        t = thelabel("$\frac1{" & decimal (2**i) & "}$", origin) scaled (1 - i/8) shifted point 1 of ray;
        unfill bbox t; draw t;
    fi
endfor

for i = 0 upto 3:
    draw origin -- 240 dir (i * alpha/3)
        if i mod 3 > 0: dashed evenly fi
        withcolor Reds 6 6;
endfor
filldraw fullcircle scaled dotlabeldiam;
label.bot("$\displaystyle \frac13 = \frac12 - \frac14 +\frac18 - \frac1{16} + \cdots $",
    point 1/2 of bbox currentpicture shifted 24 down);

\end{mplibcode}
$$
\vfill
\contrib{Eric Kincanon}

\section{Trisection of a line segment}
\vfill
$$
\begin{mplibcode}
picture P[];
pair A, B, C, D, E, F;
A = origin;
B = 80 right;
C = B rotated 60;
D = C rotated 180;
E = 1/2 [B, D];
F = p[A,B] = q[E, C];

path ca, cb;
ca = fullcircle scaled 2 abs (A-B);
cb = ca rotated 180 shifted B;

P0 = image(
    drawoptions(withpen pencircle scaled 1/4 withcolor Blues 7 4);
    draw subpath 1/45(50, 70) of ca; draw subpath 1/45(50, 70) of cb;
    draw subpath -1/45(50, 70) of ca; draw subpath -1/45(50, 70) of cb;
    drawoptions();
);
P9 = image(
    draw A -- B;
    dotlabel.ulft("$A$", A);
    dotlabel.urt("$B$", B);
);
P1 = image(draw P0; draw P9);
P2 = image(
    draw P0;
    drawoptions(withpen pencircle scaled 1/4 withcolor Blues 7 4);
    draw subpath 1/45(230, 250) of ca;
    drawoptions();
    draw C -- D withcolor 1/2;
    dotlabel.top("$C$", C);
    dotlabel.llft("$D$", D);
    draw P9;
);
P3 = image(
    draw B--D withcolor 1/2;
    draw A -- C reflectedabout(A,B) dashed evenly withcolor 1/2;
    drawdot E withpen pencircle scaled dotlabeldiam;
    label("$E$", E-(2,12));
    draw P2;
);
P4 = image(
    draw B--C dashed evenly withcolor 1/2;
    draw C--E withcolor 1/2;
    draw P3;
    dotlabel.lrt("$F$", F);
);

draw P1;
draw P2 shifted (250, 0);
draw P3 shifted (0, -220);
draw P4 shifted (250, -220);

label.bot("$\overline{AF} = \frac13\cdot\overline{AB}$",
    point 1/2 of bbox currentpicture shifted 36 down);
\end{mplibcode}
$$
\vfill
\contrib{Scott Cobel}

\section{The vertex angles of a star sum to 180°}
\vfill
$$
\begin{mplibcode}
z3 = -z5 = 120 left;
z1 = 180 dir 81;
z2 = 250 dir 130;
z4 =  90 dir -100;

z6 = z5 + 72 right;
z7 = whatever [z2, z5] = whatever [z1, z4];
z8 = whatever [z3, z5] = whatever [z1, z4];
y9 = y1; z9 - z5 = whatever * (z1 - z4);

path star;
star = z3 -- z5 -- z2 -- z4 -- z1 -- cycle;
draw star withcolor Blues 7 7;
draw z6 -- z5 -- z9 dashed evenly withcolor Blues 7 5;

def angle_point(expr a, b, c, r) = b + r * (unitvector(a-b) + unitvector(c-b)) enddef;

label("$α$", angle_point(z3, z1, z4, 12));
label("$β$", angle_point(z5, z2, z4, 16));
label("$γ$", angle_point(z5, z3, z1, 10));
label("$δ$", angle_point(z1, z4, z2, 12));
label("$ε$", angle_point(z2, z5, z3, 12));

label("$α+γ$", angle_point(z1, z8, z5, 16) + 8 down);
label("$α+γ$", angle_point(z9, z5, z6, 16) + 8 down);
label("$β+δ$", angle_point(z5, z7, z4, 18) + 1 up);
label("$β+δ$", angle_point(z2, z5, z9, 18) + 2 down);
\end{mplibcode}
$$
\vfill
\contrib{Fouad Nakhli}

\section{Viviani's theorem I}
\vfill
$$
\begin{mplibcode}
pair A', B', C', A, B, C, D, E, F, G, P, Q, R;
A' = origin;
B' = 300 right;
C' = B' rotated 60;
P = 3/8[B', C'];
xpart Q = xpart C';
ypart Q = ypart E = ypart P;
D = whatever[A', B']; xpart D = xpart P;
E = whatever[A', C'];
R = whatever[A', C']; R - P = whatever * (A' - C') rotated 90;
G = whatever[C', Q] = whatever [R, P];
A = whatever[A', B']; G-A = whatever * (C' - A');
B - B' = A - A' = C - C';
F = whatever[B, C]; F-P = whatever * (B-C) rotated 90;

def right_angle_mark(expr a, b, s) =
    subpath (1,3) of unitsquare scaled s rotated angle(b-a) shifted a
enddef;

drawoptions(withcolor 1/2);
draw right_angle_mark(D, B, 6);
draw right_angle_mark(F, P, 6);
draw right_angle_mark(G, P, 6);
draw right_angle_mark(Q, P, 6);
draw right_angle_mark(R, A', 6);
draw E--P;
draw A'--B'--C'--cycle;
drawoptions();

draw P--F withcolor Reds 7 7;
draw R--G--Q withcolor 1/2[Reds 7 7, white];
draw G--P dashed evenly scaled 3/4 withcolor Greens 7 7;
draw G--C' dashed evenly scaled 3/4 withcolor 1/2[Greens 7 7, white];

draw P--D dashed withdots scaled 1/4 withcolor Blues 7 7;
draw A--B--C--cycle;

drawdot P withpen pencircle scaled dotlabeldiam;

forsuffixes $=A, A', B, B', D, Q: label.bot("\strut$" & str $ & "$", $); endfor
forsuffixes $=C, C': label.top("$" & str $ & "$", $); endfor
label.urt("$F$", F);
label("$G$", G + 10 dir 192);

label.top(btex \vbox{\halign{\hss #\hss\cr
The perpendiculars to the sides from a point on\cr
the boundary or within an equilateral triangle\cr
add up to the height of the triangle.\cr
}} etex, point 5/2 of bbox currentpicture shifted 42 up);

label("\textit{This shows a particular example, with $C'GQ$ collinear, rather than the general case}",
point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Samuel Wolf}

\section{Viviani's theorem II}
\vfill
$$
\begin{mplibcode}

def distance(expr a, b, c) = abs ypart ((a-b) rotated -angle (c-b)) enddef;

pair a, b, c, p;
a = 89 up;
b = a rotated 120;
c = b rotated 120;
p = 21 dir 42;

numeric h[];
h0 = distance(a, b, c);
h1 = distance(p, a, b);
h2 = distance(p, b, c);
h3 = distance(p, c, a);

path t[];
t0 = a--b--c--cycle;
t1 = t0 rotated -120 shifted -point 2 of t0 scaled (h1/h0) shifted p;
t2 = t0 shifted -point 0 of t0 scaled (h2/h0) shifted p;
t3 = t0 rotated +120 shifted -point 1 of t0 scaled (h3/h0) shifted p;

z0 = 1/3[1/2[point 2 of t1, point 1 of t3], point 0 of t0];
z1 = 2/3[point 0 of t1, point 3/2 of t1];

color s[];
s1 = Reds 7 2; s2 = Oranges 7 2; s3 = Blues 7 2;
picture p[];
forsuffixes $=1,2,3: p$ = image(fill t$ withcolor s$; draw t$ --point 3/2 of t$); endfor

picture P[];
P1 = image(draw p1; draw p2; draw p3; draw t0;);
P2 = image(
    path cor;
    cor = reverse fullcircle rotated 90 scaled 4/3 h1 scaled 15/16 shifted z1;
    drawarrow subpath 1/45(20, 100) of cor withcolor Reds 7 7;
    drawarrow subpath 1/45(140, 220) of cor withcolor Reds 7 7;
    drawarrow subpath 1/45(260, 340) of cor withcolor Reds 7 7;
    draw p1 rotatedabout(z1, -120); draw p2; draw p3; draw t0;);
P3 = image(
    path cor;
    cor = reverse fullcircle rotated 90 scaled 4/3 (h1+h3) scaled 7/8 shifted z0;
    drawarrow subpath 1/45(20, 100) of cor withcolor Reds 7 7;
    drawarrow subpath 1/45(140, 220) of cor withcolor Reds 7 7;
    drawarrow subpath 1/45(260, 340) of cor withcolor Reds 7 7;
    draw point 2 of t1 -- point 1 of t3 dashed withdots scaled 1/2;
    draw p2;
    draw p1 rotatedabout(z1, -120) rotatedabout(z0, -120);
    draw p3 rotatedabout(z0, -120);
    draw t0;);

draw P1 shifted 160 up;
draw P2 shifted 90 left;
draw P3 shifted 90 right;
label.top(btex \vbox{\halign{\hss #\hss\cr
The perpendiculars to the sides from a point on\cr
the boundary or within an equilateral triangle\cr
add up to the height of the triangle.\cr
}} etex, point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Ken-Ichiroh Kawasaki}

\section{A theorem about right angles}
\vfill
$$
\begin{mplibcode}

path s, t;
s = unitsquare shifted -(1/2,1/2) scaled 210;
t = subpath (3, 2) of s -- point 1.732 of fullcircle scaled 210
  shifted point 5/2 of s -- cycle;

fill t withcolor Blues 7 1;
fill s withcolor Oranges 7 1;
for i=0 upto 3: draw t rotated 90i; endfor;
draw point 2 of t -- point 2 of t rotated 180;

label.top(btex \vbox{\halign{\hss #\hss\cr
The internal bisector of the right angle of a right\cr
triangle bisects the square on the hypotenuse\cr
}} etex, point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Roland H.\@ Eddy}

\section{Area and the projection theorem of a right triangle}
\vfill
$$
\begin{mplibcode}
def angle_arc(expr a, o, b, r) = fullcircle scaled 2r rotated angle (a-o) shifted o cutafter (o--b) enddef;

path c; c = fullcircle scaled 180;
pair A, B, C, D, E, F;
A = point 4 of c;
B = point 0 of c;
C = point 1.7 of c;
D = (xpart C, ypart A);
E = C rotatedabout(D, -90);
F = B rotatedabout(D, -90);

color r, b, g; r = Reds 7 1; g = Greens 7 1; b = Blues 7 1;
picture P[];
P1 = image(
    fill A--B--C--cycle withcolor r;
    draw unitsquare scaled 6 rotated angle (A-C) shifted C withcolor 3/4 r;
    draw unitsquare scaled 6 rotated angle (C-D) shifted D withcolor 3/4 r;
    draw angle_arc(D, A, C, 12) withpen pencircle scaled 1 withcolor Reds 7 5;
    draw angle_arc(D, C, B, 12) withpen pencircle scaled 1 withcolor Reds 7 5;
    draw D--C--B--A--C;
    label.bot("$A$", A);
    label.top("$C$", C);
    label.bot("$D$", D);
    label.bot("$B$", B);
);

P2 = image(
    fill A--D--C--cycle withcolor r;
    fill A--D--F--cycle withcolor g;
    fill F--D--E--cycle withcolor r;
    fill C--D--E--cycle withcolor b;
    draw unitsquare scaled 6 rotated angle (C-D) shifted D withcolor 3/4 r;
    draw angle_arc(D, A, C, 12) withpen pencircle scaled 1 withcolor Reds 7 5;
    draw angle_arc(D, E, F, 12) withpen pencircle scaled 1 withcolor Reds 7 5;
    drawarrow subpath (7/4, 1/4) of quartercircle scaled 42 shifted D withcolor Blues 6 6;
    draw A--F--E--C--A--E;
    draw C--F;
    label.lft ("$A$", A);
    label.top ("$C$", C);
    label.llft("$D$", D);
    label.rt  ("$E$", E);
    label.bot ("$F$", F);
);

P3 = image(
    fill A--D--C--cycle withcolor r;
    fill C--D--E--cycle withcolor b;
    z3 = whatever[A,C]; z3 - E = whatever * (A-C) rotated 90;
    begingroup; interim ahangle := 180;
    drawarrow E--z3 dashed evenly scaled 3/4 withpen pencircle scaled 1/4;
    label.urt("$h$", 1/4[z3, E]);
    endgroup;
    draw A--E--C--A;
    draw C--D;
    label.bot("$A$", A);
    label.top("$C$", C);
    label.bot("$D$", D);
    label.bot("$E$", E);
);

P4 = image(
    fill A--D--C--cycle withcolor r;
    fill A--D--F--cycle withcolor g;
    z4 = whatever[A,C]; z4 - F = whatever * (A-C) rotated 90;
    draw z4--F dashed evenly scaled 3/4 withpen pencircle scaled 1/4;
    label.urt("$h$", 1/4[z4, F]);
    draw A--C--F--A--D;
    label.lft ("$A$", A);
    label.urt ("$C$", C);
    label.rt  ("$D$", D);
    label.lrt ("$F$", F);
);

P5 = image(
    fill C--D--E--cycle withcolor b;
    draw D--E--C--D;
    label.top("$C$", C);
    label.bot("$D$", D);
    label.bot("$E$", E);
);

P6 = image(
    fill A--D--F--cycle withcolor g;
    draw A--F--D--A;
    label.lft ("$A$", A);
    label.rt  ("$D$", D);
    label.lrt ("$F$", F);
);

draw P1 shifted 120 left;
draw P2 shifted 120 right;
numeric y; y = -190;
draw P3 shifted (-120, y); label("${}={}$", (12, y+16)); draw P4 shifted (+120, y);
y := y - 112;
draw P5 shifted (-120, y-abs(D-B)); label("${}={}$", (12, y-28)); draw P6 shifted (+120, y);
label("$CD^2 = AD\cdot DB$", point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}

\section{Chords and tangents of equal length}
\vfill
$$
\begin{mplibcode}
def angle_arc(expr a, o, b, r) = fullcircle scaled 2r rotated angle (a-o) shifted o cutafter (o--b) enddef;

path C[]; pair O, P, Q, R;
C1 = fullcircle scaled 280; O = point 0 of C1;
C2 = fullcircle scaled 200 shifted O;

numeric t, u;
(t, u) = C1 intersectiontimes C2;
P = point t of C1;
Q = point 8-t of C1;
z0 = whatever[P, P + direction t of C1]; y0 = ypart point 6 of C1;
R = C2 intersectionpoint (z0--P);

draw center C1 -- P -- point 4 of C1 -- O withcolor 7/8;

forsuffixes $=P, Q, R: 
    draw O -- $ withcolor Blues 7 6; 
endfor
draw 5/4[Q, P] -- 5/4[P, Q] withcolor Reds 7 6;
draw 5/4[P, R] -- 5/4[R, P] withcolor Reds 7 6;

draw angle_arc(O, Q, P, 30);
draw angle_arc(O, Q, P, 28);
draw angle_arc(O, P, R, 30);
draw angle_arc(O, P, R, 28);

draw C1 withcolor 1/2;
draw C2 withcolor 1/2;;

dotlabel.urt("$O$", O);
dotlabel.urt("\strut $P$", P);
dotlabel.lrt("\strut $Q$", Q);
dotlabel.rt("$\;R$", R);

label.top(btex \vbox{\openup6pt\halign{\hss #\hss\cr
If circle $C_1$ passes through the center $O$ of circle $C_2$, the length\cr
of the common chord $\overline{PQ}$ is equal to the tangent segment $\overline{PR}$.\cr
}} etex, point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Roland H.\@ Eddy}

\section{Completing the square}
\vfill
$$
\begin{mplibcode}
path xx, ax, hax, haha;
numeric x, a;
x = 89; a = 34;
xx   = unitsquare shifted 1/2 left scaled x shifted 12 up;
ax   = unitsquare shifted 1/2 left xscaled x yscaled -a shifted 12 down;
hax  = unitsquare shifted 1/2 left xscaled x yscaled -1/2 a shifted 12 down;
haha = unitsquare scaled 1/2 a rotated -90 shifted point 1 of xx shifted (8, -8);

picture P[];
P1 = image(
    fill xx withcolor Oranges 7 1; draw xx; 
    label.top("$x$", point 5/2 of xx); 
    label.lft("$x$", point 7/2 of xx); 
    label("${}+{}$", origin);
    fill ax withcolor Blues 7 2; draw ax;
    label.lft("$a$", point 7/2 of ax);
);

P2 = image(
    fill xx withcolor Oranges 7 1; draw xx; 
    label("${}+{}$", origin);
    for i=0, 1:
        fill hax shifted (0, -24i) withcolor Blues 7 2; 
        draw hax shifted (0, -24i);
    endfor
);

P3 = image(
    fill xx withcolor Oranges 7 1; draw xx; 
    hax := hax shifted (point 0 of xx - point 0 of hax);
    fill hax withcolor Blues 7 2; draw hax;
    hax := hax shifted - point 0 of hax rotated 90 shifted point 1 of xx;
    fill hax withcolor Blues 7 2; draw hax;

    fill haha withcolor Blues 7 1;
    draw haha dashed withdots scaled 1/4;
);


draw P1 shifted 144 left;
label("$=$", (-72, 16));
draw P2;
label("$=$", (72, 16));
draw P3 shifted 144 right;

label.top("$x^2 + ax = \left(x + a/2\right)^2 - \left(a/2\right)^2$", 
point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Charles D.\@ Gallant}

\section{Algebraic areas I}
\vfill
$$
\begin{mplibcode}
numeric a, b;
a = 89; b = 21;
picture P[];
P1 = image(
    fill unitsquare xscaled a yscaled b shifted (0, a) withcolor Greens 7 1;
    fill unitsquare xscaled b yscaled a shifted (a, 0) withcolor Greens 7 1;
    draw (a, 0) -- (a, a+b) dashed withdots scaled 1/4;
    draw (0, a) -- (a+b, a) dashed withdots scaled 1/4;
    draw (a-b, a) -- (a-b, a+b) dashed withdots scaled 1/4;
    draw (a, a-b) -- (a+b, a-b) dashed withdots scaled 1/4;
    draw unitsquare scaled (a+b);
    label.bot("\strut $a$", (1/2a, 0));
    label.bot("\strut $b$", (a+1/2b, 0));
    label.lft("$a$", (0, 1/2a));
    label.lft("$b$", (0, a+1/2b));
);
P2 = image(
    draw unitsquare scaled (a-b);
    label.bot("\strut $a-b$", 1/2(a-b, 0));
);
P3 = image(
    draw unitsquare scaled a;
    draw unitsquare scaled b shifted (a,a);
    label.bot("\strut $a$", (1/2a, 0));
    label("$b$", (a + 1/2b, a + 1/2b));
);
P4 = image(
    fill unitsquare scaled a withcolor Greens 7 1;
    fill unitsquare scaled b shifted (a,a) withcolor Greens 7 1;
    fill unitsquare scaled (a-b) withcolor background;
    draw (a-b, a) -- (a-b, a-b) -- (a, a-b) dashed withdots scaled 1/4;
    draw (0, a-b) -- (a-b, a-b) -- (a-b, 0);
    draw unitsquare scaled a;
    draw unitsquare scaled b shifted (a,a);
    label.bot("\strut $a-b$", 1/2(a-b, 0));
    label.bot("\strut $b$", (a-1/2b, 0));
    label("$b$", (a + 1/2b, a + 1/2b));
);


numeric x, y;
draw P1;

y = 3/4 (a-b);
x = a + b + 14;
label("$+$", (x, y));

x := x + 14;
draw P2 shifted (x, 0);

x := x + a - b + 16;
label("$=$", (x, y));

x := x + 16;
draw P3 shifted (x, 0);

x := x + a + 14;
label("$+$", (x, y));

x := x + 14;
draw P4 shifted (x, 0);

label.top("$\left(a+b\right)^2 + \left(a-b\right)^2 = 2\left(a^2 + b^2\right)$", 
point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Shirley Wakin}

\section{Algebraic areas II}
\vfill
$$
\begin{mplibcode}
input arrow_label

numeric a, b, c;
a = 80; 2b = a; 2c = b;

def make_box(expr p, shade) = image(fill p withcolor shade; draw p) enddef;


path s[];
s1 = unitsquare scaled (a-b-c); 
s2 = unitsquare scaled (2c); 
s3 = unitsquare scaled (a-b+c);
s4 = unitsquare scaled (2b);
s5 = unitsquare scaled (a+b-c); 
s6 = unitsquare xscaled (a+b-c) yscaled (a-b+c);
s7 = unitsquare xscaled (a-b+c) yscaled (a+b-c);

picture t[];
t1 = make_box(s1, Reds 7 2);
t2 = make_box(s2, Oranges 7 2);
t3 = make_box(s3, YlGn 7 2);
t4 = make_box(s4, Greens 7 2);
t5 = make_box(s5, Blues 7 2);
t6 = make_box(s6, Purples 7 2);
t7 = make_box(s7, Purples 7 2);

picture P[];
P1 = image(
    draw t5;
    draw t7 shifted point 1 of s5;
    draw t1 shifted (point 2 of s5 - point 3 of s1);
    draw t3 shifted point 2 of s5;
    draw t6 shifted point 3 of s5;

    draw t4 shifted (2a + 30, 0);
    draw t2 shifted (2a + 30 + 2b + 30, 0);

    arrow_label(origin, 2a * right, "$2a$", 9);
    label.bot("\strut$2b$", (2a + 30 + b, 0));
    label.bot("\strut$2c$", (2a + 30 + 2b + 30 + c, 0));
);
P2 = image(
    draw t4;
    draw t7 shifted point 1 of s4;
    draw t6 shifted point 3 of s4;
    draw t2 shifted ((1,1) scaled (a+b-c));

    draw t5 shifted (a + b + c + 20, 0);
    draw t3 shifted (2a + 2b + 40, 0);
    draw t1 shifted (3a + b + c + 60, 0);

    arrow_label(origin, 2b * up, "$2b$", -12);
    arrow_label((0, a+b+c), (a+b+c, a+b+c), "\strut$a+b+c$", -12);
    label.rt("$2c$", (a+b+c, a+b));
    label.top("$a+b-c$", (3/2a + 3/2b + 1/2c + 20, a + b - c + 4));
    label.top("$a-b+c$", (5/2a + 3/2b + 1/2c + 40, a - b + c + 4));
    label.top("$a-b-c$", (7/2a + 1/2b + 1/2c + 60, a - b - c + 4));
);

label.top(P1, origin);
label.top(P2, (0, 2a+2b));


label.top(btex $\left(a+b+c\right)^2 
          + \left(a+b-c\right)^2 
          + \left(a-b+c\right)^2 
          + \left(a-b-c\right)^2 
          = \left(2a\right)^2 
          + \left(2b\right)^2
          + \left(2c\right)^2$ etex, 
point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Sam Pooley and K.\@ Ann Drude}

\section{Sum of squares identity}
\vfill
$$
\begin{mplibcode}
input arrow_label
picture P[];
numeric a,b,c,d;
a = sqrt(90); 1.732a = 1.414b; c + d = 3/2a; 1.414c = d;

P1 = image(
    path s[];
    s1 = unitsquare xscaled -(a*a) yscaled -(d*d);
    s2 = unitsquare xscaled  (b*b) yscaled -(d*d);
    s3 = unitsquare xscaled  (b*b) yscaled (c*c);
    s4 = unitsquare xscaled -(a*a) yscaled (c*c);
                             
    fill s1 withcolor Reds 7 1;     draw s1;
    fill s2 withcolor Greens 7 1;   draw s2;
    fill s3 withcolor Oranges 7 1;  draw s3;
    fill s4 withcolor Blues 7 1;    draw s4;

    label("$a^2d^2$", center s1);  
    label("$b^2d^2$", center s2);  
    label("$b^2c^2$", center s3);  
    label("$a^2c^2$", center s4);  

    arrow_label(point 3 of s4, point 2 of s4, "$a^2$", 8);    
    arrow_label(point 2 of s4, point 1 of s4, "$c^2$", 8);    
    arrow_label(point 2 of s3, point 3 of s3, "$b^2$", 8);    
    arrow_label(point 1 of s1, point 2 of s1, "$d^2$", 8);    
    
);
P2 = image(
    path s[];
    s1 = unitsquare xscaled -(a*d) yscaled -(a*d);
    s2 = unitsquare xscaled  (b*d) yscaled -(b*d);
    s3 = unitsquare xscaled  (b*c) yscaled (b*c);
    s4 = unitsquare xscaled -(a*c) yscaled (a*c);
                             
    fill s1 withcolor Reds 7 1;     draw s1;
    fill s2 withcolor Greens 7 1;   draw s2;
    fill s3 withcolor Oranges 7 1;  draw s3;
    fill s4 withcolor Blues 7 1;    draw s4;

    label("$\left(ad\right)^2$", center s1);  
    label("$\left(bd\right)^2$", center s2);  
    label("$\left(bc\right)^2$", center s3);  
    label("$\left(ac\right)^2$", center s4);  
) shifted (260, 0);
P3 = image(
    path s[];
    s1 = unitsquare xscaled -(a*d) yscaled -(a*d);
    s2 = unitsquare xscaled  (b*d) yscaled -(b*d) shifted (44, -12);
    s3 = unitsquare xscaled  (b*c) yscaled (b*c);
    s4 = unitsquare xscaled -(a*c) yscaled (a*c) shifted point 1 of s2;
                             
    fill s1 withcolor Reds 7 1;     draw s1;
    fill s2 withcolor Greens 7 1;   draw s2;
    fill s3 withcolor Oranges 7 1;  draw s3;
    fill s4 withcolor Blues 7 1;    draw s4;

    label("$\left(ad\right)^2$", center s1);  
    label("$\left(bd\right)^2$", center s2);  
    label("$\left(bc\right)^2$", center s3);  
    label("$\left(ac\right)^2$", center s4);  
    
    pair t, u, v; 
    t = point arctime 2b*d-a*c of s2 of s2;
    u = whatever[point 3 of s2, point 4 of s2]; ypart u = ypart t;
    v = whatever[point 1 of s4, point 2 of s4]; ypart v = ypart t;
    draw t--u dashed withdots scaled 1/2;
    draw v--point 1 of s4 dashed withdots scaled 1/2;

    arrow_label(point 2 of s2, t, "$ac$", 8);
    arrow_label(t, point 3 of s4, "$bd$", 8);
) shifted (-40, -170);
P4 = image(
    path s[];
    s1 = unitsquare xscaled -(a*d) yscaled -(a*d);
    s3 = unitsquare xscaled  (b*c) yscaled (b*c);

    s21 = unitsquare xscaled -(a*c) yscaled (b*d) shifted t;
    s22 = unitsquare xscaled -(b*d) yscaled (-a*c) shifted (t + 7 down);
    s4 = unitsquare scaled (b*d-a*c) shifted (u + 7 left);
                             
    fill s1 withcolor Reds 7 1;     draw s1;
    fill s21 withcolor 1/2[Blues 7 1, Greens 7 1];   
    fill s22 withcolor 1/2[Blues 7 1, Greens 7 1];   
    draw s21; draw s22;
    fill s3 withcolor Oranges 7 1;  draw s3;
    fill s4 withcolor Purples 7 2;    draw s4;

    label("$\left(ad\right)^2$", center s1);  
    label("$abcd$", center s21);  
    label("$abcd$", center s22);  
    label("$\left(bc\right)^2$", center s3);  
    label("$\scriptstyle \left(bd-ac\right)^2$", center s4);  

    label.top("$ac$", point 5/2 of s21);
    label.rt("$bd$", point 7/2 of s21);
    label.rt("$ac$", point 7/2 of s22);
    label.bot("$bd$", point 5/2 of s22);
    
) shifted (260, -180);
P5 = image(
    path s[];
    s1 = unitsquare xscaled -(a*d) yscaled -(a*d);
    s3 = unitsquare xscaled  (b*c) yscaled (b*c);

    s21 = unitsquare xscaled -(a*d) yscaled (b*c) shifted (-12, 12);
    s22 = unitsquare xscaled (b*c) yscaled -(a*d) shifted (12, -12);
    s4 = unitsquare scaled (b*d-a*c) shifted point 1 of s22 shifted 12 right;
                             
    fill s1 withcolor Reds 7 1;     draw s1;
    fill s21 withcolor 1/2[Blues 7 1, Greens 7 1];   
    fill s22 withcolor 1/2[Blues 7 1, Greens 7 1];   
    draw s21; draw s22;
    fill s3 withcolor Oranges 7 1;  draw s3;
    fill s4 withcolor Purples 7 2;  draw s4;

    label("$\left(ad\right)^2$", center s1);  
    label("$abcd$", center s21);  
    label("$abcd$", center s22);  
    label("$\left(bc\right)^2$", center s3);  
    label("$\scriptstyle \left(bd-ac\right)^2$", center s4);  

    label.top("$ad$", point 5/2 of s21);
    label.lft("$bc$", point 3/2 of s21);
    label.rt("$ad$", point 3/2 of s22);
    label.bot("$bc$", point 5/2 of s22);
    
) shifted (-40, -380);
P6 = image(
    path s[];
    s1 = unitsquare scaled (a*d + b*c);
    s4 = unitsquare scaled (b*d-a*c) shifted point 1 of s1 shifted 12 right;
                             
    fill s1 withcolor 1/2[1/2[Reds 7 1, Oranges 7 1], 1/2[Blues 7 1, Greens 7 1]];   
    draw s1;
    fill s4 withcolor Purples 7 2;  draw s4;

    label("$\left(ad + bc\right)^2$", center s1);  
    label("$\scriptstyle \left(bd-ac\right)^2$", center s4);  
) shifted (180, -480);


draw P1;
draw P2;
draw P3;
draw P4;
draw P5;
draw P6;

def connect_with_arrow(expr a, b) = 
    drawarrow (left-- 4 right) scaled 4 rotated angle (b-a) shifted 1/2[a,b] withcolor Blues 5 5;
enddef;
connect_with_arrow(center P1 + 10 right, center P2);
connect_with_arrow(center P2, center P3);
connect_with_arrow(center P3, center P4);
connect_with_arrow(center P4, center P5);
connect_with_arrow(center P5, center P6);

label.top(btex $\left(a^2+b^2\right)\left(c^2+d^2\right)
          = \left(ab + bc\right)^2 
          + \left(bd-ac\right)^2 $ etex, 
point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Diophantus of Alexandria}

\section{Polygonal numbers}

\vfill
$$
\begin{mplibcode}
vardef around(expr p, r) = 
    if pair p:
        fullcircle scaled 2r shifted p
    elseif path p and (length(p) = 0):
        fullcircle scaled 2r shifted point 0 of p
    elseif path p:
        for i=1 upto length(p):
            subpath (i-1, i) of p 
            shifted (r * unitvector(direction i-1/2 of p rotated -90))
            ..
        endfor 
        if not cycle p:
            for i=length(p) downto 1:
                subpath (i, i-1) of p
                shifted (r * unitvector(direction i-1/2 of p rotated 90))
                ..
            endfor
        fi cycle
    fi
enddef;
% k-th n-gonal number...
numeric k, n;  
k = 6;
n = 6;
path gon[];

for i=2 upto k:
    gon[i] = (origin for j=1 upto n-1: -- dir (180/n*j) endfor -- cycle) scaled 50(i-1);
endfor

numeric r; r = 8;

fill around(origin, r) withcolor Blues 7 3; 
draw around(origin, r); 

for i=1 upto n-1:
    path a; a = around(point i of gon2 -- point i of gon[k], r);
    fill a withcolor Oranges 7 1; draw a;
endfor

for i=2 upto n-2:
    draw origin -- point i of gon[k] dashed evenly;
endfor

for i=1 upto n-2:
    path a; a = around(
        point i+1/2 of gon[3] -- 
        point i+1/(k-1) of gon[k] -- 
        point i+1-1/(k-1) of gon[k] -- cycle, r);
    fill a withcolor Greens 7 1; draw a;
endfor

for i=2 upto k:
    draw gon[i];
    for j = i-1 upto (n-1)*i:
        drawdot point j/(i-1) of gon[i] withpen pencircle scaled r;
    endfor
endfor
drawdot origin withpen pencircle scaled r;

label.top(btex The $k$\textsuperscript{th} $n$-gonal number is 
$1 + \bigl(k-1\bigr)\bigl(n-1\bigr) + \frac12\bigl(k-2\bigr)\bigl(k-1\bigr)\bigl(n-2\bigr)$ etex, 
point 5/2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Dave Logothetti}

\section{The volume of a frustrum of a square pyramid}

\vfill
$$
\begin{mplibcode}
input isometric-projection
set_projection(22, -34);

path base, hlid, mlid;
numeric h, a, b; h = 6; b = 7; a = 3;
base = p(0, 0, 0) -- p(0, 0, b) -- p(-b, 0, b) -- p(-b, 0, 0) -- cycle;
hlid  = p(0, h, 0) -- p(0, h, a) -- p(-a, h, a) -- p(-a, h, 0) -- cycle;
mlid  = p(0, b-a, 0) -- p(0, b-a, a) -- p(-a, b-a, a) -- p(-a, b-a, 0) -- cycle;

picture P[];
P1 = image(
    path lid; lid = hlid;
    fill subpath (0, 1) of base -- subpath (1, 0) of lid -- cycle withcolor Blues 8 1;
    fill lid withcolor Blues 8 2;
    drawoptions(dashed withdots scaled 1/2 withcolor 1/2);
    draw subpath (1, 3) of base;
    draw point 2 of base -- point 2 of lid;

    drawoptions(withcolor 1/2);
    numeric t; t = 1/2;
    draw p(-t, 0, 0) -- p(-t, t, 0) -- p(0, t, 0) -- p(0, t, t) -- p(0, 0, t);
    draw p(-t, h, 0) -- p(-t, h-t, 0) -- p(0, h-t, 0) -- p(0, h-t, t) -- p(0, h, t);

    drawoptions();
    draw lid -- point 0 of base;
    draw point 3 of lid -- subpath (-1, 1) of base -- point 1 of lid;

    label.lft("$h$", p(0, 1/2 h, 0));
    label.urt("$a$", point 7/2 of lid);
    label.ulft("$a$", point 1/2 of lid);
    label.lrt("$b$", point 1/2 of base);
    label.llft("$b$", point 7/2 of base);

    label("$P_1$", p(0, -1, 0));
);
P2 = image(
    path lid; lid = mlid;
    fill subpath (0, 1) of base -- subpath (1, 0) of lid -- cycle withcolor Blues 8 1;
    fill lid withcolor Blues 8 2;
    drawoptions(dashed withdots scaled 1/2 withcolor 1/2);
    draw subpath (1, 3) of base;
    draw point 2 of base -- point 2 of lid;

    drawoptions(withcolor 1/2);
    numeric t; t = 1/2;
    draw p(-t, 0, 0) -- p(-t, t, 0) -- p(0, t, 0) -- p(0, t, t) -- p(0, 0, t);
    draw p(-t, b-a, 0) -- p(-t, b-a-t, 0) -- p(0, b-a-t, 0) -- p(0, b-a-t, t) -- p(0, b-a, t);

    drawoptions();
    draw lid -- point 0 of base;
    draw point 3 of lid -- subpath (-1, 1) of base -- point 1 of lid;

    label.lft("$b-a$", p(0, 1/2 (b-a), 0));
    label.urt("$a$", point 7/2 of lid);
    label.ulft("$a$", point 1/2 of lid);
    label.lrt("$b$", point 1/2 of base);
    label.llft("$b$", point 7/2 of base);

    label("$P_2$", p(0, -1, 0));
);
P3 = image(
    path aaa, bbb;
    aaa = (p(0, 0, 0) -- 
           p(0, 0, a) --
           p(0, a, a) --
           p(-a, a, a) --
           p(-a, a, 0) -- 
           p(-a, 0, 0) -- cycle) shifted p(0, b-a, 0);
    bbb = p(0, 0, 0) -- 
          p(0, 0, b) --
          p(0, b, b) --
          p(-b, b, b) --
          p(-b, b, 0) -- 
          p(-b, 0, 0) -- cycle;
    pair c; c = p(-a, b-a, a);
    
    drawoptions(withcolor Blues 8 1);
    fill subpath(0, 2) of bbb -- subpath (2, 0) of aaa -- cycle;
    fill subpath (3, 5) of aaa -- c -- cycle;

    drawoptions(withcolor Blues 8 2);
    fill subpath(2, 4) of bbb -- subpath (4, 2) of aaa -- cycle;
    fill subpath (-1, 1) of aaa -- c -- cycle;

    drawoptions(dashed withdots scaled 1/2 withcolor 1/2);
    draw subpath (1, 3) of base;
    draw p(-b, b, b) -- point 2 of base -- point 2 of lid;

    drawoptions(withcolor 1/2);
    for $=1,3,5: draw point $ of aaa -- c; endfor

     drawoptions();
    for $=1 upto length aaa: draw point $ of aaa -- point $ of bbb; endfor
    draw aaa;
    draw bbb;

    label.urt ("$a$", point 7/2 of lid);
    label.ulft("$a$", point 1/2 of lid);
    label.lft ("$a$", point 3/2 of aaa);
    
    label.rt("$b$", point 3/2 of bbb);
    label.lrt("$b$", point 1/2 of base);
    label.llft("$b$", point 7/2 of base);


    label("$3P_2$", p(0, -1, 0));
);

draw P1 shifted 120 left;
draw P2 shifted 120 right;
draw P3 shifted 240 down;

label.top(btex  $\displaystyle
    V_{P_1} = {h\over b-a}\cdot V_{P_2} = {h\over b-a}\cdot{1\over3}\left(b^3-a^3\right)
     = {h\over3}\left(a^2+ab+b^2\right)$ etex, 
     point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{\textit{The Moscow Papyrus}, c.\@ 1850 BCE}

\section{The volume of a hemisphere via Cavalieri's Principle}

\vfill
$$
\begin{mplibcode}
input arrow_label
input isometric-projection
set_projection(18, -32);

numeric r, h, s, tau;
tau = 6.283185307179586;
r * tau = 400 / ipscale;  
h = 3/4 r;
s = r +-+ h;

z0 = p(0,0,0); 
z1 = p(0,0,r);
z2 = p(0,r,r);
z3 = p(0,r,0);
z4 = p(tau * r, 0, 0);

z5 = p(tau * (r-h), h, 0);
z6 = p(tau * (r-h), h, h);
z7 = p(0, h, r);
z8 = p(0, h, 0);

 z9 = p(0, 0, 5r);
z10 = p(0, r, 5r);
z11 = p(0, h, 5r);

z12 = z9 shifted p(2r, 0, 0);
z13 = z10 shifted p(r, 0, 0);
z14 = 1/2[z9, z12];
z15 = z14 shifted p(0, 0, -r);
z16 = z14 shifted p(0, 0, +r);

z17 = z14 + p(-s, h, 0);
z18 = z14 + p(0, h, -s);
z19 = z14 + p(+s, h, 0);
z20 = z14 + p(0, h, +s);

path disc, base, arc, arch; 
base = for i=0 upto 11: z14 + p(r*cosd(30i), 0, r*sind(30i)) .. endfor cycle;
disc = for i=0 upto 11: z14 + p(s*cosd(30i), h, s*sind(30i)) .. endfor cycle;
numeric a, b;
a = directiontime down of base;
b = directiontime up of base;

arc = point a of base .. point a of disc .. z13 .. point b of disc .. point b of base;

drawoptions(dashed evenly withpen pencircle scaled 1/4 withcolor 1/2);
draw z1--z9--z12; draw z2--z10--z13; draw z7--z11;
draw z9--z10;

drawoptions(dashed withdots scaled 1/4 withcolor 1/2);
draw z0--z1--z4; draw z1--z2; 
draw z14 -- center disc;
fill disc withcolor Blues 7 1;
draw z13 -- center disc -- z19 -- z14;
draw subpath (b, a) of base;
draw subpath (b, a) of disc;

fill z5--z6--z7--z8--cycle withcolor Oranges 7 1;
draw z6--z7--z8;

drawoptions(dashed evenly withpen pencircle scaled 1/4 withcolor 1/2);
draw z11 -- center disc;

drawoptions();
draw z0--z4--z2--z3--z0; draw z3--z4; draw z8--z5--z6;
draw arc;
draw subpath (a, 12 + b) of base;
draw subpath (a, 12 + b) of disc;

path circ, trap, crad;
circ = fullcircle scaled (2s*ipscale) shifted center disc shifted 144 up;
crad = center circ -- point 3/4 of circ;
trap = (origin -- (tau*(r-h), 0) -- (tau*(r-h), h) -- (0, r) -- cycle) 
     shifted (0, -1/2h)
     scaled ipscale shifted (0, ypart center circ);
fill circ withcolor Blues 7 1;  
draw circ;
fill trap withcolor Oranges 7 1; draw trap;

label("${}={}$", 1/2[point 3/2 of trap, point 4 of circ]); 

drawoptions(withcolor Blues 7 6);
arrow_label(3/4[z1, z9], 3/4[z7, z11], "$h$", 0);
arrow_label(z0 shifted p(0,0,-3/4), z3 shifted p(0,0,-3/4), "$r$", 0);
arrow_label(z0 shifted p(0,0,-3/2), z4 shifted p(0,0,-3/2), "$2\pi r$", 0);
arrow_label(point 0 of trap, point 1 of trap, "$2\pi(r-h)$", 9);
label.lft("$r$", point -1/2 of trap);
label.rt("$h$", point 3/2 of trap);
label.lrt("$h$", 5/8[z5, z6]);
label.top("$r$", 1/2[z2, z3]);
label.top("$r$", 1/2[z0, z1]);
label.lrt("$r$", 1/2[z14, z19]);
drawdblarrow crad;
label.ulft("$\sqrt{r^2-h^2}$", point 5/8 of crad);

drawoptions();
label.urt("$S$", point 7/2 of arc);
label.urt("$P$", 5/8[z2, z4]);

z21 = 1/2[z13, point 6 of circ];
drawarrow (up--down) scaled 10 shifted z21 withcolor Blues 7 7;
drawarrow (up--down) scaled 10 shifted (xpart point 1/2 of trap, y21) withcolor Oranges 7 7;

label.bot(btex  $\displaystyle V_S = V_P = {1\over3}r^2\cdot2\pi r = {2\over3} \pi r^3$ etex, 
     point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}

%-----------------------------------------
\chapter{Trigonometry, Calculus, \& Analytic Geometry}

\minitoc

\section{Sine of the sum}

\vfill
$$
\begin{mplibcode}
numeric x, y, z, r;
pair A, B, C, P;

x = 75; y = 46; x + y + z = 180;
r = 180;
A = r * dir (270 - z);
B = r * dir (270 + z);
C - A = whatever * dir x;
C - B = whatever * dir (180-y);
P = whatever[A,B]; C - P = whatever * up;

path am[];
am1 = fullcircle scaled 42 rotated angle (B-A) shifted A cutafter (A--C);
am2 = fullcircle scaled 42 rotated angle (C-B) shifted B cutafter (B--A);
am3 = fullcircle scaled 42 rotated angle (A-C) shifted C cutafter (C--B);
am4 = fullcircle scaled 42 rotated angle A cutafter (origin -- 1/2[A,B]);
forsuffixes $=1,2: draw am$ withcolor 3/4; endfor
forsuffixes $=3,4: draw am$ withcolor Reds 6 5; endfor

draw subpath (1,3) of unitsquare scaled 6 shifted 1/2[A,B] withcolor 3/4;
draw subpath (1,3) of unitsquare scaled 6 shifted P withcolor 3/4;

draw fullcircle scaled 2r withcolor Blues 7 6;
fill fullcircle scaled 2  withcolor Blues 7 6;

draw C--P dashed evenly withcolor 3/4;
draw A--B--C--A--origin--1/2[A,B];

label.urt("$\alpha$", point arctime 3/4 arclength am1 of am1 of am1);
label.lft("$\beta$", point arctime 1/2 arclength am2 of am2 of am2);
label.lrt("$\gamma$", point arctime 1/2 arclength am3 of am3 of am3);
label.llft("$\gamma$", point arctime 1/2 arclength am4 of am4 of am4);
label.urt("$a$", 1/2[B,C]);
label.ulft("$b$", 1/2[C, A]);
label.bot("$c$", 1/2[A, B]);
label.top("$c/2$", 5/16[A, B]);
label.top("$r$", 1/2 A);

label.top("$\sin(\alpha+\beta) = \sin\alpha\cos\beta + \cos\alpha\sin\beta$ for $\alpha+\beta < \pi$",
   point 5/2 of bbox currentpicture shifted 42 up);

label.bot("\vbox{\openup 6pt\halign{\hss # \hss\cr $c = a \cos\beta + b \cos\alpha$\cr $r=1/2$ \quad $\Longrightarrow$ \quad $\sin\gamma = {c/2\over1/2} = c$,\enspace $\sin\alpha=a$,\enspace $\sin\beta=b$\cr $\sin\bigl(\alpha+\beta\bigr) = \sin\bigl(\pi - (\alpha+\beta)\bigr) = \sin\gamma = \sin\alpha\cos\beta + \sin\beta\cos\alpha$\cr}}", point 1/2 of bbox currentpicture shifted 12 down);

\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}

\section{Area and difference formulas}

\vfill
$$
\begin{mplibcode}
numeric x, y, a, b;
x = 56; y = 42; a = 120 / cosd(x); a * cosd(x) = b * cosd(y);

path t[]; 
t1 = origin -- b * dir y -- a * dir x -- cycle;
t2 = origin -- (xpart point 1 of t1, 0) -- point 1 of t1 -- cycle;

path a[];
a1 = fullcircle scaled 64 cutafter subpath (2,3) of t1;
a2 = fullcircle scaled 36 cutafter subpath (2,3) of t2;
a3 = fullcircle scaled 64 cutbefore subpath (2,3) of t2 cutafter subpath (2,3) of t1;  

picture P[];
P1 = image(
    fill t1 withcolor Blues 8 1;
    pair p; p = whatever[point 2 of t1, point 3 of t1];
    p - point 1 of t1 = whatever * ((point 3 of t1 - point2 of t1) rotated 90);
    drawoptions(withcolor Blues 7 3);
    draw subpath (1,3) of unitsquare scaled 5 
        rotated angle (point 3 of t1 - point 2 of t1) 
        shifted p withpen pencircle scaled 1/4;
    draw p -- point 1 of t1;
    drawoptions(withcolor Blues 7 5);
    pair q, r; 
    q = 7/8 point arctime 1/2 arclength a3 of a3 of a3; r = q + (8, -8);
    draw r .. q dashed withdots scaled 1/4 withpen pencircle scaled 1/4;
    draw a3; label("$\alpha-\beta$", r+(4,-4));
    drawoptions();
    draw t1; 
    label.ulft("$a$", point -5/8 of t1);
    label.lrt("$b$", point   5/8 of t1);
);
P2 = image(
    fill t1 withcolor Blues 8 1;
    fill t2 withcolor Oranges 8 1;
    drawoptions(withcolor Oranges 7 3);
    draw subpath (1,3) of unitsquare scaled 5 
        rotated 90
        shifted point 1 of t2
        withpen pencircle scaled 1/4;
    drawoptions(withcolor Reds 7 5);
    draw a1; label.rt("$\alpha$", point arctime 1/2 arclength a1 of a1 of a1);
    drawoptions(withcolor Oranges 7 5);
    draw a2; label.rt("$\beta$", point arctime 1/2 arclength a2 of a2 of a2);
    drawoptions();
    draw t1; draw subpath (0,2) of t2;
    label.ulft("$a$", point -9/16 of t1);
    label.lrt("$b$", point   9/16 of t1);
    label.rt(textext("\strut$a\sin\alpha$") rotated 90, 1/2[point 1 of t2, point 2 of t1]);
    label.bot("$b\cos\beta$", point 1/2 of t2);
);
P3 = image(
    fill t2 withcolor Oranges 8 1;
    drawoptions(withcolor Oranges 7 3);
    draw subpath (1,3) of unitsquare scaled 5 
        rotated 90
        shifted point 1 of t2
        withpen pencircle scaled 1/4;
    drawoptions(withcolor Oranges 7 5);
    draw a2; label.rt("$\beta$", point arctime 1/2 arclength a2 of a2 of a2);
    drawoptions();
    draw t2;
    label.ulft("$b$", point  -9/16 of t2);
    label.rt(textext("\strut$b\sin\beta$") rotated 90, point 3/2 of t2);
    label.bot("$a\cos\alpha$", point 1/2 of t2);
);
draw P1;
draw P2 shifted 124 right;
draw P3 shifted 280 right;
label("$=$", (128, 60));
label("$-$", (288, 60));

label.bot(btex \vbox{\openup 6pt\halign{\hfil $#$&${}=#$\hfil\cr
\frac12\cdot a\cdot b\sin\bigl(\alpha-\beta\bigr)&\frac12\cdot a\sin\alpha \cdot
b\cos\beta - \frac12\cdot a\cos\alpha\cdot b\sin\beta\cr
        \sin\bigl(\alpha-\beta\bigr)&\sin\alpha\cos\beta -
        \cos\alpha\sin\beta\cr}} etex, 
point 1/2 of bbox currentpicture shifted 13 down);
\end{mplibcode}
$$
\vfill
$$
\begin{mplibcode}
numeric a, b, alpha, beta;
alpha = 72; beta = 42; a = 120; a * sind(alpha) = b * cosd(beta);

path t[]; 
t1 = origin -- a * sind(alpha) * up -- a * cosd(alpha) * left -- cycle; 
t2 = origin -- b * sind(beta) * right -- b * cosd(beta) * up -- cycle;

pair p; p = whatever[point 1 of t1, point 2 of t1]; 
p - point 1 of t2 = whatever * ((point 2 of t1-point 1 of t1) rotated 90);

path a[];
a1 = fullcircle scaled 24 rotated 0 shifted point 2 of t1 cutafter subpath (1,2) of t1;
a2 = fullcircle scaled 24 rotated 270 shifted point 2 of t2 cutafter subpath (1,2) of t2;
a3 = fullcircle scaled 32 rotated (90+beta) shifted point 1 of t2 cutafter (p--point 1 of t2);

picture P[];

P1 = image(
    fill t1 withcolor Blues 8 1;
    fill t2 withcolor Oranges 8 1;

    drawoptions(withcolor Blues 7 3);
    draw subpath (1,3) of unitsquare scaled 5 
        rotated angle (point 2 of t1 - point 1 of t1) 
        shifted p withpen pencircle scaled 1/4;
    draw p -- point 1 of t2;
    drawoptions(withcolor Blues 7 5);
    draw a1; label("$\alpha$", point arctime 1/2 arclength a1 of a1 of a1 + (5,5));
    drawoptions(withcolor Oranges 7 5);
    draw a2; label("$\beta$", point arctime 1/2 arclength a2 of a2 of a2 + (2,-8));
    drawoptions(withcolor Reds 7 5);
    pair q, r; 
    q = 3/4[point 1 of t2, point arctime 1/2 arclength a3 of a3 of a3]; r = q + (9, 12);
    draw r .. q dashed withdots scaled 1/4 withpen pencircle scaled 1/4;
    draw a3; label("$\alpha-\beta$", r + 4 up);
    drawoptions();
    draw subpath (1,3) of unitsquare scaled 5 withcolor Oranges 7 3;
    draw t1; draw subpath (0, 2) of t2;
    label.ulft("$a$", point 3/2 of t1);
    label.urt("$b$", point 3/2 of t2);
);
P2 = image(
    fill t1 withcolor Blues 8 1;

    drawoptions(withcolor Blues 7 3);
    draw subpath (1,3) of unitsquare scaled 5 rotated 90 withpen pencircle scaled 1/4;
    drawoptions(withcolor Blues 7 5);
    draw a1; label("$\alpha$", point arctime 1/2 arclength a1 of a1 of a1 + (5,5));
    drawoptions();
    draw t1;
    label.rt  (textext("\strut$b\cos\beta$") rotated 90, point 1/2 of t1);
    label.ulft("$a$", point 3/2 of t1);
    label.bot ("\strut$a\cos\alpha$", point 5/2 of t1);
);
P3 = image(
    fill t2 withcolor Oranges 8 1;
    drawoptions(withcolor Oranges 7 5);
    draw a2; label("$\beta$", point arctime 1/2 arclength a2 of a2 of a2 + (2,-8));
    drawoptions();
    draw subpath (1,3) of unitsquare scaled 5 withcolor Oranges 7 3;
    draw t2;
    label.bot("\strut$b\sin\beta$", point 1/2 of t2);
    label.urt("$b$", point 3/2 of t2);
    label.lft(textext("\strut$a\sin\alpha$") rotated 90, point 5/2 of t2);
);

draw P1 shifted 200 left;
draw P2 shifted 20 left;
draw P3 shifted 60 right;
label("$=$", (-84, 64));
label("$+$", (20, 64));

label.bot(btex \vbox{\openup 6pt\halign{\hfil $#$&${}=#$\hfil\cr
\frac12 \cdot a \cdot b\cos\bigl(\alpha-\beta\bigr)&
\frac12 \cdot a\cos\alpha \cdot b\cos\beta +
\frac12 \cdot a\sin\alpha \cdot b\sin\beta\cr
        \cos\bigl(\alpha-\beta\bigr)&\cos\alpha\cos\beta +
        \sin\alpha\sin\beta\cr}} etex, 
point 1/2 of bbox currentpicture shifted 13 down);

\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}

\section{The law of cosines I}

\vfill
$$
\begin{mplibcode}
numeric a, b, theta;
path A, B, C, Am, Bm;
a = 136; b = 9/16 a; theta = 40;

A = unitsquare scaled a rotated -90;
B = unitsquare scaled b rotated theta;
C = point 3 of A 
 -- point 1 of B rotatedabout(point 3 of A, -90)
 -- point 3 of A rotatedabout(point 1 of B, +90) 
 -- point 1 of B -- cycle;

z0 = whatever[point 0 of A, point 3 of A];
point 1 of B - z0 = whatever * up;
path arc; 
arc = quartercircle rotated 180 scaled 2 abs(point 1 of B - z0) 
      shifted point 1 of B
      cutbefore subpath (0,1) of B;

Am = unitsquare scaled -abs(z0 - point 3 of A) shifted point 3 of A;
Bm = unitsquare scaled abs(point 0 of arc - point 1 of B) rotated theta shifted point 0 of arc;

picture P[];

P1 = image(
draw subpath (1,3) of unitsquare scaled 6 shifted z0 withcolor 1/2;
draw z0 -- point 1 of B dashed evenly scaled 1/2;

draw arc dashed withdots scaled 1/4;

fill A withcolor Oranges 7 1;
fill B withcolor Oranges 7 1;
fill C withcolor Blues 7 1;

fill Am withcolor Blues 7 1;
fill Bm withcolor Blues 7 1;

draw A;
draw B;
draw C;
draw Am;
draw Bm;

label.bot ("$a$", point 3/2 of A);
label.ulft("$b$", point 5/2 of B);
label.urt ("$c$", point 3/2 of C);

label("$\theta$", 16 dir 1/2 theta);
label.bot("\strut$b\cost$", 1/2 z0);
draw thelabel.top("$b\sint$", origin) rotated theta shifted point 1/2 of Bm;
);
P2 = image(
    Bm := Bm rotatedabout(point 1 of B, 90-theta);
    draw subpath (1,3) of unitsquare scaled 6 shifted z0 withcolor 1/2;
    forsuffixes $=Am, Bm, C: fill $ withcolor Blues 7 1; draw $; endfor
    label("$\left(a - b \cost \right)^2$", center Am);
    label("$\left(b \sint \right)^2$", center Bm);
    label("$c^2$", center C);
);

draw P1;
draw P2 shifted 200 right;
drawarrow 160 right -- 190 right withpen pencircle scaled 2 withcolor Blues 7 5;
label.bot(btex \vbox{\openup 8pt\halign{\hfil $#$&${}=#$\hfil\cr
c^2 & \left(b\sint\right)^2 + \left(a - b\cost\right)^2\cr
&b^2\sin^2\theta + a^2 - 2ab\cost + b^2\cos^2\theta\cr
&a^2 + b^2\left(\sin^2\theta + \cos^2\theta\right) - 2ab\cost\cr
&a^2 + b^2 - 2ab\cost\cr}} etex, point 1/2 of bbox currentpicture shifted 32 down);
\end{mplibcode}
$$
\vfill
\contrib{Timothy A.\@ Sipka}

\section{The law of cosines II}

\vfill
$$
\begin{mplibcode}
path c; numeric a, b;  
c = fullcircle scaled 421;
a = 0.98; b = 2.718;
z0 = whatever[point a of c, point a+4 of c] = whatever[point 0 of c, point b of c];

fill center c -- point 0 of c -- z0 -- cycle withcolor Greens 7 1;

% mark the angles
draw unitsquare scaled 8 rotated angle (point 4 of c - point b of c) shifted point b of c withcolor 3/4;
draw halfcircle scaled 64 shifted point 0 of c cutbefore (point 0 of c -- point b of c) withcolor Reds 7 7;
label("$\theta$", 26 dir (180 - 1/4(180 - 45b)) shifted point 0 of c); 

draw point a of c -- point a + 4 of c;
draw point 4 of c -- point 0 of c -- point b of c -- cycle;
draw c withcolor Blues 7 7;
drawdot center c withpen pencircle scaled dotlabeldiam withcolor Blues 7 7;

label.bot("$a$", 1/2 point 0 of c);
label.bot("$a$", 1/2 point 4 of c);

draw thelabel.top("$a$", 1/2 point 4 of c) rotated 45a;
draw thelabel.top("$c$", 1/2 (abs(z0), 0)) rotated 45a;
draw thelabel.top("$a-c$", 1/2[(abs(z0), 0), point 0 of c]) rotated 45a;

draw thelabel.top("$2a\cost-b$", origin) rotated -1/2(180-45b) shifted 1/2[z0, point b of c];
draw thelabel.top("$b$",              origin) rotated -1/2(180-45b) shifted 1/2[z0, point 0 of c];

label.bot(btex \vbox{\openup 8pt\halign{\hfil $#$ \hfil\cr
\bigl(2a\cost - b\bigr) \cdot b = \bigl( a - c \bigr) \cdot \bigl(a + c \bigr)\cr
c^2 = a^2 + b^2 - 2ab\cost\cr}} etex, point 1/2 of bbox currentpicture shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}

\section{The law of cosines III (via Ptolemy's theorem)}

\vfill
$$
\begin{mplibcode}
path c; numeric a, b;  
c = fullcircle scaled 421;
a = 1/4; b = -7/8;

z0 = point a of c; z1 = point 4-a of c;
z3 = point b of c; z2 = point 4-b of c;

draw fullcircle scaled 42 rotated angle (z0-z3) shifted z3 cutafter (z2--z3) withcolor Reds 7 6;
draw fullcircle scaled 42 rotated angle (z3-z2) shifted z2 cutafter (z1--z2) withcolor Reds 7 6;

draw z0--z2--z3--z0--z1;
draw z2--z1--z3 dashed evenly;

draw c withcolor Blues 7 7;

label.top("$a+2b\cos\bigl(\pi-\theta\bigr)$", 1/2[z0, z1]);
label.bot("$a$", 1/2[z2, z3]);
label.lft("$b$", 1/2[z1, z2]);
label.rt ("$b$", 1/2[z3, z0]);
label.ulft("$c$", 3/4[z2, z0]);
label.urt ("$c$", 3/4[z3, z1]);

label("$\theta$", z3 + 8 (unitvector(z0-z3)+unitvector(z1-z3)));
label("$\theta$", z2 + 8 (unitvector(z0-z2)+unitvector(z1-z2)));

label.bot(btex \vbox{\openup 8pt\halign{\hfil $#$ \hfil\cr
c \cdot c = b \cdot b + \Bigl(a + 2b \cos\bigl(\pi-\theta\bigr)\Bigr) \cdot a\cr
c^2 = a^2 + b^2 - 2ab\cost\cr}} etex, point 1/2 of bbox currentpicture shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}

\section{The double-angle formulae}

\vfill
$$
\begin{mplibcode}
path h; pair A, B, C, D, O; numeric theta;

h = halfcircle scaled 420;

O = origin;
A = point 4 of h;
B = point 0 of h;
C = point 5/4 of h;
D = (xpart C, ypart A);

2theta = angle C;

draw unitsquare scaled 8 rotated angle (C-D) shifted D withcolor 3/4;
draw unitsquare scaled 8 rotated angle (A-C) shifted C withcolor 3/4;

draw A--C--B withcolor Reds 7 7;
draw O--C--D withcolor Reds 7 7;

drawoptions(withcolor Blues 7 6);
draw h; 
label.ulft("$x^2 + y^2 = 1$", point 3 of h);
drawoptions();

primarydef o through p = (1+o/arclength(p))[point 1 of p, point 0 of p] -- (1+o/arclength(p))[point 0 of p, point 1 of p] enddef;
drawarrow 16 through (A--B);
drawarrow 16 through (O--point 2 of h);

dotlabel.bot("$A$", A);
dotlabel.bot("$B$", B);
dotlabel.urt("$C \smash{\;\bigl(\cos2\theta, \sin2\theta\bigr)}$", C);
dotlabel.bot("$D$", D);
dotlabel.llft("$O$", O);

label("$\theta$", 28 dir 1/2 theta shifted A);
label("$2\theta$", 20 dir theta);

label("$x$", B shifted 24 right);
label("$y$", point 2 of h shifted 24 up);

draw thelabel.top("$2\cost$", origin) rotated theta      shifted 1/2[A, C];
draw thelabel.top("$2\sint$", origin) rotated (theta-90) shifted 1/2[B, C];

label.bot("$\triangle ACD \sim \triangle ABC$", point 1/2 of bbox currentpicture shifted 42 down);
path p; p = bbox currentpicture shifted 20 down;

label.bot(btex \vbox{\openup 8pt\halign{\hfil $#$\hfil\cr
CD \Big/ AC = BC \Big/ AB\cr
\sin 2\theta \big/ 2 \cost = 2 \sint \big/ 2\cr
\sin 2\theta = 2\sint \cost\cr}} etex, point 1/4 of p);

label.bot(btex \vbox{\openup 8pt\halign{\hfil $#$\hfil\cr
AD \Big/ AC = AC \Big/ AB\cr
\bigl(1 + \cos 2\theta \bigr) \big/ 2 \cost = 2 \cost \big/ 2\cr
\cos 2\theta = 2\cos^2\theta - 1\cr}} etex, point 3/4 of p);

\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}

\section{The half-angle tangent formulae}

\vfill
$$
\begin{mplibcode}
path h; pair A, B, C, D, O; numeric theta;

h = halfcircle scaled 420;

O = origin;
A = point 4 of h;
B = point 0 of h;
C = point 5/4 of h;
D = (xpart C, ypart A);

theta = angle C;

draw unitsquare scaled 8 rotated angle (C-D) shifted D withcolor 3/4;
draw unitsquare scaled 8 rotated angle (A-C) shifted C withcolor 3/4;

drawoptions(withcolor Reds 7 7);
draw A--C--B;
draw O--C--D;

drawoptions();
label("$\theta/2$", 38 dir 1/4 theta shifted A);
label("$\theta/2$", 42 dir (270 + 1/4 theta) shifted C);
label("$\theta$", 20 dir 1/2 theta);

drawoptions(withcolor Blues 7 6);
draw h; 
label.ulft("$x^2 + y^2 = 1$", point 3 of h);

drawoptions();
primarydef o through p = (1+o/arclength(p))[point 1 of p, point 0 of p] -- (1+o/arclength(p))[point 0 of p, point 1 of p] enddef;
drawarrow 16 through (A--B);
drawarrow 16 through (O--point 2 of h);

label("$x$", B shifted 24 right);
label("$y$", point 2 of h shifted 24 up);

label.bot("$1$", 1/2 A);
label.ulft("$1$", 1/2 C);
label.bot("$\cost$", 1/2 D);
label.bot("$1-\cost$", 1/2[B, D]);
draw thelabel.top("$\sint$", origin) rotated 90 shifted 1/2[C,D];

label.bot(btex $\displaystyle
\tan \theta\big/2 = {\sint\over 1+\cost} = {1-\cost\over\sint}
$ etex, point 1/2 of bbox currentpicture shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{R.\@ J.\@ Walker}

\section{Mollweide's equation}

\vfill
$$
\begin{mplibcode}

z1 = 210 left;
z2 = 210 right;
z3 = 180 dir 113;

pair t; t = unitvector(z1-z3) + unitvector(z2-z3);
z4 - z3 = whatever * t;
z4 - z1 = whatever * t rotated 90;

z5 = whatever[z1,z4] = whatever[z2,z3];
z6 = whatever[z1,z4]; 
z7 = whatever[z1,z4];

z6 - z2 = whatever * (z3 - z4);
z7 - z2 = whatever * (z3 - z1);

draw subpath (1,3) of unitsquare scaled 8 rotated angle (z4-z1) shifted z4 withcolor 3/4;
draw subpath (1,3) of unitsquare scaled 8 rotated angle (z1-z6) shifted z6 withcolor 3/4;

draw z3--z4 dashed withdots scaled 1/2;
draw z2--z6 dashed withdots scaled 1/2;

drawoptions(withcolor Blues 8 7);
draw halfcircle scaled 48 shifted z1 cutafter (z1--z3);
label("$\alpha$", z1 + 32 dir 1/2 angle (z3-z1));

drawoptions(withcolor Greens 8 7);
draw reverse halfcircle scaled 48 shifted z2 cutafter (z2--z3);
label("$\beta$", z2 + 32 dir (90 + 1/2 angle (z3-z2)));

drawoptions(withcolor Oranges 8 7);
draw halfcircle scaled 48 rotated angle (z4-z3) shifted z3 cutafter (z2--z3);
draw halfcircle scaled 48 rotated angle (z7-z2) shifted z2 cutafter (z2--z6);
label("${\gamma\over2}$", z3 + 20 (unitvector(z4-z3) + unitvector(z2-z3)));

drawoptions(withcolor Reds 8 7);
picture a; a = image(
    for s=48,52:
        draw halfcircle scaled s rotated angle (z3-z2) shifted z5 cutafter (z5--z1);
    endfor
);
draw a;
draw a rotatedabout(z5, 180);
draw a rotatedabout(z5, 180) reflectedabout(z2,z6);
label("${\alpha+\beta\over2}$", z5 + 22 (unitvector(z1-z5) + unitvector(z3-z5)));

drawoptions(withcolor Purples 8 7);
draw halfcircle scaled 108 shifted z1 cutafter (z1--z4);
pair s, t; s = z1 + 58 dir 1/2 angle (z4-z1); t = s + (32, -18);
label.bot("$\alpha-\beta\over2$", t);
drawarrow t {up} .. {left} s withpen pencircle scaled 1/4;

drawoptions();
draw z1--z2--z3--cycle;
draw z1--z7--z2;

label.urt ("$a$", 1/2[z2, z3]);
label.ulft("$b$", 1/2[z3, z1]);
label.bot ("$c$", 1/2[z1, z2]);
label.lrt ("$a-b$", 1/2[z2, z7]);

label.top(btex $\displaystyle
(a-b)\cos{\gamma\over2} = c \sin\left(\alpha-\beta\over2\right)
$ etex, point 5/2 of bbox currentpicture shifted 42 up);

\end{mplibcode}
$$
\vfill
\contrib{H.\@ Arthur DeKleine}

\section{Tangent, cotangent, secant, and cosecant}

\vfill
$$
\begin{mplibcode}
numeric s, theta;
theta = 28;
s = 120;

path c, t;
c = fullcircle scaled 2s;

z0 = whatever * dir theta;
z1 = whatever * dir theta;
z2 = (x1, y0) = (xpart point 0 of c, ypart point 6 of c);

drawoptions(withcolor 3/4);
draw unitsquare scaled 6 rotated 0   shifted point 6 of c;
draw unitsquare scaled 6 rotated 90  shifted z2;
draw unitsquare scaled 6 rotated 180 shifted point 0 of c;
draw unitsquare scaled 6 rotated 270 shifted center c;

drawoptions(withcolor Blues 8 8);
draw c; 

drawoptions();
draw z0--z1--z2--cycle;
draw point 0 of c -- center c -- point 6 of c;

drawoptions(withcolor Blues 8 8);
drawdot center c withpen pencircle scaled dotlabeldiam;

drawoptions(withcolor Reds 8 7);
label("$\theta$", z0 + 24 dir 1/2 theta);
label("$\theta$", center c + 24 dir 1/2 theta);

drawoptions();
label.top("$1$", 1/2[point 0 of c, center c]);
label.lft("$1$", 1/2[point 6 of c, center c]);
label.bot("$1$", 1/2[point 6 of c, z2]);
label.rt ("$1$", 1/2[point 0 of c, z2]);
label.rt ("$\tan\theta$", 1/2[point 0 of c, z1]);
label.bot("$\cot\theta$", 1/2[point 6 of c, z0]);

picture p; 
p = thelabel.top("$\csc\theta$", origin);
unfill bbox p rotated theta shifted 1/2[z0, center c];
       draw p rotated theta shifted 1/2[z0, center c];
draw thelabel.top("$\sec\theta$", origin) rotated theta shifted 1/2[z1, center c];

label.bot(btex \vbox{\openup 8pt\halign{\hfil $#$ \hfil\cr
\tan^2\theta + 1 = \sec^2 \theta\cr
\cot^2\theta + 1 = \csc^2 \theta\cr
\left(\tan\theta + 1\right)^2 +  
\left(\cot\theta + 1\right)^2 = 
\left(\sec\theta + \csc\theta\right)^2\cr}} etex, point 1/2 of bbox currentpicture shifted 32 down);

label.bot(btex also $\displaystyle \tan\theta = {\tan\theta+1\over\cot\theta + 1}$ etex,
point 1/2 of bbox currentpicture shifted 24 down);


\end{mplibcode}
$$
\vfill
\contrib{William Romaine}

%------------------------
\section{Substitution to make a rational function of sine and cosine}
\vfill
$$
\begin{mplibcode}
numeric theta, u; theta = 60; u = 144;
z0 = u * left;
z1 = origin; 
x2 = x1; z2 = whatever * dir 1/2 theta shifted z0;
y3 = y1; z2 - z3 = whatever * (z2-z0) rotated 90;
z4 = z1 shifted (z2-z3);
z5 = z2 shifted (z2-z3);
x6 = x5; y6 = y1;
picture P[];
P0 = image(
    draw z0--z1--z2--cycle withpen pencircle scaled 1;
    label.bot("$1$", 1/2[z0, z1]);
    label.rt ("$z$", 1/2[z1, z2]);
    label("$\theta/2$", 32 dir 1/4 theta shifted z0 shifted 2 down) withcolor Reds 8 7;
);

P1 = image(
    draw unitsquare scaled 6 rotated 90 withcolor 1/2;
    draw P0;
);

P2 = image(
    draw unitsquare scaled 6 rotated 90 withcolor 1/2;
    draw z1--z3--z2; 
    draw P0;
    label.bot("$z^2$", 1/2[z1, z3]);
);

P3 = image(
    draw unitsquare scaled 6 shifted z1 withcolor 1/2;
    draw unitsquare scaled 6 shifted z4 withcolor 1/2;
    draw z5--z4--z2 -- cycle -- z0;
    draw z1--z3--z2; 
    draw P0;
    
    picture par_mark; par_mark = image(
        draw (up--down) scaled 3 rotated -10 shifted 3/4 left;
        draw (up--down) scaled 3 rotated -10 shifted 3/4 right;
    );
        
    draw par_mark rotated angle (z2-z3) shifted 1/2[z2,z3] withcolor Blues 8 7;
    draw par_mark rotated angle (z2-z3) shifted 1/2[z2,z5] withcolor Blues 8 7;

    label.ulft("$1+z^2$", 1/2[z0, z5]);
    label.bot("$z^2$", 1/2[z1, z3]);
    label.rt ("$z$", 1/2[z4, z5]);
    label("$\theta/2$", 36 dir 3/4 theta shifted z0) withcolor Reds 8 7;
);
P4 = image(
    draw unitsquare scaled 6 rotated 90 shifted z6 withcolor 1/2;
    draw z1--z2--z0--z3--z5;
    draw z0--z6--z5--cycle withpen pencircle scaled 1;
    picture t; t = thelabel("$\theta$", 18 dir 1/2 theta shifted z0);
    unfill bbox t; draw t withcolor Reds 8 7;
    label.ulft("$1+z^2$", 1/2[z0, z5]);
    label.bot("$z^2$", 1/2[z1, z3]);
    label.bot("$z^2$", 1/2[z1, z6]);
    label.bot("$1-z^2$", 1/2[z0, z6]);
    label.rt("$2z$", 1/2[z5, z6]);
);


P2 := P2 shifted (7/4u, 0);
P3 := P3 shifted (0,    -7/4u);
P4 := P4 shifted (7/4u, -7/4u);

draw P1;
draw P2;
draw P3;
draw P4;

drawoptions(withpen pencircle scaled 2 withcolor Blues 8 7);
interim linecap := butt;
interim linejoin := mitered;
interim bboxmargin := 16;
picture a; a = image(drawarrow (left--right) scaled 21);
drawoptions();

for i=1 upto 3:
    draw a rotated angle (center P[i+1] - center P[i]) shifted 1/2[center P[i], center P[i+1]];
endfor

label.bot(btex 
    $z=\tan\left(\theta/2\right) \mathbin{\:\Longrightarrow\:} 
    \hbox{$\displaystyle\sin\theta = {2z   \over 1 + z^2}$ \quad and\quad
          $\displaystyle\cos\theta = {1-z^2\over 1 + z^2}$}$ etex, 
          point 1/2 of bbox currentpicture
            shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}

%------------------------
\section{Sums of arctangents}
\vfill
$$
\begin{mplibcode}
numeric u; u = 42;
picture P[];
P1 = image(
    path t[];
    t1 = origin -- (2u, 0) -- (2u, u) -- cycle;
    t2 = origin -- (3u, -u) -- (3u, 0) -- cycle;

    fill t1 withcolor Oranges 7 1;
    fill t2 withcolor Blues 7 1;

    for x= 0 upto 3: draw (down -- up) shifted (x, 0) scaled u withcolor 3/4; endfor
    for y=-1 upto 1: draw (origin -- 3 right) shifted (0, y) scaled u withcolor 3/4; endfor

    draw fullcircle scaled 3/2 u 
        rotated angle point 1 of t2 
        cutafter subpath (2, 3) of t1 
        withcolor Reds 7 6;

    draw t1 -- subpath (0, 2) of t2 -- point 1 of t1 withpen pencircle scaled 1;
    draw point 1 of t2 -- point 2 of t1 withpen pencircle scaled 1;

    label.bot("$\displaystyle \arctan \frac12 + \arctan \frac13 = \frac\pi4$",
       point 1/2 of bbox currentpicture shifted 21 down);
);
P2 = image(
    path t[];
    t1 = origin -- (-u, 0) -- (-u, -u) -- cycle;
    t2 = origin -- (-u, 2u) -- (-u, 0) -- cycle;
    t3 = origin -- (5u, 5u) -- (-u, 2u) -- cycle;

    fill t1 withcolor Greens 7 1;
    fill t2 withcolor Oranges 7 1;
    fill t3 withcolor Blues 7 1;

    numeric y; y = -2;
    for x=-10+1, -10+1, -10+2, -10+3, -10+5, 15, 35:
        draw ((floor (x/10), incr y) -- (x - 10 * floor (x/10), y)) scaled u withcolor 3/4;
    endfor
    numeric x; x = -2;
    for y=-10+3, -10+3, -10+4, 14, 25, 35, 35:
        draw ((incr x, floor (y/10)) -- (x, y - 10 * floor (y/10))) scaled u withcolor 3/4;
    endfor

    draw halfcircle scaled 3/2 u 
        rotated angle point 1 of t3
        withcolor Reds 7 6;

    draw                  t1 withpen pencircle scaled 1;
    draw subpath (0,2) of t2 withpen pencircle scaled 1;
    draw subpath (0,2) of t3 withpen pencircle scaled 1;

    label.bot("$\displaystyle \arctan 1 + \arctan 2 + \arctan 3 = \pi$",
       point 1/2 of bbox currentpicture shifted 21 down);

);
draw P1;
label.bot(P2, point 1/2 of bbox currentpicture shifted 42 down);


\end{mplibcode}
$$
\vfill
\contrib{Edward M.\@ Harris}

%------------------------
\section{The distance between a point and a line}
\vfill
$$
\begin{mplibcode}
numeric u; u = 33;
path xx, yy;
xx = (left -- 8 right) scaled u;
yy = (down -- 11 up) scaled u;

numeric a, b, c, m;
m = 1.9;
c = -2;
a = 6.5;
b = 1;

z0 = (0, c) scaled u;
z1 = (a, a*m + c) scaled u;
z2 = (a, b) scaled u;
z3 = whatever[z0, z1]; z2 - z3 = whatever * (z0 - z1) rotated 90;

path p, t;
p = ((1/2, 1/2 m + c) -- (a+1/2, (a+1/2)*m + c)) scaled u;
t = (origin -- right -- (1, m)) scaled u shifted 1/4[z3, z1];

drawoptions(withpen pencircle scaled 1/4 withcolor Reds 8 6);
draw subpath (1,3) of unitsquare scaled 6 rotated 90 shifted point 1 of t; 
draw subpath (1,3) of unitsquare scaled 6 rotated angle (z2-z3) shifted z3;

drawoptions(withcolor Reds 8 8);
draw t;
draw z1 -- z2 -- z3;
draw p;
draw thelabel.top("$y=mx + c$", origin) rotated angle (z1-z0) shifted point 1/5 of p;

drawoptions(withcolor Blues 8 7);
label.llft("$d$", 1/2[z2, z3]);
label.rt("$\big| ma + c - b \big|$", 1/2[z2, z1]);
label.bot("$1$", point 1/2 of t);
label.rt("$m$", point 3/2 of t);
label.ulft("$\sqrt{1+m^2}$", 1/2[point 0 of t, point 2 of t]);

drawoptions();
dotlabel.lrt ("$(a, ma+c)$", z1);
dotlabel.lrt ("$(a, b)$", z2);

drawarrow xx;
drawarrow yy;
label.rt  ("$x$", point 1 of xx);
label.top ("$y$", point 1 of yy);

label.bot(btex $\displaystyle
    {d\over1} = {\left|ma+c-b\right|\over \sqrt{1+m^2}}$ etex, point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{R.\@ L.\@ Eisenman}

%------------------------
\section{The midpoint rule is better than the trapezoidal rule for concave
functions}
\vfill
$$
\begin{mplibcode}
numeric u; u = 12;
pair a, b, fa, fb, ta, tb, m, fm, am, bm;
xpart a = xpart fa = xpart ta = -4u = -xpart b = - xpart fb = - xpart tb;
ypart a = ypart b = 0;
ypart ta = 8u;
ypart tb = 11u;
ypart fa = 2u;
ypart fb = 9u;
m = 1/2[a, b];
fm = 1/2[ta, tb];
xpart am = xpart bm = xpart m;
am - fa = whatever * (tb - ta);
bm - fb = whatever * (tb - ta);

path base, lid, curve;
base = fa -- a -- b -- fb;
lid = fa -- ta -- tb -- fb;
curve = fa {dir 85} .. fm {tb-ta} .. fb;

picture P[];
P1 = image(
fill lid & reverse curve & cycle withcolor Blues 8 2;
draw base;
draw lid;
draw curve withcolor Blues 8 7;
label.bot("$a$", a);
label.bot("$b$", b);
dotlabel.bot("$a+b \over2$", m);
dotlabel.ulft("$f\left(a+b \over2\right)$", fm);
);
P2 = image(
    fill lid -- fm -- cycle withcolor Blues 8 2;
    draw base;
    draw lid;
    draw curve withcolor Blues 8 7;
    draw fa -- fm -- fb;
    draw fa -- am -- fm dashed evenly scaled 1/2;
    draw bm -- fb       dashed evenly scaled 1/2;
    drawdot m withpen pencircle scaled dotlabeldiam;
);
P3 = image(
    fill fa -- am -- bm -- fb -- fm -- cycle withcolor Blues 8 2;
    draw base;
    draw lid;
    draw curve withcolor Blues 8 7;
    draw fa -- fm -- fb;
    draw fa -- am -- fm; 
    draw bm -- fb;
    draw fa -- fb dashed evenly scaled 1/2;
    drawdot m withpen pencircle scaled dotlabeldiam;
);
P4 = image(
    fill fa -- fm -- fb -- cycle withcolor Blues 8 2;
    draw fa -- fm -- fb -- cycle;
    draw base;
    draw lid;
    draw curve withcolor Blues 8 7;
    drawdot m withpen pencircle scaled dotlabeldiam;
);
P5 = image(
    fill curve -- cycle withcolor Blues 8 2;
    draw curve -- cycle;
    draw base;
    drawdot m withpen pencircle scaled dotlabeldiam;
);

draw P1;
draw P2 shifted (12u, 0);
draw P3 shifted (24u, 0);
draw P4 shifted (6u, -16u);
draw P5 shifted (18u, -16u);

label("$\le$", (6u, 6u));
label("$=$", (18u, 6u));
label("$=$", (0u, -10u));
label("$\le$", (12u, -10u));

\end{mplibcode}
$$
\vfill
\contrib{Frank Burk}

%------------------------
\section{Integration by parts}
\vfill
$$
\begin{mplibcode}
path ff, xx, yy;
pair p, q, r, s;

ff = (80, 55) {dir 10} .. (333, 233);
xx = 5 left -- 13 right + (xpart point 1 of ff, 0);
yy = 5 down -- 13 up    + (0, ypart point 1 of ff);

numeric t; t = 1/12;

p = (xpart point t of ff, 0);
q = (xpart point 1-t of ff, 0);
r = (0, ypart point t of ff);
s = (0, ypart point 1-t of ff);

fill p -- subpath(t, 1-t) of ff -- q -- cycle withcolor Oranges 7 1;
fill r -- subpath(t, 1-t) of ff -- s -- cycle withcolor Blues 7 1;

drawoptions(withpen pencircle scaled 1/4);
draw p -- point t   of ff -- r;
draw q -- point 1-t of ff -- s;

drawarrow xx;
drawarrow yy;
draw ff withpen pencircle scaled 3/4 withcolor Reds 8 7;

label.rt("$\left\{\:\vcenter{\halign{$#$\hfil\cr u=f(x)\cr v=g(x)\cr}}\right.$", point 1 of ff);

label.rt ("$u$", point 1 of xx);
label.top("$v$", point 1 of yy);

interim labeloffset := 8;
dotlabel.lft("$s=g(b)$", s); 
dotlabel.lft("$r=g(a)$", r);
dotlabel.bot("$p=f(a)$", p); 
dotlabel.bot("$q=f(b)$", q);
dotlabel.llft("$0$", origin);

% Blues[7][1] = 1/256 (239,243,255); 0.93359375 0.94921875 0.99609375 
% Oranges 7 1 = 1/256 (254,237,222)  0.9921875 0.92578125 0.8671875   
% 
label.bot(btex \vbox{\openup 16pt\halign{\hfil $\displaystyle # $\hfil\cr
\hbox{Area\ \pdfliteral{0.93359375 0.94921875 0.99609375 rg}\vrule height 5mm width 8mm depth 2mm\pdfliteral{0 g}} + 
\hbox{Area\ \pdfliteral{0.9921875 0.92578125 0.8671875 rg}\vrule height 5mm width 8mm depth 2mm\pdfliteral{0 g}} = qs - pr\cr
\int_r^s u\:dv + \int_p^q v\: du = uv \: \bigg|_{(p, r)}^{(q, s)}\cr
\int_a^b f(x) g'(x) \:dx = f(x) g(x) \: \Big|_a^b - \int_a^b g(x) f'(x) \: dx\cr
}} etex, point 1/2 of bbox currentpicture shifted 21 down);

\end{mplibcode}
$$
\vfill
\contrib{Richard Courant}

%------------------------
\section{The graphs of $f$ and $f^{-1}$ are reflections about the line $y=x$}
\vfill
$$
\begin{mplibcode}
z0 = -8(1,1);
z1 = 221(1,1);
path xx, yy, xy;
xx = (x0, 0) -- (x1, 0);
yy = xx rotated 90;
xy = z0 -- z1;


picture P[];
P0 = image(
drawarrow xx;
drawarrow yy;
draw xy withcolor Blues 8 7;
draw thelabel.ulft("$y=x$", origin) rotated 45 shifted point 1 of xy withcolor Blues 8 7;
);

P1 = image(
    path ff, ff';
    ff = point 1/3 of xx shifted 12 up .. {dir 5} point 1 of xx shifted 72 up;
    ff' = ff reflectedabout(z0, z1);
    numeric a, b; (a, b) = point 3/8 of ff;
    drawoptions(dashed evenly withpen pencircle scaled 1/4);
    draw (a,b) -- (b, a) -- (b, b) -- (a,b) -- (a, a) -- (b, a);
    drawoptions(withcolor Reds 8 7);
    draw ff; label.rt("$y=f(x)$", point 1 of ff);
    draw ff' dashed withdots scaled 1/4; 
    label.top("$x=f(y)$, $y=f^{-1}(x)$", point 1 of ff');
    drawoptions();
    draw P0;

    dotlabel.lrt("$(a, b)$", (a, b));
    dotlabel.lrt("$(a, a)$", (a, a));
    dotlabel.lrt("$(b, b)$", (b, b));
    dotlabel.ulft("$(b, a)$", (b, a));
    label.top("$a-b$", 1/2(a+b, 2a));
);
P2 = image(
    path ff, ff';
    ff = point 7/8 of yy shifted 21 right .. {dir -8} point 1 of xx shifted 55 up;
    ff' = ff reflectedabout(z0, z1);
    numeric a, b; (a, b) = point 1/4 of ff;
    drawoptions(dashed evenly withpen pencircle scaled 1/4);
    draw (a,b) -- (b, b) -- (b, a) -- (a, a) -- cycle;
    drawoptions(withcolor Reds 8 7);
    draw ff; label.rt("$y=f(x)$", point 1 of ff);
    draw ff' dashed withdots scaled 1/4; 
    label.top("$x=f(y)$, $y=f^{-1}(x)$", point 1 of ff');
    drawoptions();
    draw P0;
    dotlabel.llft("$(a, b)$", (a, b));
    dotlabel.lrt("$(a, a)$", (a, a));
    dotlabel.lrt("$(b, b)$", (b, b));
    dotlabel.llft("$(b, a)$", (b, a));
);

label.top(P1, origin);
label.bot(P2, 21 down);

\end{mplibcode}
$$
\vfill
\contrib{Ayoub B.\@ Ayoub}

%------------------------
\section{The reflection property of the parabola}
\vfill
$$
\begin{mplibcode}
path para, dirx, xx, yy;
numeric p, s, t, u;

p = 3/2;
s = 1/8;
u = 28;
t = 12/s;
para = ((49/4/p, -7) for y=s - 7 step s until 7: -- (y * y / 4p, y) endfor) scaled u;
dirx = (-p*u, ypart point 0 of para) -- (-p*u, ypart point infinity of para);
xx = (-u-p*u, 0) -- (p*u + xpart point 0 of para, 0);
yy = dirx shifted (p*u, 0);

z0 = p * u * right;
z1 = point t of para;
z2 = z0 reflectedabout(z1, direction t of para rotated 90 shifted z1);
z3 = z2 rotatedabout(z1, 180);
z4 = 1/2[z0, z3];
z5 = z4 rotatedabout(z1, 180);

drawoptions(withcolor 3/4);
draw subpath (3.8, 4.2 + 1/45 angle (z1-z0)) of fullcircle scaled 2 abs(z0-z1) shifted z1;
draw subpath (1, 3) of unitsquare scaled 6 rotated angle (z0-z3) shifted z4;

drawoptions(withcolor Blues 8 4);
draw z4--z5;
draw z3--z1;
draw 1.2[z0,z3] -- 1.6[z3,z0];
drawoptions(withcolor Blues 8 6);
draw thelabel.ulft("$m_1 = y' = 2p/y$", origin) rotated angle (z5-z4) shifted z5;
draw thelabel.ulft("$m_2 = -y/2p$", origin) rotated angle (z0-z3) shifted 1.5[z3, z0];
numeric a; a = 1/2 angle (z1-z0);
label("$\alpha$", z1 + 36 dir (180 + 3/2 a));
label("$\beta$",  z1 + 36 dir (180 + 1/2 a));
label("$\gamma$", z1 + 36 dir (1/2 a));

drawoptions(withcolor Reds 8 7);
draw para; draw dirx;
draw thelabel.top("$y^2=4px$", origin) rotated (180 + angle direction 8 of para) shifted point 8 of para;
draw thelabel.top("$x=-p$", origin) rotated 90 shifted point 1/8 of dirx;


drawoptions();
drawarrow z0 -- z1 -- z2;
drawarrow xx;
drawarrow yy;

dotlabel.llft("$F(p,0)$", z0);
dotlabel.lrt("$Q(x,y)$", z1);
dotlabel.lft("$D(-p,y)$", z3);
label.bot("\mathsurround 6pt $QF=QD$ and $m_1\cdot m_2 = -1,$ therefore $\alpha=\beta=\gamma$", 
    point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Ayoub B.\@ Ayoub}

%------------------------
\section{Area under an arch of the cycloid}
\vfill
$$
\begin{mplibcode}
input arrow_label
picture P[];
path c, cycloid, base; 
numeric pi, r, s; r = 42; s = 1; pi = 3.141592653589793;
c = fullcircle scaled 2r rotated 90;  
cycloid = point 0 of c rotated -180 shifted (-pi * r, 0) for t = s-180 step s until 180: 
    -- point 0 of c rotated -t shifted (t / 180 * pi * r, 0) endfor;
base = point 0 of cycloid shifted 21 left -- point infinity of cycloid shifted 21 right;
P0 = image(
    draw center c -- c withcolor Blues 8 7;
    label.rt("$r$", 1/2 point 0 of c);
);
P1 = image(
    draw P0;
    draw cycloid withcolor Blues 8 7;
    draw base;
    drawarrow subpath (7, 5.8) of c scaled 1.1 withcolor Reds 8 7;
    dotlabel.top("$P$", point 0 of c);
    arrow_label(point 0 of cycloid, point infinity of cycloid, "$2\pi r$", 10);
);
P2 = image(
    draw P0;
    label("$+$", (-3/2r, 0));
    label("$+$", (+3/2r, 0));
    path cusp, psuc;
    cusp = (subpath (0, 1/2 length cycloid) of cycloid & subpath (0, 4) of c -- cycle) shifted (-2r, 0);
    psuc = cusp reflectedabout(up, down);
    numeric n, k; n = 13; k = 1;
    for $=cusp, psuc:
        draw point 0 of $ -- (xpart point 0 of $, ypart point 180 of $) dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
        for i=1 upto n:
            hide(numeric t, u, v, w; path b, a;
               b = subpath(0, -1) of $ shifted (0, 2r/n * i - r/n);
               (t, u) = b intersectiontimes subpath (0, 180) of $;
               (v, w) = b intersectiontimes subpath (180, 184) of $;
               a = unitsquare shifted 1/2 down yscaled (2r/n)
                xscaled xpart (point v of b - point t of b)
                shifted point u of $;)
            fill a withcolor Oranges 8 1; draw a withcolor Oranges 8 3;
            if i+2 = n:
                drawarrow 1/3[point u of $, (xpart point 0 of $, ypart point u of $)] 
                    -- 7/8[point u of $, (xpart point 0 of $, ypart point u of $)] withcolor Oranges 8 5;
            fi
        endfor
        draw $ withcolor Blues 8 7;
        arrow_label(point 180 of $, point -1 of $, "$2r$", 0);
        arrow_label(point 0 of $, point -1 of $, "$\pi r$", if odd incr k: - fi 8);
    endfor
);
P3 = image(
    draw P0;
    label("$+$", (-3/2r, 0));
    label("$+$", (+3/2r, 0));
    path cusp, psuc;
    cusp = (subpath (0, 1/2 length cycloid) of cycloid & subpath (0, 4) of c -- cycle) shifted (-2r, 0);
    psuc = cusp reflectedabout(up, down);
    numeric n, k; n = 13; k = 1;
    for $=cusp, psuc:
        path a[];
        for i=1 upto n:
            hide(numeric t, u, v, w; path b, a;
               b = subpath(0, -1) of $ shifted (0, 2r/n * i - r/n);
               (t, u) = b intersectiontimes subpath (0, 180) of $;
               (v, w) = b intersectiontimes subpath (180, 184) of $;
               a[i] = unitsquare shifted 1/2 down yscaled (2r/n)
                xscaled xpart (point v of b - point t of b)
                shifted (xpart point 0 of $, ypart point u of $);)
                fill a[i] withcolor Oranges 8 1; draw a[i] withcolor Oranges 8 3;
        endfor
        draw point 0 of $ -- point 3 of a[n]
            for i=n downto 1: .. point 3/2 of a[i] endfor .. point -1 of $ -- cycle
            withcolor Blues 8 7;
        draw point -1 of $ -- (xpart point 0 of $, ypart point 180 of $) dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
        arrow_label(point 180 of $, point -1 of $, "$2r$", 0);
        arrow_label(point 0 of $, point -1 of $, "$\pi r$", if odd incr k: - fi 8);
    endfor
);
draw P1;
draw P2 shifted (0, -3r);
draw P3 shifted (0, -6r);

label.bot(btex \vbox{\openup 12pt\halign{\hfil $\displaystyle #$\hfil\cr
\frac12\pi r \cdot 2r \qquad\qquad + \qquad\qquad \pi r^2 \qquad\qquad + \qquad\qquad \frac12\pi r \cdot 2r \cr
\hbox to 0pt{\hss\small therefore\qquad}A = 3\pi r^2\cr}} etex, point 1/2 of bbox currentpicture shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{Richard M.\@ Beekman}

%===========================
\chapter{Inequalities}

\minitoc

\section{The arithmetic mean -- geometric mean inequality I}

\vfill
$$
\begin{mplibcode}
input arrow_label
path h; h = halfcircle scaled .95 \mpdim{\textwidth};
z0 = point 2.7818 of h;
x1 = x0; y1 = 0;

draw point 4 of h -- z0 -- point 0 of h dashed evenly withpen pencircle scaled 1/4;
drawoptions(withcolor Blues 8 7);
draw h -- cycle; draw origin -- point 2 of h; draw z0--z1;

drawoptions();
arrow_label(point 4 of h, z1, "$a$", 8);
arrow_label(z1, point 0 of h, "$b$", 8);

label.rt("$\sqrt{ab}$", 1/2[z0, z1]);
label.rt("${a+b\over2}$", 3/4 point 2 of h);
label.bot("$\displaystyle \sqrt{ab} \le {a+b\over2}$", point 1/2 of bbox currentpicture shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{Charles D.\@ Gallant}


\section{The arithmetic mean -- geometric mean inequality II}
\vfill
$$
\begin{mplibcode}
numeric a, b; a + b = 7/8 \mpdim{\textwidth}; a = 610/377 b;
path r, s; 
s = unitsquare shifted -(1/2, 1/2) scaled (a-b);
r = unitsquare xscaled a yscaled -b shifted point 0 of s;
fill s withcolor Blues 8 2;
for t=0 upto 3: fill r rotated 90t withcolor Oranges 8 1; draw r rotated 90t; endfor
label.bot("$a$", point 5/2 of r);
label.lft("$a$", point 5/2 of r rotated 270);
label.lft("$b$", point 3/2 of r rotated 180);
label.bot("$b$", point 3/2 of r rotated 270);

label.bot("$(a+b)^2 - (a-b)^2 = 4ab$", point 1/2 of bbox currentpicture shifted 21 down);
label.bot("$\displaystyle {a+b\over 2} \ge \sqrt{ab}$", point 1/2 of bbox currentpicture shifted 13 down);

\end{mplibcode}
$$
\vfill
\contrib{Doris Schattschneider}
%---------------------
\section{The arithmetic mean -- geometric mean inequality III}
\vfill
$$
\begin{mplibcode}
input arrow_label
numeric a, b;
5b = 3a; a + b = \mpdim{\textwidth};
z1 = (1/2(a+b) +-+ 1/2(a-b), 1/2(b-a));
path C, c; C = fullcircle scaled a rotated 90; c = fullcircle scaled b rotated 90 shifted z1;
drawoptions(withcolor Blues 8 5);
draw origin -- z1; 
draw (0, y1) -- z1 dashed evenly scaled 1/2;
draw C -- point 4 of C -- point 4 of c -- c;
drawoptions();
arrow_label(point 0 of C, point 4 of C, "$a$", 10);
arrow_label(point 0 of c, point 4 of c, "$b$", -10);
arrow_label(point 4 of C, point 4 of c, "$\sqrt{ab}$", 10);
path aa; aa = (center C -- center c) shifted 12 up;
drawdblarrow aa; 
picture t; t = thelabel("$a+b\over2$", point 1/2 of aa); unfill bbox t shifted 2.7 up; draw t;

arrow_label((0, y1), center C, "$a-b\over2$", 12);

label.top("$\displaystyle {a+b\over 2} \ge \sqrt{ab}$, \quad with equality iff $a=b$", point 5 /2 of bbox currentpicture shifted 42 up);
\end{mplibcode}
$$
\vfill
\contrib{Roland H.\@ Eddy}


%---------------------
\section{Two extremum problems}
\vfill
$$
\begin{mplibcode}
path pp, xx, yy; 
numeric P, s, minx, maxx;
s = 2;
P = 1440;
minx = 4s; maxx = P/minx; 
pp = (minx, P/minx) for x=minx+s step s until maxx+eps: -- (x, P/x) endfor; 
xx = (-minx, 0) -- (maxx+minx, 0);
yy = xx rotated 90;

z0 = point 0.666 length pp of pp;
z1 = (x0+y0, 0);
z2 = (0, x0+y0);

z3 = (2 sqrt(P), 0);
z4 = (0, 2 sqrt(P));

picture T[]; 

T0 = image(
    undraw (left--right) scaled 1/2 \mpdim{\textwidth} shifted point 1/2 of xx;
    draw origin -- point 1 of xx rotated 45 dashed evenly withpen pencircle scaled 1/4;
    drawarrow xx; drawarrow yy;
    draw z1 -- z2 withcolor Blues 8 7;
    label.rt("$x$", point 1 of xx);
    label.top("$y$", point 1 of yy);
    dotlabel.urt("$\bigl(x, y\bigr)$", z0);
    dotlabel.bot("$\bigl(S, 0\bigr)$", z1); 
    dotlabel.lft("$\bigl(0, S\bigr)$", z2); 
);
T1 = image(
    draw T0;

    drawoptions(withcolor Reds 8 7); 
    draw pp; 
    label.rt("$xy=P$", point 1/2 of pp);

    draw z3 -- z4 withcolor Blues 8 7;

    drawoptions();
    dotlabel.urt("$\bigl(\sqrt{\scriptstyle P}, \sqrt{\scriptstyle P}\bigr)$", 1/2(x3, y4));
    dotlabel.bot("$\bigl(2\sqrt{\scriptstyle P}, 0\bigr)$", z3); 
    dotlabel.lft("$\bigl(0, 2\sqrt{\scriptstyle P}\bigr)$", z4); 

    label.top(btex \vbox{\hsize 3.7in\centering
    For a given product, the sum of two positive numbers is minimal when the numbers are
        equal.} etex, point 5 /2 of bbox currentpicture shifted 13 up);
);
T2 = image(
    fill unitsquare xscaled x0 yscaled y0 withcolor Reds 7 1;
    fill unitsquare scaled 1/2(x0+y0) withcolor Blues 7 1;
    fill unitsquare xscaled 1/2(x0+y0) yscaled y0 withcolor 1/2[Reds 7 1, Blues 7 1];
    draw unitsquare xscaled x0 yscaled y0 withpen pencircle scaled 1/4;
    draw unitsquare scaled 1/2(x0+y0)     withpen pencircle scaled 1/4;

    picture eq; eq = image(
        for t=-1/2, 1/2:
            draw (up--down) rotated -5 scaled 2 shifted (t, 0) withpen pencircle scaled 1/4;
        endfor
    );
    draw eq shifted (1/4x0+3/4y0, y0);
    draw eq shifted (3/4x0+1/4y0, y0);
    draw eq rotated 90 shifted (1/2x0+1/2y0, 1/4x0+3/4y0);

    draw T0;
    dotlabel.urt("${1\over2}\bigl(S, S\bigr)$", 1/2[z1,z2]);
    label.top(btex \vbox{\hsize 3.7in\centering
    For a given sum, the product of two positive numbers is maximal when the numbers are
        equal.} etex, point 5 /2 of bbox currentpicture shifted 13 up);
);

label.top(T1, 9 up);
label.bot(T2, 9 down);
\end{mplibcode}
$$
\vfill
\contrib{Paulo Montuchi and Warren Page}

%---------------------
\section{The HM–GM–AM–QM inequalities I}
\vfill
$$
\begin{mplibcode}
    pair A, G, H, M, P, Q, R;
    path c;
    c = fullcircle scaled 233;
    A = center c;
    P = point 4 of c;
    Q = point 6 of c;
    R = point 8 of c;
    M - P = (tw-18, 0);
    G = c intersectionpoint halfcircle scaled abs(M-A) shifted 1/2[A, M];
    H = (xpart G, ypart A);

    drawoptions(withcolor 3/4);
    draw unitsquare scaled 8 shifted H;
    draw unitsquare scaled 8 rotated angle (Q-A) shifted A;
    draw unitsquare scaled 8 rotated angle (A-G) shifted G;

    drawoptions(withcolor Blues 8 4);
    draw c;
    drawoptions(withcolor Blues 8 8);
    draw P -- (M -- G -- A -- Q -- cycle);
    draw G -- H;

    drawoptions(withcolor Reds 8 7);
    label.ulft("$a-b\over2$", 1/2[A, G]);
    label.lft("$a-b\over2$", 1/2[A, Q]);
    label.bot("$a+b\over2$",                           1/2[A, M]);
    label.top("$2ab\over a+b$",                        1/2[H, M]);
    label.urt("$\scriptstyle\sqrt{ab}$",                           1/2[G, M]);
    label.lrt("$\scriptstyle\sqrt{\frac12\left(a^2+b^2\right)}$",  1/2[Q, M]);

    drawoptions();
    dotlabel.llft("$A$", A);
    dotlabel.llft("$R$", R);
    dotlabel.lft("$P$", P);
    dotlabel.rt("$M$", M);
    dotlabel.bot("$Q$", Q);
    dotlabel.bot("$H$", H);
    dotlabel.urt("$G$", G);

    label.bot("$PM=a$, \quad $RM=b$, \quad $a>b>0$", point 1/2 of bbox currentpicture shifted 21 down); 
    label.bot("$HM < GM < AM < QM$", point 1/2 of bbox currentpicture shifted 13 down); 
    label.bot("${2ab\over a+b} < \sqrt{ab} < {a+b\over 2} < \sqrt{\frac12\left(a^2+b^2\right)}$", point 1/2 of bbox currentpicture shifted 13 down); 

\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}


%---------------------
\section{The HM–GM–AM–QM inequalities II}
\vfill
$$
\begin{mplibcode}
pair A, B, C, D, E, F, G;
D = origin; A = - C;
C - A = (tw, 0);
B = 7/26[A, C];
E = (B -- B shifted 400 up) intersectionpoint halfcircle scaled abs(A-C);
F - B = whatever * (E - D);
F - E = whatever * (E - D) rotated 90;
G = E - B rotated angle (E - F);


draw halfcircle scaled abs (A-C) withcolor 7/8;

drawoptions(withcolor Blues 8 4);
draw subpath (1, 3) of unitsquare scaled 8 rotated angle (D-B) shifted B;
draw subpath (1, 3) of unitsquare scaled 8 rotated angle (B-F) shifted F;
draw subpath (1, 3) of unitsquare scaled 8 rotated angle (D-E) shifted E;

drawoptions(withcolor Blues 8 7);
draw A -- C;
draw B -- E -- D;
draw B -- F -- G -- D;

drawoptions(withcolor Reds 8 7);

label.llft("$2ab\over a+b$",         1/2[B, F]);
label.lft("$\scriptstyle\sqrt{ab}$", 1/2[B, E]);
label.urt("$a+b\over2$",             1/2[D, E]);
label.rt ("$\scriptstyle\sqrt{\frac12\left(a^2+b^2\right)}$", 1/2[D, G]);

drawoptions();
input arrow_label;
arrow_label(A, B, "$a$", 10);
arrow_label(B, C, "$b$", 10);
label.top("$b-a\over2$", 1/2[B, D]);
label.top("$b-a\over2$", 1/2[E, G]);
interim labeloffset := 6;
dotlabel.urt("$A$", A);
dotlabel.ulft("$B$\enspace", B);
dotlabel.ulft("$C$", C);
dotlabel.urt("$D$", D);
dotlabel.ulft("$E$", E);
dotlabel.ulft("$F$", F);
dotlabel.ulft("$G$", G);

label.bot("$AB=a$,\quad $BC=b$,\quad $AD=DC={a+b\over2}$", point 1/2 of bbox currentpicture shifted 42 down);
label.bot("$BE \perp AB$,\quad $DE=AD$", point 1/2 of bbox currentpicture shifted 13 down);
label.bot("$FE \perp ED$,\quad $FB \parallel ED$, \quad $EG=BD={b-a\over2}$", point 1/2 of bbox currentpicture shifted 13 down);
\end{mplibcode}
$$
\vfill
\contrib{Sidney H.\@ Kung}



%---------------------
\section{The HM–GM–AM–QM inequalities III}
\vfill
$$
\begin{mplibcode}
numeric u; u = 20; 
path U, A, B;
U = unitsquare shifted -(1/2, 1/2) scaled u;
A = unitsquare scaled 3u shifted point 2 of U rotated 90;
B = unitsquare scaled 4u shifted point 0 of U;

picture P[];
P1 = image(
    for t=0, 180:
        fill A rotated t withcolor Greens 8 1;
        fill B rotated t withcolor Reds 8 1;
    endfor
    fill U withcolor Reds 8 2;
    for t=0, 180:
        draw subpath (1, 3) of A rotated t;
        draw B rotated t;
    endfor

    label.top("$a$", point 3/2 of A);
    label.lft("$a$", point 5/2 of A);
    label.top("$b$", point 5/2 of B);
    label.lft("$b$", point 3/2 of B rotated 180);

    label.rt(btex \vbox{\openup 12pt\halign{\hbox to 64pt{\hfil$#$}&${}\ge #$\hfil\cr
    2a^2 + 2b^2&\left(a+b\right)^2\cr
    \sqrt{{1\over2}\left(a^2+b^2\right)} & \displaystyle {a+b\over2}\cr}} etex,
    point 3/2 of bbox currentpicture shifted 34 right);
);

P2 = image(
    path T; T = point 0 of U -- point 1 of A -- point 1 of A rotated 90 -- cycle;
    for t=0, 90, 180, 270:
        fill T rotated t withcolor Blues 8 2;
    endfor
    for t=0, 90, 180, 270:
        draw T rotated t;
    endfor
    draw U scaled 7;

    label.top("$\sqrt a$", point 3/2 of A);
    label.lft("$\sqrt a$", point 3/2 of A rotated 90);
    label.top("$\sqrt b$", point 5/2 of B);
    label.lft("$\sqrt b$", point 5/2 of B rotated 90);
    draw thelabel.top("$\sqrt{a+b}$", origin) 
        rotated angle (point 1 of T - point 2 of T)
        shifted 1/2[point 1 of T, point 2 of T];

    label.rt(btex \vbox{\openup 12pt\halign{\hbox to 64pt{\hfil$#$}&${}\ge #$\hfil\cr
    \left(\sqrt{a + b}\right)^2&4\cdot\frac12\sqrt{\vphantom{b}a}\sqrt{b}\cr
    \displaystyle{{a+b}\over2} & \sqrt{ab}
    \cr}} etex,
    point 3/2 of bbox currentpicture shifted 34 right);

);
P3 = image(
    path R; R = point 0 of U -- point 1 of A -- point 2 of A -- point 1 of A rotated 90 -- cycle;
    for t=0, 90, 180, 270:
        fill R rotated t withcolor Oranges 8 2;
    endfor
    for t=0, 90, 180, 270:
        draw R rotated t;
    endfor

    label.top("$a\over a+b$", point 3/2 of A);
    label.lft("$a\over a+b$", point 3/2 of A rotated 90);
    label.top("$b\over a+b$", point 5/2 of B);
    label.lft("$b\over a+b$", point 5/2 of B rotated 90);

    label.rt(btex \vbox{\openup 12pt\halign{\hbox to 64pt{\hfil$#$}&${}\ge #$\hfil\cr
    1 & \displaystyle 4\cdot{a\over a+b} \cdot {b\over a+b}\cr
    \sqrt{ab} & \displaystyle{2ab\over a+b} \cr}} etex,
    point 3/2 of bbox currentpicture shifted 34 right);

);

draw P1;
draw P2 shifted (9u * down);
draw P3 shifted (18u * down);

\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}


%---------------------
\section{Five means — and their means}
\vfill
$$
\begin{mplibcode}
numeric a, b; a + b + 32 = tw; b = 4a;
path xx, yy, qq, ss, pp, arc, harc, hpp;
xx = 10 left -- (a+b+20) * right;
qq = xx rotated 45;
yy = xx rotated 90;
ss = (-10, a + b + 10) -- (a + b + 10, -10);
arc = quartercircle scaled 2 (a++b);
harc = subpath(1/2,3/2) of quartercircle scaled (sqrt(2) * (a+b)); 

numeric s, ix; ix = sqrt(a*b); s = 4;
pp = (ix, ix) for x=s+ix step s until a+b-8: -- (x, b/x*a) endfor;
pp := reverse pp reflectedabout(origin, point 1 of qq) & pp; 
numeric s, ix; ix = 1/2 sqrt(2) * (a++b); s = 4;
hpp = (ix, ix) for x=s+ix step s until b+eps: -- (x, a / 2x * a + b / 2x * b) endfor;
hpp := reverse hpp reflectedabout(origin, point 1 of qq) & hpp; 

drawoptions(withcolor Greens 8 7);
draw pp;
draw hpp;
label.rt("$xy=ab$", point infinity of pp);
label.rt("$2xy=a^2+b^2$", point infinity of hpp);

drawoptions(withcolor Oranges 8 7);
draw arc;
draw harc;
begingroup;
    interim ahlength := 2;
    z0 = 1/3[point infinity of pp, point infinity of hpp];
    z1 = 2/3[point infinity of pp, point infinity of hpp];
    path aa, bb;
    aa = (z0 -- point 0 of harc) cutafter fullcircle scaled 4 shifted point 0 of harc;
    numeric t, u;
    (t, u) = arc intersectiontimes (aa shifted (z1-z0));
    bb = (z1 -- point t of arc) cutafter fullcircle scaled 4 shifted point t of arc;

    drawarrow aa withpen pencircle scaled 1/4 withcolor Oranges 8 5;
    drawarrow bb withpen pencircle scaled 1/4 withcolor Oranges 8 5;

    label.rt("$2\left(x^2 + y^2\right) = \left(a+b\right)^2$", z0);
    label.rt("$x^2 + y^2 = a^2 + b^2$", z1);

endgroup;

drawoptions(withcolor Blues 8 7);
draw qq; draw thelabel.top("$x=y$", origin) rotated 45 shifted point 0.9 of qq;
draw ss; draw thelabel.top("$x+y=a+b$", origin) rotated -45 shifted point 0.9 of ss;

def connect(expr p, q, P, Q) = 
    draw (p, 0) -- (p, q) -- (0, q);
    label.bot("\strut" & P, (p, 0));
    label.lft("\strut" & Q, (0, q));
    fill fullcircle scaled dotlabeldiam shifted (p, q);
enddef;

drawoptions(dashed evenly scaled 1/2 withpen pencircle scaled 1/4);
connect(a, b, "$a$", "$b$");
connect(b, a, "$b$", "$a$");
numeric am, gm, rms, hm, ch;
am = 1/2(a+b);
gm = sqrt(a*b);
rms = 1/2 sqrt 2 * (a++b);
hm = 2a / (a+b) * b;
ch = a / (a+b) * a + b / (a+b) * b;

draw (hm, ch) -- (am, ch) -- (am, am) -- (ch, am) -- (ch, hm);
draw (gm, rms) -- (gm, gm) -- (rms, gm);

connect(am, am, "$am$", "$am$");
connect(gm, gm, "$gm$", "$gm$");
connect(rms, rms, "$rms$", "$rms$");
connect(ch, hm, "$cm$", "$hm$");
connect(hm, ch, "$hm$", "$cm$");

picture opendot; opendot = image(
fill fullcircle scaled dotlabeldiam; 
unfill fullcircle scaled 1/2 dotlabeldiam;
);

draw opendot shifted (hm, am);
draw opendot shifted (gm, rms);
draw opendot shifted (am, ch);
draw opendot shifted (am, hm);
draw opendot shifted (rms, gm);
draw opendot shifted (ch, am);

drawoptions();
drawarrow xx; label.rt("$x$", point 1 of xx);
drawarrow yy; label.top("$y$", point 1 of yy);

\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}

%---------------
\section{$e^\pi > \pi^e$}

\vfill
$$
\begin{mplibcode}
path ff, xx, yy;
def f(expr x) = 1/256 mlog(x) / x enddef;
numeric minx, maxx, s, u, v;
minx = 13/8; s = 1/16; maxx = 19/4;
u = 89;
v = 3328-256;
ff = ((minx, f(minx)) for x=minx+s step s until maxx: .. (x, f(x)) endfor) xscaled u yscaled v;
xx = (point 0 of ff -- (xpart point infinity of ff, ypart point 0 of ff)) shifted 30 down;
yy = (point 0 of ff -- point 0 of ff shifted (0, 0.1v)) shifted 30 left;

numeric pi, e, fpi, fe;
pi = 3.141592653589793 u;
e = 2.718281828459045 u;
fpi = f(3.141592653589793) * v;
fe = f(2.718281828459045) * v;

path ee, pp;

ee = (e, ypart point 0 of xx) -- (e, fe) -- (xpart point 0 of yy, fe);
pp = (pi, ypart point 0 of xx) -- (pi, fpi) -- (xpart point 0 of yy, fpi);

draw ee withcolor Reds 8 4;
draw pp withcolor Oranges 8 4;
draw ff withcolor Blues 8 7;

drawarrow xx;
drawarrow yy;

for x=2 upto 4: 
    draw (down--up) scaled 2 shifted (x * u, ypart point 0 of xx);
    label.bot("$" & decimal x & "$", (x * u, ypart point 0 of xx - 2));
endfor
for y=32, 35, 38,: 
    draw (left--right) scaled 2 shifted (xpart point 0 of yy, y/100 * v);
    label.lft("$" & decimal (y/100) & "$", (xpart point 0 of yy - 2, y/100 * v));
endfor

drawoptions(withcolor Reds 8 7);
label.bot("$e$", point 0 of ee shifted 4 down);
label.lft("$1/e$", point 2 of ee shifted 2 left);

drawoptions(withcolor Oranges 8 7);
label.bot("$\pi$", point 0 of pp shifted 4 down);
label.lft("$\ln\pi/\pi$", point 2 of pp shifted 2 left);

drawoptions(withcolor Blues 8 7);
label.urt("$\displaystyle y={\ln x\over x}$", point 42 of ff);

drawoptions();
label.rt("$x$", point 1 of xx);
label.top("$y$", point 1 of yy);

\end{mplibcode}
$$
\vfill
\contrib{Fouad Nakhli}


%---------------
\section{$A^B > B^A$ for $e \le A < B$}

\vfill
$$
\begin{mplibcode}
path ff, xx, yy;
def f(expr x) = 1/256 mlog(x) enddef;
numeric minx, maxx, s, u, v, A, B, e;
minx = 1/4; s = 1/4; maxx = 12;
u = (tw-40)/maxx;
v = 89;
ff = ((minx, f(minx)) for x=minx+s step s until maxx: .. (x, f(x)) endfor) xscaled u yscaled v;
xx = 24 left -- (xpart point infinity of ff + 10, 0);
yy = (0, ypart point 0 of ff) -- (0, ypart point infinity of ff + 10);
A = 1/2 maxx;
B = 3/4 maxx;
e = 2.718281828459;
primarydef o through p = (1+o/arclength(p))[point 1 of p, point 0 of p] -- (1+o/arclength(p))[point 0 of p, point 1 of p] enddef;

forsuffixes $=e, A, B:
    z$ = ($ * u, f($) * v);  
    draw ($ * u, 0) -- z$;
    path p; p = 32 through (origin -- z$);
    draw p withcolor Reds 8 7;
    if not (str $ = "e"):
        draw thelabel.rt("$y=m_" & str $ & "x$", origin) rotated angle z$ shifted point 1 of p withcolor Reds 8 8;
    fi
endfor

drawoptions(withcolor Blues 8 7);
draw ff withpen pencircle scaled 3/4;
label.rt("$y=\ln x$", point 1/2 of ff shifted 4 right);

drawoptions();
drawarrow xx;
drawarrow yy;
label.rt("$x$", point 1 of xx);
label.top("$y$", point 1 of yy);

dotlabel.lrt("\strut $0$", (0, 0));
dotlabel.lrt("\strut $1$", (u, 0));
dotlabel.bot("\strut $e$", (e*u, 0)); 
dotlabel.bot("\strut $A$", (A*u, 0)); 
dotlabel.bot("\strut $B$", (B*u, 0)); 

label.bot(btex \vbox{\openup 12pt\halign{\hfil $#$&${}\quad\mathbin{\Longrightarrow}\quad #$\cr
e \le A < B & m_A > m_B \cr
            & {\ln A \over A} > {\ln B \over B}\cr
            & A^B > B^A\cr
}} etex, point 1/2 of bbox currentpicture shifted 42 down);

\end{mplibcode}
$$
\vfill
\contrib{Charles D.\@ Gallant}


%---------------
\section{The mediant property}

\vfill
$$
\begin{mplibcode}
numeric a, b, c, d;
b + d + 16 = tw;
b = 2a;
a = 3/4 c = d;

path t[];
t1 =  origin -- (b,   0) -- (b,   a)   -- cycle;
t2 = (origin -- (d,   0) -- (d,   c)   -- cycle) shifted point 2 of t1;
t3 =  origin -- (b+d, 0) -- (b+d, a+c) -- cycle;

fill t1 withcolor Blues 8 1;
fill t2 withcolor Oranges 8 1;

forsuffixes $=1,2,3: draw subpath (1,3) of unitsquare scaled 6 rotated 90
    shifted point 1 of t$ withcolor 3/4; endfor

draw subpath(1, 3) of t1; 
draw subpath(-1, 1) of t2; 
draw t3;

label.rt("$a$", point 3/2 of t1);
label.rt("$c$", point 3/2 of t2);
label.rt("$a$", 1/2[point 1 of t3, point 1 of t2]);

label.bot("$b$", point 1/2 of t1);
label.bot("$d$", point 1/2 of t2);
label.bot("$b$", 1/2[point 1 of t3, point 1 of t1]);

label.top(btex $\displaystyle {a\over b} < {c \over d} \quad \Rightarrow \quad
{a\over b} < {a+c\over b+d} < {c\over d}$ etex, point 5/2 of bbox currentpicture shifted 42 up);

\end{mplibcode}
$$
\vfill
\contrib{Richard A.\@ Gibbs}


%---------------
\section{Regle des nombres moyens – two proofs}

\vfill
$$
\begin{mplibcode}
path xx, yy;
xx = 12 left -- 233 right;
yy = 12 down -- 144 up;

z1 = .95(1/4[point 1 of xx, point 1 of yy]);
z3 = .95(3/4[point 1 of xx, point 1 of yy]);
z2 = 1/2[z1, z3];

drawoptions(withcolor Blues 8 7);
draw origin -- z1 -- z3 -- cycle;
draw origin -- z2;
picture m; m = image(draw (up--down) scaled 2 rotated -5 shifted 1/2 left;
                     draw (up--down) scaled 2 rotated -5 shifted 1/2 right); 
draw m rotated angle (z3 - z1) shifted 1/2[z1, z2];
draw m rotated angle (z3 - z1) shifted 1/2[z2, z3];

draw thelabel.top("$y=m_1 x$", origin) rotated angle z1 shifted 3/4 z1;
draw thelabel.top("$y=m_2 x$", origin) rotated angle z2 shifted 3/4 z2;
draw thelabel.top("$y=m_3 x$", origin) rotated angle z3 shifted 3/4 z3;

drawoptions();
drawarrow xx; label.rt("$x$", point 1 of xx);
drawarrow yy; label.top("$y$", point 1 of yy);

dotlabel.urt("$(b,a)$", z1);
dotlabel.urt("$\left({b+d\over2},{a+c\over2}\right)$", z2);
dotlabel.urt("$(d,c)$", z3);
drawdot origin withpen pencircle scaled dotlabeldiam;

label(image(undraw origin -- tw * right), point 1/2 of bbox currentpicture);
label.rt("I.", point 3 of bbox currentpicture);
label.top(btex $\displaystyle a, b, c, d > 0; \quad {a\over b} < {c \over d} \quad \Longrightarrow \quad
{a\over b} < {a+c\over b+d} < {c\over d}$ etex, point 5/2 of bbox currentpicture shifted 13 up);
label.bot(btex $m_1 < m_3 \quad \Longrightarrow \quad m_1 < m_2 < m_3$ etex,  
    point 1/2 of bbox currentpicture shifted 13 down);

\end{mplibcode}
$$
\vfill
\contrib{Li Changming}
\vfill
$$
\begin{mplibcode}
numeric a, b, c, d, u;
u = 89;
55a = 34b; b = d = 1;
64c = 61d;

path A, B, C, D, A', B';

A =  unitsquare scaled u yscaled (a/b);
A' = unitsquare scaled u yscaled (c/d);
B =  unitsquare scaled u yscaled (a/b) xscaled (b/(b+d)) shifted point 1 of A shifted 42 right;
B' = A' shifted point 0 of B;
C =  unitsquare scaled u yscaled (c/d) xscaled (d/(b+d)) shifted point 1 of B;
D = A' shifted point 1 of C shifted 42 right;

draw A' dashed evenly scaled 1/2;
draw B' dashed evenly scaled 1/2;

fill A withcolor Oranges 8 2;
fill B withcolor Oranges 8 2;
fill C withcolor Blues 8 2;
fill D withcolor Blues 8 2;

draw A;
draw B;
draw C;
draw D;

vardef superlabel(expr t, z) = 
    interim bboxmargin := 6;
    save P; picture P; P = thelabel(t, origin);
    save s; path s; s = superellipse(point 3/2 of bbox P, point 5/2 of bbox P, point 7/2 of bbox P, point 1/2 of bbox P, 0.78); 
    unfill s shifted z; draw P shifted z;
enddef;

superlabel("$a\over b$", center A);
superlabel("$a\over b+d$", center B);
superlabel("$c\over b+d$", center C);
superlabel("$c\over d$", center D);

input arrow_label
arrow_label(point 0 of A, point 1 of A, "$1$",          14);
arrow_label(point 0 of B, point 1 of B, "$b\over b+d$", 14);
arrow_label(point 0 of C, point 1 of C, "$d\over b+d$", 14);
arrow_label(point 0 of D, point 1 of D, "$1$",          14);

arrow_label(1/2[point 1 of A, point 0 of B], 1/2[point 2 of A, point 3 of B], "$a\over b$", 0);
arrow_label(1/2[point 1 of C, point 0 of D], 1/2[point 2 of C, point 3 of D], "$c\over d$", 0);

label(image(undraw origin -- tw * right), point 1/2 of bbox currentpicture);
label.rt("II.", point 3 of bbox currentpicture);

label.bot(btex $\displaystyle
{a\over b} < {a\over b+d} + {c \over b+d} < {c\over d}$ etex, point 1/2 of bbox currentpicture shifted 21 down);

\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}


%---------------
\section{The sum of a positive number and its reciprocal is at least two}

\vfill
$$
\begin{mplibcode}
input superlabel

picture P[];

P1 = image(
    numeric x; x = 89/72;
    path box[]; 
    box1 = unitsquare scaled 72 xscaled x yscaled (1/x);
    box2 = box1 rotated 90;  box2 := box2 shifted (point 1 of box1 - point 3 of box2);
    box3 = box1 rotated 180; box3 := box3 shifted (point 1 of box2 - point 3 of box3);
    box4 = box1 rotated 270; box4 := box4 shifted (point 1 of box3 - point 3 of box4);

    forsuffixes $=1,2,3,4:
        fill box$ withcolor if odd $: Oranges else: Blues fi 8 2;
        superlabel("$1$", center box$);
    endfor
    forsuffixes $=1,2,3,4:
        draw subpath (-2, 1) of box$;
    endfor
    label.bot("\strut$x$"         ,   point  1/2 of box1);
    label.bot("\strut${1\over x}$",   point -1/2 of box2);
    label.lft("$x$"         ,   point 1/2 of box4);
    label.lft("${1\over x}$",   point -1/2 of box1);
    label.top("I.", point 3 of bbox currentpicture shifted 3.25 up);
);
P2 = image(
    path xx, yy;
    xx = 8 left -- 150 right;
    yy = xx rotated 90;
    numeric u; u = 100/3;
    for i=1 upto 4:
        draw (left--right) shifted (0, i*u);
        draw (up--down) shifted (i*u, 0);
    endfor
    drawarrow xx; label.rt("$x$", point 1 of xx);
    drawarrow yy; label.top("$y$", point 1 of yy);
    path a, b;
    a = ((-24/100, 224/100) -- (224/100, -24/100)) scaled u;
    b = ((1,1) for x=1+1/8 step 1/8 until 4: -- (x, 1/x) endfor) scaled u;
    b := reverse b reflectedabout(origin, point 0 of b) & b;

    draw b withcolor Oranges 8 7;
    draw a withcolor Blues 8 7;
    label.urt("$y={1\over x}$", point 40 of b) withcolor Oranges 8 7;
    label.lrt("$y=2-x$", point 1 of a shifted 3 up) withcolor Blues 8 7;
    label.llft("II.", point 3 of bbox currentpicture);
);
numeric x, u; x = 7/4; u = 80;
z1 = (0, x - 1/x) scaled u;
z2 = (-2, 0) scaled u;
z3 = -z2;
z4 = origin rotatedabout(z1, 90);
z5 = whatever[z1, z3]; x5 = x4;

P3 = image(
    draw unitsquare scaled 8 rotated 90 withcolor 1/2;
    draw origin -- z1 -- z2 -- cycle;
    label.bot("$2$", 1/2 z2);
    label.rt("$x-{1\over x}$", 1/2z1);
    label.ulft("$x+{1\over x}$", 1/2[z1, z2]);
    label("III.", point 3 of bbox currentpicture);
);
P4 = image(
    fill origin -- z1 -- z3 -- cycle withcolor Oranges 8 2;
    fill z4 -- z1 -- z5 -- cycle withcolor Blues 8 2;
    draw z1 -- origin -- z3 -- z1 -- z4 -- z5;
    draw z5 -- (x5, 0) dashed withdots scaled 1/2;
    
    label.bot("$x$", 1/2 z3);
    label.lft("$1$", 1/2 z1);
    label.rt("$1\over x$", 1/2[z4, z5]);
    label("IV.", point 3 of bbox currentpicture shifted 4 left);
    label.top("$1$", 1/2[z1, z4]);
);

draw P1 shifted 180 left;
draw P2 shifted 32 right;
draw P3 shifted (-32, -180);
draw P4 shifted (+32, -180);
    

\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}

%---------------
\section{Aristarchus' inequalities}

\vfill
$$
\begin{mplibcode}
numeric u, v, a, b, halfpi; 
u = 160;
v = 89;
a = 1.2;
b = 0.8a;
halfpi = 1.5707963267948966;

vardef sin(expr x) = sind(57.29577951308232 x) enddef;
vardef cos(expr x) = cosd(57.29577951308232 x) enddef;
vardef tan(expr x) = sin(x)/cos(x) enddef;

numeric ma; ma = a + 1/8;
path ss, tt, sbb, tbb;
ss = origin for x=1/32 step 1/32 until ma+eps: -- (x * u, sin(x) * v) endfor; 
tt = origin for x=1/32 step 1/32 until a+1/32: -- (x * u, tan(x) * v) endfor; 

sbb = origin -- (ma * u, sin(b) / b * ma * v);
tbb = origin -- (ma * u, tan(b) / b * ma * v);


path xx, yy;
xx = 12 left -- 12 right shifted (halfpi * u, 0); 
yy = xx rotated 90;

draw ss  withcolor Blues 8 7;
draw sbb withcolor Blues 8 5;
draw tt  withcolor Oranges 8 7;
draw tbb withcolor Oranges 8 5;

for $=a,b:
    draw ($*u, 0) -- ($*u, tan($)*v) dashed evenly scaled 1/2 withpen pencircle scaled 1/4;
    drawdot ($*u, sin($)*v) withpen pencircle scaled dotlabeldiam;
    drawdot ($*u, tan($)*v) withpen pencircle scaled dotlabeldiam;
endfor
drawdot (a*u, sin(b)/b*a*v) withpen pencircle scaled dotlabeldiam;
drawdot (a*u, tan(b)/b*a*v) withpen pencircle scaled dotlabeldiam;

drawarrow xx; label.rt("$x$", point 1 of xx);
drawarrow yy; label.top("$y$", point 1 of yy);

label.rt("$y=\sin x$", point infinity of ss shifted down);
label.top("$y=\tan x$", point infinity of tt);

label.urt("$y={\sin \beta \over \beta}x$", point infinity of sbb shifted 4 down);
label.urt("$y={\tan \beta \over \beta}x$", point infinity of tbb shifted 3 down);

vardef hbarlabel@#(expr t, z) = 
    draw (left--right) scaled 3/2 shifted z;
    interim labeloffset := 5;
    label@#(t, z);
enddef;
vardef vbarlabel@#(expr t, z) = 
    draw (down--up) scaled 3/2 shifted z;
    interim labeloffset := 5;
    label@#(t, z);
enddef;

hbarlabel.lft("$1$", (0, v));
dotlabel.llft("$0$", origin);
vbarlabel.bot("\strut $\beta$", (b * u, 0));
vbarlabel.bot("\strut $\alpha$", (a * u, 0));
vbarlabel.bot("\strut $\pi/2$", (halfpi * u, 0));

label.top("$\displaystyle 0 < β < α < {π \over 2} \implies {\sin α \over \sin β} < {α \over β} < {\tan α \over \tan β}$", 
point 5/2 of bbox currentpicture shifted 42 up);
label.bot("$\sin α < {\sin β \over β} α $; \quad $\tan α > {\tan β \over β} α $",
point 1/2 of bbox currentpicture shifted 42 down);
label.bot("$\displaystyle \therefore\quad {\sin α \over \sin β} < {α \over β} < {\tan α \over \tan β}$", 
point 1/2 of bbox currentpicture shifted 8 down);

\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}


%---------------
\section{The Cauchy-Schwartz inequality}

\vfill
$$
\begin{mplibcode}
path t[]; z0 = 89 dir 280; z1 = 75 dir 200;
t1 = (x1, y0) -- z0 -- z1 -- cycle;
t2 = (xpart point 2 of (t1 rotated 180), ypart point 1 of t1) 
         -- point 2 of (t1 rotated 180)     -- point 1 of t1 -- cycle;

picture P[];

for i=1,2:
    if i = 2:
        t1 := t1 rotated - angle (point 1 of t1 - point 2 of t1);
        t1 := t1 shifted - point 3/2 of t1;
        t1 := t1 shifted - (0, 1/2 abs (point 1 of t2 - point 2 of t2));

        t2 := t2 rotated (90 - angle (point 1 of t2 - point 2 of t2));
        t2 := t2 shifted (point 1 of t1 - point 2 of t2); 
    fi

    P[i] = image(
        if i=2:
            draw unitsquare scaled 6 shifted point 2 of t1;
            label.top("$\sqrt{a^2+b^2}$", point 3/2 of t1);
            label.lft("$\sqrt{x^2+y^2}$", point 3/2 of t2);
        fi
        forsuffixes r=0, 180:
            fill t1 rotated r withcolor Blues 8 2;
            fill t2 rotated r withcolor Oranges 8 2;
            draw unitsquare scaled 6 rotated angle (point 1 of t1 - point 0 of t1) shifted point 0 of t1 rotated r;
            draw unitsquare scaled 6 rotated angle (point 1 of t2 - point 0 of t2) shifted point 0 of t2 rotated r;
            draw t1 rotated r;
            draw t2 rotated r;
            if i=1:
                label("$|a|$", point  3/7 of t1 shifted 8 down rotated r);
                label("$|b|$", point -3/7 of t1 shifted 8 left rotated r);
                label("$|x|$", point  3/7 of t2 shifted 8 right rotated r);
                label("$|y|$", point -3/7 of t2 shifted 8 down rotated r);
            fi
        endfor
    );
endfor

label("$\le$", origin);
label.lft(P1, 12 left);
label.rt(P2, 12 right);

label.top(btex 
$\left|  \langle a,b \rangle \cdot            \langle x,y \rangle \right| \le 
 \left\| \langle a,b \rangle \right\| \left\| \langle x,y \rangle \right\|$
etex, point 5/2 of bbox currentpicture shifted 42 up);

label.bot(btex
$\left(|a| + |y|\right)\left(|b| + |x|\right) \le 2\left(\frac12 |a| |b| + \frac12 |x| |y|\right) 
+ \sqrt{a^2 + b^2} \sqrt{x^2 + y^2}$ 
etex, point 1/2 of bbox currentpicture shifted 21 down);

label.bot(btex
$\therefore\quad \left| ax + by \right | \le |a||x| + |b| |y| \le \sqrt{a^2 + b^2} \sqrt{x^2 + y^2}$
etex, point 1/2 of bbox currentpicture shifted 16 down);
\end{mplibcode}
$$

\vfill
\contrib{Roger B.\@ Nelsen}


%---------------
\section{Bernoulli's inequality}


\vfill
$$
\begin{mplibcode}
picture P[];
P1 = image(
    numeric u; u = 53; 
    path xx, yy, ff, dff;
    xx = (-12, 0) -- (12 + 2.5u, 0);
    yy = (0, -12-u) -- (0, 12 + 2.5u);
    
    numeric s; s = 1/8;
    vardef f(expr x) = x**1.9 - 1 enddef;
    ff = ((0, f(0)) for x = s step s until 2: .. (x, f(x)) endfor) scaled u;

    dff = (3/2 left -- 2 right) 
        scaled u 
        rotated angle direction 1/s of ff
        shifted point 1/s of ff;

    drawoptions(withcolor Blues 7 5);
    draw dff; label.rt("$y=r(x-1)$", point infinity of dff);

    drawoptions(withcolor Blues 7 7);
    draw ff; label.rt("$y=x^r - 1$", point infinity of ff);

    drawoptions();
    drawarrow xx; label.rt("$x$", point 1 of xx);
    drawarrow yy; label.top("$y$", point 1 of yy);
    
    dotlabel.llft("$0$", origin);
    dotlabel.lft("$-1$", u * down);
    dotlabel.lrt("$1$", u * right);
    unfill fullcircle scaled 3/4 dotlabeldiam shifted (u* right);

    label.top("\hbox to \textwidth{I. First semester calculus\hss}", 
                point 5/2 of bbox currentpicture shifted 8 up);
);
numeric u, v, r; 
u = 76; v = 1/8 u; r = 3.1415;
path xx, yy, ff; 
xx = 12 left -- 7/4u * right;
yy = 12 down -- 12v * up;
vardef f(expr t) = (t*u, t**(r-1) * r * v) enddef;
ff = origin for t = 1/8 step 1/8 until 7/4: .. f(t) endfor;

P20 = image(
    draw ff withcolor Blues 7 7; label.urt("$y=rt^{r-1}$", point infinity of ff) withcolor Blues 7 7;
    drawarrow xx; label.rt("$t$", point infinity of xx);
    drawarrow yy; label.top("$y$", point infinity of yy);
    dotlabel.llft("$0$", origin);
    dotlabel.bot("$1$", (u, 0));
    dotlabel.lft("$r$", (0, r*v));
);

P21 = image(
    numeric a, b; a = 8; b = 12;
    fill subpath(a, b) of ff -- (xpart point b of ff, 0) -- (xpart point a of ff, 0) -- cycle
        withcolor Blues 7 1;
    draw (0, ypart point a of ff) -- point a of ff dashed withdots scaled 1/2;
    draw (xpart point a of ff, 0) -- point a of ff -- (xpart point b of ff, ypart point a of ff);
    draw point b of ff -- (xpart point b of ff, 0);
    dotlabel.bot("\strut $x$",(xpart point b of ff, 0));
    draw P20;
    label.top("$x>1$", point 5/2 of bbox currentpicture);
    label.bot("$x^r-1 = \int_1^x rt^{r-1}\,dt > r(x-1)$", point 1/2 of bbox currentpicture shifted 8 down);
);
P22 = image(
    numeric a, b; a = 4; b = 8;
    fill subpath(a, b) of ff -- (xpart point b of ff, 0) -- (xpart point a of ff, 0) -- cycle
        withcolor Blues 7 1;
    draw (0, ypart point b of ff) -- (xpart point a of ff, ypart point b of ff) dashed withdots scaled 1/2;
    draw (xpart point a of ff, 0) -- (xpart point a of ff, ypart point b of ff) -- point b of ff;
    draw point b of ff -- (xpart point b of ff, 0);
    dotlabel.bot("\strut $x$",(xpart point a of ff, 0));
    draw P20;
    label.top("$0 < x < 1$", point 5/2 of bbox currentpicture);
    label.bot("$1-x^r = \int_x^1 rt^{r-1}\,dt < r(1-x)$", point 1/2 of bbox currentpicture shifted 8 down);
);

P2 = image(
    label.lft(P21, 12 left);
    label.rt(P22, 12 right);
    label.top("\hbox to \textwidth{II. Second semester calculus\hss}", 
                point 5/2 of bbox currentpicture shifted 8 up);
);

label.top(P1, 7 up);
label.bot(P2, 7 down);


label.top("$x>0$, $x \ne 1$, $r>1$: $x^r - 1 > r(x-1)$", point 5/2 of bbox
currentpicture shifted 13 up);
\end{mplibcode}
$$

\vfill
\contrib{Roger B.\@ Nelsen}


%---------------
\section{Napier's inequality}


\vfill
$$
\begin{mplibcode}
picture P[];
numeric u, v, s; 
u = 64; v = 42; 

path xx, yy, ff; 
xx = 12 left -- 12 right + 4u * right;
vardef f(expr x) = (x * u, mlog(x) / 256 * v) enddef;
s = 1/8;
ff = f(1/8) for t = 1/8+s step s until 4: .. f(t) endfor;
yy = (0, ypart point 0 of ff - 12) -- (0, ypart point infinity of ff + 12);

P1 = image(
    
    numeric a, b; a = 3; b = 21; path L[];
    L1 = (left--right) scaled u rotated angle direction b of ff shifted point b of ff;
    L2 = point a of ff -- point b of ff;
    L3 = (left--right) scaled u rotated angle direction a of ff shifted point a of ff;

    drawoptions(dashed withdots scaled 1/4 withcolor Blues 7 6);
    forsuffixes $=1,2,3: draw L$; endfor
    label.ulft("$L_1$", point 7/8 of L1);
    label.ulft("$L_3$", point 7/8 of L3);
    label.lrt("$L_2$", point 1/4 of L2);

    drawoptions(withcolor Reds 7 7);
    draw ff; label.rt("\rlap{$y=\ln x$}", point infinity of ff);


    drawoptions();
    forsuffixes $=a, b: 
        draw (up--down) shifted (xpart point $ of ff, 0); 
        label.top("$" & str $ & "$", (xpart point $ of ff, 0)); 

        fill fullcircle scaled dotlabeldiam shifted point $ of ff;
        unfill fullcircle scaled 2/3 dotlabeldiam shifted point $ of ff;

    endfor

    drawarrow xx; label.rt("$x$", point 1 of xx);
    drawarrow yy; label.top("$y$", point 1 of yy);
    label.bot("$\slope\left(L_1\right) < \slope\left(L_2\right) < \slope\left(L_3\right)$", point 1/2 of bbox currentpicture);
    label.top("\hbox to \textwidth{I. First semester calculus\hss}", point 5/2 of bbox currentpicture shifted 8 up);
);
path yy, ff;
ff = (1/4 u, 4 v) for x=1/4 + s step s until 4: .. (x * u, v / x) endfor;
yy = 12 down -- (0, 12 + ypart point 0 of ff);

P2 = image(
    z0 = (xpart point a of ff, 0);
    z1 = (xpart point b of ff, 0);
    path A, B, C;
    numeric w; w = abs (z1-z0);
    A = unitsquare xscaled w yscaled ypart point a of ff shifted z0;
    B = unitsquare xscaled w yscaled ypart point b of ff shifted z0;
    C = z1 -- z0 -- subpath(a,b) of ff -- cycle;
    fill C withcolor Blues 7 1;
    fill B withcolor Blues 7 2;
    draw subpath (2,3) of B withcolor 1/2;
    draw subpath (1,4) of A withcolor 1/2; 

    label.bot("\strut$a$", z0);
    label.bot("\strut$b$", z1);

    drawoptions(withcolor Reds 7 7);
    draw ff; label.top("\rlap{$y=\frac1x$}", point infinity of ff);
    drawoptions();
    drawarrow xx; label.rt("$x$", point 1 of xx);
    drawarrow yy; label.top("$y$", point 1 of yy);
    label.bot("$\frac1b(b-a) < \int_a^b \frac1x\,dx < \frac1a(b-a)$", point 1/2 of bbox currentpicture);
    label.top("\hbox to \textwidth{II. Second semester calculus\hss}", point 5/2 of bbox currentpicture shifted 8 up);
);


    label.top(P1, 7 up);
    label.bot(P2, 7 down);

    label.top("$b > a > 0$ \quad implies\quad $\displaystyle {1\over b} < {\ln b - \ln a\over b - a } < {1\over a}$", 
        point 5/2 of bbox currentpicture shifted 13 up);
\end{mplibcode}
$$

\vfill
\contrib{Roger B.\@ Nelsen}

%=============================
\chapter{Integer sums}

\minitoc
%---------------------------
\section{Sums of integers I}

\vfill
$$
\begin{mplibcode}
input paintball    
numeric n; n = 7;
for i=1 upto n-1:
    for j=1 upto n:
        draw if j > i: bball else: rball fi shifted ((j, -i) scaled 280/n);
        if i=j: draw (up--origin--right) shifted (j+1/2, -i-1/2) scaled (280/n); fi
    endfor
endfor
clip currentpicture to bbox currentpicture scaled 0.975;
label.bot("$1+2+\cdots + n = \frac12 n (n+1)$", 
point 1/2 of bbox currentpicture shifted 21 down);
\end{mplibcode}
$$
\vfill
\contrib{Ancient Greek}

%---------------------------
\section{Sums of integers II}

\vfill
$$
\begin{mplibcode}
numeric n; n = 7;
path t; t = (origin -- right -- up -- cycle) rotatedabout(1/2[right, up], 180);
for i=1 upto n:
    for j=0 upto n-i:
        draw (up--origin--right) shifted (i, j) scaled (280/n);
    endfor
    fill t shifted (i, n-i) scaled (280/n) withcolor Purples 8 3;
    draw t shifted (i, n-i) scaled (280/n);
endfor
label.bot("$1+2+\cdots + n = \frac{n^2}2 + \frac n2$", 
point 1/2 of bbox currentpicture shifted 21 down);
\end{mplibcode}
$$
\vfill
\contrib{Ian Richards}


%---------------------------
\section{Sums of odd integers I}

\vfill
$$
\begin{mplibcode}
input paintball
numeric n; n = 10;
picture half;
half = image(
    for i=1 upto n:
        for j=1 upto i:
            draw if odd i: rball else: bball fi shifted ((i, j) scaled (280/n));
        endfor
        if i < n: draw (origin -- i * up) shifted (i+1/2, 1/2) scaled (280/n); fi
    endfor
);
draw half; draw half reflectedabout(origin, (100, 100));

label.bot("$1+3+5+\cdots + (2n-1) = n^2$", 
point 1/2 of bbox currentpicture shifted 21 down);
\end{mplibcode}
$$
\vfill
\contrib{Nichomachus of Gerasa}


%---------------------------
\section{Sums of odd integers II}

\vfill
$$
\begin{mplibcode}
input paintball
numeric n, u; n = 10; u = 280/n; 
for i=0 upto 3:     
    for j=1 upto floor (n/2):
        for k=j upto n-j:
            draw if i=0: bball else: rball fi
                shifted ((k, j) scaled u)
                rotatedabout((n/2+1/2,n/2+1/2) scaled u, 90i);
            if k=j: draw (down--origin--right)
                shifted (k-1/2, j+1/2) scaled u
                rotatedabout((n/2+1/2,n/2+1/2) scaled u, 90i);
            fi
        endfor
    endfor
endfor
interim bboxmargin := -1/4;
draw bbox currentpicture;
label.bot("$1+3+\cdots + (2n-1) = \frac14\left(2n\right)^2 = n^2$", 
point 1/2 of bbox currentpicture shifted 24 down);
\end{mplibcode}
$$
\vfill
\contrib{Roger B.\@ Nelsen}


%---------------------------
\section{Sums of odd integers III}

\vfill
$$
\begin{mplibcode}
picture P[]; path t; numeric n; n = 6;
t = for i=0 upto 2: 16 down rotated 120i -- endfor cycle;

pair u, v;
u = point 2 of t - point 0 of t;
v = point 1 of t - point 2 of t;

P1 = image(
    fill t withcolor Blues 8 2; label("$\Delta$", origin);
    for i=0 upto n-1:
        for j=0 upto i:
            draw t shifted (j * v) shifted (i * u);
        endfor
        label.lrt( 
            if i=n-1: "$2n-1$" 
            elseif i=n-2: "\rotatebox{105}{$\ddots$}" 
            else: "$" & decimal (2i+1) & "$" fi, 
            point 2/3 of t shifted (i * v) shifted (i * u));
    endfor
);
P2 = image(
    path s, s';
    s = t shifted -(xpart point 1 of t, ypart point 0 of t);
    s' = s scaled n;

    forsuffixes $=s, s':
        fill $ withcolor Blues 8 2; draw $;
    endfor
    z1 = point 2 of s' scaled 1.1;
    drawoptions(dashed evenly scaled 1/2 withcolor 1/2);
    drawarrow origin -- z1;
    drawarrow origin -- (x1, 0);
    drawarrow origin -- (0, y1);
    drawoptions();
    label("$\Delta$", 1/3[point 3/2 of s, point 0 of s]);
    label("$n^2\Delta$", 1/3[point 3/2 of s', point 0 of s']);
    label.llft("$1$", point -2/3 of s);
    label.llft("$n$", point -1/2 of s');
);

interim bboxmargin := 12;
label.ulft(P1, origin);
label.urt(P2, origin shifted down);

label.bot("$\Delta+3\cdot\Delta+\cdots + (2n-1)\cdot\Delta = A = n^2\cdot\Delta$", 
    point 1/2 of bbox currentpicture shifted 24 down);
label.bot("$\displaystyle\sum_{i=1}^n \left(2i - 1\right) = n^2$",
    point 1/2 of bbox currentpicture shifted 4 down);
\end{mplibcode}
$$
\vfill
\contrib{Jenő Lehel}


%---------------------------
\section{Squares and sums of integers I}

\vfill
$$
\begin{mplibcode}
input paintball
for n=2 upto 4:
    numeric y; y = - 20 n * n;
    picture p; p = image(
        for i=1 upto n:
            for j=1 upto n:
                draw rball shifted 28(i, j);
            endfor
        endfor
        path b; b = bbox currentpicture;
        drawoptions(dashed evenly withpen pencircle scaled 1/4);
        for i=1 upto n-1:
            for j=-1, 1:
                draw (left-- 2 right) scaled 40n rotated 45 shifted ((28i-14)*j, 0);
            endfor
        endfor
        drawoptions();
        clip currentpicture to b;
    );
    label(p, (-80, y));
    label("$" 
        for i=1 upto n: & decimal i & "+" endfor
        for i=n-1 downto 2: & decimal i & "+" endfor 
        & "1 =" & decimal n & "^2$", 
        (80, y));
endfor

    label.bot("$1+2+\cdots+(n-1) + n + (n-1) + \cdots + 2 + 1 = n^2$",
    point 1/2 of bbox currentpicture shifted 42 down);
\end{mplibcode}
$$
\vfill
\contrib{Ancient Greek}

\end{document}
